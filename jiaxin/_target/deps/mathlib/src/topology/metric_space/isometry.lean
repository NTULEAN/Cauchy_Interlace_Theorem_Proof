/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Isometries of emetric and metric spaces
Authors: S√©bastien Gou√´zel
-/
import topology.metric_space.antilipschitz

/-!
# Isometries

We define isometries, i.e., maps between emetric spaces that preserve
the edistance (on metric spaces, these are exactly the maps that preserve distances),
and prove their basic properties. We also introduce isometric bijections.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `pseudo_metric_space` and we specialize to `metric_space` when needed.
-/

noncomputable theory

universes u v w
variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

open function set
open_locale topological_space ennreal

/-- An isometry (also known as isometric embedding) is a map preserving the edistance
between pseudoemetric spaces, or equivalently the distance between pseudometric space.  -/
def isometry [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] (f : Œ± ‚Üí Œ≤) : Prop :=
‚àÄx1 x2 : Œ±, edist (f x1) (f x2) = edist x1 x2

/-- On pseudometric spaces, a map is an isometry if and only if it preserves distances. -/
lemma isometry_emetric_iff_metric [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] {f : Œ± ‚Üí Œ≤} :
  isometry f ‚Üî (‚àÄx y, dist (f x) (f y) = dist x y) :=
‚ü®assume H x y, by simp [dist_edist, H x y],
assume H x y, by simp [edist_dist, H x y]‚ü©

/-- An isometry preserves edistances. -/
theorem isometry.edist_eq [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] {f : Œ± ‚Üí Œ≤}
  (hf : isometry f) (x y : Œ±) : edist (f x) (f y) = edist x y :=
hf x y

/-- An isometry preserves distances. -/
theorem isometry.dist_eq [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] {f : Œ± ‚Üí Œ≤}
  (hf : isometry f) (x y : Œ±) : dist (f x) (f y) = dist x y :=
by rw [dist_edist, dist_edist, hf]

section pseudo_emetric_isometry

variables [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] [pseudo_emetric_space Œ≥]
variables {f : Œ± ‚Üí Œ≤} {x y z : Œ±}  {s : set Œ±}

lemma isometry.lipschitz (h : isometry f) : lipschitz_with 1 f :=
lipschitz_with.of_edist_le $ Œª x y, le_of_eq (h x y)

lemma isometry.antilipschitz (h : isometry f) : antilipschitz_with 1 f :=
Œª x y, by simp only [h x y, ennreal.coe_one, one_mul, le_refl]

/-- An isometry from an emetric space is injective -/
lemma isometry.injective {Œ± : Type u} [emetric_space Œ±] {f : Œ± ‚Üí Œ≤} (h : isometry f) :
  injective f := h.antilipschitz.injective

/-- Any map on a subsingleton is an isometry -/
theorem isometry_subsingleton [subsingleton Œ±] : isometry f :=
Œªx y, by rw subsingleton.elim x y; simp

/-- The identity is an isometry -/
lemma isometry_id : isometry (id : Œ± ‚Üí Œ±) :=
Œªx y, rfl

/-- The composition of isometries is an isometry -/
theorem isometry.comp {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : isometry g) (hf : isometry f) :
  isometry (g ‚àò f) :=
assume x y, calc
  edist ((g ‚àò f) x) ((g ‚àò f) y) = edist (f x) (f y) : hg _ _
                            ... = edist x y : hf _ _

/-- An isometry from a metric space is a uniform inducing map -/
theorem isometry.uniform_inducing (hf : isometry f) :
  uniform_inducing f :=
hf.antilipschitz.uniform_inducing hf.lipschitz.uniform_continuous

lemma isometry.tendsto_nhds_iff {Œπ : Type*} {f : Œ± ‚Üí Œ≤}
  {g : Œπ ‚Üí Œ±} {a : filter Œπ} {b : Œ±} (hf : isometry f) :
  filter.tendsto g a (ùìù b) ‚Üî filter.tendsto (f ‚àò g) a (ùìù (f b)) :=
hf.uniform_inducing.inducing.tendsto_nhds_iff

/-- An isometry is continuous. -/
lemma isometry.continuous (hf : isometry f) : continuous f :=
hf.lipschitz.continuous

/-- The right inverse of an isometry is an isometry. -/
lemma isometry.right_inv {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±} (h : isometry f) (hg : right_inverse g f) :
  isometry g :=
Œª x y, by rw [‚Üê h, hg _, hg _]

/-- Isometries preserve the diameter in pseudoemetric spaces. -/
lemma isometry.ediam_image (hf : isometry f) (s : set Œ±) :
  emetric.diam (f '' s) = emetric.diam s :=
eq_of_forall_ge_iff $ Œª d,
by simp only [emetric.diam_le_iff, ball_image_iff, hf.edist_eq]

lemma isometry.ediam_range (hf : isometry f) :
  emetric.diam (range f) = emetric.diam (univ : set Œ±) :=
by { rw ‚Üê image_univ, exact hf.ediam_image univ }

lemma isometry.maps_to_emetric_ball (hf : isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
  maps_to f (emetric.ball x r) (emetric.ball (f x) r) :=
Œª y hy, by rwa [emetric.mem_ball, hf]

lemma isometry.maps_to_emetric_closed_ball (hf : isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
  maps_to f (emetric.closed_ball x r) (emetric.closed_ball (f x) r) :=
Œª y hy, by rwa [emetric.mem_closed_ball, hf]

/-- The injection from a subtype is an isometry -/
lemma isometry_subtype_coe {s : set Œ±} : isometry (coe : s ‚Üí Œ±) :=
Œªx y, rfl

lemma isometry.comp_continuous_on_iff {Œ≥} [topological_space Œ≥] (hf : isometry f) {g : Œ≥ ‚Üí Œ±}
  {s : set Œ≥} :
  continuous_on (f ‚àò g) s ‚Üî continuous_on g s :=
hf.uniform_inducing.inducing.continuous_on_iff.symm

lemma isometry.comp_continuous_iff {Œ≥} [topological_space Œ≥] (hf : isometry f) {g : Œ≥ ‚Üí Œ±} :
  continuous (f ‚àò g) ‚Üî continuous g :=
hf.uniform_inducing.inducing.continuous_iff.symm

end pseudo_emetric_isometry --section

section emetric_isometry
variables [emetric_space Œ±]

/-- An isometry from a metric space is a uniform embedding -/
theorem isometry.uniform_embedding [pseudo_emetric_space Œ≤] {f : Œ± ‚Üí Œ≤} (hf : isometry f) :
  uniform_embedding f :=
hf.antilipschitz.uniform_embedding hf.lipschitz.uniform_continuous

/-- An isometry from a metric space is an embedding -/
theorem isometry.embedding [pseudo_emetric_space Œ≤] {f : Œ± ‚Üí Œ≤} (hf : isometry f) :
  embedding f :=
hf.uniform_embedding.embedding

/-- An isometry from a complete emetric space is a closed embedding -/
theorem isometry.closed_embedding [complete_space Œ±] [emetric_space Œ≤]
  {f : Œ± ‚Üí Œ≤} (hf : isometry f) : closed_embedding f :=
hf.antilipschitz.closed_embedding hf.lipschitz.uniform_continuous

end emetric_isometry --section

namespace isometry

variables [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] {f : Œ± ‚Üí Œ≤}

/-- An isometry preserves the diameter in pseudometric spaces. -/
lemma diam_image (hf : isometry f) (s : set Œ±) : metric.diam (f '' s) = metric.diam s :=
by rw [metric.diam, metric.diam, hf.ediam_image]

lemma diam_range (hf : isometry f) : metric.diam (range f) = metric.diam (univ : set Œ±) :=
by { rw ‚Üê image_univ, exact hf.diam_image univ }

lemma maps_to_ball (hf : isometry f) (x : Œ±) (r : ‚Ñù) :
  maps_to f (metric.ball x r) (metric.ball (f x) r) :=
Œª y hy, by rwa [metric.mem_ball, hf.dist_eq]

lemma maps_to_sphere (hf : isometry f) (x : Œ±) (r : ‚Ñù) :
  maps_to f (metric.sphere x r) (metric.sphere (f x) r) :=
Œª y hy, by rwa [metric.mem_sphere, hf.dist_eq]

lemma maps_to_closed_ball (hf : isometry f) (x : Œ±) (r : ‚Ñù) :
  maps_to f (metric.closed_ball x r) (metric.closed_ball (f x) r) :=
Œª y hy, by rwa [metric.mem_closed_ball, hf.dist_eq]

end isometry

/-- `Œ±` and `Œ≤` are isometric if there is an isometric bijection between them. -/
@[nolint has_inhabited_instance] -- such a bijection need not exist
structure isometric (Œ± : Type*) (Œ≤ : Type*) [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤]
  extends Œ± ‚âÉ Œ≤ :=
(isometry_to_fun  : isometry to_fun)

infix ` ‚âÉ·µ¢ `:25 := isometric

namespace isometric

section pseudo_emetric_space
variables [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] [pseudo_emetric_space Œ≥]

instance : has_coe_to_fun (Œ± ‚âÉ·µ¢ Œ≤) (Œª _, Œ± ‚Üí Œ≤) := ‚ü®Œªe, e.to_equiv‚ü©

lemma coe_eq_to_equiv (h : Œ± ‚âÉ·µ¢ Œ≤) (a : Œ±) : h a = h.to_equiv a := rfl

@[simp] lemma coe_to_equiv (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.to_equiv = h := rfl

protected lemma isometry (h : Œ± ‚âÉ·µ¢ Œ≤) : isometry h := h.isometry_to_fun

protected lemma bijective (h : Œ± ‚âÉ·µ¢ Œ≤) : bijective h := h.to_equiv.bijective
protected lemma injective (h : Œ± ‚âÉ·µ¢ Œ≤) : injective h := h.to_equiv.injective
protected lemma surjective (h : Œ± ‚âÉ·µ¢ Œ≤) : surjective h := h.to_equiv.surjective

protected lemma edist_eq (h : Œ± ‚âÉ·µ¢ Œ≤) (x y : Œ±) : edist (h x) (h y) = edist x y :=
h.isometry.edist_eq x y

protected lemma dist_eq {Œ± Œ≤ : Type*} [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)
  (x y : Œ±) : dist (h x) (h y) = dist x y :=
h.isometry.dist_eq x y

protected lemma continuous (h : Œ± ‚âÉ·µ¢ Œ≤) : continuous h := h.isometry.continuous

@[simp] lemma ediam_image (h : Œ± ‚âÉ·µ¢ Œ≤) (s : set Œ±) : emetric.diam (h '' s) = emetric.diam s :=
h.isometry.ediam_image s

lemma to_equiv_inj : ‚àÄ ‚¶Éh‚ÇÅ h‚ÇÇ : Œ± ‚âÉ·µ¢ Œ≤‚¶Ñ, (h‚ÇÅ.to_equiv = h‚ÇÇ.to_equiv) ‚Üí h‚ÇÅ = h‚ÇÇ
| ‚ü®e‚ÇÅ, h‚ÇÅ‚ü© ‚ü®e‚ÇÇ, h‚ÇÇ‚ü© H := by { dsimp at H, subst e‚ÇÅ }

@[ext] lemma ext ‚¶Éh‚ÇÅ h‚ÇÇ : Œ± ‚âÉ·µ¢ Œ≤‚¶Ñ (H : ‚àÄ x, h‚ÇÅ x = h‚ÇÇ x) : h‚ÇÅ = h‚ÇÇ :=
to_equiv_inj $ equiv.ext H

/-- Alternative constructor for isometric bijections,
taking as input an isometry, and a right inverse. -/
def mk' {Œ± : Type u} [emetric_space Œ±] (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ±) (hfg : ‚àÄ x, f (g x) = x)
  (hf : isometry f) : Œ± ‚âÉ·µ¢ Œ≤ :=
{ to_fun := f,
  inv_fun := g,
  left_inv := Œª x, hf.injective $ hfg _,
  right_inv := hfg,
  isometry_to_fun := hf }

/-- The identity isometry of a space. -/
protected def refl (Œ± : Type*) [pseudo_emetric_space Œ±] : Œ± ‚âÉ·µ¢ Œ± :=
{ isometry_to_fun := isometry_id, .. equiv.refl Œ± }

/-- The composition of two isometric isomorphisms, as an isometric isomorphism. -/
protected def trans (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) : Œ± ‚âÉ·µ¢ Œ≥ :=
{ isometry_to_fun  := h‚ÇÇ.isometry_to_fun.comp h‚ÇÅ.isometry_to_fun,
  .. equiv.trans h‚ÇÅ.to_equiv h‚ÇÇ.to_equiv }

@[simp] lemma trans_apply (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) (x : Œ±) : h‚ÇÅ.trans h‚ÇÇ x = h‚ÇÇ (h‚ÇÅ x) := rfl

/-- The inverse of an isometric isomorphism, as an isometric isomorphism. -/
protected def symm (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ≤ ‚âÉ·µ¢ Œ± :=
{ isometry_to_fun  := h.isometry.right_inv h.right_inv,
  to_equiv := h.to_equiv.symm }

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def simps.apply (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ± ‚Üí Œ≤ := h
/-- See Note [custom simps projection] -/
def simps.symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ≤ ‚Üí Œ± := h.symm

initialize_simps_projections isometric
  (to_equiv_to_fun ‚Üí apply, to_equiv_inv_fun ‚Üí symm_apply)

@[simp] lemma symm_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : h.symm.symm = h := to_equiv_inj h.to_equiv.symm_symm

@[simp] lemma apply_symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) (y : Œ≤) : h (h.symm y) = y :=
h.to_equiv.apply_symm_apply y

@[simp] lemma symm_apply_apply (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) : h.symm (h x) = x :=
h.to_equiv.symm_apply_apply x

lemma symm_apply_eq (h : Œ± ‚âÉ·µ¢ Œ≤) {x : Œ±} {y : Œ≤} :
  h.symm y = x ‚Üî y = h x :=
h.to_equiv.symm_apply_eq

lemma eq_symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) {x : Œ±} {y : Œ≤} :
  x = h.symm y ‚Üî h x = y :=
h.to_equiv.eq_symm_apply

lemma symm_comp_self (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.symm ‚àò ‚áëh = id :=
funext $ assume a, h.to_equiv.left_inv a

lemma self_comp_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh ‚àò ‚áëh.symm = id :=
funext $ assume a, h.to_equiv.right_inv a

@[simp] lemma range_eq_univ (h : Œ± ‚âÉ·µ¢ Œ≤) : range h = univ :=
h.to_equiv.range_eq_univ

lemma image_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : image h.symm = preimage h :=
image_eq_preimage_of_inverse h.symm.to_equiv.left_inv h.symm.to_equiv.right_inv

lemma preimage_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : preimage h.symm = image h :=
(image_eq_preimage_of_inverse h.to_equiv.left_inv h.to_equiv.right_inv).symm

@[simp] lemma symm_trans_apply (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) (x : Œ≥) :
  (h‚ÇÅ.trans h‚ÇÇ).symm x = h‚ÇÅ.symm (h‚ÇÇ.symm x) := rfl

lemma ediam_univ (h : Œ± ‚âÉ·µ¢ Œ≤) : emetric.diam (univ : set Œ±) = emetric.diam (univ : set Œ≤) :=
by rw [‚Üê h.range_eq_univ, h.isometry.ediam_range]

@[simp] lemma ediam_preimage (h : Œ± ‚âÉ·µ¢ Œ≤) (s : set Œ≤) : emetric.diam (h ‚Åª¬π' s) = emetric.diam s :=
by rw [‚Üê image_symm, ediam_image]

@[simp] lemma preimage_emetric_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù‚â•0‚àû) :
  h ‚Åª¬π' (emetric.ball x r) = emetric.ball (h.symm x) r :=
by { ext y, simp [‚Üê h.edist_eq] }

@[simp] lemma preimage_emetric_closed_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù‚â•0‚àû) :
  h ‚Åª¬π' (emetric.closed_ball x r) = emetric.closed_ball (h.symm x) r :=
by { ext y, simp [‚Üê h.edist_eq] }

@[simp] lemma image_emetric_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
  h '' (emetric.ball x r) = emetric.ball (h x) r :=
by rw [‚Üê h.preimage_symm, h.symm.preimage_emetric_ball, symm_symm]

@[simp] lemma image_emetric_closed_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
  h '' (emetric.closed_ball x r) = emetric.closed_ball (h x) r :=
by rw [‚Üê h.preimage_symm, h.symm.preimage_emetric_closed_ball, symm_symm]

/-- The (bundled) homeomorphism associated to an isometric isomorphism. -/
@[simps to_equiv] protected def to_homeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ± ‚âÉ‚Çú Œ≤ :=
{ continuous_to_fun  := h.continuous,
  continuous_inv_fun := h.symm.continuous,
  to_equiv := h.to_equiv }

@[simp] lemma coe_to_homeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áë(h.to_homeomorph) = h := rfl

@[simp] lemma coe_to_homeomorph_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áë(h.to_homeomorph.symm) = h.symm := rfl

@[simp] lemma comp_continuous_on_iff {Œ≥} [topological_space Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤)
  {f : Œ≥ ‚Üí Œ±} {s : set Œ≥} :
  continuous_on (h ‚àò f) s ‚Üî continuous_on f s :=
h.to_homeomorph.comp_continuous_on_iff _ _

@[simp] lemma comp_continuous_iff {Œ≥} [topological_space Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≥ ‚Üí Œ±} :
  continuous (h ‚àò f) ‚Üî continuous f :=
h.to_homeomorph.comp_continuous_iff

@[simp] lemma comp_continuous_iff' {Œ≥} [topological_space Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≤ ‚Üí Œ≥} :
  continuous (f ‚àò h) ‚Üî continuous f :=
h.to_homeomorph.comp_continuous_iff'

/-- The group of isometries. -/
instance : group (Œ± ‚âÉ·µ¢ Œ±) :=
  { one := isometric.refl _,
    mul := Œª e‚ÇÅ e‚ÇÇ, e‚ÇÇ.trans e‚ÇÅ,
    inv := isometric.symm,
    mul_assoc := Œª e‚ÇÅ e‚ÇÇ e‚ÇÉ, rfl,
    one_mul := Œª e, ext $ Œª _, rfl,
    mul_one := Œª e, ext $ Œª _, rfl,
    mul_left_inv := Œª e, ext e.symm_apply_apply }

@[simp] lemma coe_one : ‚áë(1 : Œ± ‚âÉ·µ¢ Œ±) = id := rfl

@[simp] lemma coe_mul (e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ±) : ‚áë(e‚ÇÅ * e‚ÇÇ) = e‚ÇÅ ‚àò e‚ÇÇ := rfl

lemma mul_apply (e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ±) (x : Œ±) : (e‚ÇÅ * e‚ÇÇ) x = e‚ÇÅ (e‚ÇÇ x) := rfl

@[simp] lemma inv_apply_self (e : Œ± ‚âÉ·µ¢ Œ±) (x: Œ±) : e‚Åª¬π (e x) = x := e.symm_apply_apply x

@[simp] lemma apply_inv_self (e : Œ± ‚âÉ·µ¢ Œ±) (x: Œ±) : e (e‚Åª¬π x) = x := e.apply_symm_apply x

protected lemma complete_space [complete_space Œ≤] (e : Œ± ‚âÉ·µ¢ Œ≤) : complete_space Œ± :=
complete_space_of_is_complete_univ $ is_complete_of_complete_image e.isometry.uniform_inducing $
  by rwa [set.image_univ, isometric.range_eq_univ, ‚Üê complete_space_iff_is_complete_univ]

lemma complete_space_iff (e : Œ± ‚âÉ·µ¢ Œ≤) : complete_space Œ± ‚Üî complete_space Œ≤ :=
by { split; introI H, exacts [e.symm.complete_space, e.complete_space] }

end pseudo_emetric_space

section pseudo_metric_space

variables [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)

@[simp] lemma diam_image (s : set Œ±) : metric.diam (h '' s) = metric.diam s :=
h.isometry.diam_image s

@[simp] lemma diam_preimage (s : set Œ≤) : metric.diam (h ‚Åª¬π' s) = metric.diam s :=
by rw [‚Üê image_symm, diam_image]

lemma diam_univ : metric.diam (univ : set Œ±) = metric.diam (univ : set Œ≤) :=
congr_arg ennreal.to_real h.ediam_univ

@[simp] lemma preimage_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
  h ‚Åª¬π' (metric.ball x r) = metric.ball (h.symm x) r :=
by { ext y, simp [‚Üê h.dist_eq] }

@[simp] lemma preimage_sphere (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
  h ‚Åª¬π' (metric.sphere x r) = metric.sphere (h.symm x) r :=
by { ext y, simp [‚Üê h.dist_eq] }

@[simp] lemma preimage_closed_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
  h ‚Åª¬π' (metric.closed_ball x r) = metric.closed_ball (h.symm x) r :=
by { ext y, simp [‚Üê h.dist_eq] }

@[simp] lemma image_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) :
  h '' (metric.ball x r) = metric.ball (h x) r :=
by rw [‚Üê h.preimage_symm, h.symm.preimage_ball, symm_symm]

@[simp] lemma image_sphere (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) :
  h '' (metric.sphere x r) = metric.sphere (h x) r :=
by rw [‚Üê h.preimage_symm, h.symm.preimage_sphere, symm_symm]

@[simp] lemma image_closed_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) :
  h '' (metric.closed_ball x r) = metric.closed_ball (h x) r :=
by rw [‚Üê h.preimage_symm, h.symm.preimage_closed_ball, symm_symm]

end pseudo_metric_space

end isometric

/-- An isometry induces an isometric isomorphism between the source space and the
range of the isometry. -/
@[simps to_equiv apply { simp_rhs := tt }]
def isometry.isometric_on_range [emetric_space Œ±] [pseudo_emetric_space Œ≤] {f : Œ± ‚Üí Œ≤}
  (h : isometry f) : Œ± ‚âÉ·µ¢ range f :=
{ isometry_to_fun := Œªx y, by simpa [subtype.edist_eq] using h x y,
  to_equiv := equiv.of_injective f h.injective }
