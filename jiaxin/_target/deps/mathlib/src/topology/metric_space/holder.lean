/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import topology.metric_space.lipschitz
import analysis.special_functions.pow

/-!
# H√∂lder continuous functions

In this file we define H√∂lder continuity on a set and on the whole space. We also prove some basic
properties of H√∂lder continuous functions.

## Main definitions

* `holder_on_with`: `f : X ‚Üí Y` is said to be *H√∂lder continuous* with constant `C : ‚Ñù‚â•0` and
  exponent `r : ‚Ñù‚â•0` on a set `s`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y ‚àà s`;
* `holder_with`: `f : X ‚Üí Y` is said to be *H√∂lder continuous* with constant `C : ‚Ñù‚â•0` and exponent
  `r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`.

## Implementation notes

We use the type `‚Ñù‚â•0` (a.k.a. `nnreal`) for `C` because this type has coercion both to `‚Ñù` and
`‚Ñù‚â•0‚àû`, so it can be easily used both in inequalities about `dist` and `edist`. We also use `‚Ñù‚â•0`
for `r` to ensure that `d ^ r` is monotone in `d`. It might be a good idea to use
`‚Ñù>0` for `r` but we don't have this type in `mathlib` (yet).

## Tags

H√∂lder continuity, Lipschitz continuity

 -/

variables {X Y Z : Type*}

open filter set
open_locale nnreal ennreal topological_space

section emetric

variables [pseudo_emetric_space X] [pseudo_emetric_space Y] [pseudo_emetric_space Z]

/-- A function `f : X ‚Üí Y` between two `pseudo_emetric_space`s is H√∂lder continuous with constant
`C : ‚Ñù‚â•0` and exponent `r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`. -/
def holder_with (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) : Prop :=
‚àÄ x y, edist (f x) (f y) ‚â§ C * edist x y ^ (r : ‚Ñù)

/-- A function `f : X ‚Üí Y` between two `pseudo_emeteric_space`s is H√∂lder continuous with constant
`C : ‚Ñù‚â•0` and exponent `r : ‚Ñù‚â•0` on a set `s : set X`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r`
for all `x y ‚àà s`. -/
def holder_on_with (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) (s : set X) : Prop :=
‚àÄ (x ‚àà s) (y ‚àà s), edist (f x) (f y) ‚â§ C * edist x y ^ (r : ‚Ñù)

@[simp] lemma holder_on_with_empty (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) : holder_on_with C r f ‚àÖ :=
Œª x hx, hx.elim

@[simp] lemma holder_on_with_singleton (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) (x : X) : holder_on_with C r f {x} :=
by { rintro a (rfl : a = x) b (rfl : b = a), rw edist_self, exact zero_le _ }

lemma set.subsingleton.holder_on_with {s : set X} (hs : s.subsingleton) (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) :
  holder_on_with C r f s :=
hs.induction_on (holder_on_with_empty C r f) (holder_on_with_singleton C r f)

lemma holder_on_with_univ {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} :
  holder_on_with C r f univ ‚Üî holder_with C r f :=
by simp only [holder_on_with, holder_with, mem_univ, true_implies_iff]

@[simp] lemma holder_on_with_one {C : ‚Ñù‚â•0} {f : X ‚Üí Y} {s : set X} :
  holder_on_with C 1 f s ‚Üî lipschitz_on_with C f s :=
by simp only [holder_on_with, lipschitz_on_with, nnreal.coe_one, ennreal.rpow_one]

alias holder_on_with_one ‚Üî _ lipschitz_on_with.holder_on_with

@[simp] lemma holder_with_one {C : ‚Ñù‚â•0} {f : X ‚Üí Y} :
  holder_with C 1 f ‚Üî lipschitz_with C f :=
holder_on_with_univ.symm.trans $ holder_on_with_one.trans lipschitz_on_univ

alias holder_with_one ‚Üî _ lipschitz_with.holder_with

lemma holder_with_id : holder_with 1 1 (id : X ‚Üí X) :=
lipschitz_with.id.holder_with

protected lemma holder_with.holder_on_with {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} (h : holder_with C r f)
  (s : set X) :
  holder_on_with C r f s :=
Œª x _ y _, h x y

namespace holder_on_with

variables {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} {s t : set X}

lemma edist_le (h : holder_on_with C r f s) {x y : X} (hx : x ‚àà s) (hy : y ‚àà s) :
  edist (f x) (f y) ‚â§ C * edist x y ^ (r : ‚Ñù) :=
h x hx y hy

lemma edist_le_of_le (h : holder_on_with C r f s) {x y : X} (hx : x ‚àà s) (hy : y ‚àà s)
  {d : ‚Ñù‚â•0‚àû} (hd : edist x y ‚â§ d) :
  edist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
(h.edist_le hx hy).trans (mul_le_mul_left' (ennreal.rpow_le_rpow hd r.coe_nonneg) _)

lemma comp {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} {t : set Y} (hg : holder_on_with Cg rg g t)
  {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y} (hf : holder_on_with Cf rf f s) (hst : maps_to f s t) :
  holder_on_with (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) s :=
begin
  intros x hx y hy,
  rw [ennreal.coe_mul, mul_comm rg, nnreal.coe_mul, ennreal.rpow_mul, mul_assoc,
    ‚Üê ennreal.coe_rpow_of_nonneg _ rg.coe_nonneg, ‚Üê ennreal.mul_rpow_of_nonneg _ _ rg.coe_nonneg],
  exact hg.edist_le_of_le (hst hx) (hst hy) (hf.edist_le hx hy)
end

lemma comp_holder_with {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} {t : set Y} (hg : holder_on_with Cg rg g t)
  {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y} (hf : holder_with Cf rf f) (ht : ‚àÄ x, f x ‚àà t) :
  holder_with (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) :=
holder_on_with_univ.mp $ hg.comp (hf.holder_on_with univ) (Œª x _, ht x)

/-- A H√∂lder continuous function is uniformly continuous -/
protected lemma uniform_continuous_on (hf : holder_on_with C r f s) (h0 : 0 < r) :
  uniform_continuous_on f s :=
begin
  refine emetric.uniform_continuous_on_iff.2 (ŒªŒµ Œµpos, _),
  have : tendsto (Œª d : ‚Ñù‚â•0‚àû, (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù)) (ùìù 0) (ùìù 0),
    from ennreal.tendsto_const_mul_rpow_nhds_zero_of_pos ennreal.coe_ne_top h0,
  rcases ennreal.nhds_zero_basis.mem_iff.1 (this (gt_mem_nhds Œµpos)) with ‚ü®Œ¥, Œ¥0, H‚ü©,
  exact ‚ü®Œ¥, Œ¥0, Œª x hx y hy h, (hf.edist_le hx hy).trans_lt (H h)‚ü©,
end

protected lemma continuous_on (hf : holder_on_with C r f s) (h0 : 0 < r) : continuous_on f s :=
(hf.uniform_continuous_on h0).continuous_on

protected lemma mono (hf : holder_on_with C r f s) (ht : t ‚äÜ s) : holder_on_with C r f t :=
Œª x hx y hy, hf.edist_le (ht hx) (ht hy)

lemma ediam_image_le_of_le (hf : holder_on_with C r f s) {d : ‚Ñù‚â•0‚àû} (hd : emetric.diam s ‚â§ d) :
  emetric.diam (f '' s) ‚â§ C * d ^ (r : ‚Ñù) :=
emetric.diam_image_le_iff.2 $ Œª x hx y hy, hf.edist_le_of_le hx hy $
  (emetric.edist_le_diam_of_mem hx hy).trans hd

lemma ediam_image_le (hf : holder_on_with C r f s) :
  emetric.diam (f '' s) ‚â§ C * emetric.diam s ^ (r : ‚Ñù) :=
hf.ediam_image_le_of_le le_rfl

lemma ediam_image_le_of_subset (hf : holder_on_with C r f s) (ht : t ‚äÜ s) :
  emetric.diam (f '' t) ‚â§ C * emetric.diam t ^ (r : ‚Ñù) :=
(hf.mono ht).ediam_image_le

lemma ediam_image_le_of_subset_of_le (hf : holder_on_with C r f s) (ht : t ‚äÜ s) {d : ‚Ñù‚â•0‚àû}
  (hd : emetric.diam t ‚â§ d) :
  emetric.diam (f '' t) ‚â§ C * d ^ (r : ‚Ñù) :=
(hf.mono ht).ediam_image_le_of_le hd

lemma ediam_image_inter_le_of_le (hf : holder_on_with C r f s) {d : ‚Ñù‚â•0‚àû}
  (hd : emetric.diam t ‚â§ d) :
  emetric.diam (f '' (t ‚à© s)) ‚â§ C * d ^ (r : ‚Ñù) :=
hf.ediam_image_le_of_subset_of_le (inter_subset_right _ _) $
  (emetric.diam_mono $ inter_subset_left _ _).trans hd

lemma ediam_image_inter_le (hf : holder_on_with C r f s) (t : set X) :
  emetric.diam (f '' (t ‚à© s)) ‚â§ C * emetric.diam t ^ (r : ‚Ñù) :=
hf.ediam_image_inter_le_of_le le_rfl

end holder_on_with

namespace holder_with

variables {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

lemma edist_le (h : holder_with C r f) (x y : X) :
  edist (f x) (f y) ‚â§ C * edist x y ^ (r : ‚Ñù) :=
h x y

lemma edist_le_of_le (h : holder_with C r f) {x y : X} {d : ‚Ñù‚â•0‚àû} (hd : edist x y ‚â§ d) :
  edist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
(h.holder_on_with univ).edist_le_of_le trivial trivial hd

lemma comp {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} (hg : holder_with Cg rg g)
  {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y} (hf : holder_with Cf rf f) :
  holder_with (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) :=
(hg.holder_on_with univ).comp_holder_with hf (Œª _, trivial)

lemma comp_holder_on_with {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} (hg : holder_with Cg rg g)
  {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y} {s : set X} (hf : holder_on_with Cf rf f s) :
  holder_on_with (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) s :=
(hg.holder_on_with univ).comp hf (Œª _ _, trivial)

/-- A H√∂lder continuous function is uniformly continuous -/
protected lemma uniform_continuous (hf : holder_with C r f) (h0 : 0 < r) : uniform_continuous f :=
uniform_continuous_on_univ.mp $ (hf.holder_on_with univ).uniform_continuous_on h0

protected lemma continuous (hf : holder_with C r f) (h0 : 0 < r) : continuous f :=
(hf.uniform_continuous h0).continuous

lemma ediam_image_le (hf : holder_with C r f) (s : set X) :
  emetric.diam (f '' s) ‚â§ C * emetric.diam s ^ (r : ‚Ñù) :=
emetric.diam_image_le_iff.2 $ Œª x hx y hy, hf.edist_le_of_le $ emetric.edist_le_diam_of_mem hx hy

end holder_with

end emetric

section metric

variables [pseudo_metric_space X] [pseudo_metric_space Y] {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

namespace holder_with

lemma nndist_le_of_le (hf : holder_with C r f) {x y : X} {d : ‚Ñù‚â•0} (hd : nndist x y ‚â§ d) :
  nndist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
begin
  rw [‚Üê ennreal.coe_le_coe, ‚Üê edist_nndist, ennreal.coe_mul,
    ‚Üê ennreal.coe_rpow_of_nonneg _ r.coe_nonneg],
  apply hf.edist_le_of_le,
  rwa [edist_nndist, ennreal.coe_le_coe],
end

lemma nndist_le (hf : holder_with C r f) (x y : X) :
  nndist (f x) (f y) ‚â§ C * nndist x y ^ (r : ‚Ñù) :=
hf.nndist_le_of_le le_rfl

lemma dist_le_of_le (hf : holder_with C r f) {x y : X} {d : ‚Ñù} (hd : dist x y ‚â§ d) :
  dist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
begin
  lift d to ‚Ñù‚â•0 using dist_nonneg.trans hd,
  rw dist_nndist at hd ‚ä¢,
  norm_cast at hd ‚ä¢,
  exact hf.nndist_le_of_le hd
end

lemma dist_le (hf : holder_with C r f) (x y : X) :
  dist (f x) (f y) ‚â§ C * dist x y ^ (r : ‚Ñù) :=
hf.dist_le_of_le le_rfl

end holder_with

end metric
