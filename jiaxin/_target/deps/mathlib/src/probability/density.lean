/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.decomposition.radon_nikodym
import measure_theory.measure.lebesgue

/-!
# Probability density function

This file defines the probability density function of random variables, by which we mean
measurable functions taking values in a Borel space. In particular, a measurable function `f`
is said to the probability density function of a random variable `X` if for all measurable
sets `S`, `‚Ñô(X ‚àà S) = ‚à´ x in S, f x dx`. Probability density functions are one way of describing
the distribution of a random variable, and are useful for calculating probabilities and
finding moments (although the latter is better achieved with moment generating functions).

This file also defines the continuous uniform distribution and proves some properties about
random variables with this distribution.

## Main definitions

* `measure_theory.has_pdf` : A random variable `X : Œ± ‚Üí E` is said to `has_pdf` with
  respect to the measure `‚Ñô` on `Œ±` and `Œº` on `E` if there exists a measurable function `f`
  such that the push-forward measure of `‚Ñô` along `X` equals `Œº.with_density f`.
* `measure_theory.pdf` : If `X` is a random variable that `has_pdf X ‚Ñô Œº`, then `pdf X`
  is the measurable function `f` such that the push-forward measure of `‚Ñô` along `X` equals
  `Œº.with_density f`.
* `measure_theory.pdf.uniform` : A random variable `X` is said to follow the uniform
  distribution if it has a constant probability density function with a compact, non-null support.

## Main results

* `measure_theory.pdf.integral_fun_mul_eq_integral` : Law of the unconscious statistician,
  i.e. if a random variable `X : Œ± ‚Üí E` has pdf `f`, then `ùîº(g(X)) = ‚à´ x, g x * f x dx` for
  all measurable `g : E ‚Üí ‚Ñù`.
* `measure_theory.pdf.integral_mul_eq_integral` : A real-valued random variable `X` with
  pdf `f` has expectation `‚à´ x, x * f x dx`.
* `measure_theory.pdf.uniform.integral_eq` : If `X` follows the uniform distribution with
  its pdf having support `s`, then `X` has expectation `(Œª s)‚Åª¬π * ‚à´ x in s, x dx` where `Œª`
  is the Lebesgue measure.

## TODOs

Ultimately, we would also like to define characteristic functions to describe distributions as
it exists for all random variables. However, to define this, we will need Fourier transforms
which we currently do not have.
-/

noncomputable theory
open_locale classical measure_theory nnreal ennreal

namespace measure_theory

open topological_space measure_theory.measure

variables {Œ± E : Type*} [normed_group E] [measurable_space E] [second_countable_topology E]
  [normed_space ‚Ñù E] [complete_space E] [borel_space E]

/-- A random variable `X : Œ± ‚Üí E` is said to `has_pdf` with respect to the measure `‚Ñô` on `Œ±` and
`Œº` on `E` if there exists a measurable function `f` such that the push-forward measure of `‚Ñô`
along `X` equals `Œº.with_density f`. -/
class has_pdf {m : measurable_space Œ±} (X : Œ± ‚Üí E)
  (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) : Prop :=
(pdf' : measurable X ‚àß ‚àÉ (f : E ‚Üí ‚Ñù‚â•0‚àû), measurable f ‚àß map X ‚Ñô = Œº.with_density f)

@[measurability]
lemma has_pdf.measurable {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : has_pdf X ‚Ñô Œº] :
  measurable X :=
hX.pdf'.1

/-- If `X` is a random variable that `has_pdf X ‚Ñô Œº`, then `pdf X` is the measurable function `f`
such that the push-forward measure of `‚Ñô` along `X` equals `Œº.with_density f`. -/
def pdf {m : measurable_space Œ±} (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) :=
if hX : has_pdf X ‚Ñô Œº then classical.some hX.pdf'.2 else 0

lemma pdf_undef {m : measurable_space Œ±} {‚Ñô : measure Œ±} {Œº : measure E} {X : Œ± ‚Üí E}
  (h : ¬¨ has_pdf X ‚Ñô Œº) :
  pdf X ‚Ñô Œº = 0 :=
by simp only [pdf, dif_neg h]

lemma has_pdf_of_pdf_ne_zero {m : measurable_space Œ±} {‚Ñô : measure Œ±} {Œº : measure E} {X : Œ± ‚Üí E}
  (h : pdf X ‚Ñô Œº ‚â† 0) : has_pdf X ‚Ñô Œº :=
begin
  by_contra hpdf,
  rw [pdf, dif_neg hpdf] at h,
  exact hpdf (false.rec (has_pdf X ‚Ñô Œº) (h rfl))
end

lemma pdf_eq_zero_of_not_measurable {m : measurable_space Œ±}
  {‚Ñô : measure Œ±} {Œº : measure E} {X : Œ± ‚Üí E} (hX : ¬¨ measurable X) :
  pdf X ‚Ñô Œº = 0 :=
pdf_undef (Œª hpdf, hX hpdf.pdf'.1)

lemma measurable_of_pdf_ne_zero {m : measurable_space Œ±}
  {‚Ñô : measure Œ±} {Œº : measure E} (X : Œ± ‚Üí E) (h : pdf X ‚Ñô Œº ‚â† 0) :
  measurable X :=
by { by_contra hX, exact h (pdf_eq_zero_of_not_measurable hX) }

@[measurability]
lemma measurable_pdf {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) :
  measurable (pdf X ‚Ñô Œº) :=
begin
  by_cases hX : has_pdf X ‚Ñô Œº,
  { rw [pdf, dif_pos hX],
    exact (classical.some_spec hX.pdf'.2).1 },
  { rw [pdf, dif_neg hX],
    exact measurable_zero }
end

lemma map_eq_with_density_pdf {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : has_pdf X ‚Ñô Œº] :
  measure.map X ‚Ñô = Œº.with_density (pdf X ‚Ñô Œº) :=
begin
  rw [pdf, dif_pos hX],
  exact (classical.some_spec hX.pdf'.2).2
end

lemma map_eq_set_lintegral_pdf {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : has_pdf X ‚Ñô Œº]
  {s : set E} (hs : measurable_set s) :
  measure.map X ‚Ñô s = ‚à´‚Åª x in s, pdf X ‚Ñô Œº x ‚àÇŒº :=
by rw [‚Üê with_density_apply _ hs, map_eq_with_density_pdf X ‚Ñô Œº]

namespace pdf

variables {m : measurable_space Œ±} {‚Ñô : measure Œ±} {Œº : measure E}

lemma lintegral_eq_measure_univ {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº] :
  ‚à´‚Åª x, pdf X ‚Ñô Œº x ‚àÇŒº = ‚Ñô set.univ :=
begin
  rw [‚Üê set_lintegral_univ, ‚Üê map_eq_set_lintegral_pdf X ‚Ñô Œº measurable_set.univ,
      measure.map_apply (has_pdf.measurable X ‚Ñô Œº) measurable_set.univ, set.preimage_univ],
end

lemma ae_lt_top [is_finite_measure ‚Ñô] {Œº : measure E} {X : Œ± ‚Üí E} :
  ‚àÄ·µê x ‚àÇŒº, pdf X ‚Ñô Œº x < ‚àû :=
begin
  by_cases hpdf : has_pdf X ‚Ñô Œº,
  { haveI := hpdf,
    refine ae_lt_top (measurable_pdf X ‚Ñô Œº) _,
    rw lintegral_eq_measure_univ,
    exact (measure_lt_top _ _).ne },
  { rw [pdf, dif_neg hpdf],
    exact filter.eventually_of_forall (Œª x, with_top.zero_lt_top) }
end

lemma of_real_to_real_ae_eq [is_finite_measure ‚Ñô] {X : Œ± ‚Üí E} :
  (Œª x, ennreal.of_real (pdf X ‚Ñô Œº x).to_real) =·µê[Œº] pdf X ‚Ñô Œº :=
of_real_to_real_ae_eq ae_lt_top

lemma integrable_iff_integrable_mul_pdf [is_finite_measure ‚Ñô] {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº]
  {f : E ‚Üí ‚Ñù} (hf : measurable f) :
  integrable (Œª x, f (X x)) ‚Ñô ‚Üî integrable (Œª x, f x * (pdf X ‚Ñô Œº x).to_real) Œº :=
begin
  rw [‚Üê integrable_map_measure hf.ae_measurable (has_pdf.measurable X ‚Ñô Œº),
      map_eq_with_density_pdf X ‚Ñô Œº,
      integrable_with_density_iff (measurable_pdf _ _ _) ae_lt_top],
  apply_instance
end

/-- **The Law of the Unconscious Statistician**: Given a random variable `X` and a measurable
function `f`, `f ‚àò X` is a random variable with expectation `‚à´ x, f x * pdf X ‚àÇŒº`
where `Œº` is a measure on the codomain of `X`. -/
lemma integral_fun_mul_eq_integral [is_finite_measure ‚Ñô]
  {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº] {f : E ‚Üí ‚Ñù} (hf : measurable f) :
  ‚à´ x, f x * (pdf X ‚Ñô Œº x).to_real ‚àÇŒº = ‚à´ x, f (X x) ‚àÇ‚Ñô :=
begin
  by_cases hpdf : integrable (Œª x, f x * (pdf X ‚Ñô Œº x).to_real) Œº,
  { rw [‚Üê integral_map (has_pdf.measurable X ‚Ñô Œº) hf.ae_measurable,
        map_eq_with_density_pdf X ‚Ñô Œº,
        integral_eq_lintegral_pos_part_sub_lintegral_neg_part hpdf,
        integral_eq_lintegral_pos_part_sub_lintegral_neg_part,
        lintegral_with_density_eq_lintegral_mul _ (measurable_pdf X ‚Ñô Œº) hf.neg.ennreal_of_real,
        lintegral_with_density_eq_lintegral_mul _ (measurable_pdf X ‚Ñô Œº) hf.ennreal_of_real],
    { congr' 2,
      { have : ‚àÄ x, ennreal.of_real (f x * (pdf X ‚Ñô Œº x).to_real) =
          ennreal.of_real (pdf X ‚Ñô Œº x).to_real * ennreal.of_real (f x),
        { intro x,
          rw [mul_comm, ennreal.of_real_mul ennreal.to_real_nonneg] },
        simp_rw [this],
        exact lintegral_congr_ae (filter.eventually_eq.mul of_real_to_real_ae_eq (ae_eq_refl _)) },
      { have : ‚àÄ x, ennreal.of_real (- (f x * (pdf X ‚Ñô Œº x).to_real)) =
          ennreal.of_real (pdf X ‚Ñô Œº x).to_real * ennreal.of_real (-f x),
        { intro x,
          rw [neg_mul_eq_neg_mul, mul_comm, ennreal.of_real_mul ennreal.to_real_nonneg] },
        simp_rw [this],
        exact lintegral_congr_ae (filter.eventually_eq.mul of_real_to_real_ae_eq
          (ae_eq_refl _)) } },
    { refine ‚ü®hf.ae_measurable, _‚ü©,
      rw [has_finite_integral, lintegral_with_density_eq_lintegral_mul _
            (measurable_pdf _ _ _) hf.nnnorm.coe_nnreal_ennreal],
      have : (Œª x, (pdf X ‚Ñô Œº * Œª x, ‚Üë‚à•f x‚à•‚Çä) x) =·µê[Œº] (Œª x, ‚à•f x * (pdf X ‚Ñô Œº x).to_real‚à•‚Çä),
      { simp_rw [‚Üê smul_eq_mul, nnnorm_smul, ennreal.coe_mul],
        rw [smul_eq_mul, mul_comm],
        refine filter.eventually_eq.mul (ae_eq_refl _) (ae_eq_trans of_real_to_real_ae_eq.symm _),
        convert ae_eq_refl _,
        ext1 x,
        exact real.ennnorm_eq_of_real ennreal.to_real_nonneg },
      rw lintegral_congr_ae this,
      exact hpdf.2 } },
  { rw [integral_undef hpdf, integral_undef],
    rwa ‚Üê integrable_iff_integrable_mul_pdf hf at hpdf,
    all_goals { apply_instance } }
end

lemma map_absolutely_continuous {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº] : map X ‚Ñô ‚â™ Œº :=
by { rw map_eq_with_density_pdf X ‚Ñô Œº, exact with_density_absolutely_continuous _ _, }

/-- A random variable that `has_pdf` is quasi-measure preserving. -/
lemma to_quasi_measure_preserving {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº] : quasi_measure_preserving X ‚Ñô Œº :=
{ measurable := has_pdf.measurable X ‚Ñô Œº,
  absolutely_continuous := map_absolutely_continuous, }

lemma have_lebesgue_decomposition_of_has_pdf {X : Œ± ‚Üí E} [hX' : has_pdf X ‚Ñô Œº] :
  (map X ‚Ñô).have_lebesgue_decomposition Œº :=
‚ü®‚ü®‚ü®0, pdf X ‚Ñô Œº‚ü©,
  by simp only [zero_add, measurable_pdf X ‚Ñô Œº, true_and, mutually_singular.zero_left,
    map_eq_with_density_pdf X ‚Ñô Œº] ‚ü©‚ü©

lemma has_pdf_iff {X : Œ± ‚Üí E} :
  has_pdf X ‚Ñô Œº ‚Üî measurable X ‚àß (map X ‚Ñô).have_lebesgue_decomposition Œº ‚àß map X ‚Ñô ‚â™ Œº :=
begin
  split,
  { intro hX',
    exactI ‚ü®hX'.pdf'.1, have_lebesgue_decomposition_of_has_pdf, map_absolutely_continuous‚ü© },
  { rintros ‚ü®hX, h_decomp, h‚ü©,
    haveI := h_decomp,
    refine ‚ü®‚ü®hX, (measure.map X ‚Ñô).rn_deriv Œº, measurable_rn_deriv _ _, _‚ü©‚ü©,
    rwa with_density_rn_deriv_eq }
end

lemma has_pdf_iff_of_measurable {X : Œ± ‚Üí E} (hX : measurable X) :
  has_pdf X ‚Ñô Œº ‚Üî (map X ‚Ñô).have_lebesgue_decomposition Œº ‚àß map X ‚Ñô ‚â™ Œº :=
by { rw has_pdf_iff, simp only [hX, true_and], }

section

variables {F : Type*} [normed_group F] [measurable_space F] [second_countable_topology F]
  [normed_space ‚Ñù F] [complete_space F] [borel_space F] {ŒΩ : measure F}

/-- A random variable that `has_pdf` transformed under a `quasi_measure_preserving`
map also `has_pdf` if `(map g (map X ‚Ñô)).have_lebesgue_decomposition Œº`.

`quasi_measure_preserving_has_pdf'` is more useful in the case we are working with a
probability measure and a real-valued random variable. -/
lemma quasi_measure_preserving_has_pdf {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº]
  {g : E ‚Üí F} (hg : quasi_measure_preserving g Œº ŒΩ)
  (hmap : (map g (map X ‚Ñô)).have_lebesgue_decomposition ŒΩ) :
  has_pdf (g ‚àò X) ‚Ñô ŒΩ :=
begin
  rw [has_pdf_iff, ‚Üê map_map hg.measurable (has_pdf.measurable X ‚Ñô Œº)],
  refine ‚ü®hg.measurable.comp (has_pdf.measurable X ‚Ñô Œº), hmap, _‚ü©,
  rw [map_eq_with_density_pdf X ‚Ñô Œº],
  refine absolutely_continuous.mk (Œª s hsm hs, _),
  rw [map_apply hg.measurable hsm, with_density_apply _ (hg.measurable hsm)],
  have := hg.absolutely_continuous hs,
  rw map_apply hg.measurable hsm at this,
  exact set_lintegral_measure_zero _ _ this,
end

lemma quasi_measure_preserving_has_pdf' [is_finite_measure ‚Ñô] [sigma_finite ŒΩ]
  {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº] {g : E ‚Üí F} (hg : quasi_measure_preserving g Œº ŒΩ) :
  has_pdf (g ‚àò X) ‚Ñô ŒΩ :=
quasi_measure_preserving_has_pdf hg infer_instance

end

section real

variables [is_finite_measure ‚Ñô] {X : Œ± ‚Üí ‚Ñù}

/-- A real-valued random variable `X` `has_pdf X ‚Ñô Œª` (where `Œª` is the Lebesgue measure) if and
only if the push-forward measure of `‚Ñô` along `X` is absolutely continuous with respect to `Œª`. -/
lemma real.has_pdf_iff_of_measurable (hX : measurable X) : has_pdf X ‚Ñô ‚Üî map X ‚Ñô ‚â™ volume :=
begin
  rw [has_pdf_iff_of_measurable hX, and_iff_right_iff_imp],
  exact Œª h, infer_instance,
end

lemma real.has_pdf_iff : has_pdf X ‚Ñô ‚Üî measurable X ‚àß map X ‚Ñô ‚â™ volume :=
begin
  by_cases hX : measurable X,
  { rw [real.has_pdf_iff_of_measurable hX, iff_and_self],
    exact Œª h, hX,
    apply_instance },
  { exact ‚ü®Œª h, false.elim (hX h.pdf'.1), Œª h, false.elim (hX h.1)‚ü©, }
end

/-- If `X` is a real-valued random variable that has pdf `f`, then the expectation of `X` equals
`‚à´ x, x * f x ‚àÇŒª` where `Œª` is the Lebesgue measure. -/
lemma integral_mul_eq_integral [has_pdf X ‚Ñô] :
  ‚à´ x, x * (pdf X ‚Ñô volume x).to_real = ‚à´ x, X x ‚àÇ‚Ñô :=
integral_fun_mul_eq_integral measurable_id

lemma has_finite_integral_mul {f : ‚Ñù ‚Üí ‚Ñù} {g : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû}
  (hg : pdf X ‚Ñô =·µê[volume] g) (hgi : ‚à´‚Åª x, ‚à•f x‚à•‚Çä * g x ‚â† ‚àû) :
  has_finite_integral (Œª x, f x * (pdf X ‚Ñô volume x).to_real) :=
begin
  rw has_finite_integral,
  have : (Œª x, ‚Üë‚à•f x‚à•‚Çä * g x) =·µê[volume] (Œª x, ‚à•f x * (pdf X ‚Ñô volume x).to_real‚à•‚Çä),
  { refine ae_eq_trans (filter.eventually_eq.mul (ae_eq_refl (Œª x, ‚à•f x‚à•‚Çä))
      (ae_eq_trans hg.symm of_real_to_real_ae_eq.symm)) _,
    simp_rw [‚Üê smul_eq_mul, nnnorm_smul, ennreal.coe_mul, smul_eq_mul],
    refine filter.eventually_eq.mul (ae_eq_refl _) _,
    convert ae_eq_refl _,
    ext1 x,
    exact real.ennnorm_eq_of_real ennreal.to_real_nonneg },
  rwa [lt_top_iff_ne_top, ‚Üê lintegral_congr_ae this],
end

end real

section

/-! **Uniform Distribution** -/

/-- A random variable `X` has uniform distribution if it has a probability density function `f`
with support `s` such that `f = (Œº s)‚Åª¬π 1‚Çõ` a.e. where `1‚Çõ` is the indicator function for `s`. -/
def is_uniform {m : measurable_space Œ±} (X : Œ± ‚Üí E) (support : set E)
  (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) :=
pdf X ‚Ñô Œº =·µê[Œº] support.indicator ((Œº support)‚Åª¬π ‚Ä¢ 1)

namespace is_uniform

lemma has_pdf {m : measurable_space Œ±} {X : Œ± ‚Üí E} {‚Ñô : measure Œ±} {Œº : measure E}
  {support : set E} (hns : Œº support ‚â† 0) (hnt : Œº support ‚â† ‚ä§) (hu : is_uniform X support ‚Ñô Œº) :
  has_pdf X ‚Ñô Œº :=
has_pdf_of_pdf_ne_zero
begin
  intro hpdf,
  rw [is_uniform, hpdf] at hu,
  suffices : Œº (support ‚à© function.support ((Œº support)‚Åª¬π ‚Ä¢ 1)) = 0,
  { have heq : function.support ((Œº support)‚Åª¬π ‚Ä¢ (1 : E ‚Üí ‚Ñù‚â•0‚àû)) = set.univ,
    { ext x,
      rw [function.mem_support],
      simp [hnt] },
    rw [heq, set.inter_univ] at this,
    exact hns this },
  exact set.indicator_ae_eq_zero hu.symm,
end

lemma pdf_to_real_ae_eq {m : measurable_space Œ±}
  {X : Œ± ‚Üí E} {‚Ñô : measure Œ±} {Œº : measure E} {s : set E} (hX : is_uniform X s ‚Ñô Œº) :
  (Œª x, (pdf X ‚Ñô Œº x).to_real) =·µê[Œº]
  (Œª x, (s.indicator ((Œº s)‚Åª¬π ‚Ä¢ (1 : E ‚Üí ‚Ñù‚â•0‚àû)) x).to_real) :=
filter.eventually_eq.fun_comp hX ennreal.to_real

variables [is_finite_measure ‚Ñô] {X : Œ± ‚Üí ‚Ñù}
variables {s : set ‚Ñù} (hms : measurable_set s) (hns : volume s ‚â† 0)

include hms hns

lemma mul_pdf_integrable (hcs : is_compact s) (huX : is_uniform X s ‚Ñô) :
  integrable (Œª x : ‚Ñù, x * (pdf X ‚Ñô volume x).to_real) :=
begin
  by_cases hsupp : volume s = ‚àû,
  { have : pdf X ‚Ñô =·µê[volume] 0,
    { refine ae_eq_trans huX _,
      simp [hsupp] },
    refine integrable.congr (integrable_zero _ _ _) _,
    rw [(by simp : (Œª x, 0 : ‚Ñù ‚Üí ‚Ñù) = (Œª x, x * (0 : ‚Ñù‚â•0‚àû).to_real))],
    refine filter.eventually_eq.mul (ae_eq_refl _)
      (filter.eventually_eq.fun_comp this.symm ennreal.to_real) },
  refine ‚ü®ae_measurable_id'.mul (measurable_pdf X ‚Ñô).ae_measurable.ennreal_to_real, _‚ü©,
  refine has_finite_integral_mul huX _,
  set ind := (volume s)‚Åª¬π ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû) with hind,
  have : ‚àÄ x, ‚Üë‚à•x‚à•‚Çä * s.indicator ind x = s.indicator (Œª x, ‚à•x‚à•‚Çä * ind x) x :=
      Œª x, (s.indicator_mul_right (Œª x, ‚Üë‚à•x‚à•‚Çä) ind).symm,
  simp only [this, lintegral_indicator _ hms, hind, mul_one,
             algebra.id.smul_eq_mul, pi.one_apply, pi.smul_apply],
  rw lintegral_mul_const _ measurable_nnnorm.coe_nnreal_ennreal,
  { refine (ennreal.mul_lt_top (set_lintegral_lt_top_of_is_compact
      hsupp hcs continuous_nnnorm).ne (ennreal.inv_lt_top.2 (pos_iff_ne_zero.mpr hns)).ne).ne },
  { apply_instance }
end

/-- A real uniform random variable `X` with support `s` has expectation
`(Œª s)‚Åª¬π * ‚à´ x in s, x ‚àÇŒª` where `Œª` is the Lebesgue measure. -/
lemma integral_eq (hnt : volume s ‚â† ‚ä§) (huX : is_uniform X s ‚Ñô) :
  ‚à´ x, X x ‚àÇ‚Ñô = (volume s)‚Åª¬π.to_real * ‚à´ x in s, x :=
begin
  haveI := has_pdf hns hnt huX,
  rw ‚Üê integral_mul_eq_integral,
  all_goals { try { apply_instance } },
  rw integral_congr_ae (filter.eventually_eq.mul (ae_eq_refl _) (pdf_to_real_ae_eq huX)),
  have : ‚àÄ x, x * (s.indicator ((volume s)‚Åª¬π ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû)) x).to_real =
    x * (s.indicator ((volume s)‚Åª¬π.to_real ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù)) x),
  { refine Œª x, congr_arg ((*) x) _,
    by_cases hx : x ‚àà s,
    { simp [set.indicator_of_mem hx] },
    { simp [set.indicator_of_not_mem hx] }},
  simp_rw [this, ‚Üê s.indicator_mul_right (Œª x, x),  integral_indicator hms],
  change ‚à´ x in s, x * ((volume s)‚Åª¬π.to_real ‚Ä¢ 1) ‚àÇ(volume) = _,
  rw [integral_mul_right, mul_comm, algebra.id.smul_eq_mul, mul_one],
end .

end is_uniform

end

end pdf

end measure_theory
