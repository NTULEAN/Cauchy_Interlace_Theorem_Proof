/-
Copyright (c) 2019 Scott Morrison All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import control.equiv_functor
import category_theory.groupoid
import category_theory.whiskering
import category_theory.types

/-!
# The core of a category

The core of a category `C` is the (non-full) subcategory of `C` consisting of all objects,
and all isomorphisms. We construct it as a `groupoid`.

`core.inclusion : core C ‚•§ C` gives the faithful inclusion into the original category.

Any functor `F` from a groupoid `G` into `C` factors through `core C`,
but this is not functorial with respect to `F`.
-/

namespace category_theory

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ -- morphism levels before object levels. See note [category_theory universes].

/-- The core of a category C is the groupoid whose morphisms are all the
isomorphisms of C. -/
@[nolint has_inhabited_instance]
def core (C : Type u‚ÇÅ) := C

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C]

instance core_category : groupoid.{v‚ÇÅ} (core C) :=
{ hom  := Œª X Y : C, X ‚âÖ Y,
  inv  := Œª X Y f, iso.symm f,
  id   := Œª X, iso.refl X,
  comp := Œª X Y Z f g, iso.trans f g }

namespace core
@[simp] lemma id_hom (X : core C) : iso.hom (ùüô X) = ùüô X := rfl
@[simp] lemma comp_hom {X Y Z : core C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g).hom = f.hom ‚â´ g.hom :=
rfl

variables (C)

/-- The core of a category is naturally included in the category. -/
def inclusion : core C ‚•§ C :=
{ obj := id,
  map := Œª X Y f, f.hom }

instance : faithful (inclusion C) := {}

variables {C} {G : Type u‚ÇÇ} [groupoid.{v‚ÇÇ} G]

/-- A functor from a groupoid to a category C factors through the core of C. -/
-- Note that this function is not functorial
-- (consider the two functors from [0] to [1], and the natural transformation between them).
noncomputable
def functor_to_core (F : G ‚•§ C) : G ‚•§ core C :=
{ obj := Œª X, F.obj X,
  map := Œª X Y f, ‚ü®F.map f, F.map (inv f)‚ü© }

/--
We can functorially associate to any functor from a groupoid to the core of a category `C`,
a functor from the groupoid to `C`, simply by composing with the embedding `core C ‚•§ C`.
-/
def forget_functor_to_core : (G ‚•§ core C) ‚•§ (G ‚•§ C) := (whiskering_right _ _ _).obj (inclusion C)
end core

/--
`of_equiv_functor m` lifts a type-level `equiv_functor`
to a categorical functor `core (Type u‚ÇÅ) ‚•§ core (Type u‚ÇÇ)`.
-/
def of_equiv_functor (m : Type u‚ÇÅ ‚Üí Type u‚ÇÇ) [equiv_functor m] :
  core (Type u‚ÇÅ) ‚•§ core (Type u‚ÇÇ) :=
{ obj       := m,
  map       := Œª Œ± Œ≤ f, (equiv_functor.map_equiv m f.to_equiv).to_iso,
  -- These are not very pretty.
  map_id' := Œª Œ±, begin ext, exact (congr_fun (equiv_functor.map_refl _) x), end,
  map_comp' := Œª Œ± Œ≤ Œ≥ f g,
  begin
    ext,
    simp only [equiv_functor.map_equiv_apply, equiv.to_iso_hom,
      function.comp_app, core.comp_hom, types_comp],
    erw [iso.to_equiv_comp, equiv_functor.map_trans],
  end, }

end category_theory
