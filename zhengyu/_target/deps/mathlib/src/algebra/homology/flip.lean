/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.homological_complex

/-!
# Flip a complex of complexes

For now we don't have double complexes as a distinct thing,
but we can model them as complexes of complexes.

Here we show how to flip a complex of complexes over the diagonal,
exchanging the horizontal and vertical directions.

-/

universes v u

open category_theory category_theory.limits

namespace homological_complex

variables {V : Type u} [category.{v} V] [has_zero_morphisms V]
variables {Œπ : Type*} {c : complex_shape Œπ} {Œπ' : Type*} {c' : complex_shape Œπ'}

/--
Flip a complex of complexes over the diagonal,
exchanging the horizontal and vertical directions.
-/
@[simps]
def flip_obj (C : homological_complex (homological_complex V c) c') :
  homological_complex (homological_complex V c') c :=
{ X := Œª i,
  { X := Œª j, (C.X j).X i,
    d := Œª j j', (C.d j j').f i,
    shape' := Œª j j' w, by { rw C.shape j j' w, simp, },
    d_comp_d' := Œª j‚ÇÅ j‚ÇÇ j‚ÇÉ _ _, congr_hom (C.d_comp_d j‚ÇÅ j‚ÇÇ j‚ÇÉ) i, },
  d := Œª i i',
  { f := Œª j, (C.X j).d i i',
    comm' := Œª j j' h, ((C.d j j').comm i i').symm, },
  shape' := Œª i i' w, by { ext j, exact (C.X j).shape i i' w, } }.

variables V c c'

/-- Flipping a complex of complexes over the diagonal, as a functor. -/
@[simps]
def flip : homological_complex (homological_complex V c) c' ‚•§
  homological_complex (homological_complex V c') c :=
{ obj := Œª C, flip_obj C,
  map := Œª C D f,
  { f := Œª i,
    { f := Œª j, (f.f j).f i,
      comm' := Œª j j' h, congr_hom (f.comm j j') i, }, }, }.

/-- Auxiliary definition for `homological_complex.flip_equivalence` .-/
@[simps]
def flip_equivalence_unit_iso :
  ùü≠ (homological_complex (homological_complex V c) c') ‚âÖ flip V c c' ‚ãô flip V c' c :=
nat_iso.of_components
  (Œª C,
  { hom :=
    { f := Œª i, { f := Œª j, ùüô ((C.X i).X j), },
      comm' := Œª i j h, by { ext, dsimp, simp only [category.id_comp, category.comp_id] }, },
    inv :=
    { f := Œª i, { f := Œª j, ùüô ((C.X i).X j), },
      comm' := Œª i j h, by { ext, dsimp, simp only [category.id_comp, category.comp_id] }, } })
  (Œª X Y f, by { ext, dsimp, simp only [category.id_comp, category.comp_id], })

/-- Auxiliary definition for `homological_complex.flip_equivalence` .-/
@[simps]
def flip_equivalence_counit_iso :
  flip V c' c ‚ãô flip V c c' ‚âÖ ùü≠ (homological_complex (homological_complex V c') c) :=
nat_iso.of_components
  (Œª C,
  { hom :=
    { f := Œª i, { f := Œª j, ùüô ((C.X i).X j), },
      comm' := Œª i j h, by { ext, dsimp, simp only [category.id_comp, category.comp_id] }, },
    inv :=
    { f := Œª i, { f := Œª j, ùüô ((C.X i).X j), },
      comm' := Œª i j h, by { ext, dsimp, simp only [category.id_comp, category.comp_id] }, } })
  (Œª X Y f, by { ext, dsimp, simp only [category.id_comp, category.comp_id], })

/-- Flipping a complex of complexes over the diagonal, as an equivalence of categories. -/
@[simps]
def flip_equivalence :
  homological_complex (homological_complex V c) c' ‚âå
    homological_complex (homological_complex V c') c :=
{ functor := flip V c c',
  inverse := flip V c' c,
  unit_iso := flip_equivalence_unit_iso V c c',
  counit_iso := flip_equivalence_counit_iso V c c', }

end homological_complex
