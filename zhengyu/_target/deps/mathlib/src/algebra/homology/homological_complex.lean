/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import algebra.homology.complex_shape
import category_theory.subobject.limits
import category_theory.graded_object

/-!
# Homological complexes.

A `homological_complex V c` with a "shape" controlled by `c : complex_shape Œπ`
has chain groups `X i` (objects in `V`) indexed by `i : Œπ`,
and a differential `d i j` whenever `c.rel i j`.

We in fact ask for differentials `d i j` for all `i j : Œπ`,
but have a field `shape'` requiring that these are zero when not allowed by `c`.
This avoids a lot of dependent type theory hell!

The composite of any two differentials `d i j ‚â´ d j k` must be zero.

We provide `chain_complex V Œ±` for
`Œ±`-indexed chain complexes in which `d i j ‚â† 0` only if `j + 1 = i`,
and similarly `cochain_complex V Œ±`, with `i = j + 1`.

There is a category structure, where morphisms are chain maps.

For `C : homological_complex V c`, we define `C.X_next i`, which is either `C.X j` for some
arbitrarily chosen `j` such that `c.r i j`, or the zero object if there is no such `j`.
Similarly we have `C.X_prev j`.
Defined in terms of these we have `C.d_from i : C.X i ‚ü∂ C.X_next i` and
`C.d_to j : C.X_prev j ‚ü∂ C.X j`, which are either defined as `C.d i j`, or zero, as needed.
-/

universes v u

open category_theory category_theory.limits

variables {Œπ : Type*}
variables (V : Type u) [category.{v} V] [has_zero_morphisms V]

/--
A `homological_complex V c` with a "shape" controlled by `c : complex_shape Œπ`
has chain groups `X i` (objects in `V`) indexed by `i : Œπ`,
and a differential `d i j` whenever `c.rel i j`.

We in fact ask for differentials `d i j` for all `i j : Œπ`,
but have a field `shape'` requiring that these are zero when not allowed by `c`.
This avoids a lot of dependent type theory hell!

The composite of any two differentials `d i j ‚â´ d j k` must be zero.
-/
structure homological_complex (c : complex_shape Œπ) :=
(X : Œπ ‚Üí V)
(d : Œ† i j, X i ‚ü∂ X j)
(shape' : ‚àÄ i j, ¬¨ c.rel i j ‚Üí d i j = 0 . obviously)
(d_comp_d' : ‚àÄ i j k, c.rel i j ‚Üí c.rel j k ‚Üí d i j ‚â´ d j k = 0 . obviously)

namespace homological_complex

restate_axiom shape'
attribute [simp] shape

variables {V} {c : complex_shape Œπ}

@[simp, reassoc] lemma d_comp_d (C : homological_complex V c) (i j k : Œπ) :
  C.d i j ‚â´ C.d j k = 0 :=
begin
  by_cases hij : c.rel i j,
  { by_cases hjk : c.rel j k,
    { exact C.d_comp_d' i j k hij hjk },
    { rw [C.shape j k hjk, comp_zero] } },
  { rw [C.shape i j hij, zero_comp] }
end

end homological_complex

/--
An `Œ±`-indexed chain complex is a `homological_complex`
in which `d i j ‚â† 0` only if `j + 1 = i`.
-/
abbreviation chain_complex (Œ± : Type*) [add_right_cancel_semigroup Œ±] [has_one Œ±] : Type* :=
homological_complex V (complex_shape.down Œ±)

/--
An `Œ±`-indexed cochain complex is a `homological_complex`
in which `d i j ‚â† 0` only if `i + 1 = j`.
-/
abbreviation cochain_complex (Œ± : Type*) [add_right_cancel_semigroup Œ±] [has_one Œ±] : Type* :=
homological_complex V (complex_shape.up Œ±)

namespace chain_complex

@[simp] lemma prev (Œ± : Type*) [add_right_cancel_semigroup Œ±] [has_one Œ±] (i : Œ±) :
  (complex_shape.down Œ±).prev i = some ‚ü®i+1, rfl‚ü© :=
option.choice_eq _

@[simp] lemma next (Œ± : Type*) [add_group Œ±] [has_one Œ±] (i : Œ±) :
  (complex_shape.down Œ±).next i = some ‚ü®i-1, sub_add_cancel i 1‚ü© :=
option.choice_eq _

@[simp] lemma next_nat_zero :
  (complex_shape.down ‚Ñï).next 0 = none :=
@option.choice_eq_none _ ‚ü®by rintro ‚ü®j, ‚ü®‚ü©‚ü©‚ü©

@[simp] lemma next_nat_succ (i : ‚Ñï) :
  (complex_shape.down ‚Ñï).next (i+1) = some ‚ü®i, rfl‚ü© :=
option.choice_eq _

end chain_complex

namespace cochain_complex

@[simp] lemma prev (Œ± : Type*) [add_group Œ±] [has_one Œ±] (i : Œ±) :
  (complex_shape.up Œ±).prev i = some ‚ü®i-1, sub_add_cancel i 1‚ü© :=
option.choice_eq _

@[simp] lemma next (Œ± : Type*) [add_right_cancel_semigroup Œ±] [has_one Œ±] (i : Œ±) :
  (complex_shape.up Œ±).next i = some ‚ü®i+1, rfl‚ü© :=
option.choice_eq _

@[simp] lemma prev_nat_zero :
  (complex_shape.up ‚Ñï).prev 0 = none :=
@option.choice_eq_none _ ‚ü®by rintro ‚ü®j, ‚ü®‚ü©‚ü©‚ü©

@[simp] lemma prev_nat_succ (i : ‚Ñï) :
  (complex_shape.up ‚Ñï).prev (i+1) = some ‚ü®i, rfl‚ü© :=
option.choice_eq _

end cochain_complex

namespace homological_complex
variables {V} {c : complex_shape Œπ} (C : homological_complex V c)

/--
A morphism of homological complexes consists of maps between the chain groups,
commuting with the differentials.
-/
@[ext] structure hom (A B : homological_complex V c) :=
(f : ‚àÄ i, A.X i ‚ü∂ B.X i)
(comm' : ‚àÄ i j, c.rel i j ‚Üí f i ‚â´ B.d i j = A.d i j ‚â´ f j . obviously)

@[simp, reassoc]
lemma hom.comm {A B : homological_complex V c} (f : A.hom B) (i j : Œπ) :
  f.f i ‚â´ B.d i j = A.d i j ‚â´ f.f j :=
begin
  by_cases hij : c.rel i j,
  { exact f.comm' i j hij },
  rw [A.shape i j hij, B.shape i j hij, comp_zero, zero_comp],
end

instance (A B : homological_complex V c) : inhabited (hom A B) :=
‚ü®{ f := Œª i, 0 }‚ü©

/-- Identity chain map. -/
def id (A : homological_complex V c) : hom A A :=
{ f := Œª _, ùüô _ }

/-- Composition of chain maps. -/
def comp (A B C : homological_complex V c) (œÜ : hom A B) (œà : hom B C) : hom A C :=
{ f := Œª i, œÜ.f i ‚â´ œà.f i }

section
local attribute [simp] id comp

instance : category (homological_complex V c) :=
{ hom := hom,
  id := id,
  comp := comp }

end

@[simp] lemma id_f (C : homological_complex V c) (i : Œπ) : hom.f (ùüô C) i = ùüô (C.X i) := rfl
@[simp] lemma comp_f {C‚ÇÅ C‚ÇÇ C‚ÇÉ : homological_complex V c} (f : C‚ÇÅ ‚ü∂ C‚ÇÇ) (g : C‚ÇÇ ‚ü∂ C‚ÇÉ) (i : Œπ) :
  (f ‚â´ g).f i = f.f i ‚â´ g.f i :=
rfl

-- We'll use this later to show that `homological_complex V c` is preadditive when `V` is.
lemma hom_f_injective {C‚ÇÅ C‚ÇÇ : homological_complex V c} :
  function.injective (Œª f : hom C‚ÇÅ C‚ÇÇ, f.f) :=
by tidy

instance : has_zero_morphisms (homological_complex V c) :=
{ has_zero := Œª C D, ‚ü®{ f := Œª i, 0 }‚ü© }

@[simp] lemma zero_apply (C D : homological_complex V c) (i : Œπ) :
  (0 : C ‚ü∂ D).f i = 0 := rfl

open_locale zero_object

instance [has_zero_object V] : has_zero_object (homological_complex V c) :=
{ zero :=
  { X := Œª i, 0,
    d := Œª i j, 0 },
  unique_from := Œª C, ‚ü®‚ü®0‚ü©, Œª f, by ext‚ü©,
  unique_to := Œª C, ‚ü®‚ü®0‚ü©, Œª f, by ext‚ü© }

instance [has_zero_object V] : inhabited (homological_complex V c) := ‚ü®0‚ü©

lemma congr_hom {C D : homological_complex V c} {f g : C ‚ü∂ D} (w : f = g) (i : Œπ) : f.f i = g.f i :=
congr_fun (congr_arg hom.f w) i

section
variables (V c)

/-- The functor picking out the `i`-th object of a complex. -/
@[simps] def eval (i : Œπ) : homological_complex V c ‚•§ V :=
{ obj := Œª C, C.X i,
  map := Œª C D f, f.f i, }

/-- The functor forgetting the differential in a complex, obtaining a graded object. -/
@[simps] def forget : homological_complex V c ‚•§ graded_object Œπ V :=
{ obj := Œª C, C.X,
  map := Œª _ _ f, f.f }

/-- Forgetting the differentials than picking out the `i`-th object is the same as
just picking out the `i`-th object. -/
@[simps] def forget_eval (i : Œπ) : forget V c ‚ãô graded_object.eval i ‚âÖ eval V c i :=
nat_iso.of_components (Œª X, iso.refl _) (by tidy)

end

open_locale classical
noncomputable theory

/--
If `C.d i j` and `C.d i j'` are both allowed, then we must have `j = j'`,
and so the differentials only differ by an `eq_to_hom`.
-/
@[simp] lemma d_comp_eq_to_hom {i j j' : Œπ} (rij : c.rel i j) (rij' : c.rel i j') :
  C.d i j' ‚â´ eq_to_hom (congr_arg C.X (c.next_eq rij' rij)) = C.d i j :=
begin
  have P : ‚àÄ h : j' = j, C.d i j' ‚â´ eq_to_hom (congr_arg C.X h) = C.d i j,
  { rintro rfl, simp },
  apply P,
end

/--
If `C.d i j` and `C.d i' j` are both allowed, then we must have `i = i'`,
and so the differentials only differ by an `eq_to_hom`.
-/
@[simp] lemma eq_to_hom_comp_d {i i' j : Œπ} (rij : c.rel i j) (rij' : c.rel i' j) :
  eq_to_hom (congr_arg C.X (c.prev_eq rij rij')) ‚â´ C.d i' j = C.d i j :=
begin
  have P : ‚àÄ h : i = i', eq_to_hom (congr_arg C.X h) ‚â´ C.d i' j = C.d i j,
  { rintro rfl, simp },
  apply P,
end

lemma kernel_eq_kernel [has_kernels V] {i j j' : Œπ} (r : c.rel i j) (r' : c.rel i j') :
  kernel_subobject (C.d i j) = kernel_subobject (C.d i j') :=
begin
  rw ‚Üêd_comp_eq_to_hom C r r',
  apply kernel_subobject_comp_mono,
end

lemma image_eq_image [has_images V] [has_equalizers V]
  {i i' j : Œπ} (r : c.rel i j) (r' : c.rel i' j) :
  image_subobject (C.d i j) = image_subobject (C.d i' j) :=
begin
  rw ‚Üêeq_to_hom_comp_d C r r',
  apply image_subobject_iso_comp,
end

section

variables [has_zero_object V]
open_locale zero_object

/-- Either `C.X i`, if there is some `i` with `c.rel i j`, or the zero object. -/
def X_prev (j : Œπ) : V :=
match c.prev j with
| none := 0
| (some ‚ü®i,_‚ü©) := C.X i
end

/-- If `c.rel i j`, then `C.X_prev j` is isomorphic to `C.X i`. -/
def X_prev_iso {i j : Œπ} (r : c.rel i j) :
  C.X_prev j ‚âÖ C.X i :=
eq_to_iso begin
  dsimp [X_prev],
  rw c.prev_eq_some r,
  refl,
end

/-- If there is no `i` so `c.rel i j`, then `C.X_prev j` is isomorphic to `0`. -/
def X_prev_iso_zero {j : Œπ} (h : c.prev j = none) :
  C.X_prev j ‚âÖ 0 :=
eq_to_iso begin
  dsimp [X_prev],
  rw h,
  refl,
end

/-- Either `C.X j`, if there is some `j` with `c.rel i j`, or the zero object. -/
def X_next (i : Œπ) : V :=
match c.next i with
| none := 0
| (some ‚ü®j,_‚ü©) := C.X j
end

/-- If `c.rel i j`, then `C.X_next i` is isomorphic to `C.X j`. -/
def X_next_iso {i j : Œπ} (r : c.rel i j) :
  C.X_next i ‚âÖ C.X j :=
eq_to_iso begin
  dsimp [X_next],
  rw c.next_eq_some r,
  refl,
end

/-- If there is no `j` so `c.rel i j`, then `C.X_next i` is isomorphic to `0`. -/
def X_next_iso_zero {i : Œπ} (h : c.next i = none) :
  C.X_next i ‚âÖ 0 :=
eq_to_iso begin
  dsimp [X_next],
  rw h,
  refl,
end

/--
The differential mapping into `C.X j`, or zero if there isn't one.
-/
def d_to (j : Œπ) : C.X_prev j ‚ü∂ C.X j :=
match c.prev j with
| none := (0 : C.X_prev j ‚ü∂ C.X j)
| (some ‚ü®i, w‚ü©) := (C.X_prev_iso w).hom ‚â´ C.d i j
end

/--
The differential mapping out of `C.X i`, or zero if there isn't one.
-/
def d_from (i : Œπ) : C.X i ‚ü∂ C.X_next i :=
match c.next i with
| none := (0 : C.X i ‚ü∂ C.X_next i)
| (some ‚ü®j, w‚ü©) := C.d i j ‚â´ (C.X_next_iso w).inv
end

lemma d_to_eq {i j : Œπ} (r : c.rel i j) :
  C.d_to j = (C.X_prev_iso r).hom ‚â´ C.d i j :=
begin
  dsimp [d_to, X_prev_iso],
  rw c.prev_eq_some r,
  refl,
end

@[simp]
lemma d_to_eq_zero {j : Œπ} (h : c.prev j = none) :
  C.d_to j = 0 :=
begin
  dsimp [d_to],
  rw h, refl,
end

lemma d_from_eq {i j : Œπ} (r : c.rel i j) :
  C.d_from i = C.d i j ‚â´ (C.X_next_iso r).inv :=
begin
  dsimp [d_from, X_next_iso],
  rw c.next_eq_some r,
  refl,
end

@[simp]
lemma d_from_eq_zero {i : Œπ} (h : c.next i = none) :
  C.d_from i = 0 :=
begin
  dsimp [d_from],
  rw h, refl,
end

@[simp, reassoc] lemma X_prev_iso_comp_d_to {i j : Œπ} (r : c.rel i j) :
  (C.X_prev_iso r).inv ‚â´ C.d_to j = C.d i j :=
by simp [C.d_to_eq r]

@[simp, reassoc] lemma X_prev_iso_zero_comp_d_to {j : Œπ} (h : c.prev j = none) :
  (C.X_prev_iso_zero h).inv ‚â´ C.d_to j = 0 :=
by simp [h]

@[simp, reassoc] lemma d_from_comp_X_next_iso {i j : Œπ} (r : c.rel i j) :
  C.d_from i ‚â´ (C.X_next_iso r).hom = C.d i j :=
by simp [C.d_from_eq r]

@[simp, reassoc] lemma d_from_comp_X_next_iso_zero {i : Œπ} (h : c.next i = none) :
  C.d_from i ‚â´ (C.X_next_iso_zero h).hom = 0 :=
by simp [h]

@[simp]
lemma d_to_comp_d_from (j : Œπ) : C.d_to j ‚â´ C.d_from j = 0 :=
begin
  rcases h‚ÇÅ : c.next j with _ | ‚ü®k,w‚ÇÅ‚ü©,
  { rw [d_from_eq_zero _ h‚ÇÅ], simp },
  { rw [d_from_eq _ w‚ÇÅ],
    rcases h‚ÇÇ : c.prev j with _ | ‚ü®i,w‚ÇÇ‚ü©,
    { rw [d_to_eq_zero _ h‚ÇÇ], simp },
    { rw [d_to_eq _ w‚ÇÇ], simp } }
end

lemma kernel_from_eq_kernel [has_kernels V] {i j : Œπ} (r : c.rel i j) :
  kernel_subobject (C.d_from i) = kernel_subobject (C.d i j) :=
begin
  rw C.d_from_eq r,
  apply kernel_subobject_comp_mono,
end

lemma image_to_eq_image [has_images V] [has_equalizers V]
  {i j : Œπ} (r : c.rel i j) :
  image_subobject (C.d_to j) = image_subobject (C.d i j) :=
begin
  rw C.d_to_eq r,
  apply image_subobject_iso_comp,
end

end

namespace hom

variables {C‚ÇÅ C‚ÇÇ C‚ÇÉ : homological_complex V c}

/-- The `i`-th component of an isomorphism of chain complexes. -/
@[simps]
def iso_app (f : C‚ÇÅ ‚âÖ C‚ÇÇ) (i : Œπ) : C‚ÇÅ.X i ‚âÖ C‚ÇÇ.X i :=
(eval V c i).map_iso f

/-- Construct an isomorphism of chain complexes from isomorphism of the objects
which commute with the differentials. -/
@[simps]
def iso_of_components (f : Œ† i, C‚ÇÅ.X i ‚âÖ C‚ÇÇ.X i)
  (hf : ‚àÄ i j, c.rel i j ‚Üí (f i).hom ‚â´ C‚ÇÇ.d i j = C‚ÇÅ.d i j ‚â´ (f j).hom) :
  C‚ÇÅ ‚âÖ C‚ÇÇ :=
{ hom := { f := Œª i, (f i).hom, comm' := hf },
  inv :=
  { f := Œª i, (f i).inv,
    comm' := Œª i j hij,
    calc (f i).inv ‚â´ C‚ÇÅ.d i j
        = (f i).inv ‚â´ (C‚ÇÅ.d i j ‚â´ (f j).hom) ‚â´ (f j).inv : by simp
    ... = (f i).inv ‚â´ ((f i).hom ‚â´ C‚ÇÇ.d i j) ‚â´ (f j).inv : by rw hf i j hij
    ... =  C‚ÇÇ.d i j ‚â´ (f j).inv : by simp },
  hom_inv_id' := by { ext i, exact (f i).hom_inv_id },
  inv_hom_id' := by { ext i, exact (f i).inv_hom_id } }

@[simp] lemma iso_of_components_app (f : Œ† i, C‚ÇÅ.X i ‚âÖ C‚ÇÇ.X i)
  (hf : ‚àÄ i j, c.rel i j ‚Üí (f i).hom ‚â´ C‚ÇÇ.d i j = C‚ÇÅ.d i j ‚â´ (f j).hom) (i : Œπ) :
  iso_app (iso_of_components f hf) i = f i :=
by { ext, simp, }

variables [has_zero_object V]
open_locale zero_object

/-! Lemmas relating chain maps and `d_to`/`d_from`. -/

/-- `f.prev j` is `f.f i` if there is some `r i j`, and zero otherwise. -/
def prev (f : hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : C‚ÇÅ.X_prev j ‚ü∂ C‚ÇÇ.X_prev j :=
match c.prev j with
| none := 0
| some ‚ü®i,w‚ü© := (C‚ÇÅ.X_prev_iso w).hom ‚â´ f.f i ‚â´ (C‚ÇÇ.X_prev_iso w).inv
end

lemma prev_eq (f : hom C‚ÇÅ C‚ÇÇ) {i j : Œπ} (w : c.rel i j) :
  f.prev j = (C‚ÇÅ.X_prev_iso w).hom ‚â´ f.f i ‚â´ (C‚ÇÇ.X_prev_iso w).inv :=
begin
  dsimp [prev],
  rw c.prev_eq_some w,
  refl,
end

/-- `f.next i` is `f.f j` if there is some `r i j`, and zero otherwise. -/
def next (f : hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : C‚ÇÅ.X_next i ‚ü∂ C‚ÇÇ.X_next i :=
match c.next i with
| none := 0
| some ‚ü®j,w‚ü© := (C‚ÇÅ.X_next_iso w).hom ‚â´ f.f j ‚â´ (C‚ÇÇ.X_next_iso w).inv
end

lemma next_eq (f : hom C‚ÇÅ C‚ÇÇ) {i j : Œπ} (w : c.rel i j) :
  f.next i = (C‚ÇÅ.X_next_iso w).hom ‚â´ f.f j ‚â´ (C‚ÇÇ.X_next_iso w).inv :=
begin
  dsimp [next],
  rw c.next_eq_some w,
  refl,
end

@[simp, reassoc]
lemma comm_from (f : hom C‚ÇÅ C‚ÇÇ) (i : Œπ) :
  f.f i ‚â´ C‚ÇÇ.d_from i = C‚ÇÅ.d_from i ‚â´ f.next i :=
begin
  rcases h : c.next i with _ | ‚ü®j,w‚ü©,
  { simp [h] },
  { simp [d_from_eq _ w, next_eq _ w] }
end

@[simp, reassoc]
lemma comm_to (f : hom C‚ÇÅ C‚ÇÇ) (j : Œπ) :
  f.prev j ‚â´ C‚ÇÇ.d_to j = C‚ÇÅ.d_to j ‚â´ f.f j :=
begin
  rcases h : c.prev j with _ | ‚ü®j,w‚ü©,
  { simp [h] },
  { simp [d_to_eq _ w, prev_eq _ w] }
end

/--
A morphism of chain complexes
induces a morphism of arrows of the differentials out of each object.
-/
def sq_from (f : hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : arrow.mk (C‚ÇÅ.d_from i) ‚ü∂ arrow.mk (C‚ÇÇ.d_from i) :=
arrow.hom_mk (f.comm_from i)

@[simp] lemma sq_from_left (f : hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : (f.sq_from i).left = f.f i := rfl
@[simp] lemma sq_from_right (f : hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : (f.sq_from i).right = f.next i := rfl

@[simp] lemma sq_from_id (C‚ÇÅ : homological_complex V c) (i : Œπ) : sq_from (ùüô C‚ÇÅ) i = ùüô _ :=
begin
  rcases h : c.next i with _ | ‚ü®j,w‚ü©,
  { ext,
    { refl },
    { dsimp, simp only [next, h],
      symmetry,
      apply zero_of_target_iso_zero,
      exact X_next_iso_zero _ h } },
  { ext, refl, dsimp, simp [next, h] }
end

@[simp] lemma sq_from_comp (f : C‚ÇÅ ‚ü∂ C‚ÇÇ) (g : C‚ÇÇ ‚ü∂ C‚ÇÉ) (i : Œπ) :
  sq_from (f ‚â´ g) i = sq_from f i ‚â´ sq_from g i :=
begin
  rcases h : c.next i with _ | ‚ü®j,w‚ü©,
  { ext,
    { refl },
    { dsimp, simp only [next, h],
      symmetry,
      apply zero_of_target_iso_zero,
      exact X_next_iso_zero _ h } },
  { ext, refl, dsimp, simp [next, h] }
end

/--
A morphism of chain complexes
induces a morphism of arrows of the differentials into each object.
-/
def sq_to (f : hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : arrow.mk (C‚ÇÅ.d_to j) ‚ü∂ arrow.mk (C‚ÇÇ.d_to j) :=
arrow.hom_mk (f.comm_to j)

@[simp] lemma sq_to_left (f : hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : (f.sq_to j).left = f.prev j := rfl
@[simp] lemma sq_to_right (f : hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : (f.sq_to j).right = f.f j := rfl

end hom

end homological_complex

namespace chain_complex

section of
variables {V} {Œ± : Type*} [add_right_cancel_semigroup Œ±] [has_one Œ±] [decidable_eq Œ±]

/--
Construct an `Œ±`-indexed chain complex from a dependently-typed differential.
-/
def of (X : Œ± ‚Üí V) (d : Œ† n, X (n+1) ‚ü∂ X n) (sq : ‚àÄ n, d (n+1) ‚â´ d n = 0) : chain_complex V Œ± :=
{ X := X,
  d := Œª i j, if h : i = j + 1 then
    eq_to_hom (by subst h) ‚â´ d j
  else
    0,
  shape' := Œª i j w, by rw dif_neg (ne.symm w),
  d_comp_d' := Œª i j k hij hjk,
  begin
    dsimp at hij hjk, substs hij hjk,
    simp only [category.id_comp, dif_pos rfl, eq_to_hom_refl],
    exact sq k,
  end }

variables (X : Œ± ‚Üí V) (d : Œ† n, X (n+1) ‚ü∂ X n) (sq : ‚àÄ n, d (n+1) ‚â´ d n = 0)

@[simp] lemma of_X (n : Œ±) : (of X d sq).X n = X n := rfl
@[simp] lemma of_d (j : Œ±) : (of X d sq).d (j+1) j = d j :=
by { dsimp [of], rw [if_pos rfl, category.id_comp] }
lemma of_d_ne {i j : Œ±} (h : i ‚â† j + 1) : (of X d sq).d i j = 0 :=
by { dsimp [of], rw [dif_neg h], }

end of

section of_hom

variables {V} {Œ± : Type*} [add_right_cancel_semigroup Œ±] [has_one Œ±] [decidable_eq Œ±]

variables (X : Œ± ‚Üí V) (d_X : Œ† n, X (n+1) ‚ü∂ X n) (sq_X : ‚àÄ n, d_X (n+1) ‚â´ d_X n = 0)
  (Y : Œ± ‚Üí V) (d_Y : Œ† n, Y (n+1) ‚ü∂ Y n) (sq_Y : ‚àÄ n, d_Y (n+1) ‚â´ d_Y n = 0)

/--
A constructor for chain maps between `Œ±`-indexed chain complexes built using `chain_complex.of`,
from a dependently typed collection of morphisms.
-/
@[simps] def of_hom (f : Œ† i : Œ±, X i ‚ü∂ Y i) (comm : ‚àÄ i : Œ±, f (i+1) ‚â´ d_Y i = d_X i ‚â´ f i) :
  of X d_X sq_X ‚ü∂ of Y d_Y sq_Y :=
{ f := f,
  comm' := Œª n m,
  begin
    by_cases h : n = m + 1,
    { subst h,
      simpa using comm m, },
    { rw [of_d_ne X _ _ h, of_d_ne Y _ _ h], simp }
  end }

end of_hom

section mk

/--
Auxiliary structure for setting up the recursion in `mk`.
This is purely an implementation detail: for some reason just using the dependent 6-tuple directly
results in `mk_aux` taking much longer (well over the `-T100000` limit) to elaborate.
-/
@[nolint has_inhabited_instance]
structure mk_struct :=
(X‚ÇÄ X‚ÇÅ X‚ÇÇ : V)
(d‚ÇÄ : X‚ÇÅ ‚ü∂ X‚ÇÄ)
(d‚ÇÅ : X‚ÇÇ ‚ü∂ X‚ÇÅ)
(s : d‚ÇÅ ‚â´ d‚ÇÄ = 0)

variables {V}

/-- Flatten to a tuple. -/
def mk_struct.flat (t : mk_struct V) :
  Œ£' (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÅ ‚ü∂ X‚ÇÄ) (d‚ÇÅ : X‚ÇÇ ‚ü∂ X‚ÇÅ), d‚ÇÅ ‚â´ d‚ÇÄ = 0 :=
‚ü®t.X‚ÇÄ, t.X‚ÇÅ, t.X‚ÇÇ, t.d‚ÇÄ, t.d‚ÇÅ, t.s‚ü©

variables (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÅ ‚ü∂ X‚ÇÄ) (d‚ÇÅ : X‚ÇÇ ‚ü∂ X‚ÇÅ) (s : d‚ÇÅ ‚â´ d‚ÇÄ = 0)
  (succ : Œ† (t : Œ£' (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÅ ‚ü∂ X‚ÇÄ) (d‚ÇÅ : X‚ÇÇ ‚ü∂ X‚ÇÅ), d‚ÇÅ ‚â´ d‚ÇÄ = 0),
    Œ£' (X‚ÇÉ : V) (d‚ÇÇ : X‚ÇÉ ‚ü∂ t.2.2.1), d‚ÇÇ ‚â´ t.2.2.2.2.1 = 0)

/-- Auxiliary definition for `mk`. -/
def mk_aux :
  Œ† n : ‚Ñï, mk_struct V
| 0 := ‚ü®X‚ÇÄ, X‚ÇÅ, X‚ÇÇ, d‚ÇÄ, d‚ÇÅ, s‚ü©
| (n+1) :=
  let p := mk_aux n in
  ‚ü®p.X‚ÇÅ, p.X‚ÇÇ, (succ p.flat).1, p.d‚ÇÅ, (succ p.flat).2.1, (succ p.flat).2.2‚ü©

/--
A inductive constructor for `‚Ñï`-indexed chain complexes.

You provide explicitly the first two differentials,
then a function which takes two differentials and the fact they compose to zero,
and returns the next object, its differential, and the fact it composes appropiately to zero.

See also `mk'`, which only sees the previous differential in the inductive step.
-/
def mk : chain_complex V ‚Ñï :=
of (Œª n, (mk_aux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).X‚ÇÄ) (Œª n, (mk_aux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).d‚ÇÄ)
  (Œª n, (mk_aux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).s)

@[simp] lemma mk_X_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 0 = X‚ÇÄ := rfl
@[simp] lemma mk_X_1 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 1 = X‚ÇÅ := rfl
@[simp] lemma mk_X_2 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 2 = X‚ÇÇ := rfl
@[simp] lemma mk_d_1_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 1 0 = d‚ÇÄ :=
by { change ite (1 = 0 + 1) (ùüô X‚ÇÅ ‚â´ d‚ÇÄ) 0 = d‚ÇÄ, rw [if_pos rfl, category.id_comp] }
@[simp] lemma mk_d_2_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 2 1 = d‚ÇÅ :=
by { change ite (2 = 1 + 1) (ùüô X‚ÇÇ ‚â´ d‚ÇÅ) 0 = d‚ÇÅ, rw [if_pos rfl, category.id_comp] }
-- TODO simp lemmas for the inductive steps? It's not entirely clear that they are needed.

/--
A simpler inductive constructor for `‚Ñï`-indexed chain complexes.

You provide explicitly the first differential,
then a function which takes a differential,
and returns the next object, its differential, and the fact it composes appropriately to zero.
-/
def mk' (X‚ÇÄ X‚ÇÅ : V) (d : X‚ÇÅ ‚ü∂ X‚ÇÄ)
  (succ' : Œ† (t : Œ£ (X‚ÇÄ X‚ÇÅ : V), X‚ÇÅ ‚ü∂ X‚ÇÄ), Œ£' (X‚ÇÇ : V) (d : X‚ÇÇ ‚ü∂ t.2.1), d ‚â´ t.2.2 = 0) :
  chain_complex V ‚Ñï :=
mk X‚ÇÄ X‚ÇÅ (succ' ‚ü®X‚ÇÄ, X‚ÇÅ, d‚ü©).1 d (succ' ‚ü®X‚ÇÄ, X‚ÇÅ, d‚ü©).2.1 (succ' ‚ü®X‚ÇÄ, X‚ÇÅ, d‚ü©).2.2
  (Œª t, succ' ‚ü®t.2.1, t.2.2.1, t.2.2.2.2.1‚ü©)

variables (succ' : Œ† (t : Œ£ (X‚ÇÄ X‚ÇÅ : V), X‚ÇÅ ‚ü∂ X‚ÇÄ), Œ£' (X‚ÇÇ : V) (d : X‚ÇÇ ‚ü∂ t.2.1), d ‚â´ t.2.2 = 0)

@[simp] lemma mk'_X_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 0 = X‚ÇÄ := rfl
@[simp] lemma mk'_X_1 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 1 = X‚ÇÅ := rfl
@[simp] lemma mk'_d_1_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').d 1 0 = d‚ÇÄ :=
by { change ite (1 = 0 + 1) (ùüô X‚ÇÅ ‚â´ d‚ÇÄ) 0 = d‚ÇÄ, rw [if_pos rfl, category.id_comp] }
-- TODO simp lemmas for the inductive steps? It's not entirely clear that they are needed.

end mk

section mk_hom

variables {V} (P Q : chain_complex V ‚Ñï)
  (zero : P.X 0 ‚ü∂ Q.X 0)
  (one : P.X 1 ‚ü∂ Q.X 1)
  (one_zero_comm : one ‚â´ Q.d 1 0 = P.d 1 0 ‚â´ zero)
  (succ : ‚àÄ (n : ‚Ñï)
    (p : Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n+1) ‚ü∂ Q.X (n+1)), f' ‚â´ Q.d (n+1) n = P.d (n+1) n ‚â´ f),
    Œ£' f'' : P.X (n+2) ‚ü∂ Q.X (n+2), f'' ‚â´ Q.d (n+2) (n+1) = P.d (n+2) (n+1) ‚â´ p.2.1)

/--
An auxiliary construction for `mk_hom`.

Here we build by induction a family of commutative squares,
but don't require at the type level that these successive commutative squares actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a chain map)
in `mk_hom`.
-/
def mk_hom_aux :
  Œ† n, Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n+1) ‚ü∂ Q.X (n+1)), f' ‚â´ Q.d (n+1) n = P.d (n+1) n ‚â´ f
| 0 := ‚ü®zero, one, one_zero_comm‚ü©
| (n+1) := ‚ü®(mk_hom_aux n).2.1,
    (succ n (mk_hom_aux n)).1, (succ n (mk_hom_aux n)).2‚ü©

/--
A constructor for chain maps between `‚Ñï`-indexed chain complexes,
working by induction on commutative squares.

You need to provide the components of the chain map in degrees 0 and 1,
show that these form a commutative square,
and then give a construction of each component,
and the fact that it forms a commutative square with the previous component,
using as an inductive hypothesis the data (and commutativity) of the previous two components.
-/
def mk_hom : P ‚ü∂ Q :=
{ f := Œª n, (mk_hom_aux P Q zero one one_zero_comm succ n).1,
  comm' := Œª n m,
  begin
    rintro (rfl : m + 1 = n),
    exact (mk_hom_aux P Q zero one one_zero_comm succ m).2.2,
  end }

@[simp] lemma mk_hom_f_0 : (mk_hom P Q zero one one_zero_comm succ).f 0 = zero := rfl
@[simp] lemma mk_hom_f_1 : (mk_hom P Q zero one one_zero_comm succ).f 1 = one := rfl
@[simp] lemma mk_hom_f_succ_succ (n : ‚Ñï) :
  (mk_hom P Q zero one one_zero_comm succ).f (n+2) =
    (succ n ‚ü®(mk_hom P Q zero one one_zero_comm succ).f n,
      (mk_hom P Q zero one one_zero_comm succ).f (n+1),
        (mk_hom P Q zero one one_zero_comm succ).comm (n+1) n‚ü©).1 :=
begin
  dsimp [mk_hom, mk_hom_aux],
  induction n; congr,
end

end mk_hom

end chain_complex

namespace cochain_complex

section of
variables {V} {Œ± : Type*} [add_right_cancel_semigroup Œ±] [has_one Œ±] [decidable_eq Œ±]

/--
Construct an `Œ±`-indexed cochain complex from a dependently-typed differential.
-/
def of (X : Œ± ‚Üí V) (d : Œ† n, X n ‚ü∂ X (n+1)) (sq : ‚àÄ n, d n ‚â´ d (n+1) = 0) : cochain_complex V Œ± :=
{ X := X,
  d := Œª i j, if h : i + 1 = j then
    d _ ‚â´ eq_to_hom (by subst h)
  else
    0,
  shape' := Œª i j w, by {rw dif_neg, exact w},
  d_comp_d' := Œª i j k,
  begin
    split_ifs with h h' h',
    { substs h h',
      simp [sq] },
    all_goals { simp },
  end }

variables (X : Œ± ‚Üí V) (d : Œ† n, X n ‚ü∂ X (n+1)) (sq : ‚àÄ n, d n ‚â´ d (n+1) = 0)

@[simp] lemma of_X (n : Œ±) : (of X d sq).X n = X n := rfl
@[simp] lemma of_d (j : Œ±) : (of X d sq).d j (j+1) = d j :=
by { dsimp [of], rw [if_pos rfl, category.comp_id] }
lemma of_d_ne {i j : Œ±} (h : i + 1 ‚â† j) : (of X d sq).d i j = 0 :=
by { dsimp [of], rw [dif_neg h] }

end of

section of_hom

variables {V} {Œ± : Type*} [add_right_cancel_semigroup Œ±] [has_one Œ±] [decidable_eq Œ±]

variables (X : Œ± ‚Üí V) (d_X : Œ† n, X n ‚ü∂ X (n+1)) (sq_X : ‚àÄ n, d_X n ‚â´ d_X (n+1) = 0)
  (Y : Œ± ‚Üí V) (d_Y : Œ† n, Y n ‚ü∂ Y (n+1)) (sq_Y : ‚àÄ n, d_Y n ‚â´ d_Y (n+1) = 0)

/--
A constructor for chain maps between `Œ±`-indexed cochain complexes built using `cochain_complex.of`,
from a dependently typed collection of morphisms.
-/
@[simps] def of_hom (f : Œ† i : Œ±, X i ‚ü∂ Y i) (comm : ‚àÄ i : Œ±, f i ‚â´ d_Y i = d_X i ‚â´ f (i+1)) :
  of X d_X sq_X ‚ü∂ of Y d_Y sq_Y :=
{ f := f,
  comm' := Œª n m,
  begin
    by_cases h : n + 1 = m,
    { subst h,
      simpa using comm n },
    { rw [of_d_ne X _ _ h, of_d_ne Y _ _ h], simp }
  end }

end of_hom

section mk

/--
Auxiliary structure for setting up the recursion in `mk`.
This is purely an implementation detail: for some reason just using the dependent 6-tuple directly
results in `mk_aux` taking much longer (well over the `-T100000` limit) to elaborate.
-/
@[nolint has_inhabited_instance]
structure mk_struct :=
(X‚ÇÄ X‚ÇÅ X‚ÇÇ : V)
(d‚ÇÄ : X‚ÇÄ ‚ü∂ X‚ÇÅ)
(d‚ÇÅ : X‚ÇÅ ‚ü∂ X‚ÇÇ)
(s : d‚ÇÄ ‚â´ d‚ÇÅ = 0)

variables {V}

/-- Flatten to a tuple. -/
def mk_struct.flat (t : mk_struct V) :
  Œ£' (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÄ ‚ü∂ X‚ÇÅ) (d‚ÇÅ : X‚ÇÅ ‚ü∂ X‚ÇÇ), d‚ÇÄ ‚â´ d‚ÇÅ = 0 :=
‚ü®t.X‚ÇÄ, t.X‚ÇÅ, t.X‚ÇÇ, t.d‚ÇÄ, t.d‚ÇÅ, t.s‚ü©

variables (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÄ ‚ü∂ X‚ÇÅ) (d‚ÇÅ : X‚ÇÅ ‚ü∂ X‚ÇÇ) (s : d‚ÇÄ ‚â´ d‚ÇÅ = 0)
  (succ : Œ† (t : Œ£' (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÄ ‚ü∂ X‚ÇÅ) (d‚ÇÅ : X‚ÇÅ ‚ü∂ X‚ÇÇ), d‚ÇÄ ‚â´ d‚ÇÅ = 0),
    Œ£' (X‚ÇÉ : V) (d‚ÇÇ : t.2.2.1 ‚ü∂ X‚ÇÉ), t.2.2.2.2.1 ‚â´ d‚ÇÇ = 0)

/-- Auxiliary definition for `mk`. -/
def mk_aux :
  Œ† n : ‚Ñï, mk_struct V
| 0 := ‚ü®X‚ÇÄ, X‚ÇÅ, X‚ÇÇ, d‚ÇÄ, d‚ÇÅ, s‚ü©
| (n+1) :=
  let p := mk_aux n in
  ‚ü®p.X‚ÇÅ, p.X‚ÇÇ, (succ p.flat).1, p.d‚ÇÅ, (succ p.flat).2.1, (succ p.flat).2.2‚ü©

/--
A inductive constructor for `‚Ñï`-indexed cochain complexes.

You provide explicitly the first two differentials,
then a function which takes two differentials and the fact they compose to zero,
and returns the next object, its differential, and the fact it composes appropiately to zero.

See also `mk'`, which only sees the previous differential in the inductive step.
-/
def mk : cochain_complex V ‚Ñï :=
of (Œª n, (mk_aux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).X‚ÇÄ) (Œª n, (mk_aux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).d‚ÇÄ)
  (Œª n, (mk_aux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).s)

@[simp] lemma mk_X_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 0 = X‚ÇÄ := rfl
@[simp] lemma mk_X_1 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 1 = X‚ÇÅ := rfl
@[simp] lemma mk_X_2 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 2 = X‚ÇÇ := rfl
@[simp] lemma mk_d_1_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 0 1 = d‚ÇÄ :=
by { change ite (1 = 0 + 1) (d‚ÇÄ ‚â´ ùüô X‚ÇÅ) 0 = d‚ÇÄ, rw [if_pos rfl, category.comp_id] }
@[simp] lemma mk_d_2_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 1 2 = d‚ÇÅ :=
by { change ite (2 = 1 + 1) (d‚ÇÅ ‚â´ ùüô X‚ÇÇ) 0 = d‚ÇÅ, rw [if_pos rfl, category.comp_id] }
-- TODO simp lemmas for the inductive steps? It's not entirely clear that they are needed.

/--
A simpler inductive constructor for `‚Ñï`-indexed cochain complexes.

You provide explicitly the first differential,
then a function which takes a differential,
and returns the next object, its differential, and the fact it composes appropriately to zero.
-/
def mk' (X‚ÇÄ X‚ÇÅ : V) (d : X‚ÇÄ ‚ü∂ X‚ÇÅ)
  (succ' : Œ† (t : Œ£ (X‚ÇÄ X‚ÇÅ : V), X‚ÇÄ ‚ü∂ X‚ÇÅ), Œ£' (X‚ÇÇ : V) (d : t.2.1 ‚ü∂ X‚ÇÇ), t.2.2 ‚â´ d = 0) :
  cochain_complex V ‚Ñï :=
mk X‚ÇÄ X‚ÇÅ (succ' ‚ü®X‚ÇÄ, X‚ÇÅ, d‚ü©).1 d (succ' ‚ü®X‚ÇÄ, X‚ÇÅ, d‚ü©).2.1 (succ' ‚ü®X‚ÇÄ, X‚ÇÅ, d‚ü©).2.2
  (Œª t, succ' ‚ü®t.2.1, t.2.2.1, t.2.2.2.2.1‚ü©)

variables (succ' : Œ† (t : Œ£ (X‚ÇÄ X‚ÇÅ : V), X‚ÇÄ ‚ü∂ X‚ÇÅ), Œ£' (X‚ÇÇ : V) (d : t.2.1 ‚ü∂ X‚ÇÇ), t.2.2 ‚â´ d = 0)

@[simp] lemma mk'_X_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 0 = X‚ÇÄ := rfl
@[simp] lemma mk'_X_1 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 1 = X‚ÇÅ := rfl
@[simp] lemma mk'_d_1_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').d 0 1 = d‚ÇÄ :=
by { change ite (1 = 0 + 1) (d‚ÇÄ ‚â´ ùüô X‚ÇÅ) 0 = d‚ÇÄ, rw [if_pos rfl, category.comp_id] }
-- TODO simp lemmas for the inductive steps? It's not entirely clear that they are needed.

end mk

section mk_hom

variables {V} (P Q : cochain_complex V ‚Ñï)
  (zero : P.X 0 ‚ü∂ Q.X 0)
  (one : P.X 1 ‚ü∂ Q.X 1)
  (one_zero_comm : zero ‚â´ Q.d 0 1 = P.d 0 1 ‚â´ one)
  (succ : ‚àÄ (n : ‚Ñï)
    (p : Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n+1) ‚ü∂ Q.X (n+1)), f ‚â´ Q.d n (n+1) = P.d n (n+1) ‚â´ f'),
    Œ£' f'' : P.X (n+2) ‚ü∂ Q.X (n+2), p.2.1 ‚â´ Q.d (n+1) (n+2) = P.d (n+1) (n+2) ‚â´ f'')

/--
An auxiliary construction for `mk_hom`.

Here we build by induction a family of commutative squares,
but don't require at the type level that these successive commutative squares actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a chain map)
in `mk_hom`.
-/
def mk_hom_aux :
  Œ† n, Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n+1) ‚ü∂ Q.X (n+1)), f ‚â´ Q.d n (n+1) = P.d n (n+1) ‚â´ f'
| 0 := ‚ü®zero, one, one_zero_comm‚ü©
| (n+1) := ‚ü®(mk_hom_aux n).2.1,
    (succ n (mk_hom_aux n)).1, (succ n (mk_hom_aux n)).2‚ü©

/--
A constructor for chain maps between `‚Ñï`-indexed cochain complexes,
working by induction on commutative squares.

You need to provide the components of the chain map in degrees 0 and 1,
show that these form a commutative square,
and then give a construction of each component,
and the fact that it forms a commutative square with the previous component,
using as an inductive hypothesis the data (and commutativity) of the previous two components.
-/
def mk_hom : P ‚ü∂ Q :=
{ f := Œª n, (mk_hom_aux P Q zero one one_zero_comm succ n).1,
  comm' := Œª n m,
  begin
    rintro (rfl : n + 1 = m),
    exact (mk_hom_aux P Q zero one one_zero_comm succ n).2.2,
  end }

@[simp] lemma mk_hom_f_0 : (mk_hom P Q zero one one_zero_comm succ).f 0 = zero := rfl
@[simp] lemma mk_hom_f_1 : (mk_hom P Q zero one one_zero_comm succ).f 1 = one := rfl
@[simp] lemma mk_hom_f_succ_succ (n : ‚Ñï) :
  (mk_hom P Q zero one one_zero_comm succ).f (n+2) =
    (succ n ‚ü®(mk_hom P Q zero one one_zero_comm succ).f n,
      (mk_hom P Q zero one one_zero_comm succ).f (n+1),
        (mk_hom P Q zero one one_zero_comm succ).comm n (n+1)‚ü©).1 :=
begin
  dsimp [mk_hom, mk_hom_aux],
  induction n; congr,
end

end mk_hom

end cochain_complex
