/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.Gamma_Spec_adjunction
import algebraic_geometry.open_immersion
import category_theory.limits.opposites

/-!
# Affine schemes

We define the category of `AffineScheme`s as the essential image of `Spec`.
We also define predicates about affine schemes and affine open sets.

## Main definitions

* `algebraic_geometry.AffineScheme`: The category of affine schemes.
* `algebraic_geometry.is_affine`: A scheme is affine if the canonical map `X ‚ü∂ Spec Œì(X)` is an
  isomorphism.
* `algebraic_geometry.Scheme.iso_Spec`: The canonical isomorphism `X ‚âÖ Spec Œì(X)` for an affine
  scheme.
* `algebraic_geometry.AffineScheme.equiv_CommRing`: The equivalence of categories
  `AffineScheme ‚âå CommRing·µí·µñ` given by `AffineScheme.Spec : CommRing·µí·µñ ‚•§ AffineScheme` and
  `AffineScheme.Œì : AffineScheme·µí·µñ ‚•§ CommRing`.
* `algebraic_geometry.is_affine_open`: An open subset of a scheme is affine if the open subscheme is
  affine.
* `algebraic_geometry.is_affine_open.from_Spec`: The immersion `Spec ùí™‚Çì(U) ‚ü∂ X` for an affine `U`.

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

namespace algebraic_geometry

/-- The category of affine schemes -/
def AffineScheme := Scheme.Spec.ess_image

/-- A Scheme is affine if the canonical map `X ‚ü∂ Spec Œì(X)` is an isomorphism. -/
class is_affine (X : Scheme) : Prop :=
(affine : is_iso (Œì_Spec.adjunction.unit.app X))

attribute [instance] is_affine.affine

/-- The canonical isomorphism `X ‚âÖ Spec Œì(X)` for an affine scheme. -/
def Scheme.iso_Spec (X : Scheme) [is_affine X] :
  X ‚âÖ Scheme.Spec.obj (op $ Scheme.Œì.obj $ op X) :=
as_iso (Œì_Spec.adjunction.unit.app X)

lemma mem_AffineScheme (X : Scheme) : X ‚àà AffineScheme ‚Üî is_affine X :=
‚ü®Œª h, ‚ü®functor.ess_image.unit_is_iso h‚ü©, Œª h, @@mem_ess_image_of_unit_is_iso _ _ _ X h.1‚ü©

instance is_affine_AffineScheme (X : AffineScheme.{u}) : is_affine (X : Scheme.{u}) :=
(mem_AffineScheme _).mp X.prop

instance Spec_is_affine (R : CommRing·µí·µñ) : is_affine (Scheme.Spec.obj R) :=
(mem_AffineScheme _).mp (Scheme.Spec.obj_mem_ess_image R)

lemma is_affine_of_iso {X Y : Scheme} (f : X ‚ü∂ Y) [is_iso f] [h : is_affine Y] :
  is_affine X :=
by { rw [‚Üê mem_AffineScheme] at h ‚ä¢, exact functor.ess_image.of_iso (as_iso f).symm h }

namespace AffineScheme

/-- The `Spec` functor into the category of affine schemes. -/
@[derive [full, faithful, ess_surj], simps]
def Spec : CommRing·µí·µñ ‚•§ AffineScheme := Scheme.Spec.to_ess_image

/-- The forgetful functor `AffineScheme ‚•§ Scheme`. -/
@[derive [full, faithful], simps]
def forget_to_Scheme : AffineScheme ‚•§ Scheme := Scheme.Spec.ess_image_inclusion

/-- The global section functor of an affine scheme. -/
def Œì : AffineScheme·µí·µñ ‚•§ CommRing := forget_to_Scheme.op ‚ãô Scheme.Œì

/-- The category of affine schemes is equivalent to the category of commutative rings. -/
def equiv_CommRing : AffineScheme ‚âå CommRing·µí·µñ :=
equiv_ess_image_of_reflective.symm

instance Œì_is_equiv : is_equivalence Œì.{u} :=
begin
  haveI : is_equivalence Œì.{u}.right_op.op := is_equivalence.of_equivalence equiv_CommRing.op,
  exact (functor.is_equivalence_trans Œì.{u}.right_op.op (op_op_equivalence _).functor : _),
end

instance : has_colimits AffineScheme.{u} :=
begin
  haveI := adjunction.has_limits_of_equivalence.{u} Œì.{u},
  haveI : has_colimits AffineScheme.{u} ·µí·µñ·µí·µñ := has_colimits_op_of_has_limits,
  exactI adjunction.has_colimits_of_equivalence.{u} (op_op_equivalence AffineScheme.{u}).inverse
end

instance : has_limits AffineScheme.{u} :=
begin
  haveI := adjunction.has_colimits_of_equivalence Œì.{u},
  haveI : has_limits AffineScheme.{u} ·µí·µñ·µí·µñ := limits.has_limits_op_of_has_colimits,
  exactI adjunction.has_limits_of_equivalence (op_op_equivalence AffineScheme.{u}).inverse
end

end AffineScheme

/-- An open subset of a scheme is affine if the open subscheme is affine. -/
def is_affine_open {X : Scheme} (U : opens X.carrier) : Prop :=
is_affine (X.restrict U.open_embedding)

lemma range_is_affine_open_of_open_immersion {X Y : Scheme} [is_affine X] (f : X ‚ü∂ Y)
  [H : is_open_immersion f] : is_affine_open ‚ü®set.range f.1.base, H.base_open.open_range‚ü© :=
begin
  refine is_affine_of_iso (is_open_immersion.iso_of_range_eq f (Y.of_restrict _) _).inv,
  exact subtype.range_coe.symm,
  apply_instance
end

lemma top_is_affine_open (X : Scheme) [is_affine X] : is_affine_open (‚ä§ : opens X.carrier) :=
begin
  convert range_is_affine_open_of_open_immersion (ùüô X),
  ext1,
  exact set.range_id.symm
end

instance Scheme.affine_basis_cover_is_affine (X : Scheme) (i : X.affine_basis_cover.J) :
  is_affine (X.affine_basis_cover.obj i) :=
algebraic_geometry.Spec_is_affine _

lemma is_basis_affine_open (X : Scheme) :
  opens.is_basis { U : opens X.carrier | is_affine_open U } :=
begin
  rw opens.is_basis_iff_nbhd,
  rintros U x (hU : x ‚àà (U : set X.carrier)),
  obtain ‚ü®S, hS, hxS, hSU‚ü© := X.affine_basis_cover_is_basis.exists_subset_of_mem_open hU U.prop,
  refine ‚ü®‚ü®S, X.affine_basis_cover_is_basis.is_open hS‚ü©, _, hxS, hSU‚ü©,
  rcases hS with ‚ü®i, rfl‚ü©,
  exact range_is_affine_open_of_open_immersion _,
end

/-- The open immersion `Spec ùí™‚Çì(U) ‚ü∂ X` for an affine `U`. -/
def is_affine_open.from_Spec {X : Scheme} {U : opens X.carrier} (hU : is_affine_open U) :
  Scheme.Spec.obj (op $ X.presheaf.obj $ op U) ‚ü∂ X :=
begin
  haveI : is_affine (X.restrict U.open_embedding) := hU,
  have : U.open_embedding.is_open_map.functor.obj ‚ä§ = U,
  { ext1, exact set.image_univ.trans subtype.range_coe },
  exact Scheme.Spec.map (X.presheaf.map (eq_to_hom this.symm).op).op ‚â´
    (X.restrict U.open_embedding).iso_Spec.inv ‚â´ X.of_restrict _
end

instance is_affine_open.is_open_immersion_from_Spec {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) :
  is_open_immersion hU.from_Spec :=
by { delta is_affine_open.from_Spec, apply_instance }

lemma is_affine_open.from_Spec_range {X : Scheme} {U : opens X.carrier} (hU : is_affine_open U) :
  set.range hU.from_Spec.1.base = (U : set X.carrier) :=
begin
  delta is_affine_open.from_Spec,
  erw [‚Üê category.assoc, Scheme.comp_val_base],
  rw [coe_comp, set.range_comp, set.range_iff_surjective.mpr, set.image_univ],
  exact subtype.range_coe,
  rw ‚Üê Top.epi_iff_surjective,
  apply_instance
end

lemma is_affine_open.from_Spec_image_top {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) :
  hU.is_open_immersion_from_Spec.base_open.is_open_map.functor.obj ‚ä§ = U :=
by { ext1, exact set.image_univ.trans hU.from_Spec_range }

lemma is_affine_open.is_compact {X : Scheme} {U : opens X.carrier} (hU : is_affine_open U) :
  is_compact (U : set X.carrier) :=
begin
  convert @is_compact.image _ _ _ _ set.univ hU.from_Spec.1.base
    prime_spectrum.compact_space.1 (by continuity),
  convert hU.from_Spec_range.symm,
  exact set.image_univ
end

instance Scheme.quasi_compact_of_affine (X : Scheme) [is_affine X] : compact_space X.carrier :=
‚ü®(top_is_affine_open X).is_compact‚ü©

lemma is_affine_open.from_Spec_base_preimage
  {X : Scheme} {U : opens X.carrier} (hU : is_affine_open U) :
    (opens.map hU.from_Spec.val.base).obj U = ‚ä§ :=
begin
  ext1,
  change hU.from_Spec.1.base ‚Åª¬π' (U : set X.carrier) = set.univ,
  rw [‚Üê hU.from_Spec_range, ‚Üê set.image_univ],
  exact set.preimage_image_eq _ PresheafedSpace.is_open_immersion.base_open.inj
end

lemma Scheme.Spec_map_presheaf_map_eq_to_hom {X : Scheme} {U V : opens X.carrier} (h : U = V) (W) :
  (Scheme.Spec.map (X.presheaf.map (eq_to_hom h).op).op).val.c.app W =
    eq_to_hom (by { cases h, dsimp, induction W using opposite.rec, congr, ext1, simpa }) :=
begin
  have : Scheme.Spec.map (X.presheaf.map (ùüô (op U))).op = ùüô _,
  { rw [X.presheaf.map_id, op_id, Scheme.Spec.map_id]  },
  cases h,
  refine (Scheme.congr_app this _).trans _,
  erw category.id_comp,
  simpa
end

lemma is_affine_open.Spec_Œì_identity_hom_app_from_Spec {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) :
  (Spec_Œì_identity.hom.app (X.presheaf.obj $ op U)) ‚â´ hU.from_Spec.1.c.app (op U) =
    (Scheme.Spec.obj _).presheaf.map (eq_to_hom hU.from_Spec_base_preimage).op :=
begin
  haveI : is_affine _ := hU,
  have e‚ÇÅ :=
    Spec_Œì_identity.hom.naturality (X.presheaf.map (eq_to_hom U.open_embedding_obj_top).op),
  rw ‚Üê is_iso.comp_inv_eq at e‚ÇÅ,
  have e‚ÇÇ := Œì_Spec.adjunction_unit_app_app_top (X.restrict U.open_embedding),
  erw ‚Üê e‚ÇÇ at e‚ÇÅ,
  simp only [functor.id_map, quiver.hom.unop_op, functor.comp_map, ‚Üê functor.map_inv, ‚Üê op_inv,
    LocallyRingedSpace.Œì_map, category.assoc, functor.right_op_map, inv_eq_to_hom] at e‚ÇÅ,
  delta is_affine_open.from_Spec Scheme.iso_Spec,
  rw [Scheme.comp_val_c_app, Scheme.comp_val_c_app, ‚Üê e‚ÇÅ],
  simp_rw category.assoc,
  erw ‚Üê X.presheaf.map_comp_assoc,
  rw ‚Üê op_comp,
  have e‚ÇÉ : U.open_embedding.is_open_map.adjunction.counit.app U ‚â´
    eq_to_hom U.open_embedding_obj_top.symm =
    U.open_embedding.is_open_map.functor.map (eq_to_hom U.inclusion_map_eq_top) :=
    subsingleton.elim _ _,
  have e‚ÇÑ : X.presheaf.map _ ‚â´ _ = _ :=
    (as_iso (Œì_Spec.adjunction.unit.app (X.restrict U.open_embedding)))
    .inv.1.c.naturality_assoc (eq_to_hom U.inclusion_map_eq_top).op _,
  erw [e‚ÇÉ, e‚ÇÑ, ‚Üê Scheme.comp_val_c_app_assoc, iso.inv_hom_id],
  simp only [eq_to_hom_map, eq_to_hom_op, Scheme.Spec_map_presheaf_map_eq_to_hom],
  erw [Scheme.Spec_map_presheaf_map_eq_to_hom, category.id_comp],
  simpa only [eq_to_hom_trans]
end

@[elementwise]
lemma is_affine_open.from_Spec_app_eq {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) :
  hU.from_Spec.1.c.app (op U) = Spec_Œì_identity.inv.app (X.presheaf.obj $ op U) ‚â´
    (Scheme.Spec.obj _).presheaf.map (eq_to_hom hU.from_Spec_base_preimage).op :=
by rw [‚Üê hU.Spec_Œì_identity_hom_app_from_Spec, iso.inv_hom_id_app_assoc]

lemma is_affine_open.basic_open_is_affine {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) (f : X.presheaf.obj (op U)) : is_affine_open (X.basic_open f) :=
begin
  convert range_is_affine_open_of_open_immersion (Scheme.Spec.map (CommRing.of_hom
    (algebra_map (X.presheaf.obj (op U)) (localization.away f))).op ‚â´ hU.from_Spec),
  ext1,
  have : hU.from_Spec.val.base '' (hU.from_Spec.val.base ‚Åª¬π' (X.basic_open f : set X.carrier)) =
    (X.basic_open f : set X.carrier),
  { rw [set.image_preimage_eq_inter_range, set.inter_eq_left_iff_subset, hU.from_Spec_range],
    exact Scheme.basic_open_subset _ _ },
  rw [subtype.coe_mk, Scheme.comp_val_base, ‚Üê this, coe_comp, set.range_comp],
  congr' 1,
  refine (congr_arg coe $ Scheme.preimage_basic_open hU.from_Spec f).trans _,
  refine eq.trans _ (prime_spectrum.localization_away_comap_range (localization.away f) f).symm,
  congr' 1,
  have : (opens.map hU.from_Spec.val.base).obj U = ‚ä§,
  { ext1,
    change hU.from_Spec.1.base ‚Åª¬π' (U : set X.carrier) = set.univ,
    rw [‚Üê hU.from_Spec_range, ‚Üê set.image_univ],
    exact set.preimage_image_eq _ PresheafedSpace.is_open_immersion.base_open.inj },
  refine eq.trans _ (basic_open_eq_of_affine f),
  have lm : ‚àÄ s, (opens.map hU.from_Spec.val.base).obj U ‚äì s = s := Œª s, this.symm ‚ñ∏ top_inf_eq,
  refine eq.trans _ (lm _),
  refine eq.trans _
    ((Scheme.Spec.obj $ op $ X.presheaf.obj $ op U).basic_open_res _ (eq_to_hom this).op),
  rw ‚Üê comp_apply,
  congr' 2,
  rw iso.eq_inv_comp,
  erw hU.Spec_Œì_identity_hom_app_from_Spec,
end

lemma Scheme.map_prime_spectrum_basic_open_of_affine (X : Scheme) [is_affine X]
  (f : Scheme.Œì.obj (op X)) :
  (opens.map X.iso_Spec.hom.1.base).obj (prime_spectrum.basic_open f) = X.basic_open f :=
begin
  rw ‚Üê basic_open_eq_of_affine,
  transitivity (opens.map X.iso_Spec.hom.1.base).obj ((Scheme.Spec.obj
    (op (Scheme.Œì.obj (op X)))).basic_open ((inv (X.iso_Spec.hom.1.c.app
      (op ((opens.map (inv X.iso_Spec.hom).val.base).obj ‚ä§)))) ((X.presheaf.map (eq_to_hom _)) f))),
  congr,
  { rw [‚Üê is_iso.inv_eq_inv, is_iso.inv_inv, is_iso.iso.inv_inv, nat_iso.app_hom],
    erw ‚Üê Œì_Spec.adjunction_unit_app_app_top,
    refl },
  { rw eq_to_hom_map, refl },
  { dsimp, congr },
  { refine (Scheme.preimage_basic_open _ _).trans _,
    rw [is_iso.inv_hom_id_apply, Scheme.basic_open_res_eq] }
end

lemma is_basis_basic_open (X : Scheme) [is_affine X] :
  opens.is_basis (set.range (X.basic_open : X.presheaf.obj (op ‚ä§) ‚Üí opens X.carrier)) :=
begin
  delta opens.is_basis,
  convert prime_spectrum.is_basis_basic_opens.inducing
    (Top.homeo_of_iso (Scheme.forget_to_Top.map_iso X.iso_Spec)).inducing using 1,
  ext,
  simp only [set.mem_image, exists_exists_eq_and],
  split,
  { rintro ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©,
    refine ‚ü®_, ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, _‚ü©,
    exact congr_arg subtype.val (X.map_prime_spectrum_basic_open_of_affine x) },
  { rintro ‚ü®_, ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, rfl‚ü©,
    refine ‚ü®_, ‚ü®x, rfl‚ü©, _‚ü©,
    exact congr_arg subtype.val (X.map_prime_spectrum_basic_open_of_affine x).symm }
end

/-- The prime ideal of `ùí™‚Çì(U)` corresponding to a point `x : U`. -/
noncomputable
def is_affine_open.prime_ideal_of {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) (x : U) :
  prime_spectrum (X.presheaf.obj $ op U) :=
((Scheme.Spec.map (X.presheaf.map (eq_to_hom $
  show U.open_embedding.is_open_map.functor.obj ‚ä§ = U, from
    opens.ext (set.image_univ.trans subtype.range_coe)).op).op).1.base
  ((@@Scheme.iso_Spec (X.restrict U.open_embedding) hU).hom.1.base x))

lemma is_affine_open.from_Spec_prime_ideal_of {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) (x : U) :
  hU.from_Spec.val.base (hU.prime_ideal_of x) = x.1 :=
begin
  dsimp only [is_affine_open.from_Spec, subtype.coe_mk],
  erw [‚Üê Scheme.comp_val_base_apply, ‚Üê Scheme.comp_val_base_apply],
  simpa only [‚Üê functor.map_comp_assoc, ‚Üê functor.map_comp, ‚Üê op_comp, eq_to_hom_trans, op_id,
    eq_to_hom_refl, category_theory.functor.map_id, category.id_comp, iso.hom_inv_id_assoc]
end

lemma is_affine_open.is_localization_stalk_aux {X : Scheme} (U : opens X.carrier)
  [is_affine (X.restrict U.open_embedding)] :
  (inv (Œì_Spec.adjunction.unit.app (X.restrict U.open_embedding))).1.c.app
    (op ((opens.map U.inclusion).obj U)) =
      X.presheaf.map (eq_to_hom $ by rw opens.inclusion_map_eq_top :
        U.open_embedding.is_open_map.functor.obj ‚ä§ ‚ü∂
          (U.open_embedding.is_open_map.functor.obj ((opens.map U.inclusion).obj U))).op ‚â´
      to_Spec_Œì (X.presheaf.obj $ op (U.open_embedding.is_open_map.functor.obj ‚ä§)) ‚â´
      (Scheme.Spec.obj $ op $ X.presheaf.obj $ _).presheaf.map
        (eq_to_hom (by { rw [opens.inclusion_map_eq_top], refl }) : unop _ ‚ü∂ ‚ä§).op :=
begin
  have e : (opens.map (inv (Œì_Spec.adjunction.unit.app (X.restrict U.open_embedding))).1.base).obj
    ((opens.map U.inclusion).obj U) = ‚ä§,
  by { rw [opens.inclusion_map_eq_top], refl },
  rw [Scheme.inv_val_c_app, is_iso.comp_inv_eq, Scheme.app_eq _ e,
    Œì_Spec.adjunction_unit_app_app_top],
  simp only [category.assoc, eq_to_hom_op],
  erw ‚Üê functor.map_comp_assoc,
  rw [eq_to_hom_trans, eq_to_hom_refl, category_theory.functor.map_id,
    category.id_comp],
  erw Spec_Œì_identity.inv_hom_id_app_assoc,
  simp only [eq_to_hom_map, eq_to_hom_trans],
end

lemma is_affine_open.is_localization_stalk {X : Scheme} {U : opens X.carrier}
  (hU : is_affine_open U) (x : U) :
  is_localization.at_prime (X.presheaf.stalk x) (hU.prime_ideal_of x).as_ideal :=
begin
  haveI : is_affine _ := hU,
  haveI : nonempty U := ‚ü®x‚ü©,
  rcases x with ‚ü®x, hx‚ü©,
  let y := hU.prime_ideal_of ‚ü®x, hx‚ü©,
  have : hU.from_Spec.val.base y = x := hU.from_Spec_prime_ideal_of ‚ü®x, hx‚ü©,
  change is_localization y.as_ideal.prime_compl _,
  clear_value y,
  subst this,
  apply (is_localization.is_localization_iff_of_ring_equiv _
    (as_iso $ PresheafedSpace.stalk_map hU.from_Spec.1 y).CommRing_iso_to_ring_equiv).mpr,
  convert structure_sheaf.is_localization.to_stalk _ _ using 1,
  delta structure_sheaf.stalk_algebra,
  congr' 1,
  rw ring_hom.algebra_map_to_algebra,
  refine (PresheafedSpace.stalk_map_germ hU.from_Spec.1 _ ‚ü®_, _‚ü©).trans _,
  delta is_affine_open.from_Spec Scheme.iso_Spec structure_sheaf.to_stalk,
  simp only [Scheme.comp_val_c_app, category.assoc],
  dsimp only [functor.op, as_iso_inv, unop_op],
  erw is_affine_open.is_localization_stalk_aux,
  simp only [category.assoc],
  conv_lhs { rw ‚Üê category.assoc },
  erw [‚Üê X.presheaf.map_comp, Spec_Œì_naturality_assoc],
  congr' 1,
  simp only [‚Üê category.assoc],
  transitivity _ ‚â´ (structure_sheaf (X.presheaf.obj $ op U)).1.germ ‚ü®_, _‚ü©,
  { refl },
  convert ((structure_sheaf (X.presheaf.obj $ op U)).1.germ_res (hom_of_le le_top) ‚ü®_, _‚ü©) using 2,
  rw category.assoc,
  erw nat_trans.naturality,
  rw [‚Üê LocallyRingedSpace.Œì_map_op, ‚Üê LocallyRingedSpace.Œì.map_comp_assoc, ‚Üê op_comp],
  erw ‚Üê Scheme.Spec.map_comp,
  rw [‚Üê op_comp, ‚Üê X.presheaf.map_comp],
  transitivity LocallyRingedSpace.Œì.map (quiver.hom.op $ Scheme.Spec.map
    (X.presheaf.map (ùüô (op U))).op) ‚â´ _,
  { congr },
  simp only [category_theory.functor.map_id, op_id],
  erw category_theory.functor.map_id,
  rw category.id_comp,
  refl
end

end algebraic_geometry
