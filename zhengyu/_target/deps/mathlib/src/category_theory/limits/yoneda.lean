/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.limits.functor_category

/-!
# Limit properties relating to the (co)yoneda embedding.

We calculate the colimit of `Y ‚Ü¶ (X ‚ü∂ Y)`, which is just `punit`.
(This is used in characterising cofinal functors.)

We also show the (co)yoneda embeddings preserve limits and jointly reflect them.
-/

open opposite
open category_theory
open category_theory.limits

universes v u

namespace category_theory

namespace coyoneda
variables {C : Type v} [small_category C]

/--
The colimit cocone over `coyoneda.obj X`, with cocone point `punit`.
-/
@[simps]
def colimit_cocone (X : C·µí·µñ) : cocone (coyoneda.obj X) :=
{ X := punit,
  Œπ := { app := by tidy, } }

/--
The proposed colimit cocone over `coyoneda.obj X` is a colimit cocone.
-/
@[simps]
def colimit_cocone_is_colimit (X : C·µí·µñ) : is_colimit (colimit_cocone X) :=
{ desc := Œª s x, s.Œπ.app (unop X) (ùüô _),
  fac' := Œª s Y, by { ext f, convert congr_fun (s.w f).symm (ùüô (unop X)), simp, },
  uniq' := Œª s m w, by { ext ‚ü®‚ü©, rw ‚Üê w, simp, } }

instance (X : C·µí·µñ) : has_colimit (coyoneda.obj X) :=
has_colimit.mk { cocone := _, is_colimit := colimit_cocone_is_colimit X }

/--
The colimit of `coyoneda.obj X` is isomorphic to `punit`.
-/
noncomputable
def colimit_coyoneda_iso (X : C·µí·µñ) : colimit (coyoneda.obj X) ‚âÖ punit :=
colimit.iso_colimit_cocone { cocone := _, is_colimit := colimit_cocone_is_colimit X }

end coyoneda

variables {C : Type u} [category.{v} C]

open limits

/-- The yoneda embedding `yoneda.obj X : C·µí·µñ ‚•§ Type v` for `X : C` preserves limits. -/
instance yoneda_preserves_limits (X : C) : preserves_limits (yoneda.obj X) :=
{ preserves_limits_of_shape := Œª J ùí•, by exactI
  { preserves_limit := Œª K,
    { preserves := Œª c t,
      { lift := Œª s x, quiver.hom.unop (t.lift ‚ü®op X, Œª j, (s.œÄ.app j x).op, Œª j‚ÇÅ j‚ÇÇ Œ±, _‚ü©),
        fac' := Œª s j, funext $ Œª x, quiver.hom.op_inj (t.fac _ _),
        uniq' := Œª s m w, funext $ Œª x,
        begin
          refine quiver.hom.op_inj (t.uniq ‚ü®op X, _, _‚ü© _ (Œª j, _)),
          { dsimp, simp [‚Üê s.w Œ±] }, -- See library note [dsimp, simp]
          { exact quiver.hom.unop_inj (congr_fun (w j) x) },
        end } } } }

/-- The coyoneda embedding `coyoneda.obj X : C ‚•§ Type v` for `X : C·µí·µñ` preserves limits. -/
instance coyoneda_preserves_limits (X : C·µí·µñ) : preserves_limits (coyoneda.obj X) :=
{ preserves_limits_of_shape := Œª J ùí•, by exactI
  { preserves_limit := Œª K,
    { preserves := Œª c t,
      { lift := Œª s x, t.lift ‚ü®unop X, Œª j, s.œÄ.app j x, Œª j‚ÇÅ j‚ÇÇ Œ±, by { dsimp, simp [‚Üê s.w Œ±]}‚ü©,
          -- See library note [dsimp, simp]
        fac' := Œª s j, funext $ Œª x, t.fac _ _,
        uniq' := Œª s m w, funext $ Œª x,
        begin
          refine (t.uniq ‚ü®unop X, _‚ü© _ (Œª j, _)),
          exact congr_fun (w j) x,
        end } } } }

/-- The yoneda embeddings jointly reflect limits. -/
def yoneda_jointly_reflects_limits (J : Type v) [small_category J] (K : J ‚•§ C·µí·µñ) (c : cone K)
  (t : Œ† (X : C), is_limit ((yoneda.obj X).map_cone c)) : is_limit c :=
let s' : Œ† (s : cone K), cone (K ‚ãô yoneda.obj s.X.unop) :=
  Œª s, ‚ü®punit, Œª j _, (s.œÄ.app j).unop, Œª j‚ÇÅ j‚ÇÇ Œ±, funext $ Œª _, quiver.hom.op_inj (s.w Œ±).symm‚ü©
in
{ lift := Œª s, ((t s.X.unop).lift (s' s) punit.star).op,
  fac' := Œª s j, quiver.hom.unop_inj (congr_fun ((t s.X.unop).fac (s' s) j) punit.star),
  uniq' := Œª s m w,
  begin
    apply quiver.hom.unop_inj,
    suffices : (Œª (x : punit), m.unop) = (t s.X.unop).lift (s' s),
    { apply congr_fun this punit.star },
    apply (t _).uniq (s' s) _ (Œª j, _),
    ext,
    exact quiver.hom.op_inj (w j),
  end }

/-- The coyoneda embeddings jointly reflect limits. -/
def coyoneda_jointly_reflects_limits (J : Type v) [small_category J] (K : J ‚•§ C) (c : cone K)
  (t : Œ† (X : C·µí·µñ), is_limit ((coyoneda.obj X).map_cone c)) : is_limit c :=
let s' : Œ† (s : cone K), cone (K ‚ãô coyoneda.obj (op s.X)) :=
  Œª s, ‚ü®punit, Œª j _, s.œÄ.app j, Œª j‚ÇÅ j‚ÇÇ Œ±, funext $ Œª _, (s.w Œ±).symm‚ü©
in
{ lift := Œª s, (t (op s.X)).lift (s' s) punit.star,
  fac' := Œª s j, congr_fun ((t _).fac (s' s) j) punit.star,
  uniq' := Œª s m w,
  begin
    suffices : (Œª (x : punit), m) = (t _).lift (s' s),
    { apply congr_fun this punit.star },
    apply (t _).uniq (s' s) _ (Œª j, _),
    ext,
    exact (w j),
  end }

variables {D : Type u} [small_category D]

instance yoneda_functor_preserves_limits : preserves_limits (@yoneda D _) :=
begin
  apply preserves_limits_of_evaluation,
  intro K,
  change preserves_limits (coyoneda.obj K),
  apply_instance
end

instance coyoneda_functor_preserves_limits : preserves_limits (@coyoneda D _) :=
begin
  apply preserves_limits_of_evaluation,
  intro K,
  change preserves_limits (yoneda.obj K),
  apply_instance
end

instance yoneda_functor_reflects_limits : reflects_limits (@yoneda D _) :=
limits.fully_faithful_reflects_limits _

instance coyoneda_functor_reflects_limits : reflects_limits (@coyoneda D _) :=
limits.fully_faithful_reflects_limits _

end category_theory
