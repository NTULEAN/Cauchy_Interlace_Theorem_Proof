/-
Copyright (c) 2021 Henry Swanson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Henry Swanson, Patrick Massot
-/
import analysis.special_functions.exponential
import combinatorics.derangements.finite
import order.filter.basic

/-!
# Derangement exponential series

This file proves that the probability of a permutation on n elements being a derangement is 1/e.
The specific lemma is `num_derangements_tendsto_inv_e`.
-/
open filter

open_locale big_operators
open_locale topological_space

theorem num_derangements_tendsto_inv_e :
  tendsto (Œª n, (num_derangements n : ‚Ñù) / n.factorial) at_top
  (ùìù (real.exp (-1))) :=
begin
  -- we show that d(n)/n! is the partial sum of exp(-1), but offset by 1.
  -- this isn't entirely obvious, since we have to ensure that asc_factorial and
  -- factorial interact in the right way, e.g., that k ‚â§ n always
  let s : ‚Ñï ‚Üí ‚Ñù := Œª n, ‚àë k in finset.range n, (-1 : ‚Ñù)^k / k.factorial,
  suffices : ‚àÄ n : ‚Ñï, (num_derangements n : ‚Ñù) / n.factorial = s(n+1),
  { simp_rw this,
    -- shift the function by 1, and then use the fact that the partial sums
    -- converge to the infinite sum
    rw tendsto_add_at_top_iff_nat 1,
    apply has_sum.tendsto_sum_nat,
    -- there's no specific lemma for ‚Ñù that ‚àë x^k/k! sums to exp(x), but it's
    -- true in more general fields, so use that lemma
    rw real.exp_eq_exp_‚Ñù_‚Ñù,
    exact exp_series_field_has_sum_exp (-1 : ‚Ñù) },
  intro n,
  rw [‚Üê int.cast_coe_nat, num_derangements_sum],
  push_cast,
  rw finset.sum_div,
  -- get down to individual terms
  refine finset.sum_congr (refl _) _,
  intros k hk,
  have h_le : k ‚â§ n := finset.mem_range_succ_iff.mp hk,
  rw [nat.asc_factorial_eq_div, add_tsub_cancel_of_le h_le],
  push_cast [nat.factorial_dvd_factorial h_le],
  field_simp [nat.factorial_ne_zero],
  ring,
end
