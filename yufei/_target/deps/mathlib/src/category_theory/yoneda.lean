/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.hom_functor
import category_theory.currying
import category_theory.products.basic

/-!
# The Yoneda embedding

The Yoneda embedding as a functor `yoneda : C ‚•§ (C·µí·µñ ‚•§ Type v‚ÇÅ)`,
along with an instance that it is `fully_faithful`.

Also the Yoneda lemma, `yoneda_lemma : (yoneda_pairing C) ‚âÖ (yoneda_evaluation C)`.

## References
* [Stacks: Opposite Categories and the Yoneda Lemma](https://stacks.math.columbia.edu/tag/001L)
-/

namespace category_theory
open opposite

universes v‚ÇÅ u‚ÇÅ u‚ÇÇ-- morphism levels before object levels. See note [category_theory universes].

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C]

/--
The Yoneda embedding, as a functor from `C` into presheaves on `C`.

See https://stacks.math.columbia.edu/tag/001O.
-/
@[simps]
def yoneda : C ‚•§ (C·µí·µñ ‚•§ Type v‚ÇÅ) :=
{ obj := Œª X,
  { obj := Œª Y, unop Y ‚ü∂ X,
    map := Œª Y Y' f g, f.unop ‚â´ g,
    map_comp' := Œª _ _ _ f g, begin ext, dsimp, erw [category.assoc] end,
    map_id' := Œª Y, begin ext, dsimp, erw [category.id_comp] end },
  map := Œª X X' f, { app := Œª Y g, g ‚â´ f } }

/--
The co-Yoneda embedding, as a functor from `C·µí·µñ` into co-presheaves on `C`.
-/
@[simps] def coyoneda : C·µí·µñ ‚•§ (C ‚•§ Type v‚ÇÅ) :=
{ obj := Œª X,
  { obj := Œª Y, unop X ‚ü∂ Y,
    map := Œª Y Y' f g, g ‚â´ f },
  map := Œª X X' f, { app := Œª Y g, f.unop ‚â´ g } }

namespace yoneda

lemma obj_map_id {X Y : C} (f : op X ‚ü∂ op Y) :
  (yoneda.obj X).map f (ùüô X) = (yoneda.map f.unop).app (op Y) (ùüô Y) :=
by { dsimp, simp }

@[simp] lemma naturality {X Y : C} (Œ± : yoneda.obj X ‚ü∂ yoneda.obj Y)
  {Z Z' : C} (f : Z ‚ü∂ Z') (h : Z' ‚ü∂ X) : f ‚â´ Œ±.app (op Z') h = Œ±.app (op Z) (f ‚â´ h) :=
(functor_to_types.naturality _ _ Œ± f.op h).symm

/--
The Yoneda embedding is full.

See https://stacks.math.columbia.edu/tag/001P.
-/
instance yoneda_full : full (yoneda : C ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ) :=
{ preimage := Œª X Y f, f.app (op X) (ùüô X) }

/--
The Yoneda embedding is faithful.

See https://stacks.math.columbia.edu/tag/001P.
-/
instance yoneda_faithful : faithful (yoneda : C ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ) :=
{ map_injective' := Œª X Y f g p, by convert (congr_fun (congr_app p (op X)) (ùüô X)); dsimp; simp }

/-- Extensionality via Yoneda. The typical usage would be
```
-- Goal is `X ‚âÖ Y`
apply yoneda.ext,
-- Goals are now functions `(Z ‚ü∂ X) ‚Üí (Z ‚ü∂ Y)`, `(Z ‚ü∂ Y) ‚Üí (Z ‚ü∂ X)`, and the fact that these
functions are inverses and natural in `Z`.
```
-/
def ext (X Y : C)
  (p : Œ† {Z : C}, (Z ‚ü∂ X) ‚Üí (Z ‚ü∂ Y)) (q : Œ† {Z : C}, (Z ‚ü∂ Y) ‚Üí (Z ‚ü∂ X))
  (h‚ÇÅ : Œ† {Z : C} (f : Z ‚ü∂ X), q (p f) = f) (h‚ÇÇ : Œ† {Z : C} (f : Z ‚ü∂ Y), p (q f) = f)
  (n : Œ† {Z Z' : C} (f : Z' ‚ü∂ Z) (g : Z ‚ü∂ X), p (f ‚â´ g) = f ‚â´ p g) : X ‚âÖ Y :=
@preimage_iso _ _ _ _ yoneda _ _ _ _
  (nat_iso.of_components (Œª Z, { hom := p, inv := q, }) (by tidy))

/--
If `yoneda.map f` is an isomorphism, so was `f`.
-/
lemma is_iso {X Y : C} (f : X ‚ü∂ Y) [is_iso (yoneda.map f)] : is_iso f :=
is_iso_of_fully_faithful yoneda f

end yoneda

namespace coyoneda

@[simp] lemma naturality {X Y : C·µí·µñ} (Œ± : coyoneda.obj X ‚ü∂ coyoneda.obj Y)
  {Z Z' : C} (f : Z' ‚ü∂ Z) (h : unop X ‚ü∂ Z') : (Œ±.app Z' h) ‚â´ f = Œ±.app Z (h ‚â´ f) :=
(functor_to_types.naturality _ _ Œ± f h).symm

instance coyoneda_full : full (coyoneda : C·µí·µñ ‚•§ C ‚•§ Type v‚ÇÅ) :=
{ preimage := Œª X Y f, (f.app _ (ùüô X.unop)).op }

instance coyoneda_faithful : faithful (coyoneda : C·µí·µñ ‚•§ C ‚•§ Type v‚ÇÅ) :=
{ map_injective' := Œª X Y f g p,
  begin
    have t := congr_fun (congr_app p X.unop) (ùüô _),
    simpa using congr_arg quiver.hom.op t,
  end }

/--
If `coyoneda.map f` is an isomorphism, so was `f`.
-/
lemma is_iso {X Y : C·µí·µñ} (f : X ‚ü∂ Y) [is_iso (coyoneda.map f)] : is_iso f :=
is_iso_of_fully_faithful coyoneda f

/-- The identity functor on `Type` is isomorphic to the coyoneda functor coming from `punit`. -/
def punit_iso : coyoneda.obj (opposite.op punit) ‚âÖ ùü≠ (Type v‚ÇÅ) :=
nat_iso.of_components
  (Œª X, { hom := Œª f, f ‚ü®‚ü©, inv := Œª x _, x })
  (by tidy)

end coyoneda

namespace functor


/--
A functor `F : C·µí·µñ ‚•§ Type v‚ÇÅ` is representable if there is object `X` so `F ‚âÖ yoneda.obj X`.

See https://stacks.math.columbia.edu/tag/001Q.
-/
class representable (F : C·µí·µñ ‚•§ Type v‚ÇÅ) : Prop :=
(has_representation : ‚àÉ X (f : yoneda.obj X ‚ü∂ F), is_iso f)

instance {X : C} : representable (yoneda.obj X) :=
{ has_representation := ‚ü®X, ùüô _, infer_instance‚ü© }

/--
A functor `F : C ‚•§ Type v‚ÇÅ` is corepresentable if there is object `X` so `F ‚âÖ coyoneda.obj X`.

See https://stacks.math.columbia.edu/tag/001Q.
-/
class corepresentable (F : C ‚•§ Type v‚ÇÅ) : Prop :=
(has_corepresentation : ‚àÉ X (f : coyoneda.obj X ‚ü∂ F), is_iso f)

instance {X : C·µí·µñ} : corepresentable (coyoneda.obj X) :=
{ has_corepresentation := ‚ü®X, ùüô _, infer_instance‚ü© }

-- instance : corepresentable (ùü≠ (Type v‚ÇÅ)) :=
-- corepresentable_of_nat_iso (op punit) coyoneda.punit_iso

section representable
variables (F : C·µí·µñ ‚•§ Type v‚ÇÅ)
variable [F.representable]

/-- The representing object for the representable functor `F`. -/
noncomputable def repr_X : C :=
(representable.has_representation : ‚àÉ X (f : _ ‚ü∂ F), _).some

/-- The (forward direction of the) isomorphism witnessing `F` is representable. -/
noncomputable def repr_f : yoneda.obj F.repr_X ‚ü∂ F :=
representable.has_representation.some_spec.some

/--
The representing element for the representable functor `F`, sometimes called the universal
element of the functor.
-/
noncomputable def repr_x : F.obj (op F.repr_X) :=
F.repr_f.app (op F.repr_X) (ùüô F.repr_X)

instance : is_iso F.repr_f :=
representable.has_representation.some_spec.some_spec

/--
An isomorphism between `F` and a functor of the form `C(-, F.repr_X)`.  Note the components
`F.repr_w.app X` definitionally have type `(X.unop ‚ü∂ F.repr_X) ‚âÖ F.obj X`.
-/
noncomputable def repr_w : yoneda.obj F.repr_X ‚âÖ F := as_iso F.repr_f

@[simp] lemma repr_w_hom : F.repr_w.hom = F.repr_f := rfl

lemma repr_w_app_hom (X : C·µí·µñ) (f : unop X ‚ü∂ F.repr_X) :
  (F.repr_w.app X).hom f = F.map f.op F.repr_x :=
begin
  change F.repr_f.app X f = (F.repr_f.app (op F.repr_X) ‚â´ F.map f.op) (ùüô F.repr_X),
  rw ‚ÜêF.repr_f.naturality,
  dsimp,
  simp
end

end representable

section corepresentable

variables (F : C ‚•§ Type v‚ÇÅ)
variable [F.corepresentable]

/-- The representing object for the corepresentable functor `F`. -/
noncomputable def corepr_X : C :=
(corepresentable.has_corepresentation : ‚àÉ X (f : _ ‚ü∂ F), _).some.unop

/-- The (forward direction of the) isomorphism witnessing `F` is corepresentable. -/
noncomputable def corepr_f : coyoneda.obj (op F.corepr_X) ‚ü∂ F :=
corepresentable.has_corepresentation.some_spec.some

/--
The representing element for the corepresentable functor `F`, sometimes called the universal
element of the functor.
-/
noncomputable def corepr_x : F.obj F.corepr_X :=
F.corepr_f.app F.corepr_X (ùüô F.corepr_X)

instance : is_iso F.corepr_f :=
corepresentable.has_corepresentation.some_spec.some_spec

/--
An isomorphism between `F` and a functor of the form `C(F.corepr X, -)`. Note the components
`F.corepr_w.app X` definitionally have type `F.corepr_X ‚ü∂ X ‚âÖ F.obj X`.
-/
noncomputable def corepr_w : coyoneda.obj (op F.corepr_X) ‚âÖ F := as_iso F.corepr_f

lemma corepr_w_app_hom (X : C) (f : F.corepr_X ‚ü∂ X) :
  (F.corepr_w.app X).hom f = F.map f F.corepr_x :=
begin
  change F.corepr_f.app X f = (F.corepr_f.app F.corepr_X ‚â´ F.map f) (ùüô F.corepr_X),
  rw ‚ÜêF.corepr_f.naturality,
  dsimp,
  simp
end

end corepresentable

end functor

lemma representable_of_nat_iso (F : C·µí·µñ ‚•§ Type v‚ÇÅ) {G} (i : F ‚âÖ G) [F.representable] :
  G.representable :=
{ has_representation := ‚ü®F.repr_X, F.repr_f ‚â´ i.hom, infer_instance‚ü© }

lemma corepresentable_of_nat_iso (F : C ‚•§ Type v‚ÇÅ) {G} (i : F ‚âÖ G) [F.corepresentable] :
  G.corepresentable :=
{ has_corepresentation := ‚ü®op F.corepr_X, F.corepr_f ‚â´ i.hom, infer_instance‚ü© }

instance : functor.corepresentable (ùü≠ (Type v‚ÇÅ)) :=
corepresentable_of_nat_iso (coyoneda.obj (op punit)) coyoneda.punit_iso

open opposite

variables (C)

-- We need to help typeclass inference with some awkward universe levels here.
instance prod_category_instance_1 : category ((C·µí·µñ ‚•§ Type v‚ÇÅ) √ó C·µí·µñ) :=
category_theory.prod.{(max u‚ÇÅ v‚ÇÅ) v‚ÇÅ} (C·µí·µñ ‚•§ Type v‚ÇÅ) C·µí·µñ

instance prod_category_instance_2 : category (C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ)) :=
category_theory.prod.{v‚ÇÅ (max u‚ÇÅ v‚ÇÅ)} C·µí·µñ (C·µí·µñ ‚•§ Type v‚ÇÅ)

open yoneda

/--
The "Yoneda evaluation" functor, which sends `X : C·µí·µñ` and `F : C·µí·µñ ‚•§ Type`
to `F.obj X`, functorially in both `X` and `F`.
-/
def yoneda_evaluation : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ) ‚•§ Type (max u‚ÇÅ v‚ÇÅ) :=
evaluation_uncurried C·µí·µñ (Type v‚ÇÅ) ‚ãô ulift_functor.{u‚ÇÅ}

@[simp] lemma yoneda_evaluation_map_down
  (P Q : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ)) (Œ± : P ‚ü∂ Q) (x : (yoneda_evaluation C).obj P) :
  ((yoneda_evaluation C).map Œ± x).down = Œ±.2.app Q.1 (P.2.map Œ±.1 x.down) := rfl

/--
The "Yoneda pairing" functor, which sends `X : C·µí·µñ` and `F : C·µí·µñ ‚•§ Type`
to `yoneda.op.obj X ‚ü∂ F`, functorially in both `X` and `F`.
-/
def yoneda_pairing : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ) ‚•§ Type (max u‚ÇÅ v‚ÇÅ) :=
functor.prod yoneda.op (ùü≠ (C·µí·µñ ‚•§ Type v‚ÇÅ)) ‚ãô functor.hom (C·µí·µñ ‚•§ Type v‚ÇÅ)

@[simp] lemma yoneda_pairing_map
  (P Q : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ)) (Œ± : P ‚ü∂ Q) (Œ≤ : (yoneda_pairing C).obj P) :
  (yoneda_pairing C).map Œ± Œ≤ = yoneda.map Œ±.1.unop ‚â´ Œ≤ ‚â´ Œ±.2 := rfl

/--
The Yoneda lemma asserts that that the Yoneda pairing
`(X : C·µí·µñ, F : C·µí·µñ ‚•§ Type) ‚Ü¶ (yoneda.obj (unop X) ‚ü∂ F)`
is naturally isomorphic to the evaluation `(X, F) ‚Ü¶ F.obj X`.

See https://stacks.math.columbia.edu/tag/001P.
-/
def yoneda_lemma : yoneda_pairing C ‚âÖ yoneda_evaluation C :=
{ hom :=
  { app := Œª F x, ulift.up ((x.app F.1) (ùüô (unop F.1))),
    naturality' :=
    begin
      intros X Y f, ext, dsimp,
      erw [category.id_comp, ‚Üêfunctor_to_types.naturality],
      simp only [category.comp_id, yoneda_obj_map],
    end },
  inv :=
  { app := Œª F x,
    { app := Œª X a, (F.2.map a.op) x.down,
      naturality' :=
      begin
        intros X Y f, ext, dsimp,
        rw [functor_to_types.map_comp_apply]
      end },
    naturality' :=
    begin
      intros X Y f, ext, dsimp,
      rw [‚Üêfunctor_to_types.naturality, functor_to_types.map_comp_apply]
    end },
  hom_inv_id' :=
  begin
    ext, dsimp,
    erw [‚Üêfunctor_to_types.naturality,
         obj_map_id],
    simp only [yoneda_map_app, quiver.hom.unop_op],
    erw [category.id_comp],
  end,
  inv_hom_id' :=
  begin
    ext, dsimp,
    rw [functor_to_types.map_id_apply]
  end }.

variables {C}

/--
The isomorphism between `yoneda.obj X ‚ü∂ F` and `F.obj (op X)`
(we need to insert a `ulift` to get the universes right!)
given by the Yoneda lemma.
-/
@[simps] def yoneda_sections (X : C) (F : C·µí·µñ ‚•§ Type v‚ÇÅ) :
  (yoneda.obj X ‚ü∂ F) ‚âÖ ulift.{u‚ÇÅ} (F.obj (op X)) :=
(yoneda_lemma C).app (op X, F)

/--
We have a type-level equivalence between natural transformations from the yoneda embedding
and elements of `F.obj X`, without any universe switching.
-/
def yoneda_equiv {X : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} : (yoneda.obj X ‚ü∂ F) ‚âÉ F.obj (op X) :=
(yoneda_sections X F).to_equiv.trans equiv.ulift

@[simp]
lemma yoneda_equiv_apply {X : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (f : yoneda.obj X ‚ü∂ F) :
  yoneda_equiv f = f.app (op X) (ùüô X) :=
rfl

@[simp]
lemma yoneda_equiv_symm_app_apply {X : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (x : F.obj (op X))
  (Y : C·µí·µñ) (f : Y.unop ‚ü∂ X) :
  (yoneda_equiv.symm x).app Y f = F.map f.op x :=
rfl

lemma yoneda_equiv_naturality {X Y : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (f : yoneda.obj X ‚ü∂ F) (g : Y ‚ü∂ X) :
  F.map g.op (yoneda_equiv f) = yoneda_equiv (yoneda.map g ‚â´ f) :=
begin
  change (f.app (op X) ‚â´ F.map g.op) (ùüô X) = f.app (op Y) (ùüô Y ‚â´ g),
  rw ‚Üêf.naturality,
  dsimp,
  simp,
end

/--
When `C` is a small category, we can restate the isomorphism from `yoneda_sections`
without having to change universes.
-/
def yoneda_sections_small {C : Type u‚ÇÅ} [small_category C] (X : C)
  (F : C·µí·µñ ‚•§ Type u‚ÇÅ) :
  (yoneda.obj X ‚ü∂ F) ‚âÖ F.obj (op X) :=
yoneda_sections X F ‚â™‚â´ ulift_trivial _

@[simp]
lemma yoneda_sections_small_hom {C : Type u‚ÇÅ} [small_category C] (X : C)
  (F : C·µí·µñ ‚•§ Type u‚ÇÅ) (f : yoneda.obj X ‚ü∂ F) :
  (yoneda_sections_small X F).hom f = f.app _ (ùüô _) :=
rfl

@[simp]
lemma yoneda_sections_small_inv_app_apply {C : Type u‚ÇÅ} [small_category C] (X : C)
  (F : C·µí·µñ ‚•§ Type u‚ÇÅ) (t : F.obj (op X)) (Y : C·µí·µñ) (f : Y.unop ‚ü∂ X) :
  ((yoneda_sections_small X F).inv t).app Y f = F.map f.op t :=
rfl

local attribute[ext] functor.ext

/-- The curried version of yoneda lemma when `C` is small. -/
def curried_yoneda_lemma {C : Type u‚ÇÅ} [small_category C] :
  (yoneda.op ‚ãô coyoneda : C·µí·µñ ‚•§ (C·µí·µñ ‚•§ Type u‚ÇÅ) ‚•§ Type u‚ÇÅ) ‚âÖ evaluation C·µí·µñ (Type u‚ÇÅ) :=
eq_to_iso (by tidy) ‚â™‚â´ curry.map_iso (yoneda_lemma C ‚â™‚â´
  iso_whisker_left (evaluation_uncurried C·µí·µñ (Type u‚ÇÅ)) ulift_functor_trivial) ‚â™‚â´
    eq_to_iso (by tidy)

/-- The curried version of yoneda lemma when `C` is small. -/
def curried_yoneda_lemma' {C : Type u‚ÇÅ} [small_category C] :
  yoneda ‚ãô (whiskering_left C·µí·µñ (C·µí·µñ ‚•§ Type u‚ÇÅ)·µí·µñ (Type u‚ÇÅ)).obj yoneda.op ‚âÖ ùü≠ (C·µí·µñ ‚•§ Type u‚ÇÅ) :=
eq_to_iso (by tidy) ‚â™‚â´ curry.map_iso (iso_whisker_left (prod.swap _ _)
  (yoneda_lemma C ‚â™‚â´ iso_whisker_left
    (evaluation_uncurried C·µí·µñ (Type u‚ÇÅ)) ulift_functor_trivial : _)) ‚â™‚â´ eq_to_iso (by tidy)

end category_theory
