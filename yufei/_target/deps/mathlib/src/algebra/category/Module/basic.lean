/-
Copyright (c) 2019 Robert A. Spencer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert A. Spencer, Markus Himmel
-/
import algebra.category.Group.basic
import category_theory.limits.shapes.kernels
import category_theory.linear
import linear_algebra.basic

/-!
# The category of `R`-modules

`Module.{v} R` is the category of bundled `R`-modules with carrier in the universe `v`. We show
that it is preadditive and show that being an isomorphism, monomorphism and epimorphism is
equivalent to being a linear equivalence, an injective linear map and a surjective linear map,
respectively.

## Implementation details

To construct an object in the category of `R`-modules from a type `M` with an instance of the
`module` typeclass, write `of R M`. There is a coercion in the other direction.

Similarly, there is a coercion from morphisms in `Module R` to linear maps.

Unfortunately, Lean is not smart enough to see that, given an object `M : Module R`, the expression
`of R M`, where we coerce `M` to the carrier type, is definitionally equal to `M` itself.
This means that to go the other direction, i.e., from linear maps/equivalences to (iso)morphisms
in the category of `R`-modules, we have to take care not to inadvertently end up with an
`of R M` where `M` is already an object. Hence, given `f : M ‚Üí‚Çó[R] N`,
* if `M N : Module R`, simply use `f`;
* if `M : Module R` and `N` is an unbundled `R`-module, use `‚Üøf` or `as_hom_left f`;
* if `M` is an unbundled `R`-module and `N : Module R`, use `‚Üæf` or `as_hom_right f`;
* if `M` and `N` are unbundled `R`-modules, use `‚Üüf` or `as_hom f`.

Similarly, given `f : M ‚âÉ‚Çó[R] N`, use `to_Module_iso`, `to_Module_iso'_left`, `to_Module_iso'_right`
or `to_Module_iso'`, respectively.

The arrow notations are localized, so you may have to `open_locale Module` to use them. Note that
the notation for `as_hom_left` clashes with the notation used to promote functions between types to
morphisms in the category `Type`, so to avoid confusion, it is probably a good idea to avoid having
the locales `Module` and `category_theory.Type` open at the same time.

If you get an error when trying to apply a theorem and the `convert` tactic produces goals of the
form `M = of R M`, then you probably used an incorrect variant of `as_hom` or `to_Module_iso`.

-/

open category_theory
open category_theory.limits
open category_theory.limits.walking_parallel_pair

universes v u

variables (R : Type u) [ring R]

/-- The category of R-modules and their morphisms.

 Note that in the case of `R = ‚Ñ§`, we can not
impose here that the `‚Ñ§`-multiplication field from the module structure is defeq to the one coming
from the `is_add_comm_group` structure (contrary to what we do for all module structures in
mathlib), which creates some difficulties down the road. -/
structure Module :=
(carrier : Type v)
[is_add_comm_group : add_comm_group carrier]
[is_module : module R carrier]

attribute [instance] Module.is_add_comm_group Module.is_module

namespace Module

instance : has_coe_to_sort (Module.{v} R) (Type v) := ‚ü®Module.carrier‚ü©

instance Module_category : category (Module.{v} R) :=
{ hom   := Œª M N, M ‚Üí‚Çó[R] N,
  id    := Œª M, 1,
  comp  := Œª A B C f g, g.comp f,
  id_comp' := Œª X Y f, linear_map.id_comp _,
  comp_id' := Œª X Y f, linear_map.comp_id _,
  assoc' := Œª W X Y Z f g h, linear_map.comp_assoc _ _ _ }

instance Module_concrete_category : concrete_category.{v} (Module.{v} R) :=
{ forget := { obj := Œª R, R, map := Œª R S f, (f : R ‚Üí S) },
  forget_faithful := { } }

instance has_forget_to_AddCommGroup : has_forget‚ÇÇ (Module R) AddCommGroup :=
{ forget‚ÇÇ :=
  { obj := Œª M, AddCommGroup.of M,
    map := Œª M‚ÇÅ M‚ÇÇ f, linear_map.to_add_monoid_hom f } }

-- TODO: instantiate `linear_map_class` once that gets defined
instance (M N : Module R) : add_monoid_hom_class (M ‚ü∂ N) M N :=
{ coe := Œª f, f,
  .. linear_map.add_monoid_hom_class }

/-- The object in the category of R-modules associated to an R-module -/
def of (X : Type v) [add_comm_group X] [module R X] : Module R := ‚ü®X‚ü©

@[simp] lemma forget‚ÇÇ_obj (X : Module R) :
  (forget‚ÇÇ (Module R) AddCommGroup).obj X = AddCommGroup.of X :=
rfl

@[simp] lemma forget‚ÇÇ_obj_Module_of (X : Type v) [add_comm_group X] [module R X] :
  (forget‚ÇÇ (Module R) AddCommGroup).obj (of R X) = AddCommGroup.of X :=
rfl

@[simp] lemma forget‚ÇÇ_map (X Y : Module R) (f : X ‚ü∂ Y) :
  (forget‚ÇÇ (Module R) AddCommGroup).map f = linear_map.to_add_monoid_hom f :=
rfl

/-- Typecheck a `linear_map` as a morphism in `Module R`. -/
def of_hom {R : Type u} [ring R] {X Y : Type u} [add_comm_group X] [module R X] [add_comm_group Y]
  [module R Y] (f : X ‚Üí‚Çó[R] Y) : of R X ‚ü∂ of R Y := f

instance : has_zero (Module R) := ‚ü®of R punit‚ü©
instance : inhabited (Module R) := ‚ü®0‚ü©

@[simp]
lemma coe_of (X : Type u) [add_comm_group X] [module R X] : (of R X : Type u) = X := rfl

variables {R}

/-- Forgetting to the underlying type and then building the bundled object returns the original
module. -/
@[simps]
def of_self_iso (M : Module R) : Module.of R M ‚âÖ M :=
{ hom := ùüô M, inv := ùüô M }

instance : subsingleton (of R punit) :=
by { rw coe_of R punit, apply_instance }

instance : has_zero_object (Module.{v} R) :=
{ zero := 0,
  unique_to := Œª X,
  { default := (0 : punit ‚Üí‚Çó[R] X),
    uniq := Œª _, linear_map.ext $ Œª x,
      have h : x = 0, from dec_trivial,
      by simp only [h, linear_map.map_zero]},
  unique_from := Œª X,
  { default := (0 : X ‚Üí‚Çó[R] punit),
    uniq := Œª _, linear_map.ext $ Œª x, dec_trivial } }

variables {R} {M N U : Module.{v} R}

@[simp] lemma id_apply (m : M) : (ùüô M : M ‚Üí M) m = m := rfl

@[simp] lemma coe_comp (f : M ‚ü∂ N) (g : N ‚ü∂ U) :
  ((f ‚â´ g) : M ‚Üí U) = g ‚àò f := rfl

lemma comp_def (f : M ‚ü∂ N) (g : N ‚ü∂ U) : f ‚â´ g = g.comp f := rfl

end Module

variables {R}
variables {X‚ÇÅ X‚ÇÇ : Type v}

/-- Reinterpreting a linear map in the category of `R`-modules. -/
def Module.as_hom [add_comm_group X‚ÇÅ] [module R X‚ÇÅ] [add_comm_group X‚ÇÇ] [module R X‚ÇÇ] :
  (X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ) ‚Üí (Module.of R X‚ÇÅ ‚ü∂ Module.of R X‚ÇÇ) := id

localized "notation `‚Üü` f : 1024 := Module.as_hom f" in Module

/-- Reinterpreting a linear map in the category of `R`-modules. -/
def Module.as_hom_right [add_comm_group X‚ÇÅ] [module R X‚ÇÅ] {X‚ÇÇ : Module.{v} R} :
  (X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ) ‚Üí (Module.of R X‚ÇÅ ‚ü∂ X‚ÇÇ) := id

localized "notation `‚Üæ` f : 1024 := Module.as_hom_right f" in Module

/-- Reinterpreting a linear map in the category of `R`-modules. -/
def Module.as_hom_left {X‚ÇÅ : Module.{v} R} [add_comm_group X‚ÇÇ] [module R X‚ÇÇ] :
  (X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ) ‚Üí (X‚ÇÅ ‚ü∂ Module.of R X‚ÇÇ) := id

localized "notation `‚Üø` f : 1024 := Module.as_hom_left f" in Module

/-- Build an isomorphism in the category `Module R` from a `linear_equiv` between `module`s. -/
@[simps]
def linear_equiv.to_Module_iso
  {g‚ÇÅ : add_comm_group X‚ÇÅ} {g‚ÇÇ : add_comm_group X‚ÇÇ} {m‚ÇÅ : module R X‚ÇÅ} {m‚ÇÇ : module R X‚ÇÇ}
  (e : X‚ÇÅ ‚âÉ‚Çó[R] X‚ÇÇ) :
  Module.of R X‚ÇÅ ‚âÖ Module.of R X‚ÇÇ :=
{ hom := (e : X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ),
  inv := (e.symm : X‚ÇÇ ‚Üí‚Çó[R] X‚ÇÅ),
  hom_inv_id' := begin ext, exact e.left_inv x, end,
  inv_hom_id' := begin ext, exact e.right_inv x, end, }

/--
Build an isomorphism in the category `Module R` from a `linear_equiv` between `module`s.

This version is better than `linear_equiv_to_Module_iso` when applicable, because Lean can't see
`Module.of R M` is defeq to `M` when `M : Module R`. -/
@[simps]
def linear_equiv.to_Module_iso' {M N : Module.{v} R} (i : M ‚âÉ‚Çó[R] N) : M ‚âÖ N :=
{ hom := i,
  inv := i.symm,
  hom_inv_id' := linear_map.ext $ Œª x, by simp,
  inv_hom_id' := linear_map.ext $ Œª x, by simp }

/--
Build an isomorphism in the category `Module R` from a `linear_equiv` between `module`s.

This version is better than `linear_equiv_to_Module_iso` when applicable, because Lean can't see
`Module.of R M` is defeq to `M` when `M : Module R`. -/
@[simps]
def linear_equiv.to_Module_iso'_left {X‚ÇÅ : Module.{v} R} {g‚ÇÇ : add_comm_group X‚ÇÇ} {m‚ÇÇ : module R X‚ÇÇ}
  (e : X‚ÇÅ ‚âÉ‚Çó[R] X‚ÇÇ) : X‚ÇÅ ‚âÖ Module.of R X‚ÇÇ :=
{ hom := (e : X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ),
  inv := (e.symm : X‚ÇÇ ‚Üí‚Çó[R] X‚ÇÅ),
  hom_inv_id' := linear_map.ext $ Œª x, by simp,
  inv_hom_id' := linear_map.ext $ Œª x, by simp }

/--
Build an isomorphism in the category `Module R` from a `linear_equiv` between `module`s.

This version is better than `linear_equiv_to_Module_iso` when applicable, because Lean can't see
`Module.of R M` is defeq to `M` when `M : Module R`. -/
@[simps]
def linear_equiv.to_Module_iso'_right {g‚ÇÅ : add_comm_group X‚ÇÅ} {m‚ÇÅ : module R X‚ÇÅ}
  {X‚ÇÇ : Module.{v} R} (e : X‚ÇÅ ‚âÉ‚Çó[R] X‚ÇÇ) : Module.of R X‚ÇÅ ‚âÖ X‚ÇÇ :=
{ hom := (e : X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ),
  inv := (e.symm : X‚ÇÇ ‚Üí‚Çó[R] X‚ÇÅ),
  hom_inv_id' := linear_map.ext $ Œª x, by simp,
  inv_hom_id' := linear_map.ext $ Œª x, by simp }

namespace category_theory.iso

/-- Build a `linear_equiv` from an isomorphism in the category `Module R`. -/
@[simps]
def to_linear_equiv {X Y : Module R} (i : X ‚âÖ Y) : X ‚âÉ‚Çó[R] Y :=
{ to_fun    := i.hom,
  inv_fun   := i.inv,
  left_inv  := by tidy,
  right_inv := by tidy,
  map_add'  := by tidy,
  map_smul' := by tidy, }.

end category_theory.iso

/-- linear equivalences between `module`s are the same as (isomorphic to) isomorphisms
in `Module` -/
@[simps]
def linear_equiv_iso_Module_iso {X Y : Type u} [add_comm_group X] [add_comm_group Y] [module R X]
  [module R Y] :
  (X ‚âÉ‚Çó[R] Y) ‚âÖ (Module.of R X ‚âÖ Module.of R Y) :=
{ hom := Œª e, e.to_Module_iso,
  inv := Œª i, i.to_linear_equiv, }

namespace Module

instance : preadditive (Module.{v} R) :=
{ add_comp' := Œª P Q R f f' g,
    show (f + f') ‚â´ g = f ‚â´ g + f' ‚â´ g, by { ext, simp },
  comp_add' := Œª P Q R f g g',
    show f ‚â´ (g + g') = f ‚â´ g + f ‚â´ g', by { ext, simp } }

section
variables {S : Type u} [comm_ring S]

instance : linear S (Module.{v} S) :=
{ hom_module := Œª X Y, linear_map.module,
  smul_comp' := by { intros, ext, simp },
  comp_smul' := by { intros, ext, simp }, }

end

end Module

instance (M : Type u) [add_comm_group M] [module R M] : has_coe (submodule R M) (Module R) :=
‚ü® Œª N, Module.of R N ‚ü©
