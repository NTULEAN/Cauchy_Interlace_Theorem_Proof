/-
Copyright (c) 2021 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import topology.local_homeomorph

/-!
# Local homeomorphisms

This file defines local homeomorphisms.

## Main definitions

* `is_locally_homeomorph`: A function `f : X ‚Üí Y` satisfies `is_locally_homeomorph` if for each
  point `x : X`, the restriction of `f` to some open neighborhood `U` of `x` gives a homeomorphism
  between `U` and an open subset of `Y`.

  Note that `is_locally_homeomorph` is a global condition. This is in contrast to
  `local_homeomorph`, which is a homeomorphism between specific open subsets.
-/

open_locale topological_space

variables {X Y Z : Type*} [topological_space X] [topological_space Y] [topological_space Z]
  (g : Y ‚Üí Z) (f : X ‚Üí  Y)

/-- A function `f : X ‚Üí Y` satisfies `is_locally_homeomorph` if each `x : x` is contained in
  the source of some `e : local_homeomorph X Y` with `f = e`. -/
def is_locally_homeomorph :=
‚àÄ x : X, ‚àÉ e : local_homeomorph X Y, x ‚àà e.source ‚àß f = e

namespace is_locally_homeomorph

/-- Proves that `f` satisfies `is_locally_homeomorph`. The condition `h` is weaker than definition
of `is_locally_homeomorph`, since it only requires `e : local_homeomorph X Y` to agree with `f` on
its source `e.source`, as opposed to on the whole space `X`. -/
lemma mk (h : ‚àÄ x : X, ‚àÉ e : local_homeomorph X Y, x ‚àà e.source ‚àß ‚àÄ x, x ‚àà e.source ‚Üí f x = e x) :
  is_locally_homeomorph f :=
begin
  intro x,
  obtain ‚ü®e, hx, he‚ü© := h x,
  exact ‚ü®{ to_fun := f,
    map_source' := Œª x hx, by rw he x hx; exact e.map_source' hx,
    left_inv' := Œª x hx, by rw he x hx; exact e.left_inv' hx,
    right_inv' := Œª y hy, by rw he _ (e.map_target' hy); exact e.right_inv' hy,
    continuous_to_fun := (continuous_on_congr he).mpr e.continuous_to_fun,
    .. e }, hx, rfl‚ü©,
end

variables {g f}

lemma map_nhds_eq (hf : is_locally_homeomorph f) (x : X) : (ùìù x).map f = ùìù (f x) :=
begin
  obtain ‚ü®e, hx, rfl‚ü© := hf x,
  exact e.map_nhds_eq hx,
end

protected lemma continuous (hf : is_locally_homeomorph f) : continuous f :=
continuous_iff_continuous_at.mpr (Œª x, le_of_eq (hf.map_nhds_eq x))

lemma is_open_map (hf : is_locally_homeomorph f) : is_open_map f :=
is_open_map.of_nhds_le (Œª x, ge_of_eq (hf.map_nhds_eq x))

protected lemma comp (hg : is_locally_homeomorph g) (hf : is_locally_homeomorph f) :
  is_locally_homeomorph (g ‚àò f) :=
begin
  intro x,
  obtain ‚ü®eg, hxg, rfl‚ü© := hg (f x),
  obtain ‚ü®ef, hxf, rfl‚ü© := hf x,
  exact ‚ü®ef.trans eg, ‚ü®hxf, hxg‚ü©, rfl‚ü©,
end

end is_locally_homeomorph
