/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import analysis.mean_inequalities

/-!
# `L^p` distance on finite products of metric spaces
Given finitely many metric spaces, one can put the max distance on their product, but there is also
a whole family of natural distances, indexed by a real parameter `p ‚àà [1, ‚àû)`, that also induce
the product topology. We define them in this file. The distance on `Œ† i, Œ± i` is given by
$$
d(x, y) = \left(\sum d(x_i, y_i)^p\right)^{1/p}.
$$

We give instances of this construction for emetric spaces, metric spaces, normed groups and normed
spaces.

To avoid conflicting instances, all these are defined on a copy of the original Pi type, named
`pi_Lp p Œ±`. The assumpion `[fact (1 ‚â§ p)]` is required for the metric and normed space instances.

We ensure that the topology and uniform structure on `pi_Lp p Œ±` are (defeq to) the product
topology and product uniformity, to be able to use freely continuity statements for the coordinate
functions, for instance.

## Implementation notes

We only deal with the `L^p` distance on a product of finitely many metric spaces, which may be
distinct. A closely related construction is `lp`, the `L^p` norm on a product of (possibly
infinitely many) normed spaces, where the norm is
$$
\left(\sum ‚à•f (x)‚à•^p \right)^{1/p}.
$$
However, the topology induced by this construction is not the product topology, and some functions
have infinite `L^p` norm. These subtleties are not present in the case of finitely many metric
spaces, hence it is worth devoting a file to this specific case which is particularly well behaved.

Another related construction is `measure_theory.Lp`, the `L^p` norm on the space of functions from
a measure space to a normed space, where the norm is
$$
\left(\int ‚à•f (x)‚à•^p dŒº\right)^{1/p}.
$$
This has all the same subtleties as `lp`, and the further subtlety that this only
defines a seminorm (as almost everywhere zero functions have zero `L^p` norm).
The construction `pi_Lp` corresponds to the special case of `measure_theory.Lp` in which the basis
is a finite space equipped with the counting measure.

To prove that the topology (and the uniform structure) on a finite product with the `L^p` distance
are the same as those coming from the `L^‚àû` distance, we could argue that the `L^p` and `L^‚àû` norms
are equivalent on `‚Ñù^n` for abstract (norm equivalence) reasons. Instead, we give a more explicit
(easy) proof which provides a comparison between these two norms with explicit constants.

We also set up the theory for `pseudo_emetric_space` and `pseudo_metric_space`.
-/

open real set filter is_R_or_C
open_locale big_operators uniformity topological_space nnreal ennreal

noncomputable theory

variables {Œπ : Type*}

/-- A copy of a Pi type, on which we will put the `L^p` distance. Since the Pi type itself is
already endowed with the `L^‚àû` distance, we need the type synonym to avoid confusing typeclass
resolution. Also, we let it depend on `p`, to get a whole family of type on which we can put
different distances. -/
@[nolint unused_arguments]
def pi_Lp {Œπ : Type*} (p : ‚Ñù) (Œ± : Œπ ‚Üí Type*) : Type* := Œ† (i : Œπ), Œ± i

instance {Œπ : Type*} (p : ‚Ñù) (Œ± : Œπ ‚Üí Type*) [‚àÄ i, inhabited (Œ± i)] : inhabited (pi_Lp p Œ±) :=
‚ü®Œª i, default‚ü©

instance fact_one_le_one_real : fact ((1:‚Ñù) ‚â§ 1) := ‚ü®rfl.le‚ü©
instance fact_one_le_two_real : fact ((1:‚Ñù) ‚â§ 2) := ‚ü®one_le_two‚ü©

namespace pi_Lp

variables (p : ‚Ñù) [fact_one_le_p : fact (1 ‚â§ p)] (Œ± : Œπ ‚Üí Type*) (Œ≤ : Œπ ‚Üí Type*)

/-- Canonical bijection between `pi_Lp p Œ±` and the original Pi type. We introduce it to be able
to compare the `L^p` and `L^‚àû` distances through it. -/
protected def equiv : pi_Lp p Œ± ‚âÉ Œ† (i : Œπ), Œ± i :=
equiv.refl _

section
/-!
### The uniformity on finite `L^p` products is the product uniformity

In this section, we put the `L^p` edistance on `pi_Lp p Œ±`, and we check that the uniformity
coming from this edistance coincides with the product uniformity, by showing that the canonical
map to the Pi type (with the `L^‚àû` distance) is a uniform embedding, as it is both Lipschitz and
antiLipschitz.

We only register this emetric space structure as a temporary instance, as the true instance (to be
registered later) will have as uniformity exactly the product uniformity, instead of the one coming
from the edistance (which is equal to it, but not defeq). See Note [forgetful inheritance]
explaining why having definitionally the right uniformity is often important.
-/

variables [‚àÄ i, emetric_space (Œ± i)] [‚àÄ i, pseudo_emetric_space (Œ≤ i)] [fintype Œπ]
include fact_one_le_p

/-- Endowing the space `pi_Lp p Œ≤` with the `L^p` pseudoedistance. This definition is not
satisfactory, as it does not register the fact that the topology and the uniform structure coincide
with the product one. Therefore, we do not register it as an instance. Using this as a temporary
pseudoemetric space instance, we will show that the uniform structure is equal (but not defeq) to
the product one, and then register an instance in which we replace the uniform structure by the
product one using this pseudoemetric space and `pseudo_emetric_space.replace_uniformity`. -/
def pseudo_emetric_aux : pseudo_emetric_space (pi_Lp p Œ≤) :=
have pos : 0 < p := lt_of_lt_of_le zero_lt_one fact_one_le_p.out,
{ edist          := Œª f g, (‚àë (i : Œπ), (edist (f i) (g i)) ^ p) ^ (1/p),
  edist_self     := Œª f, by simp [edist, ennreal.zero_rpow_of_pos pos,
                                  ennreal.zero_rpow_of_pos (inv_pos.2 pos)],
  edist_comm     := Œª f g, by simp [edist, edist_comm],
  edist_triangle := Œª f g h, calc
    (‚àë (i : Œπ), edist (f i) (h i) ^ p) ^ (1 / p) ‚â§
    (‚àë (i : Œπ), (edist (f i) (g i) + edist (g i) (h i)) ^ p) ^ (1 / p) :
    begin
      apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ le_of_lt pos),
      refine finset.sum_le_sum (Œª i hi, _),
      exact ennreal.rpow_le_rpow (edist_triangle _ _ _) (le_trans zero_le_one fact_one_le_p.out)
    end
    ... ‚â§
    (‚àë (i : Œπ), edist (f i) (g i) ^ p) ^ (1 / p) + (‚àë (i : Œπ), edist (g i) (h i) ^ p) ^ (1 / p) :
      ennreal.Lp_add_le _ _ _ fact_one_le_p.out }

/-- Endowing the space `pi_Lp p Œ±` with the `L^p` edistance. This definition is not satisfactory,
as it does not register the fact that the topology and the uniform structure coincide with the
product one. Therefore, we do not register it as an instance. Using this as a temporary emetric
space instance, we will show that the uniform structure is equal (but not defeq) to the product
one, and then register an instance in which we replace the uniform structure by the product one
using this emetric space and `emetric_space.replace_uniformity`. -/
def emetric_aux : emetric_space (pi_Lp p Œ±) :=
{ eq_of_edist_eq_zero := Œª f g hfg,
  begin
    have pos : 0 < p := lt_of_lt_of_le zero_lt_one fact_one_le_p.out,
    letI h := pseudo_emetric_aux p Œ±,
    have h : edist f g = (‚àë (i : Œπ), (edist (f i) (g i)) ^ p) ^ (1/p) := rfl,
    simp [h, ennreal.rpow_eq_zero_iff, pos, asymm pos, finset.sum_eq_zero_iff_of_nonneg] at hfg,
    exact funext hfg
  end,
  ..pseudo_emetric_aux p Œ± }

local attribute [instance] pi_Lp.emetric_aux pi_Lp.pseudo_emetric_aux

lemma lipschitz_with_equiv : lipschitz_with 1 (pi_Lp.equiv p Œ≤) :=
begin
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one fact_one_le_p.out,
  have cancel : p * (1/p) = 1 := mul_div_cancel' 1 (ne_of_gt pos),
  assume x y,
  simp only [edist, forall_prop_of_true, one_mul, finset.mem_univ, finset.sup_le_iff,
             ennreal.coe_one],
  assume i,
  calc
  edist (x i) (y i) = (edist (x i) (y i) ^ p) ^ (1/p) :
    by simp [‚Üê ennreal.rpow_mul, cancel, -one_div]
  ... ‚â§ (‚àë (i : Œπ), edist (x i) (y i) ^ p) ^ (1 / p) :
  begin
    apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ le_of_lt pos),
    exact finset.single_le_sum (Œª i hi, (bot_le : (0 : ‚Ñù‚â•0‚àû) ‚â§ _)) (finset.mem_univ i)
  end
end

lemma antilipschitz_with_equiv :
  antilipschitz_with ((fintype.card Œπ : ‚Ñù‚â•0) ^ (1/p)) (pi_Lp.equiv p Œ≤) :=
begin
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one fact_one_le_p.out,
  have nonneg : 0 ‚â§ 1 / p := one_div_nonneg.2 (le_of_lt pos),
  have cancel : p * (1/p) = 1 := mul_div_cancel' 1 (ne_of_gt pos),
  assume x y,
  simp [edist, -one_div],
  calc (‚àë (i : Œπ), edist (x i) (y i) ^ p) ^ (1 / p) ‚â§
  (‚àë (i : Œπ), edist (pi_Lp.equiv p Œ≤ x) (pi_Lp.equiv p Œ≤ y) ^ p) ^ (1 / p) :
  begin
    apply ennreal.rpow_le_rpow _ nonneg,
    apply finset.sum_le_sum (Œª i hi, _),
    apply ennreal.rpow_le_rpow _ (le_of_lt pos),
    exact finset.le_sup (finset.mem_univ i)
  end
  ... = (((fintype.card Œπ : ‚Ñù‚â•0)) ^ (1/p) : ‚Ñù‚â•0) *
    edist (pi_Lp.equiv p Œ≤ x) (pi_Lp.equiv p Œ≤ y) :
  begin
    simp only [nsmul_eq_mul, finset.card_univ, ennreal.rpow_one, finset.sum_const,
      ennreal.mul_rpow_of_nonneg _ _ nonneg, ‚Üêennreal.rpow_mul, cancel],
    have : (fintype.card Œπ : ‚Ñù‚â•0‚àû) = (fintype.card Œπ : ‚Ñù‚â•0) :=
      (ennreal.coe_nat (fintype.card Œπ)).symm,
    rw [this, ennreal.coe_rpow_of_nonneg _ nonneg]
  end
end

lemma aux_uniformity_eq :
  ùì§ (pi_Lp p Œ≤) = @uniformity _ (Pi.uniform_space _) :=
begin
  have A : uniform_inducing (pi_Lp.equiv p Œ≤) :=
    (antilipschitz_with_equiv p Œ≤).uniform_inducing
    (lipschitz_with_equiv p Œ≤).uniform_continuous,
  have : (Œª (x : pi_Lp p Œ≤ √ó pi_Lp p Œ≤),
    ((pi_Lp.equiv p Œ≤) x.fst, (pi_Lp.equiv p Œ≤) x.snd)) = id,
    by ext i; refl,
  rw [‚Üê A.comap_uniformity, this, comap_id]
end

end

/-! ### Instances on finite `L^p` products -/

instance uniform_space [‚àÄ i, uniform_space (Œ≤ i)] : uniform_space (pi_Lp p Œ≤) :=
Pi.uniform_space _

variable [fintype Œπ]
include fact_one_le_p

/-- pseudoemetric space instance on the product of finitely many pseudoemetric spaces, using the
`L^p` pseudoedistance, and having as uniformity the product uniformity. -/
instance [‚àÄ i, pseudo_emetric_space (Œ≤ i)] : pseudo_emetric_space (pi_Lp p Œ≤) :=
(pseudo_emetric_aux p Œ≤).replace_uniformity (aux_uniformity_eq p Œ≤).symm

/-- emetric space instance on the product of finitely many emetric spaces, using the `L^p`
edistance, and having as uniformity the product uniformity. -/
instance [‚àÄ i, emetric_space (Œ± i)] : emetric_space (pi_Lp p Œ±) :=
(emetric_aux p Œ±).replace_uniformity (aux_uniformity_eq p Œ±).symm

omit fact_one_le_p
protected lemma edist {p : ‚Ñù} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄ i, pseudo_emetric_space (Œ≤ i)] (x y : pi_Lp p Œ≤) :
  edist x y = (‚àë (i : Œπ), (edist (x i) (y i)) ^ p) ^ (1/p) := rfl
include fact_one_le_p

/-- pseudometric space instance on the product of finitely many psuedometric spaces, using the
`L^p` distance, and having as uniformity the product uniformity. -/
instance [‚àÄ i, pseudo_metric_space (Œ≤ i)] : pseudo_metric_space (pi_Lp p Œ≤) :=
begin
  /- we construct the instance from the pseudo emetric space instance to avoid checking again that
  the uniformity is the same as the product uniformity, but we register nevertheless a nice formula
  for the distance -/
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one fact_one_le_p.out,
  refine pseudo_emetric_space.to_pseudo_metric_space_of_dist
    (Œªf g, (‚àë (i : Œπ), (dist (f i) (g i)) ^ p) ^ (1/p)) (Œª f g, _) (Œª f g, _),
  { simp [pi_Lp.edist, ennreal.rpow_eq_top_iff, asymm pos, pos,
          ennreal.sum_eq_top_iff, edist_ne_top] },
  { have A : ‚àÄ (i : Œπ), i ‚àà (finset.univ : finset Œπ) ‚Üí edist (f i) (g i) ^ p ‚â† ‚ä§ :=
      Œª i hi, by simp [lt_top_iff_ne_top, edist_ne_top, le_of_lt pos],
    simp [dist, -one_div, pi_Lp.edist, ‚Üê ennreal.to_real_rpow,
          ennreal.to_real_sum A, dist_edist] }
end

/-- metric space instance on the product of finitely many metric spaces, using the `L^p` distance,
and having as uniformity the product uniformity. -/
instance [‚àÄ i, metric_space (Œ± i)] : metric_space (pi_Lp p Œ±) :=
begin
  /- we construct the instance from the emetric space instance to avoid checking again that the
  uniformity is the same as the product uniformity, but we register nevertheless a nice formula
  for the distance -/
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one fact_one_le_p.out,
  refine emetric_space.to_metric_space_of_dist
    (Œªf g, (‚àë (i : Œπ), (dist (f i) (g i)) ^ p) ^ (1/p)) (Œª f g, _) (Œª f g, _),
  { simp [pi_Lp.edist, ennreal.rpow_eq_top_iff, asymm pos, pos,
          ennreal.sum_eq_top_iff, edist_ne_top] },
  { have A : ‚àÄ (i : Œπ), i ‚àà (finset.univ : finset Œπ) ‚Üí edist (f i) (g i) ^ p ‚â† ‚ä§ :=
      Œª i hi, by simp [edist_ne_top, pos.le],
    simp [dist, -one_div, pi_Lp.edist, ‚Üê ennreal.to_real_rpow,
          ennreal.to_real_sum A, dist_edist] }
end

omit fact_one_le_p
protected lemma dist {p : ‚Ñù} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄ i, pseudo_metric_space (Œ≤ i)] (x y : pi_Lp p Œ≤) :
  dist x y = (‚àë (i : Œπ), (dist (x i) (y i)) ^ p) ^ (1/p) := rfl
include fact_one_le_p

/-- seminormed group instance on the product of finitely many normed groups, using the `L^p`
norm. -/
instance semi_normed_group [‚àÄi, semi_normed_group (Œ≤ i)] : semi_normed_group (pi_Lp p Œ≤) :=
{ norm := Œªf, (‚àë (i : Œπ), norm (f i) ^ p) ^ (1/p),
  dist_eq := Œª x y, by { simp [pi_Lp.dist, dist_eq_norm, sub_eq_add_neg] },
  .. pi.add_comm_group }

/-- normed group instance on the product of finitely many normed groups, using the `L^p` norm. -/
instance normed_group [‚àÄi, normed_group (Œ± i)] : normed_group (pi_Lp p Œ±) :=
{ ..pi_Lp.semi_normed_group p Œ± }

omit fact_one_le_p
lemma norm_eq {p : ‚Ñù} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄi, semi_normed_group (Œ≤ i)] (f : pi_Lp p Œ≤) :
  ‚à•f‚à• = (‚àë (i : Œπ), ‚à•f i‚à• ^ p) ^ (1/p) := rfl

lemma norm_eq_of_nat {p : ‚Ñù} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄi, semi_normed_group (Œ≤ i)] (n : ‚Ñï) (h : p = n) (f : pi_Lp p Œ≤) :
  ‚à•f‚à• = (‚àë (i : Œπ), ‚à•f i‚à• ^ n) ^ (1/(n : ‚Ñù)) :=
by simp [norm_eq, h, real.sqrt_eq_rpow, ‚Üêreal.rpow_nat_cast]
include fact_one_le_p

variables (ùïú : Type*) [normed_field ùïú]

/-- The product of finitely many normed spaces is a normed space, with the `L^p` norm. -/
instance normed_space [‚àÄi, semi_normed_group (Œ≤ i)] [‚àÄi, normed_space ùïú (Œ≤ i)] :
  normed_space ùïú (pi_Lp p Œ≤) :=
{ norm_smul_le :=
  begin
    assume c f,
    have : p * (1 / p) = 1 := mul_div_cancel' 1 (lt_of_lt_of_le zero_lt_one fact_one_le_p.out).ne',
    simp only [pi_Lp.norm_eq, norm_smul, mul_rpow, norm_nonneg, ‚Üêfinset.mul_sum, pi.smul_apply],
    rw [mul_rpow (rpow_nonneg_of_nonneg (norm_nonneg _) _), ‚Üê rpow_mul (norm_nonneg _),
        this, rpow_one],
    exact finset.sum_nonneg (Œª i hi, rpow_nonneg_of_nonneg (norm_nonneg _) _)
  end,
  .. pi.module Œπ Œ≤ ùïú }

/- Register simplification lemmas for the applications of `pi_Lp` elements, as the usual lemmas
for Pi types will not trigger. -/
variables {ùïú p Œ±}
[‚àÄi, semi_normed_group (Œ≤ i)] [‚àÄi, normed_space ùïú (Œ≤ i)] (c : ùïú) (x y : pi_Lp p Œ≤) (i : Œπ)

@[simp] lemma add_apply : (x + y) i = x i + y i := rfl
@[simp] lemma sub_apply : (x - y) i = x i - y i := rfl
@[simp] lemma smul_apply : (c ‚Ä¢ x) i = c ‚Ä¢ x i := rfl
@[simp] lemma neg_apply : (-x) i = - (x i) := rfl

end pi_Lp
