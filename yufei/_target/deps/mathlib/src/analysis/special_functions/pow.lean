/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, S√©bastien Gou√´zel,
  R√©my Degenne
-/
import analysis.special_functions.complex.log

/-!
# Power function on `‚ÑÇ`, `‚Ñù`, `‚Ñù‚â•0`, and `‚Ñù‚â•0‚àû`

We construct the power functions `x ^ y` where
* `x` and `y` are complex numbers,
* or `x` and `y` are real numbers,
* or `x` is a nonnegative real number and `y` is a real number;
* or `x` is a number from `[0, +‚àû]` (a.k.a. `‚Ñù‚â•0‚àû`) and `y` is a real number.

We also prove basic properties of these functions.
-/

noncomputable theory

open_locale classical real topological_space nnreal ennreal filter
open filter

namespace complex

/-- The complex power function `x^y`, given by `x^y = exp(y log x)` (where `log` is the principal
determination of the logarithm), unless `x = 0` where one sets `0^0 = 1` and `0^y = 0` for
`y ‚â† 0`. -/
noncomputable def cpow (x y : ‚ÑÇ) : ‚ÑÇ :=
if x = 0
  then if y = 0
    then 1
    else 0
  else exp (log x * y)

noncomputable instance : has_pow ‚ÑÇ ‚ÑÇ := ‚ü®cpow‚ü©

@[simp] lemma cpow_eq_pow (x y : ‚ÑÇ) : cpow x y = x ^ y := rfl

lemma cpow_def (x y : ‚ÑÇ) : x ^ y =
  if x = 0
    then if y = 0
      then 1
      else 0
    else exp (log x * y) := rfl

lemma cpow_def_of_ne_zero {x : ‚ÑÇ} (hx : x ‚â† 0) (y : ‚ÑÇ) : x ^ y = exp (log x * y) := if_neg hx

@[simp] lemma cpow_zero (x : ‚ÑÇ) : x ^ (0 : ‚ÑÇ) = 1 := by simp [cpow_def]

@[simp] lemma cpow_eq_zero_iff (x y : ‚ÑÇ) : x ^ y = 0 ‚Üî x = 0 ‚àß y ‚â† 0 :=
by { simp only [cpow_def], split_ifs; simp [*, exp_ne_zero] }

@[simp] lemma zero_cpow {x : ‚ÑÇ} (h : x ‚â† 0) : (0 : ‚ÑÇ) ^ x = 0 :=
by simp [cpow_def, *]

lemma zero_cpow_eq_iff {x : ‚ÑÇ} {a : ‚ÑÇ} : 0 ^ x = a ‚Üî (x ‚â† 0 ‚àß a = 0) ‚à® (x = 0 ‚àß a = 1) :=
begin
  split,
  { intros hyp,
    simp [cpow_def] at hyp,
    by_cases x = 0,
    { subst h, simp only [if_true, eq_self_iff_true] at hyp, right, exact ‚ü®rfl, hyp.symm‚ü©},
    { rw if_neg h at hyp, left, exact ‚ü®h, hyp.symm‚ü©, }, },
  { rintro (‚ü®h, rfl‚ü©|‚ü®rfl,rfl‚ü©),
    { exact zero_cpow h, },
    { exact cpow_zero _, }, },
end

lemma eq_zero_cpow_iff {x : ‚ÑÇ} {a : ‚ÑÇ} : a = 0 ^ x ‚Üî (x ‚â† 0 ‚àß a = 0) ‚à® (x = 0 ‚àß a = 1) :=
by rw [‚Üêzero_cpow_eq_iff, eq_comm]

@[simp] lemma cpow_one (x : ‚ÑÇ) : x ^ (1 : ‚ÑÇ) = x :=
if hx : x = 0 then by simp [hx, cpow_def]
else by rw [cpow_def, if_neg (one_ne_zero : (1 : ‚ÑÇ) ‚â† 0), if_neg hx, mul_one, exp_log hx]

@[simp] lemma one_cpow (x : ‚ÑÇ) : (1 : ‚ÑÇ) ^ x = 1 :=
by rw cpow_def; split_ifs; simp [one_ne_zero, *] at *

lemma cpow_add {x : ‚ÑÇ} (y z : ‚ÑÇ) (hx : x ‚â† 0) : x ^ (y + z) = x ^ y * x ^ z :=
by simp [cpow_def]; simp [*, exp_add, mul_add] at *

lemma cpow_mul {x y : ‚ÑÇ} (z : ‚ÑÇ) (h‚ÇÅ : -œÄ < (log x * y).im) (h‚ÇÇ : (log x * y).im ‚â§ œÄ) :
  x ^ (y * z) = (x ^ y) ^ z :=
begin
  simp only [cpow_def],
  split_ifs;
  simp [*, exp_ne_zero, log_exp h‚ÇÅ h‚ÇÇ, mul_assoc] at *
end

lemma cpow_neg (x y : ‚ÑÇ) : x ^ -y = (x ^ y)‚Åª¬π :=
by simp [cpow_def]; split_ifs; simp [exp_neg]

lemma cpow_sub {x : ‚ÑÇ} (y z : ‚ÑÇ) (hx : x ‚â† 0) : x ^ (y - z) = x ^ y / x ^ z :=
by rw [sub_eq_add_neg, cpow_add _ _ hx, cpow_neg, div_eq_mul_inv]

lemma cpow_neg_one (x : ‚ÑÇ) : x ^ (-1 : ‚ÑÇ) = x‚Åª¬π :=
by simpa using cpow_neg x 1

@[simp] lemma cpow_nat_cast (x : ‚ÑÇ) : ‚àÄ (n : ‚Ñï), x ^ (n : ‚ÑÇ) = x ^ n
| 0       := by simp
| (n + 1) := if hx : x = 0 then by simp only [hx, pow_succ,
    complex.zero_cpow (nat.cast_ne_zero.2 (nat.succ_ne_zero _)), zero_mul]
  else by simp [cpow_add, hx, pow_add, cpow_nat_cast n]

@[simp] lemma cpow_int_cast (x : ‚ÑÇ) : ‚àÄ (n : ‚Ñ§), x ^ (n : ‚ÑÇ) = x ^ n
| (n : ‚Ñï) := by simp; refl
| -[1+ n] := by rw zpow_neg_succ_of_nat;
  simp only [int.neg_succ_of_nat_coe, int.cast_neg, complex.cpow_neg, inv_eq_one_div,
    int.cast_coe_nat, cpow_nat_cast]

lemma cpow_nat_inv_pow (x : ‚ÑÇ) {n : ‚Ñï} (hn : 0 < n) : (x ^ (n‚Åª¬π : ‚ÑÇ)) ^ n = x :=
begin
  suffices : im (log x * n‚Åª¬π) ‚àà set.Ioc (-œÄ) œÄ,
  { rw [‚Üê cpow_nat_cast, ‚Üê cpow_mul _ this.1 this.2, inv_mul_cancel, cpow_one],
    exact_mod_cast hn.ne' },
  rw [mul_comm, ‚Üê of_real_nat_cast, ‚Üê of_real_inv, of_real_mul_im, ‚Üê div_eq_inv_mul],
  have hn' : 0 < (n : ‚Ñù), by assumption_mod_cast,
  have hn1 : 1 ‚â§ (n : ‚Ñù), by exact_mod_cast (nat.succ_le_iff.2 hn),
  split,
  { rw lt_div_iff hn',
    calc -œÄ * n ‚â§ -œÄ * 1 : mul_le_mul_of_nonpos_left hn1 (neg_nonpos.2 real.pi_pos.le)
    ... = -œÄ : mul_one _
    ... < im (log x) : neg_pi_lt_log_im _ },
  { rw div_le_iff hn',
    calc im (log x) ‚â§ œÄ : log_im_le_pi _
    ... = œÄ * 1 : (mul_one œÄ).symm
    ... ‚â§ œÄ * n : mul_le_mul_of_nonneg_left hn1 real.pi_pos.le }
end

end complex

section lim

open complex

variables {Œ± : Type*}

lemma zero_cpow_eq_nhds {b : ‚ÑÇ} (hb : b ‚â† 0) :
  (0 : ‚ÑÇ).cpow =·∂†[ùìù b] 0 :=
begin
  suffices : ‚àÄ·∂† (x : ‚ÑÇ) in (ùìù b), x ‚â† 0,
  from this.mono (Œª x hx, by rw [cpow_eq_pow, zero_cpow hx, pi.zero_apply]),
  exact is_open.eventually_mem is_open_ne hb,
end

lemma cpow_eq_nhds {a b : ‚ÑÇ} (ha : a ‚â† 0) :
  (Œª x, x.cpow b) =·∂†[ùìù a] Œª x, exp (log x * b) :=
begin
  suffices : ‚àÄ·∂† (x : ‚ÑÇ) in (ùìù a), x ‚â† 0,
    from this.mono (Œª x hx, by { dsimp only, rw [cpow_eq_pow, cpow_def_of_ne_zero hx], }),
  exact is_open.eventually_mem is_open_ne ha,
end

lemma cpow_eq_nhds' {p : ‚ÑÇ √ó ‚ÑÇ} (hp_fst : p.fst ‚â† 0) :
  (Œª x, x.1 ^ x.2) =·∂†[ùìù p] Œª x, exp (log x.1 * x.2) :=
begin
  suffices : ‚àÄ·∂† (x : ‚ÑÇ √ó ‚ÑÇ) in (ùìù p), x.1 ‚â† 0,
    from this.mono (Œª x hx, by { dsimp only, rw cpow_def_of_ne_zero hx, }),
  refine is_open.eventually_mem _ hp_fst,
  change is_open {x : ‚ÑÇ √ó ‚ÑÇ | x.1 = 0}·∂ú,
  rw is_open_compl_iff,
  exact is_closed_eq continuous_fst continuous_const,
end

lemma continuous_at_const_cpow {a b : ‚ÑÇ} (ha : a ‚â† 0) : continuous_at (cpow a) b :=
begin
  have cpow_eq : cpow a = Œª b, exp (log a * b),
    by { ext1 b, rw [cpow_eq_pow, cpow_def_of_ne_zero ha], },
  rw cpow_eq,
  exact continuous_exp.continuous_at.comp (continuous_at.mul continuous_at_const continuous_at_id),
end

lemma continuous_at_const_cpow' {a b : ‚ÑÇ} (h : b ‚â† 0) : continuous_at (cpow a) b :=
begin
  by_cases ha : a = 0,
  { rw [ha, continuous_at_congr (zero_cpow_eq_nhds h)], exact continuous_at_const, },
  { exact continuous_at_const_cpow ha, },
end

lemma continuous_at_cpow_const {a b : ‚ÑÇ} (ha : 0 < a.re ‚à® a.im ‚â† 0) :
  continuous_at (Œª x, cpow x b) a :=
begin
  have ha_ne_zero : a ‚â† 0, by { intro h, cases ha; { rw h at ha, simpa using ha, }, },
  rw continuous_at_congr (cpow_eq_nhds ha_ne_zero),
  refine continuous_exp.continuous_at.comp _,
  exact continuous_at.mul (continuous_at_clog ha) continuous_at_const,
end

lemma continuous_at_cpow {p : ‚ÑÇ √ó ‚ÑÇ} (hp_fst : 0 < p.fst.re ‚à® p.fst.im ‚â† 0) :
  continuous_at (Œª x : ‚ÑÇ √ó ‚ÑÇ, x.1 ^ x.2) p :=
begin
  have hp_fst_ne_zero : p.fst ‚â† 0,
    by { intro h, cases hp_fst; { rw h at hp_fst, simpa using hp_fst, }, },
  rw continuous_at_congr (cpow_eq_nhds' hp_fst_ne_zero),
  refine continuous_exp.continuous_at.comp _,
  refine continuous_at.mul (continuous_at.comp _ continuous_fst.continuous_at)
    continuous_snd.continuous_at,
  exact continuous_at_clog hp_fst,
end

lemma filter.tendsto.cpow {l : filter Œ±} {f g : Œ± ‚Üí ‚ÑÇ} {a b : ‚ÑÇ} (hf : tendsto f l (ùìù a))
  (hg : tendsto g l (ùìù b)) (ha : 0 < a.re ‚à® a.im ‚â† 0) :
  tendsto (Œª x, f x ^ g x) l (ùìù (a ^ b)) :=
(@continuous_at_cpow (a,b) ha).tendsto.comp (hf.prod_mk_nhds hg)

lemma filter.tendsto.const_cpow {l : filter Œ±} {f : Œ± ‚Üí ‚ÑÇ} {a b : ‚ÑÇ} (hf : tendsto f l (ùìù b))
  (h : a ‚â† 0 ‚à® b ‚â† 0) :
  tendsto (Œª x, a ^ f x) l (ùìù (a ^ b)) :=
begin
  cases h,
  { exact (continuous_at_const_cpow h).tendsto.comp hf, },
  { exact (continuous_at_const_cpow' h).tendsto.comp hf, },
end

variables [topological_space Œ±] {f g : Œ± ‚Üí ‚ÑÇ} {s : set Œ±} {a : Œ±}

lemma continuous_within_at.cpow (hf : continuous_within_at f s a) (hg : continuous_within_at g s a)
  (h0 : 0 < (f a).re ‚à® (f a).im ‚â† 0) :
  continuous_within_at (Œª x, f x ^ g x) s a :=
hf.cpow hg h0

lemma continuous_within_at.const_cpow {b : ‚ÑÇ} (hf : continuous_within_at f s a)
  (h : b ‚â† 0 ‚à® f a ‚â† 0) :
  continuous_within_at (Œª x, b ^ f x) s a :=
hf.const_cpow h

lemma continuous_at.cpow (hf : continuous_at f a) (hg : continuous_at g a)
  (h0 : 0 < (f a).re ‚à® (f a).im ‚â† 0) :
  continuous_at (Œª x, f x ^ g x) a :=
hf.cpow hg h0

lemma continuous_at.const_cpow {b : ‚ÑÇ} (hf : continuous_at f a) (h : b ‚â† 0 ‚à® f a ‚â† 0) :
  continuous_at (Œª x, b ^ f x) a :=
hf.const_cpow h

lemma continuous_on.cpow (hf : continuous_on f s) (hg : continuous_on g s)
  (h0 : ‚àÄ a ‚àà s, 0 < (f a).re ‚à® (f a).im ‚â† 0) :
  continuous_on (Œª x, f x ^ g x) s :=
Œª a ha, (hf a ha).cpow (hg a ha) (h0 a ha)

lemma continuous_on.const_cpow {b : ‚ÑÇ} (hf : continuous_on f s) (h : b ‚â† 0 ‚à® ‚àÄ a ‚àà s, f a ‚â† 0) :
  continuous_on (Œª x, b ^ f x) s :=
Œª a ha, (hf a ha).const_cpow (h.imp id $ Œª h, h a ha)

lemma continuous.cpow (hf : continuous f) (hg : continuous g)
  (h0 : ‚àÄ a, 0 < (f a).re ‚à® (f a).im ‚â† 0) :
  continuous (Œª x, f x ^ g x) :=
continuous_iff_continuous_at.2 $ Œª a, (hf.continuous_at.cpow hg.continuous_at (h0 a))

lemma continuous.const_cpow {b : ‚ÑÇ} (hf : continuous f) (h : b ‚â† 0 ‚à® ‚àÄ a, f a ‚â† 0) :
  continuous (Œª x, b ^ f x) :=
continuous_iff_continuous_at.2 $ Œª a, (hf.continuous_at.const_cpow $ h.imp id $ Œª h, h a)

end lim

namespace real

/-- The real power function `x^y`, defined as the real part of the complex power function.
For `x > 0`, it is equal to `exp(y log x)`. For `x = 0`, one sets `0^0=1` and `0^y=0` for `y ‚â† 0`.
For `x < 0`, the definition is somewhat arbitary as it depends on the choice of a complex
determination of the logarithm. With our conventions, it is equal to `exp (y log x) cos (œÄy)`. -/
noncomputable def rpow (x y : ‚Ñù) := ((x : ‚ÑÇ) ^ (y : ‚ÑÇ)).re

noncomputable instance : has_pow ‚Ñù ‚Ñù := ‚ü®rpow‚ü©

@[simp] lemma rpow_eq_pow (x y : ‚Ñù) : rpow x y = x ^ y := rfl

lemma rpow_def (x y : ‚Ñù) : x ^ y = ((x : ‚ÑÇ) ^ (y : ‚ÑÇ)).re := rfl

lemma rpow_def_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : x ^ y =
  if x = 0
    then if y = 0
      then 1
      else 0
    else exp (log x * y) :=
by simp only [rpow_def, complex.cpow_def];
  split_ifs;
  simp [*, (complex.of_real_log hx).symm, -complex.of_real_mul, -is_R_or_C.of_real_mul,
    (complex.of_real_mul _ _).symm, complex.exp_of_real_re] at *

lemma rpow_def_of_pos {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : x ^ y = exp (log x * y) :=
by rw [rpow_def_of_nonneg (le_of_lt hx), if_neg (ne_of_gt hx)]

lemma exp_mul (x y : ‚Ñù) : exp (x * y) = (exp x) ^ y :=
by rw [rpow_def_of_pos (exp_pos _), log_exp]

lemma rpow_eq_zero_iff_of_nonneg {x y : ‚Ñù} (hx : 0 ‚â§ x) : x ^ y = 0 ‚Üî x = 0 ‚àß y ‚â† 0 :=
by { simp only [rpow_def_of_nonneg hx], split_ifs; simp [*, exp_ne_zero] }

open_locale real

lemma rpow_def_of_neg {x : ‚Ñù} (hx : x < 0) (y : ‚Ñù) : x ^ y = exp (log x * y) * cos (y * œÄ) :=
begin
  rw [rpow_def, complex.cpow_def, if_neg],
  have : complex.log x * y = ‚Üë(log(-x) * y) + ‚Üë(y * œÄ) * complex.I,
  { simp only [complex.log, abs_of_neg hx, complex.arg_of_real_of_neg hx,
      complex.abs_of_real, complex.of_real_mul], ring },
  { rw [this, complex.exp_add_mul_I, ‚Üê complex.of_real_exp, ‚Üê complex.of_real_cos,
      ‚Üê complex.of_real_sin, mul_add, ‚Üê complex.of_real_mul, ‚Üê mul_assoc, ‚Üê complex.of_real_mul,
      complex.add_re, complex.of_real_re, complex.mul_re, complex.I_re, complex.of_real_im,
      real.log_neg_eq_log],
    ring },
  { rw complex.of_real_eq_zero, exact ne_of_lt hx }
end

lemma rpow_def_of_nonpos {x : ‚Ñù} (hx : x ‚â§ 0) (y : ‚Ñù) : x ^ y =
  if x = 0
    then if y = 0
      then 1
      else 0
    else exp (log x * y) * cos (y * œÄ) :=
by split_ifs; simp [rpow_def, *]; exact rpow_def_of_neg (lt_of_le_of_ne hx h) _

lemma rpow_pos_of_pos {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : 0 < x ^ y :=
by rw rpow_def_of_pos hx; apply exp_pos

@[simp] lemma rpow_zero (x : ‚Ñù) : x ^ (0 : ‚Ñù) = 1 := by simp [rpow_def]

@[simp] lemma zero_rpow {x : ‚Ñù} (h : x ‚â† 0) : (0 : ‚Ñù) ^ x = 0 :=
by simp [rpow_def, *]

lemma zero_rpow_eq_iff {x : ‚Ñù} {a : ‚Ñù} : 0 ^ x = a ‚Üî (x ‚â† 0 ‚àß a = 0) ‚à® (x = 0 ‚àß a = 1) :=
begin
  split,
  { intros hyp,
    simp [rpow_def] at hyp,
    by_cases x = 0,
    { subst h,
      simp only [complex.one_re, complex.of_real_zero, complex.cpow_zero] at hyp,
      exact or.inr ‚ü®rfl, hyp.symm‚ü©},
    { rw complex.zero_cpow (complex.of_real_ne_zero.mpr h) at hyp,
      exact or.inl ‚ü®h, hyp.symm‚ü©, }, },
  { rintro (‚ü®h,rfl‚ü©|‚ü®rfl,rfl‚ü©),
    { exact zero_rpow h, },
    { exact rpow_zero _, }, },
end

lemma eq_zero_rpow_iff {x : ‚Ñù} {a : ‚Ñù} : a = 0 ^ x ‚Üî (x ‚â† 0 ‚àß a = 0) ‚à® (x = 0 ‚àß a = 1) :=
by rw [‚Üêzero_rpow_eq_iff, eq_comm]

@[simp] lemma rpow_one (x : ‚Ñù) : x ^ (1 : ‚Ñù) = x := by simp [rpow_def]

@[simp] lemma one_rpow (x : ‚Ñù) : (1 : ‚Ñù) ^ x = 1 := by simp [rpow_def]

lemma zero_rpow_le_one (x : ‚Ñù) : (0 : ‚Ñù) ^ x ‚â§ 1 :=
by { by_cases h : x = 0; simp [h, zero_le_one] }

lemma zero_rpow_nonneg (x : ‚Ñù) : 0 ‚â§ (0 : ‚Ñù) ^ x :=
by { by_cases h : x = 0; simp [h, zero_le_one] }

lemma rpow_nonneg_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : 0 ‚â§ x ^ y :=
by rw [rpow_def_of_nonneg hx];
  split_ifs; simp only [zero_le_one, le_refl, le_of_lt (exp_pos _)]

lemma abs_rpow_le_abs_rpow (x y : ‚Ñù) : |x ^ y| ‚â§ |x| ^ y :=
begin
  rcases lt_trichotomy 0 x with (hx|rfl|hx),
  { rw [abs_of_pos hx, abs_of_pos (rpow_pos_of_pos hx _)] },
  { rw [abs_zero, abs_of_nonneg (rpow_nonneg_of_nonneg le_rfl _)] },
  { rw [abs_of_neg hx, rpow_def_of_neg hx, rpow_def_of_pos (neg_pos.2 hx), log_neg_eq_log,
      abs_mul, abs_of_pos (exp_pos _)],
    exact mul_le_of_le_one_right (exp_pos _).le (abs_cos_le_one _) }
end

lemma abs_rpow_le_exp_log_mul (x y : ‚Ñù) : |x ^ y| ‚â§ exp (log x * y) :=
begin
  refine (abs_rpow_le_abs_rpow x y).trans _,
  by_cases hx : x = 0,
  { by_cases hy : y = 0; simp [hx, hy, zero_le_one] },
  { rw [rpow_def_of_pos (abs_pos.2 hx), log_abs] }
end

lemma abs_rpow_of_nonneg {x y : ‚Ñù} (hx_nonneg : 0 ‚â§ x) : |x ^ y| = |x| ^ y :=
begin
  have h_rpow_nonneg : 0 ‚â§ x ^ y, from real.rpow_nonneg_of_nonneg hx_nonneg _,
  rw [abs_eq_self.mpr hx_nonneg, abs_eq_self.mpr h_rpow_nonneg],
end

lemma norm_rpow_of_nonneg {x y : ‚Ñù} (hx_nonneg : 0 ‚â§ x) : ‚à•x ^ y‚à• = ‚à•x‚à• ^ y :=
by { simp_rw real.norm_eq_abs, exact abs_rpow_of_nonneg hx_nonneg, }

end real

namespace complex

lemma of_real_cpow {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : ((x ^ y : ‚Ñù) : ‚ÑÇ) = (x : ‚ÑÇ) ^ (y : ‚ÑÇ) :=
by simp [real.rpow_def_of_nonneg hx, complex.cpow_def]; split_ifs; simp [complex.of_real_log hx]

@[simp] lemma abs_cpow_real (x : ‚ÑÇ) (y : ‚Ñù) : abs (x ^ (y : ‚ÑÇ)) = x.abs ^ y :=
begin
  rw [real.rpow_def_of_nonneg (abs_nonneg _), complex.cpow_def],
  split_ifs;
  simp [*, abs_of_nonneg (le_of_lt (real.exp_pos _)), complex.log, complex.exp_add,
    add_mul, mul_right_comm _ I, exp_mul_I, abs_cos_add_sin_mul_I,
    (complex.of_real_mul _ _).symm, -complex.of_real_mul, -is_R_or_C.of_real_mul] at *
end

@[simp] lemma abs_cpow_inv_nat (x : ‚ÑÇ) (n : ‚Ñï) : abs (x ^ (n‚Åª¬π : ‚ÑÇ)) = x.abs ^ (n‚Åª¬π : ‚Ñù) :=
by rw ‚Üê abs_cpow_real; simp [-abs_cpow_real]

end complex

namespace real

variables {x y z : ‚Ñù}

lemma rpow_add {x : ‚Ñù} (hx : 0 < x) (y z : ‚Ñù) : x ^ (y + z) = x ^ y * x ^ z :=
by simp only [rpow_def_of_pos hx, mul_add, exp_add]

lemma rpow_add' {x : ‚Ñù} (hx : 0 ‚â§ x) {y z : ‚Ñù} (h : y + z ‚â† 0) : x ^ (y + z) = x ^ y * x ^ z :=
begin
  rcases hx.eq_or_lt with rfl|pos,
  { rw [zero_rpow h, zero_eq_mul],
    have : y ‚â† 0 ‚à® z ‚â† 0, from not_and_distrib.1 (Œª ‚ü®hy, hz‚ü©, h $ hy.symm ‚ñ∏ hz.symm ‚ñ∏ zero_add 0),
    exact this.imp zero_rpow zero_rpow },
  { exact rpow_add pos _ _ }
end

/-- For `0 ‚â§ x`, the only problematic case in the equality `x ^ y * x ^ z = x ^ (y + z)` is for
`x = 0` and `y + z = 0`, where the right hand side is `1` while the left hand side can vanish.
The inequality is always true, though, and given in this lemma. -/
lemma le_rpow_add {x : ‚Ñù} (hx : 0 ‚â§ x) (y z : ‚Ñù) : x ^ y * x ^ z ‚â§ x ^ (y + z) :=
begin
  rcases le_iff_eq_or_lt.1 hx with H|pos,
  { by_cases h : y + z = 0,
    { simp only [H.symm, h, rpow_zero],
      calc (0 : ‚Ñù) ^ y * 0 ^ z ‚â§ 1 * 1 :
        mul_le_mul (zero_rpow_le_one y) (zero_rpow_le_one z) (zero_rpow_nonneg z) zero_le_one
      ... = 1 : by simp },
    { simp [rpow_add', ‚Üê H, h] } },
  { simp [rpow_add pos] }
end

lemma rpow_mul {x : ‚Ñù} (hx : 0 ‚â§ x) (y z : ‚Ñù) : x ^ (y * z) = (x ^ y) ^ z :=
by rw [‚Üê complex.of_real_inj, complex.of_real_cpow (rpow_nonneg_of_nonneg hx _),
    complex.of_real_cpow hx, complex.of_real_mul, complex.cpow_mul, complex.of_real_cpow hx];
  simp only [(complex.of_real_mul _ _).symm, (complex.of_real_log hx).symm,
    complex.of_real_im, neg_lt_zero, pi_pos, le_of_lt pi_pos]

lemma rpow_neg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : x ^ -y = (x ^ y)‚Åª¬π :=
by simp only [rpow_def_of_nonneg hx]; split_ifs; simp [*, exp_neg] at *

lemma rpow_sub {x : ‚Ñù} (hx : 0 < x) (y z : ‚Ñù) : x ^ (y - z) = x ^ y / x ^ z :=
by simp only [sub_eq_add_neg, rpow_add hx, rpow_neg (le_of_lt hx), div_eq_mul_inv]

lemma rpow_sub' {x : ‚Ñù} (hx : 0 ‚â§ x) {y z : ‚Ñù} (h : y - z ‚â† 0) :
  x ^ (y - z) = x ^ y / x ^ z :=
by { simp only [sub_eq_add_neg] at h ‚ä¢, simp only [rpow_add' hx h, rpow_neg hx, div_eq_mul_inv] }

lemma rpow_add_int {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñ§) : x ^ (y + n) = x ^ y * x ^ n :=
by rw [rpow_def, complex.of_real_add, complex.cpow_add _ _ (complex.of_real_ne_zero.mpr hx),
  complex.of_real_int_cast, complex.cpow_int_cast, ‚Üê complex.of_real_zpow, mul_comm,
  complex.of_real_mul_re, ‚Üê rpow_def, mul_comm]

lemma rpow_add_nat {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñï) : x ^ (y + n) = x ^ y * x ^ n :=
rpow_add_int hx y n

lemma rpow_sub_int {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñ§) : x ^ (y - n) = x ^ y / x ^ n :=
by simpa using rpow_add_int hx y (-n)

lemma rpow_sub_nat {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñï) : x ^ (y - n) = x ^ y / x ^ n :=
rpow_sub_int hx y n

lemma rpow_add_one {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) : x ^ (y + 1) = x ^ y * x :=
by simpa using rpow_add_nat hx y 1

lemma rpow_sub_one {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) : x ^ (y - 1) = x ^ y / x :=
by simpa using rpow_sub_nat hx y 1

@[simp, norm_cast] lemma rpow_int_cast (x : ‚Ñù) (n : ‚Ñ§) : x ^ (n : ‚Ñù) = x ^ n :=
by simp only [rpow_def, ‚Üê complex.of_real_zpow, complex.cpow_int_cast,
  complex.of_real_int_cast, complex.of_real_re]

@[simp, norm_cast] lemma rpow_nat_cast (x : ‚Ñù) (n : ‚Ñï) : x ^ (n : ‚Ñù) = x ^ n :=
rpow_int_cast x n

lemma rpow_neg_one (x : ‚Ñù) : x ^ (-1 : ‚Ñù) = x‚Åª¬π :=
begin
  suffices H : x ^ ((-1 : ‚Ñ§) : ‚Ñù) = x‚Åª¬π, by exact_mod_cast H,
  simp only [rpow_int_cast, zpow_one, zpow_neg‚ÇÄ],
end

lemma mul_rpow {x y z : ‚Ñù} (h : 0 ‚â§ x) (h‚ÇÅ : 0 ‚â§ y) : (x*y)^z = x^z * y^z :=
begin
  iterate 3 { rw real.rpow_def_of_nonneg }, split_ifs; simp * at *,
  { have hx : 0 < x,
    { cases lt_or_eq_of_le h with h‚ÇÇ h‚ÇÇ, { exact h‚ÇÇ },
      exfalso, apply h_2, exact eq.symm h‚ÇÇ },
    have hy : 0 < y,
    { cases lt_or_eq_of_le h‚ÇÅ with h‚ÇÇ h‚ÇÇ, { exact h‚ÇÇ },
      exfalso, apply h_3, exact eq.symm h‚ÇÇ },
    rw [log_mul (ne_of_gt hx) (ne_of_gt hy), add_mul, exp_add]},
  { exact h‚ÇÅ },
  { exact h },
  { exact mul_nonneg h h‚ÇÅ },
end

lemma inv_rpow (hx : 0 ‚â§ x) (y : ‚Ñù) : (x‚Åª¬π)^y = (x^y)‚Åª¬π :=
by simp only [‚Üê rpow_neg_one, ‚Üê rpow_mul hx, mul_comm]

lemma div_rpow (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (z : ‚Ñù) : (x / y) ^ z = x^z / y^z :=
by simp only [div_eq_mul_inv, mul_rpow hx (inv_nonneg.2 hy), inv_rpow hy]

lemma log_rpow {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : log (x^y) = y * (log x) :=
begin
  apply exp_injective,
  rw [exp_log (rpow_pos_of_pos hx y), ‚Üê exp_log hx, mul_comm, rpow_def_of_pos (exp_pos (log x)) y],
end

lemma rpow_lt_rpow (hx : 0 ‚â§ x) (hxy : x < y) (hz : 0 < z) : x^z < y^z :=
begin
  rw le_iff_eq_or_lt at hx, cases hx,
  { rw [‚Üê hx, zero_rpow (ne_of_gt hz)], exact rpow_pos_of_pos (by rwa ‚Üê hx at hxy) _ },
  rw [rpow_def_of_pos hx, rpow_def_of_pos (lt_trans hx hxy), exp_lt_exp],
  exact mul_lt_mul_of_pos_right (log_lt_log hx hxy) hz
end

lemma rpow_le_rpow {x y z: ‚Ñù} (h : 0 ‚â§ x) (h‚ÇÅ : x ‚â§ y) (h‚ÇÇ : 0 ‚â§ z) : x^z ‚â§ y^z :=
begin
  rcases eq_or_lt_of_le h‚ÇÅ with rfl|h‚ÇÅ', { refl },
  rcases eq_or_lt_of_le h‚ÇÇ with rfl|h‚ÇÇ', { simp },
  exact le_of_lt (rpow_lt_rpow h h‚ÇÅ' h‚ÇÇ')
end

lemma rpow_lt_rpow_iff (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z < y ^ z ‚Üî x < y :=
‚ü®lt_imp_lt_of_le_imp_le $ Œª h, rpow_le_rpow hy h (le_of_lt hz), Œª h, rpow_lt_rpow hx h hz‚ü©

lemma rpow_le_rpow_iff (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z ‚â§ y ^ z ‚Üî x ‚â§ y :=
le_iff_le_iff_lt_iff_lt.2 $ rpow_lt_rpow_iff hy hx hz

lemma rpow_lt_rpow_of_exponent_lt (hx : 1 < x) (hyz : y < z) : x^y < x^z :=
begin
  repeat {rw [rpow_def_of_pos (lt_trans zero_lt_one hx)]},
  rw exp_lt_exp, exact mul_lt_mul_of_pos_left hyz (log_pos hx),
end

lemma rpow_le_rpow_of_exponent_le (hx : 1 ‚â§ x) (hyz : y ‚â§ z) : x^y ‚â§ x^z :=
begin
  repeat {rw [rpow_def_of_pos (lt_of_lt_of_le zero_lt_one hx)]},
  rw exp_le_exp, exact mul_le_mul_of_nonneg_left hyz (log_nonneg hx),
end

@[simp] lemma rpow_le_rpow_left_iff (hx : 1 < x) : x ^ y ‚â§ x ^ z ‚Üî y ‚â§ z :=
begin
  have x_pos : 0 < x := lt_trans zero_lt_one hx,
  rw [‚Üêlog_le_log (rpow_pos_of_pos x_pos y) (rpow_pos_of_pos x_pos z),
      log_rpow x_pos, log_rpow x_pos, mul_le_mul_right (log_pos hx)],
end

@[simp] lemma rpow_lt_rpow_left_iff (hx : 1 < x) : x ^ y < x ^ z ‚Üî y < z :=
by rw [lt_iff_not_ge', rpow_le_rpow_left_iff hx, lt_iff_not_ge']

lemma rpow_lt_rpow_of_exponent_gt (hx0 : 0 < x) (hx1 : x < 1) (hyz : z < y) :
  x^y < x^z :=
begin
  repeat {rw [rpow_def_of_pos hx0]},
  rw exp_lt_exp, exact mul_lt_mul_of_neg_left hyz (log_neg hx0 hx1),
end

lemma rpow_le_rpow_of_exponent_ge (hx0 : 0 < x) (hx1 : x ‚â§ 1) (hyz : z ‚â§ y) :
  x^y ‚â§ x^z :=
begin
  repeat {rw [rpow_def_of_pos hx0]},
  rw exp_le_exp, exact mul_le_mul_of_nonpos_left hyz (log_nonpos (le_of_lt hx0) hx1),
end

@[simp] lemma rpow_le_rpow_left_iff_of_base_lt_one (hx0 : 0 < x) (hx1 : x < 1) :
  x ^ y ‚â§ x ^ z ‚Üî z ‚â§ y :=
begin
  rw [‚Üêlog_le_log (rpow_pos_of_pos hx0 y) (rpow_pos_of_pos hx0 z),
      log_rpow hx0, log_rpow hx0, mul_le_mul_right_of_neg (log_neg hx0 hx1)],
end

@[simp] lemma rpow_lt_rpow_left_iff_of_base_lt_one (hx0 : 0 < x) (hx1 : x < 1) :
  x ^ y < x ^ z ‚Üî z < y :=
by rw [lt_iff_not_ge', rpow_le_rpow_left_iff_of_base_lt_one hx0 hx1, lt_iff_not_ge']

lemma rpow_lt_one {x z : ‚Ñù} (hx1 : 0 ‚â§ x) (hx2 : x < 1) (hz : 0 < z) : x^z < 1 :=
by { rw ‚Üê one_rpow z, exact rpow_lt_rpow hx1 hx2 hz }

lemma rpow_le_one {x z : ‚Ñù} (hx1 : 0 ‚â§ x) (hx2 : x ‚â§ 1) (hz : 0 ‚â§ z) : x^z ‚â§ 1 :=
by { rw ‚Üê one_rpow z, exact rpow_le_rpow hx1 hx2 hz }

lemma rpow_lt_one_of_one_lt_of_neg {x z : ‚Ñù} (hx : 1 < x) (hz : z < 0) : x^z < 1 :=
by { convert rpow_lt_rpow_of_exponent_lt hx hz, exact (rpow_zero x).symm }

lemma rpow_le_one_of_one_le_of_nonpos {x z : ‚Ñù} (hx : 1 ‚â§ x) (hz : z ‚â§ 0) : x^z ‚â§ 1 :=
by { convert rpow_le_rpow_of_exponent_le hx hz, exact (rpow_zero x).symm }

lemma one_lt_rpow {x z : ‚Ñù} (hx : 1 < x) (hz : 0 < z) : 1 < x^z :=
by { rw ‚Üê one_rpow z, exact rpow_lt_rpow zero_le_one hx hz }

lemma one_le_rpow {x z : ‚Ñù} (hx : 1 ‚â§ x) (hz : 0 ‚â§ z) : 1 ‚â§ x^z :=
by { rw ‚Üê one_rpow z, exact rpow_le_rpow zero_le_one hx hz }

lemma one_lt_rpow_of_pos_of_lt_one_of_neg (hx1 : 0 < x) (hx2 : x < 1) (hz : z < 0) :
  1 < x^z :=
by { convert rpow_lt_rpow_of_exponent_gt hx1 hx2 hz, exact (rpow_zero x).symm }

lemma one_le_rpow_of_pos_of_le_one_of_nonpos (hx1 : 0 < x) (hx2 : x ‚â§ 1) (hz : z ‚â§ 0) :
  1 ‚â§ x^z :=
by { convert rpow_le_rpow_of_exponent_ge hx1 hx2 hz, exact (rpow_zero x).symm }

lemma rpow_lt_one_iff_of_pos (hx : 0 < x) : x ^ y < 1 ‚Üî 1 < x ‚àß y < 0 ‚à® x < 1 ‚àß 0 < y :=
by rw [rpow_def_of_pos hx, exp_lt_one_iff, mul_neg_iff, log_pos_iff hx, log_neg_iff hx]

lemma rpow_lt_one_iff (hx : 0 ‚â§ x) : x ^ y < 1 ‚Üî x = 0 ‚àß y ‚â† 0 ‚à® 1 < x ‚àß y < 0 ‚à® x < 1 ‚àß 0 < y :=
begin
  rcases hx.eq_or_lt with (rfl|hx),
  { rcases em (y = 0) with (rfl|hy); simp [*, lt_irrefl, zero_lt_one] },
  { simp [rpow_lt_one_iff_of_pos hx, hx.ne.symm] }
end

lemma one_lt_rpow_iff_of_pos (hx : 0 < x) : 1 < x ^ y ‚Üî 1 < x ‚àß 0 < y ‚à® x < 1 ‚àß y < 0 :=
by rw [rpow_def_of_pos hx, one_lt_exp_iff, mul_pos_iff, log_pos_iff hx, log_neg_iff hx]

lemma one_lt_rpow_iff (hx : 0 ‚â§ x) : 1 < x ^ y ‚Üî 1 < x ‚àß 0 < y ‚à® 0 < x ‚àß x < 1 ‚àß y < 0 :=
begin
  rcases hx.eq_or_lt with (rfl|hx),
  { rcases em (y = 0) with (rfl|hy); simp [*, lt_irrefl, (@zero_lt_one ‚Ñù _ _).not_lt] },
  { simp [one_lt_rpow_iff_of_pos hx, hx] }
end

lemma rpow_le_rpow_of_exponent_ge' (hx0 : 0 ‚â§ x) (hx1 : x ‚â§ 1) (hz : 0 ‚â§ z) (hyz : z ‚â§ y) :
  x^y ‚â§ x^z :=
begin
  rcases eq_or_lt_of_le hx0 with rfl | hx0',
  { rcases eq_or_lt_of_le hz with rfl | hz',
    { exact (rpow_zero 0).symm ‚ñ∏ (rpow_le_one hx0 hx1 hyz), },
    rw [zero_rpow, zero_rpow]; linarith, },
  { exact rpow_le_rpow_of_exponent_ge hx0' hx1 hyz, },
end

lemma rpow_left_inj_on {x : ‚Ñù} (hx : x ‚â† 0) :
  set.inj_on (Œª y : ‚Ñù, y^x) {y : ‚Ñù | 0 ‚â§ y} :=
begin
  rintros y hy z hz (hyz : y ^ x = z ^ x),
  rw [‚Üêrpow_one y, ‚Üêrpow_one z, ‚Üê_root_.mul_inv_cancel hx, rpow_mul hy, rpow_mul hz, hyz]
end

lemma le_rpow_iff_log_le (hx : 0 < x) (hy : 0 < y) :
  x ‚â§ y^z ‚Üî real.log x ‚â§ z * real.log y :=
by rw [‚Üêreal.log_le_log hx (real.rpow_pos_of_pos hy z), real.log_rpow hy]

lemma le_rpow_of_log_le (hx : 0 ‚â§ x) (hy : 0 < y) (h : real.log x ‚â§ z * real.log y) :
  x ‚â§ y^z :=
begin
  obtain hx | rfl := hx.lt_or_eq,
  { exact (le_rpow_iff_log_le hx hy).2 h },
  exact (real.rpow_pos_of_pos hy z).le,
end

lemma lt_rpow_iff_log_lt (hx : 0 < x) (hy : 0 < y) :
  x < y^z ‚Üî real.log x < z * real.log y :=
by rw [‚Üêreal.log_lt_log_iff hx (real.rpow_pos_of_pos hy z), real.log_rpow hy]

lemma lt_rpow_of_log_lt (hx : 0 ‚â§ x) (hy : 0 < y) (h : real.log x < z * real.log y) :
  x < y^z :=
begin
  obtain hx | rfl := hx.lt_or_eq,
  { exact (lt_rpow_iff_log_lt hx hy).2 h },
  exact real.rpow_pos_of_pos hy z,
end

lemma rpow_le_one_iff_of_pos (hx : 0 < x) : x ^ y ‚â§ 1 ‚Üî 1 ‚â§ x ‚àß y ‚â§ 0 ‚à® x ‚â§ 1 ‚àß 0 ‚â§ y :=
by rw [rpow_def_of_pos hx, exp_le_one_iff, mul_nonpos_iff, log_nonneg_iff hx, log_nonpos_iff hx]

lemma pow_nat_rpow_nat_inv {x : ‚Ñù} (hx : 0 ‚â§ x) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ n) ^ (n‚Åª¬π : ‚Ñù) = x :=
have hn0 : (n : ‚Ñù) ‚â† 0, by simpa [pos_iff_ne_zero] using hn,
by rw [‚Üê rpow_nat_cast, ‚Üê rpow_mul hx, mul_inv_cancel hn0, rpow_one]

lemma rpow_nat_inv_pow_nat {x : ‚Ñù} (hx : 0 ‚â§ x) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ (n‚Åª¬π : ‚Ñù)) ^ n = x :=
have hn0 : (n : ‚Ñù) ‚â† 0, by simpa [pos_iff_ne_zero] using hn,
by rw [‚Üê rpow_nat_cast, ‚Üê rpow_mul hx, inv_mul_cancel hn0, rpow_one]

lemma continuous_at_const_rpow {a b : ‚Ñù} (h : a ‚â† 0) : continuous_at (rpow a) b :=
begin
  have : rpow a = Œª x : ‚Ñù, ((a : ‚ÑÇ) ^ (x : ‚ÑÇ)).re, by { ext1 x, rw [rpow_eq_pow, rpow_def], },
  rw this,
  refine complex.continuous_re.continuous_at.comp _,
  refine (continuous_at_const_cpow _).comp complex.continuous_of_real.continuous_at,
  norm_cast,
  exact h,
end

lemma continuous_at_const_rpow' {a b : ‚Ñù} (h : b ‚â† 0) : continuous_at (rpow a) b :=
begin
  have : rpow a = Œª x : ‚Ñù, ((a : ‚ÑÇ) ^ (x : ‚ÑÇ)).re, by { ext1 x, rw [rpow_eq_pow, rpow_def], },
  rw this,
  refine complex.continuous_re.continuous_at.comp _,
  refine (continuous_at_const_cpow' _).comp complex.continuous_of_real.continuous_at,
  norm_cast,
  exact h,
end

lemma rpow_eq_nhds_of_neg {p : ‚Ñù √ó ‚Ñù} (hp_fst : p.fst < 0) :
  (Œª x : ‚Ñù √ó ‚Ñù, x.1 ^ x.2) =·∂†[ùìù p] Œª x, exp (log x.1 * x.2) * cos (x.2 * œÄ) :=
begin
  suffices : ‚àÄ·∂† (x : ‚Ñù √ó ‚Ñù) in (ùìù p), x.1 < 0,
    from this.mono (Œª x hx, by { dsimp only, rw rpow_def_of_neg hx, }),
  exact is_open.eventually_mem (is_open_lt continuous_fst continuous_const) hp_fst,
end

lemma rpow_eq_nhds_of_pos {p : ‚Ñù √ó ‚Ñù} (hp_fst : 0 < p.fst) :
  (Œª x : ‚Ñù √ó ‚Ñù, x.1 ^ x.2) =·∂†[ùìù p] Œª x, exp (log x.1 * x.2) :=
begin
  suffices : ‚àÄ·∂† (x : ‚Ñù √ó ‚Ñù) in (ùìù p), 0 < x.1,
    from this.mono (Œª x hx, by { dsimp only, rw rpow_def_of_pos hx, }),
  exact is_open.eventually_mem (is_open_lt continuous_const continuous_fst) hp_fst,
end

lemma continuous_at_rpow_of_ne (p : ‚Ñù √ó ‚Ñù) (hp : p.1 ‚â† 0) :
  continuous_at (Œª p : ‚Ñù √ó ‚Ñù, p.1 ^ p.2) p :=
begin
  rw ne_iff_lt_or_gt at hp,
  cases hp,
  { rw continuous_at_congr (rpow_eq_nhds_of_neg hp),
    refine continuous_at.mul _ (continuous_cos.continuous_at.comp _),
    { refine continuous_exp.continuous_at.comp (continuous_at.mul _ continuous_snd.continuous_at),
      refine (continuous_at_log _).comp continuous_fst.continuous_at,
      exact hp.ne, },
    { exact continuous_snd.continuous_at.mul continuous_at_const, }, },
  { rw continuous_at_congr (rpow_eq_nhds_of_pos hp),
    refine continuous_exp.continuous_at.comp (continuous_at.mul _ continuous_snd.continuous_at),
    refine (continuous_at_log _).comp continuous_fst.continuous_at,
    exact hp.lt.ne.symm, },
end

lemma continuous_at_rpow_of_pos (p : ‚Ñù √ó ‚Ñù) (hp : 0 < p.2) :
  continuous_at (Œª p : ‚Ñù √ó ‚Ñù, p.1 ^ p.2) p :=
begin
  cases p with x y,
  obtain hx|rfl := ne_or_eq x 0,
  { exact continuous_at_rpow_of_ne (x, y) hx },
  have A : tendsto (Œª p : ‚Ñù √ó ‚Ñù, exp (log p.1 * p.2)) (ùìù[‚â†] 0 √ó·∂† ùìù y) (ùìù 0) :=
    tendsto_exp_at_bot.comp
      ((tendsto_log_nhds_within_zero.comp tendsto_fst).at_bot_mul hp tendsto_snd),
  have B : tendsto (Œª p : ‚Ñù √ó ‚Ñù, p.1 ^ p.2) (ùìù[‚â†] 0 √ó·∂† ùìù y) (ùìù 0) :=
    squeeze_zero_norm (Œª p, abs_rpow_le_exp_log_mul p.1 p.2) A,
  have C : tendsto (Œª p : ‚Ñù √ó ‚Ñù, p.1 ^ p.2) (ùìù[{0}] 0 √ó·∂† ùìù y) (pure 0),
  { rw [nhds_within_singleton, tendsto_pure, pure_prod, eventually_map],
    exact (lt_mem_nhds hp).mono (Œª y hy, zero_rpow hy.ne') },
  simpa only [‚Üê sup_prod, ‚Üê nhds_within_union, set.compl_union_self, nhds_within_univ, nhds_prod_eq,
    continuous_at, zero_rpow hp.ne'] using B.sup (C.mono_right (pure_le_nhds _))
end

lemma continuous_at_rpow (p : ‚Ñù √ó ‚Ñù) (h : p.1 ‚â† 0 ‚à® 0 < p.2) :
  continuous_at (Œª p : ‚Ñù √ó ‚Ñù, p.1 ^ p.2) p :=
h.elim (Œª h, continuous_at_rpow_of_ne p h) (Œª h, continuous_at_rpow_of_pos p h)

end real

section

variable {Œ± : Type*}

lemma filter.tendsto.rpow {l : filter Œ±} {f g : Œ± ‚Üí ‚Ñù} {x y : ‚Ñù}
  (hf : tendsto f l (ùìù x)) (hg : tendsto g l (ùìù y)) (h : x ‚â† 0 ‚à® 0 < y) :
  tendsto (Œª t, f t ^ g t) l (ùìù (x ^ y)) :=
(real.continuous_at_rpow (x, y) h).tendsto.comp (hf.prod_mk_nhds hg)

lemma filter.tendsto.rpow_const {l : filter Œ±} {f : Œ± ‚Üí ‚Ñù} {x p : ‚Ñù}
  (hf : tendsto f l (ùìù x)) (h : x ‚â† 0 ‚à® 0 ‚â§ p) :
  tendsto (Œª a, f a ^ p) l (ùìù (x ^ p)) :=
if h0 : 0 = p then h0 ‚ñ∏ by simp [tendsto_const_nhds]
else hf.rpow tendsto_const_nhds (h.imp id $ Œª h', h'.lt_of_ne h0)

variables [topological_space Œ±] {f g : Œ± ‚Üí ‚Ñù} {s : set Œ±} {x : Œ±} {p : ‚Ñù}

lemma continuous_at.rpow (hf : continuous_at f x) (hg : continuous_at g x) (h : f x ‚â† 0 ‚à® 0 < g x) :
  continuous_at (Œª t, f t ^ g t) x :=
hf.rpow hg h

lemma continuous_within_at.rpow (hf : continuous_within_at f s x) (hg : continuous_within_at g s x)
  (h : f x ‚â† 0 ‚à® 0 < g x) :
  continuous_within_at (Œª t, f t ^ g t) s x :=
hf.rpow hg h

lemma continuous_on.rpow (hf : continuous_on f s) (hg : continuous_on g s)
  (h : ‚àÄ x ‚àà s, f x ‚â† 0 ‚à® 0 < g x) :
  continuous_on (Œª t, f t ^ g t) s :=
Œª t ht, (hf t ht).rpow (hg t ht) (h t ht)

lemma continuous.rpow (hf : continuous f) (hg : continuous g) (h : ‚àÄ x, f x ‚â† 0 ‚à® 0 < g x) :
  continuous (Œª x, f x ^ g x) :=
continuous_iff_continuous_at.2 $ Œª x, (hf.continuous_at.rpow hg.continuous_at (h x))

lemma continuous_within_at.rpow_const (hf : continuous_within_at f s x) (h : f x ‚â† 0 ‚à® 0 ‚â§ p) :
  continuous_within_at (Œª x, f x ^ p) s x :=
hf.rpow_const h

lemma continuous_at.rpow_const (hf : continuous_at f x) (h : f x ‚â† 0 ‚à® 0 ‚â§ p) :
  continuous_at (Œª x, f x ^ p) x :=
hf.rpow_const h

lemma continuous_on.rpow_const (hf : continuous_on f s) (h : ‚àÄ x ‚àà s, f x ‚â† 0 ‚à® 0 ‚â§ p) :
  continuous_on (Œª x, f x ^ p) s :=
Œª x hx, (hf x hx).rpow_const (h x hx)

lemma continuous.rpow_const (hf : continuous f) (h : ‚àÄ x, f x ‚â† 0 ‚à® 0 ‚â§ p) :
  continuous (Œª x, f x ^ p) :=
continuous_iff_continuous_at.2 $ Œª x, hf.continuous_at.rpow_const (h x)

end

namespace real

variables {z x y : ‚Ñù}

section sqrt

lemma sqrt_eq_rpow (x : ‚Ñù) : sqrt x = x ^ (1/(2:‚Ñù)) :=
begin
  obtain h | h := le_or_lt 0 x,
  { rw [‚Üê mul_self_inj_of_nonneg (sqrt_nonneg _) (rpow_nonneg_of_nonneg h _), mul_self_sqrt h,
      ‚Üê sq, ‚Üê rpow_nat_cast, ‚Üê rpow_mul h],
    norm_num },
  { have : 1 / (2:‚Ñù) * œÄ = œÄ / (2:‚Ñù), ring,
    rw [sqrt_eq_zero_of_nonpos h.le, rpow_def_of_neg h, this, cos_pi_div_two, mul_zero] }
end

end sqrt

end real

section limits
open real filter

/-- The function `x ^ y` tends to `+‚àû` at `+‚àû` for any positive real `y`. -/
lemma tendsto_rpow_at_top {y : ‚Ñù} (hy : 0 < y) : tendsto (Œª x : ‚Ñù, x ^ y) at_top at_top :=
begin
  rw tendsto_at_top_at_top,
  intro b,
  use (max b 0) ^ (1/y),
  intros x hx,
  exact le_of_max_le_left
    (by { convert rpow_le_rpow (rpow_nonneg_of_nonneg (le_max_right b 0) (1/y)) hx (le_of_lt hy),
      rw [‚Üê rpow_mul (le_max_right b 0), (eq_div_iff (ne_of_gt hy)).mp rfl, rpow_one] }),
end

/-- The function `x ^ (-y)` tends to `0` at `+‚àû` for any positive real `y`. -/
lemma tendsto_rpow_neg_at_top {y : ‚Ñù} (hy : 0 < y) : tendsto (Œª x : ‚Ñù, x ^ (-y)) at_top (ùìù 0) :=
tendsto.congr' (eventually_eq_of_mem (Ioi_mem_at_top 0) (Œª x hx, (rpow_neg (le_of_lt hx) y).symm))
  (tendsto_rpow_at_top hy).inv_tendsto_at_top

/-- The function `x ^ (a / (b * x + c))` tends to `1` at `+‚àû`, for any real numbers `a`, `b`, and
`c` such that `b` is nonzero. -/
lemma tendsto_rpow_div_mul_add (a b c : ‚Ñù) (hb : 0 ‚â† b) :
  tendsto (Œª x, x ^ (a / (b*x+c))) at_top (ùìù 1) :=
begin
  refine tendsto.congr' _ ((tendsto_exp_nhds_0_nhds_1.comp
    (by simpa only [mul_zero, pow_one] using ((@tendsto_const_nhds _ _ _ a _).mul
      (tendsto_div_pow_mul_exp_add_at_top b c 1 hb (by norm_num))))).comp (tendsto_log_at_top)),
  apply eventually_eq_of_mem (Ioi_mem_at_top (0:‚Ñù)),
  intros x hx,
  simp only [set.mem_Ioi, function.comp_app] at hx ‚ä¢,
  rw [exp_log hx, ‚Üê exp_log (rpow_pos_of_pos hx (a / (b * x + c))), log_rpow hx (a / (b * x + c))],
  field_simp,
end

/-- The function `x ^ (1 / x)` tends to `1` at `+‚àû`. -/
lemma tendsto_rpow_div : tendsto (Œª x, x ^ ((1:‚Ñù) / x)) at_top (ùìù 1) :=
by { convert tendsto_rpow_div_mul_add (1:‚Ñù) _ (0:‚Ñù) zero_ne_one, ring_nf }

/-- The function `x ^ (-1 / x)` tends to `1` at `+‚àû`. -/
lemma tendsto_rpow_neg_div : tendsto (Œª x, x ^ (-(1:‚Ñù) / x)) at_top (ùìù 1) :=
by { convert tendsto_rpow_div_mul_add (-(1:‚Ñù)) _ (0:‚Ñù) zero_ne_one, ring_nf }

end limits

namespace nnreal

/-- The nonnegative real power function `x^y`, defined for `x : ‚Ñù‚â•0` and `y : ‚Ñù ` as the
restriction of the real power function. For `x > 0`, it is equal to `exp (y log x)`. For `x = 0`,
one sets `0 ^ 0 = 1` and `0 ^ y = 0` for `y ‚â† 0`. -/
noncomputable def rpow (x : ‚Ñù‚â•0) (y : ‚Ñù) : ‚Ñù‚â•0 :=
‚ü®(x : ‚Ñù) ^ y, real.rpow_nonneg_of_nonneg x.2 y‚ü©

noncomputable instance : has_pow ‚Ñù‚â•0 ‚Ñù := ‚ü®rpow‚ü©

@[simp] lemma rpow_eq_pow (x : ‚Ñù‚â•0) (y : ‚Ñù) : rpow x y = x ^ y := rfl

@[simp, norm_cast] lemma coe_rpow (x : ‚Ñù‚â•0) (y : ‚Ñù) : ((x ^ y : ‚Ñù‚â•0) : ‚Ñù) = (x : ‚Ñù) ^ y := rfl

@[simp] lemma rpow_zero (x : ‚Ñù‚â•0) : x ^ (0 : ‚Ñù) = 1 :=
nnreal.eq $ real.rpow_zero _

@[simp] lemma rpow_eq_zero_iff {x : ‚Ñù‚â•0} {y : ‚Ñù} : x ^ y = 0 ‚Üî x = 0 ‚àß y ‚â† 0 :=
begin
  rw [‚Üê nnreal.coe_eq, coe_rpow, ‚Üê nnreal.coe_eq_zero],
  exact real.rpow_eq_zero_iff_of_nonneg x.2
end

@[simp] lemma zero_rpow {x : ‚Ñù} (h : x ‚â† 0) : (0 : ‚Ñù‚â•0) ^ x = 0 :=
nnreal.eq $ real.zero_rpow h

@[simp] lemma rpow_one (x : ‚Ñù‚â•0) : x ^ (1 : ‚Ñù) = x :=
nnreal.eq $ real.rpow_one _

@[simp] lemma one_rpow (x : ‚Ñù) : (1 : ‚Ñù‚â•0) ^ x = 1 :=
nnreal.eq $ real.one_rpow _

lemma rpow_add {x : ‚Ñù‚â•0} (hx : x ‚â† 0) (y z : ‚Ñù) : x ^ (y + z) = x ^ y * x ^ z :=
nnreal.eq $ real.rpow_add (pos_iff_ne_zero.2 hx) _ _

lemma rpow_add' (x : ‚Ñù‚â•0) {y z : ‚Ñù} (h : y + z ‚â† 0) : x ^ (y + z) = x ^ y * x ^ z :=
nnreal.eq $ real.rpow_add' x.2 h

lemma rpow_mul (x : ‚Ñù‚â•0) (y z : ‚Ñù) : x ^ (y * z) = (x ^ y) ^ z :=
nnreal.eq $ real.rpow_mul x.2 y z

lemma rpow_neg (x : ‚Ñù‚â•0) (y : ‚Ñù) : x ^ -y = (x ^ y)‚Åª¬π :=
nnreal.eq $ real.rpow_neg x.2 _

lemma rpow_neg_one (x : ‚Ñù‚â•0) : x ^ (-1 : ‚Ñù) = x ‚Åª¬π :=
by simp [rpow_neg]

lemma rpow_sub {x : ‚Ñù‚â•0} (hx : x ‚â† 0) (y z : ‚Ñù) : x ^ (y - z) = x ^ y / x ^ z :=
nnreal.eq $ real.rpow_sub (pos_iff_ne_zero.2 hx) y z

lemma rpow_sub' (x : ‚Ñù‚â•0) {y z : ‚Ñù} (h : y - z ‚â† 0) :
  x ^ (y - z) = x ^ y / x ^ z :=
nnreal.eq $ real.rpow_sub' x.2 h

lemma rpow_inv_rpow_self {y : ‚Ñù} (hy : y ‚â† 0) (x : ‚Ñù‚â•0) : (x ^ y) ^ (1 / y) = x :=
by field_simp [‚Üê rpow_mul]

lemma rpow_self_rpow_inv {y : ‚Ñù} (hy : y ‚â† 0) (x : ‚Ñù‚â•0) : (x ^ (1 / y)) ^ y = x :=
by field_simp [‚Üê rpow_mul]

lemma inv_rpow (x : ‚Ñù‚â•0) (y : ‚Ñù) : (x‚Åª¬π) ^ y = (x ^ y)‚Åª¬π :=
nnreal.eq $ real.inv_rpow x.2 y

lemma div_rpow (x y : ‚Ñù‚â•0) (z : ‚Ñù) : (x / y) ^ z = x ^ z / y ^ z :=
nnreal.eq $ real.div_rpow x.2 y.2 z

lemma sqrt_eq_rpow (x : ‚Ñù‚â•0) : sqrt x = x ^ (1/(2:‚Ñù)) :=
begin
  refine nnreal.eq _,
  push_cast,
  exact real.sqrt_eq_rpow x.1,
end

@[simp, norm_cast] lemma rpow_nat_cast (x : ‚Ñù‚â•0) (n : ‚Ñï) : x ^ (n : ‚Ñù) = x ^ n :=
nnreal.eq $ by simpa only [coe_rpow, coe_pow] using real.rpow_nat_cast x n

lemma mul_rpow {x y : ‚Ñù‚â•0} {z : ‚Ñù}  : (x*y)^z = x^z * y^z :=
nnreal.eq $ real.mul_rpow x.2 y.2

lemma rpow_le_rpow {x y : ‚Ñù‚â•0} {z: ‚Ñù} (h‚ÇÅ : x ‚â§ y) (h‚ÇÇ : 0 ‚â§ z) : x^z ‚â§ y^z :=
real.rpow_le_rpow x.2 h‚ÇÅ h‚ÇÇ

lemma rpow_lt_rpow {x y : ‚Ñù‚â•0} {z: ‚Ñù} (h‚ÇÅ : x < y) (h‚ÇÇ : 0 < z) : x^z < y^z :=
real.rpow_lt_rpow x.2 h‚ÇÅ h‚ÇÇ

lemma rpow_lt_rpow_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : 0 < z) : x ^ z < y ^ z ‚Üî x < y :=
real.rpow_lt_rpow_iff x.2 y.2 hz

lemma rpow_le_rpow_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : 0 < z) : x ^ z ‚â§ y ^ z ‚Üî x ‚â§ y :=
real.rpow_le_rpow_iff x.2 y.2 hz

lemma le_rpow_one_div_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : 0 < z) :  x ‚â§ y ^ (1 / z) ‚Üî x ^ z ‚â§ y :=
by rw [‚Üê rpow_le_rpow_iff hz, rpow_self_rpow_inv hz.ne']

lemma rpow_one_div_le_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : 0 < z) :  x ^ (1 / z) ‚â§ y ‚Üî x ‚â§ y ^ z :=
by rw [‚Üê rpow_le_rpow_iff hz, rpow_self_rpow_inv hz.ne']

lemma rpow_lt_rpow_of_exponent_lt {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx : 1 < x) (hyz : y < z) : x^y < x^z :=
real.rpow_lt_rpow_of_exponent_lt hx hyz

lemma rpow_le_rpow_of_exponent_le {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx : 1 ‚â§ x) (hyz : y ‚â§ z) : x^y ‚â§ x^z :=
real.rpow_le_rpow_of_exponent_le hx hyz

lemma rpow_lt_rpow_of_exponent_gt {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx0 : 0 < x) (hx1 : x < 1) (hyz : z < y) :
  x^y < x^z :=
real.rpow_lt_rpow_of_exponent_gt hx0 hx1 hyz

lemma rpow_le_rpow_of_exponent_ge {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx0 : 0 < x) (hx1 : x ‚â§ 1) (hyz : z ‚â§ y) :
  x^y ‚â§ x^z :=
real.rpow_le_rpow_of_exponent_ge hx0 hx1 hyz

lemma rpow_pos {p : ‚Ñù} {x : ‚Ñù‚â•0} (hx_pos : 0 < x) : 0 < x^p :=
begin
  have rpow_pos_of_nonneg : ‚àÄ {p : ‚Ñù}, 0 < p ‚Üí 0 < x^p,
  { intros p hp_pos,
    rw ‚Üêzero_rpow hp_pos.ne',
    exact rpow_lt_rpow hx_pos hp_pos },
  rcases lt_trichotomy 0 p with hp_pos|rfl|hp_neg,
  { exact rpow_pos_of_nonneg hp_pos },
  { simp only [zero_lt_one, rpow_zero] },
  { rw [‚Üêneg_neg p, rpow_neg, inv_pos],
    exact rpow_pos_of_nonneg (neg_pos.mpr hp_neg) },
end

lemma rpow_lt_one {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx : 0 ‚â§ x) (hx1 : x < 1) (hz : 0 < z) : x^z < 1 :=
real.rpow_lt_one hx hx1 hz

lemma rpow_le_one {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx2 : x ‚â§ 1) (hz : 0 ‚â§ z) : x^z ‚â§ 1 :=
real.rpow_le_one x.2 hx2 hz

lemma rpow_lt_one_of_one_lt_of_neg {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx : 1 < x) (hz : z < 0) : x^z < 1 :=
real.rpow_lt_one_of_one_lt_of_neg hx hz

lemma rpow_le_one_of_one_le_of_nonpos {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx : 1 ‚â§ x) (hz : z ‚â§ 0) : x^z ‚â§ 1 :=
real.rpow_le_one_of_one_le_of_nonpos hx hz

lemma one_lt_rpow {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx : 1 < x) (hz : 0 < z) : 1 < x^z :=
real.one_lt_rpow hx hz

lemma one_le_rpow {x : ‚Ñù‚â•0} {z : ‚Ñù} (h : 1 ‚â§ x) (h‚ÇÅ : 0 ‚â§ z) : 1 ‚â§ x^z :=
real.one_le_rpow h h‚ÇÅ

lemma one_lt_rpow_of_pos_of_lt_one_of_neg {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx1 : 0 < x) (hx2 : x < 1)
  (hz : z < 0) : 1 < x^z :=
real.one_lt_rpow_of_pos_of_lt_one_of_neg hx1 hx2 hz

lemma one_le_rpow_of_pos_of_le_one_of_nonpos {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx1 : 0 < x) (hx2 : x ‚â§ 1)
  (hz : z ‚â§ 0) : 1 ‚â§ x^z :=
real.one_le_rpow_of_pos_of_le_one_of_nonpos hx1 hx2 hz

lemma rpow_le_self_of_le_one {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx : x ‚â§ 1) (h_one_le : 1 ‚â§ z) : x ^ z ‚â§ x :=
begin
  rcases eq_bot_or_bot_lt x with rfl | (h : 0 < x),
  { have : z ‚â† 0 := by linarith,
    simp [this] },
  nth_rewrite 1 ‚Üênnreal.rpow_one x,
  exact nnreal.rpow_le_rpow_of_exponent_ge h hx h_one_le,
end

lemma rpow_left_injective {x : ‚Ñù} (hx : x ‚â† 0) : function.injective (Œª y : ‚Ñù‚â•0, y^x) :=
Œª y z hyz, by simpa only [rpow_inv_rpow_self hx] using congr_arg (Œª y, y ^ (1 / x)) hyz

lemma rpow_eq_rpow_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : z ‚â† 0) : x ^ z = y ^ z ‚Üî x = y :=
(rpow_left_injective hz).eq_iff

lemma rpow_left_surjective {x : ‚Ñù} (hx : x ‚â† 0) : function.surjective (Œª y : ‚Ñù‚â•0, y^x) :=
Œª y, ‚ü®y ^ x‚Åª¬π, by simp_rw [‚Üêrpow_mul, _root_.inv_mul_cancel hx, rpow_one]‚ü©

lemma rpow_left_bijective {x : ‚Ñù} (hx : x ‚â† 0) : function.bijective (Œª y : ‚Ñù‚â•0, y^x) :=
‚ü®rpow_left_injective hx, rpow_left_surjective hx‚ü©

lemma eq_rpow_one_div_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : z ‚â† 0) :  x = y ^ (1 / z) ‚Üî x ^ z = y :=
by rw [‚Üê rpow_eq_rpow_iff hz, rpow_self_rpow_inv hz]

lemma rpow_one_div_eq_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : z ‚â† 0) :  x ^ (1 / z) = y ‚Üî x = y ^ z :=
by rw [‚Üê rpow_eq_rpow_iff hz, rpow_self_rpow_inv hz]

lemma pow_nat_rpow_nat_inv (x : ‚Ñù‚â•0) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ n) ^ (n‚Åª¬π : ‚Ñù) = x :=
by { rw [‚Üê nnreal.coe_eq, coe_rpow, nnreal.coe_pow], exact real.pow_nat_rpow_nat_inv x.2 hn }

lemma rpow_nat_inv_pow_nat (x : ‚Ñù‚â•0) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ (n‚Åª¬π : ‚Ñù)) ^ n = x :=
by { rw [‚Üê nnreal.coe_eq, nnreal.coe_pow, coe_rpow], exact real.rpow_nat_inv_pow_nat x.2 hn }

lemma continuous_at_rpow {x : ‚Ñù‚â•0} {y : ‚Ñù} (h : x ‚â† 0 ‚à® 0 < y) :
  continuous_at (Œªp:‚Ñù‚â•0√ó‚Ñù, p.1^p.2) (x, y) :=
begin
  have : (Œªp:‚Ñù‚â•0√ó‚Ñù, p.1^p.2) = real.to_nnreal ‚àò (Œªp:‚Ñù√ó‚Ñù, p.1^p.2) ‚àò (Œªp:‚Ñù‚â•0 √ó ‚Ñù, (p.1.1, p.2)),
  { ext p,
    rw [coe_rpow, real.coe_to_nnreal _ (real.rpow_nonneg_of_nonneg p.1.2 _)],
    refl },
  rw this,
  refine nnreal.continuous_of_real.continuous_at.comp (continuous_at.comp _ _),
  { apply real.continuous_at_rpow,
    simp at h,
    rw ‚Üê (nnreal.coe_eq_zero x) at h,
    exact h },
  { exact ((continuous_subtype_val.comp continuous_fst).prod_mk continuous_snd).continuous_at }
end

lemma _root_.real.to_nnreal_rpow_of_nonneg {x y : ‚Ñù} (hx : 0 ‚â§ x) :
  real.to_nnreal (x ^ y) = (real.to_nnreal x) ^ y :=
begin
  nth_rewrite 0 ‚Üê real.coe_to_nnreal x hx,
  rw [‚Üênnreal.coe_rpow, real.to_nnreal_coe],
end

end nnreal

open filter

lemma filter.tendsto.nnrpow {Œ± : Type*} {f : filter Œ±} {u : Œ± ‚Üí ‚Ñù‚â•0} {v : Œ± ‚Üí ‚Ñù} {x : ‚Ñù‚â•0} {y : ‚Ñù}
  (hx : tendsto u f (ùìù x)) (hy : tendsto v f (ùìù y)) (h : x ‚â† 0 ‚à® 0 < y) :
  tendsto (Œª a, (u a) ^ (v a)) f (ùìù (x ^ y)) :=
tendsto.comp (nnreal.continuous_at_rpow h) (hx.prod_mk_nhds hy)

namespace nnreal

lemma continuous_at_rpow_const {x : ‚Ñù‚â•0} {y : ‚Ñù} (h : x ‚â† 0 ‚à® 0 ‚â§ y) :
  continuous_at (Œª z, z^y) x :=
h.elim (Œª h, tendsto_id.nnrpow tendsto_const_nhds (or.inl h)) $
  Œª h, h.eq_or_lt.elim
    (Œª h, h ‚ñ∏ by simp only [rpow_zero, continuous_at_const])
    (Œª h, tendsto_id.nnrpow tendsto_const_nhds (or.inr h))

lemma continuous_rpow_const {y : ‚Ñù} (h : 0 ‚â§ y) :
  continuous (Œª x : ‚Ñù‚â•0, x^y) :=
continuous_iff_continuous_at.2 $ Œª x, continuous_at_rpow_const (or.inr h)

theorem tendsto_rpow_at_top {y : ‚Ñù} (hy : 0 < y) :
  tendsto (Œª (x : ‚Ñù‚â•0), x ^ y) at_top at_top :=
begin
  rw filter.tendsto_at_top_at_top,
  intros b,
  obtain ‚ü®c, hc‚ü© := tendsto_at_top_at_top.mp (tendsto_rpow_at_top hy) b,
  use c.to_nnreal,
  intros a ha,
  exact_mod_cast hc a (real.to_nnreal_le_iff_le_coe.mp ha),
end

end nnreal

namespace ennreal

/-- The real power function `x^y` on extended nonnegative reals, defined for `x : ‚Ñù‚â•0‚àû` and
`y : ‚Ñù` as the restriction of the real power function if `0 < x < ‚ä§`, and with the natural values
for `0` and `‚ä§` (i.e., `0 ^ x = 0` for `x > 0`, `1` for `x = 0` and `‚ä§` for `x < 0`, and
`‚ä§ ^ x = 1 / 0 ^ x`). -/
noncomputable def rpow : ‚Ñù‚â•0‚àû ‚Üí ‚Ñù ‚Üí ‚Ñù‚â•0‚àû
| (some x) y := if x = 0 ‚àß y < 0 then ‚ä§ else (x ^ y : ‚Ñù‚â•0)
| none     y := if 0 < y then ‚ä§ else if y = 0 then 1 else 0

noncomputable instance : has_pow ‚Ñù‚â•0‚àû ‚Ñù := ‚ü®rpow‚ü©

@[simp] lemma rpow_eq_pow (x : ‚Ñù‚â•0‚àû) (y : ‚Ñù) : rpow x y = x ^ y := rfl

@[simp] lemma rpow_zero {x : ‚Ñù‚â•0‚àû} : x ^ (0 : ‚Ñù) = 1 :=
by cases x; { dsimp only [(^), rpow], simp [lt_irrefl] }

lemma top_rpow_def (y : ‚Ñù) : (‚ä§ : ‚Ñù‚â•0‚àû) ^ y = if 0 < y then ‚ä§ else if y = 0 then 1 else 0 :=
rfl

@[simp] lemma top_rpow_of_pos {y : ‚Ñù} (h : 0 < y) : (‚ä§ : ‚Ñù‚â•0‚àû) ^ y = ‚ä§ :=
by simp [top_rpow_def, h]

@[simp] lemma top_rpow_of_neg {y : ‚Ñù} (h : y < 0) : (‚ä§ : ‚Ñù‚â•0‚àû) ^ y = 0 :=
by simp [top_rpow_def, asymm h, ne_of_lt h]

@[simp] lemma zero_rpow_of_pos {y : ‚Ñù} (h : 0 < y) : (0 : ‚Ñù‚â•0‚àû) ^ y = 0 :=
begin
  rw [‚Üê ennreal.coe_zero, ‚Üê ennreal.some_eq_coe],
  dsimp only [(^), rpow],
  simp [h, asymm h, ne_of_gt h],
end

@[simp] lemma zero_rpow_of_neg {y : ‚Ñù} (h : y < 0) : (0 : ‚Ñù‚â•0‚àû) ^ y = ‚ä§ :=
begin
  rw [‚Üê ennreal.coe_zero, ‚Üê ennreal.some_eq_coe],
  dsimp only [(^), rpow],
  simp [h, ne_of_gt h],
end

lemma zero_rpow_def (y : ‚Ñù) : (0 : ‚Ñù‚â•0‚àû) ^ y = if 0 < y then 0 else if y = 0 then 1 else ‚ä§ :=
begin
  rcases lt_trichotomy 0 y with H|rfl|H,
  { simp [H, ne_of_gt, zero_rpow_of_pos, lt_irrefl] },
  { simp [lt_irrefl] },
  { simp [H, asymm H, ne_of_lt, zero_rpow_of_neg] }
end

@[simp] lemma zero_rpow_mul_self (y : ‚Ñù) : (0 : ‚Ñù‚â•0‚àû) ^ y * 0 ^ y = 0 ^ y :=
by { rw zero_rpow_def, split_ifs, exacts [zero_mul _, one_mul _, top_mul_top] }

@[norm_cast] lemma coe_rpow_of_ne_zero {x : ‚Ñù‚â•0} (h : x ‚â† 0) (y : ‚Ñù) :
  (x : ‚Ñù‚â•0‚àû) ^ y = (x ^ y : ‚Ñù‚â•0) :=
begin
  rw [‚Üê ennreal.some_eq_coe],
  dsimp only [(^), rpow],
  simp [h]
end

@[norm_cast] lemma coe_rpow_of_nonneg (x : ‚Ñù‚â•0) {y : ‚Ñù} (h : 0 ‚â§ y) :
  (x : ‚Ñù‚â•0‚àû) ^ y = (x ^ y : ‚Ñù‚â•0) :=
begin
  by_cases hx : x = 0,
  { rcases le_iff_eq_or_lt.1 h with H|H,
    { simp [hx, H.symm] },
    { simp [hx, zero_rpow_of_pos H, nnreal.zero_rpow (ne_of_gt H)] } },
  { exact coe_rpow_of_ne_zero hx _ }
end

lemma coe_rpow_def (x : ‚Ñù‚â•0) (y : ‚Ñù) :
  (x : ‚Ñù‚â•0‚àû) ^ y = if x = 0 ‚àß y < 0 then ‚ä§ else (x ^ y : ‚Ñù‚â•0) := rfl

@[simp] lemma rpow_one (x : ‚Ñù‚â•0‚àû) : x ^ (1 : ‚Ñù) = x :=
by cases x; dsimp only [(^), rpow]; simp [zero_lt_one, not_lt_of_le zero_le_one]

@[simp] lemma one_rpow (x : ‚Ñù) : (1 : ‚Ñù‚â•0‚àû) ^ x = 1 :=
by { rw [‚Üê coe_one, coe_rpow_of_ne_zero one_ne_zero], simp }

@[simp] lemma rpow_eq_zero_iff {x : ‚Ñù‚â•0‚àû} {y : ‚Ñù} :
  x ^ y = 0 ‚Üî (x = 0 ‚àß 0 < y) ‚à® (x = ‚ä§ ‚àß y < 0) :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with H|H|H;
    simp [H, top_rpow_of_neg, top_rpow_of_pos, le_of_lt] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with H|H|H;
      simp [h, H, zero_rpow_of_neg, zero_rpow_of_pos, le_of_lt] },
    { simp [coe_rpow_of_ne_zero h, h] } }
end

@[simp] lemma rpow_eq_top_iff {x : ‚Ñù‚â•0‚àû} {y : ‚Ñù} :
  x ^ y = ‚ä§ ‚Üî (x = 0 ‚àß y < 0) ‚à® (x = ‚ä§ ‚àß 0 < y) :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with H|H|H;
    simp [H, top_rpow_of_neg, top_rpow_of_pos, le_of_lt] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with H|H|H;
      simp [h, H, zero_rpow_of_neg, zero_rpow_of_pos, le_of_lt] },
    { simp [coe_rpow_of_ne_zero h, h] } }
end

lemma rpow_eq_top_iff_of_pos {x : ‚Ñù‚â•0‚àû} {y : ‚Ñù} (hy : 0 < y) : x ^ y = ‚ä§ ‚Üî x = ‚ä§ :=
by simp [rpow_eq_top_iff, hy, asymm hy]

lemma rpow_eq_top_of_nonneg (x : ‚Ñù‚â•0‚àû) {y : ‚Ñù} (hy0 : 0 ‚â§ y) : x ^ y = ‚ä§ ‚Üí x = ‚ä§ :=
begin
  rw ennreal.rpow_eq_top_iff,
  intro h,
  cases h,
  { exfalso, rw lt_iff_not_ge at h, exact h.right hy0, },
  { exact h.left, },
end

lemma rpow_ne_top_of_nonneg {x : ‚Ñù‚â•0‚àû} {y : ‚Ñù} (hy0 : 0 ‚â§ y) (h : x ‚â† ‚ä§) : x ^ y ‚â† ‚ä§ :=
mt (ennreal.rpow_eq_top_of_nonneg x hy0) h

lemma rpow_lt_top_of_nonneg {x : ‚Ñù‚â•0‚àû} {y : ‚Ñù} (hy0 : 0 ‚â§ y) (h : x ‚â† ‚ä§) : x ^ y < ‚ä§ :=
lt_top_iff_ne_top.mpr (ennreal.rpow_ne_top_of_nonneg hy0 h)

lemma rpow_add {x : ‚Ñù‚â•0‚àû} (y z : ‚Ñù) (hx : x ‚â† 0) (h'x : x ‚â† ‚ä§) : x ^ (y + z) = x ^ y * x ^ z :=
begin
  cases x, { exact (h'x rfl).elim },
  have : x ‚â† 0 := Œª h, by simpa [h] using hx,
  simp [coe_rpow_of_ne_zero this, nnreal.rpow_add this]
end

lemma rpow_neg (x : ‚Ñù‚â•0‚àû) (y : ‚Ñù) : x ^ -y = (x ^ y)‚Åª¬π :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with H|H|H;
    simp [top_rpow_of_pos, top_rpow_of_neg, H, neg_pos.mpr] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with H|H|H;
      simp [h, zero_rpow_of_pos, zero_rpow_of_neg, H, neg_pos.mpr] },
    { have A : x ^ y ‚â† 0, by simp [h],
      simp [coe_rpow_of_ne_zero h, ‚Üê coe_inv A, nnreal.rpow_neg] } }
end

lemma rpow_sub {x : ‚Ñù‚â•0‚àû} (y z : ‚Ñù) (hx : x ‚â† 0) (h'x : x ‚â† ‚ä§) : x ^ (y - z) = x ^ y / x ^ z :=
by rw [sub_eq_add_neg, rpow_add _ _ hx h'x, rpow_neg, div_eq_mul_inv]

lemma rpow_neg_one (x : ‚Ñù‚â•0‚àû) : x ^ (-1 : ‚Ñù) = x ‚Åª¬π :=
by simp [rpow_neg]

lemma rpow_mul (x : ‚Ñù‚â•0‚àû) (y z : ‚Ñù) : x ^ (y * z) = (x ^ y) ^ z :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
    rcases lt_trichotomy z 0 with Hz|Hz|Hz;
    simp [Hy, Hz, zero_rpow_of_neg, zero_rpow_of_pos, top_rpow_of_neg, top_rpow_of_pos,
          mul_pos_of_neg_of_neg, mul_neg_of_neg_of_pos, mul_neg_of_pos_of_neg] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
      rcases lt_trichotomy z 0 with Hz|Hz|Hz;
      simp [h, Hy, Hz, zero_rpow_of_neg, zero_rpow_of_pos, top_rpow_of_neg, top_rpow_of_pos,
            mul_pos_of_neg_of_neg, mul_neg_of_neg_of_pos, mul_neg_of_pos_of_neg] },
    { have : x ^ y ‚â† 0, by simp [h],
      simp [coe_rpow_of_ne_zero h, coe_rpow_of_ne_zero this, nnreal.rpow_mul] } }
end

@[simp, norm_cast] lemma rpow_nat_cast (x : ‚Ñù‚â•0‚àû) (n : ‚Ñï) : x ^ (n : ‚Ñù) = x ^ n :=
begin
  cases x,
  { cases n;
    simp [top_rpow_of_pos (nat.cast_add_one_pos _), top_pow (nat.succ_pos _)] },
  { simp [coe_rpow_of_nonneg _ (nat.cast_nonneg n)] }
end

lemma mul_rpow_eq_ite (x y : ‚Ñù‚â•0‚àû) (z : ‚Ñù) :
  (x * y) ^ z = if (x = 0 ‚àß y = ‚ä§ ‚à® x = ‚ä§ ‚àß y = 0) ‚àß z < 0 then ‚ä§ else x ^ z * y ^ z :=
begin
  rcases eq_or_ne z 0 with rfl|hz, { simp },
  replace hz := hz.lt_or_lt,
  wlog hxy : x ‚â§ y := le_total x y using [x y, y x] tactic.skip,
  { rcases eq_or_ne x 0 with rfl|hx0,
    { induction y using with_top.rec_top_coe; cases hz with hz hz; simp [*, hz.not_lt] },
    rcases eq_or_ne y 0 with rfl|hy0, { exact (hx0 (bot_unique hxy)).elim },
    induction x using with_top.rec_top_coe, { cases hz with hz hz; simp [hz, top_unique hxy] },
    induction y using with_top.rec_top_coe, { cases hz with hz hz; simp * },
    simp only [*, false_and, and_false, false_or, if_false],
    norm_cast at *,
    rw [coe_rpow_of_ne_zero (mul_ne_zero hx0 hy0), nnreal.mul_rpow] },
  { convert this using 2; simp only [mul_comm, and_comm, or_comm] }
end

lemma mul_rpow_of_ne_top {x y : ‚Ñù‚â•0‚àû} (hx : x ‚â† ‚ä§) (hy : y ‚â† ‚ä§) (z : ‚Ñù) :
  (x * y) ^ z = x^z * y^z :=
by simp [*, mul_rpow_eq_ite]

@[norm_cast] lemma coe_mul_rpow (x y : ‚Ñù‚â•0) (z : ‚Ñù) :
  ((x : ‚Ñù‚â•0‚àû) * y) ^ z = x^z * y^z :=
mul_rpow_of_ne_top coe_ne_top coe_ne_top z

lemma mul_rpow_of_ne_zero {x y : ‚Ñù‚â•0‚àû} (hx : x ‚â† 0) (hy : y ‚â† 0) (z : ‚Ñù) :
  (x * y) ^ z = x ^ z * y ^ z :=
by simp [*, mul_rpow_eq_ite]

lemma mul_rpow_of_nonneg (x y : ‚Ñù‚â•0‚àû) {z : ‚Ñù} (hz : 0 ‚â§ z) :
  (x * y) ^ z = x ^ z * y ^ z :=
by simp [hz.not_lt, mul_rpow_eq_ite]

lemma inv_rpow (x : ‚Ñù‚â•0‚àû) (y : ‚Ñù) : (x‚Åª¬π) ^ y = (x ^ y)‚Åª¬π :=
begin
  rcases eq_or_ne y 0 with rfl|hy, { simp only [rpow_zero, inv_one] },
  replace hy := hy.lt_or_lt,
  rcases eq_or_ne x 0 with rfl|h0, { cases hy; simp * },
  rcases eq_or_ne x ‚ä§ with rfl|h_top, { cases hy; simp * },
  apply eq_inv_of_mul_eq_one,
  rw [‚Üê mul_rpow_of_ne_zero (inv_ne_zero.2 h_top) h0, inv_mul_cancel h0 h_top, one_rpow]
end

lemma div_rpow_of_nonneg (x y : ‚Ñù‚â•0‚àû) {z : ‚Ñù} (hz : 0 ‚â§ z) :
  (x / y) ^ z = x ^ z / y ^ z :=
by rw [div_eq_mul_inv, mul_rpow_of_nonneg _ _ hz, inv_rpow, div_eq_mul_inv]

lemma strict_mono_rpow_of_pos {z : ‚Ñù} (h : 0 < z) : strict_mono (Œª x : ‚Ñù‚â•0‚àû, x ^ z) :=
begin
  intros x y hxy,
  lift x to ‚Ñù‚â•0 using ne_top_of_lt hxy,
  rcases eq_or_ne y ‚àû with rfl|hy,
  { simp only [top_rpow_of_pos h, coe_rpow_of_nonneg _ h.le, coe_lt_top] },
  { lift y to ‚Ñù‚â•0 using hy,
    simp only [coe_rpow_of_nonneg _ h.le, nnreal.rpow_lt_rpow (coe_lt_coe.1 hxy) h, coe_lt_coe] }
end

lemma monotone_rpow_of_nonneg {z : ‚Ñù} (h : 0 ‚â§ z) : monotone (Œª x : ‚Ñù‚â•0‚àû, x ^ z) :=
h.eq_or_lt.elim (Œª h0, h0 ‚ñ∏ by simp only [rpow_zero, monotone_const])
  (Œª h0, (strict_mono_rpow_of_pos h0).monotone)

/-- Bundles `Œª x : ‚Ñù‚â•0‚àû, x ^ y` into an order isomorphism when `y : ‚Ñù` is positive,
where the inverse is `Œª x : ‚Ñù‚â•0‚àû, x ^ (1 / y)`. -/
@[simps apply] def order_iso_rpow (y : ‚Ñù) (hy : 0 < y) : ‚Ñù‚â•0‚àû ‚âÉo ‚Ñù‚â•0‚àû :=
(strict_mono_rpow_of_pos hy).order_iso_of_right_inverse (Œª x, x ^ y) (Œª x, x ^ (1 / y))
  (Œª x, by { dsimp, rw [‚Üêrpow_mul, one_div_mul_cancel hy.ne.symm, rpow_one] })

lemma order_iso_rpow_symm_apply (y : ‚Ñù) (hy : 0 < y) :
  (order_iso_rpow y hy).symm = order_iso_rpow (1 / y) (one_div_pos.2 hy) :=
by { simp only [order_iso_rpow, one_div_one_div], refl }

lemma rpow_le_rpow {x y : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (h‚ÇÅ : x ‚â§ y) (h‚ÇÇ : 0 ‚â§ z) : x^z ‚â§ y^z :=
monotone_rpow_of_nonneg h‚ÇÇ h‚ÇÅ

lemma rpow_lt_rpow {x y : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (h‚ÇÅ : x < y) (h‚ÇÇ : 0 < z) : x^z < y^z :=
strict_mono_rpow_of_pos h‚ÇÇ h‚ÇÅ

lemma rpow_le_rpow_iff {x y : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hz : 0 < z) : x ^ z ‚â§ y ^ z ‚Üî x ‚â§ y :=
(strict_mono_rpow_of_pos hz).le_iff_le

lemma rpow_lt_rpow_iff {x y : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hz : 0 < z) :  x ^ z < y ^ z ‚Üî x < y :=
(strict_mono_rpow_of_pos hz).lt_iff_lt

lemma le_rpow_one_div_iff {x y : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hz : 0 < z) :  x ‚â§ y ^ (1 / z) ‚Üî x ^ z ‚â§ y :=
begin
  nth_rewrite 0 ‚Üêrpow_one x,
  nth_rewrite 0 ‚Üê@_root_.mul_inv_cancel _ _ z  hz.ne',
  rw [rpow_mul, ‚Üêone_div, @rpow_le_rpow_iff _ _ (1/z) (by simp [hz])],
end

lemma lt_rpow_one_div_iff {x y : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hz : 0 < z) : x < y ^ (1 / z) ‚Üî x ^ z < y :=
begin
  nth_rewrite 0 ‚Üêrpow_one x,
  nth_rewrite 0 ‚Üê@_root_.mul_inv_cancel _ _ z (ne_of_lt hz).symm,
  rw [rpow_mul, ‚Üêone_div, @rpow_lt_rpow_iff _ _ (1/z) (by simp [hz])],
end

lemma rpow_lt_rpow_of_exponent_lt {x : ‚Ñù‚â•0‚àû} {y z : ‚Ñù} (hx : 1 < x) (hx' : x ‚â† ‚ä§) (hyz : y < z) :
  x^y < x^z :=
begin
  lift x to ‚Ñù‚â•0 using hx',
  rw [one_lt_coe_iff] at hx,
  simp [coe_rpow_of_ne_zero (ne_of_gt (lt_trans zero_lt_one hx)),
        nnreal.rpow_lt_rpow_of_exponent_lt hx hyz]
end

lemma rpow_le_rpow_of_exponent_le {x : ‚Ñù‚â•0‚àû} {y z : ‚Ñù} (hx : 1 ‚â§ x) (hyz : y ‚â§ z) : x^y ‚â§ x^z :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
    rcases lt_trichotomy z 0 with Hz|Hz|Hz;
    simp [Hy, Hz, top_rpow_of_neg, top_rpow_of_pos, le_refl];
    linarith },
  { simp only [one_le_coe_iff, some_eq_coe] at hx,
    simp [coe_rpow_of_ne_zero (ne_of_gt (lt_of_lt_of_le zero_lt_one hx)),
          nnreal.rpow_le_rpow_of_exponent_le hx hyz] }
end

lemma rpow_lt_rpow_of_exponent_gt {x : ‚Ñù‚â•0‚àû} {y z : ‚Ñù} (hx0 : 0 < x) (hx1 : x < 1) (hyz : z < y) :
  x^y < x^z :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_lt_of_le hx1 le_top),
  simp at hx0 hx1,
  simp [coe_rpow_of_ne_zero (ne_of_gt hx0), nnreal.rpow_lt_rpow_of_exponent_gt hx0 hx1 hyz]
end

lemma rpow_le_rpow_of_exponent_ge {x : ‚Ñù‚â•0‚àû} {y z : ‚Ñù} (hx1 : x ‚â§ 1) (hyz : z ‚â§ y) :
  x^y ‚â§ x^z :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_le_of_lt hx1 coe_lt_top),
  by_cases h : x = 0,
  { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
    rcases lt_trichotomy z 0 with Hz|Hz|Hz;
    simp [Hy, Hz, h, zero_rpow_of_neg, zero_rpow_of_pos, le_refl];
    linarith },
  { simp at hx1,
    simp [coe_rpow_of_ne_zero h,
          nnreal.rpow_le_rpow_of_exponent_ge (bot_lt_iff_ne_bot.mpr h) hx1 hyz] }
end

lemma rpow_le_self_of_le_one {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : x ‚â§ 1) (h_one_le : 1 ‚â§ z) : x ^ z ‚â§ x :=
begin
  nth_rewrite 1 ‚Üêennreal.rpow_one x,
  exact ennreal.rpow_le_rpow_of_exponent_ge hx h_one_le,
end

lemma le_rpow_self_of_one_le {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : 1 ‚â§ x) (h_one_le : 1 ‚â§ z) : x ‚â§ x ^ z :=
begin
  nth_rewrite 0 ‚Üêennreal.rpow_one x,
  exact ennreal.rpow_le_rpow_of_exponent_le hx h_one_le,
end

lemma rpow_pos_of_nonneg {p : ‚Ñù} {x : ‚Ñù‚â•0‚àû} (hx_pos : 0 < x) (hp_nonneg : 0 ‚â§ p) : 0 < x^p :=
begin
  by_cases hp_zero : p = 0,
  { simp [hp_zero, ennreal.zero_lt_one], },
  { rw ‚Üêne.def at hp_zero,
    have hp_pos := lt_of_le_of_ne hp_nonneg hp_zero.symm,
    rw ‚Üêzero_rpow_of_pos hp_pos, exact rpow_lt_rpow hx_pos hp_pos, },
end

lemma rpow_pos {p : ‚Ñù} {x : ‚Ñù‚â•0‚àû} (hx_pos : 0 < x) (hx_ne_top : x ‚â† ‚ä§) : 0 < x^p :=
begin
  cases lt_or_le 0 p with hp_pos hp_nonpos,
  { exact rpow_pos_of_nonneg hx_pos (le_of_lt hp_pos), },
  { rw [‚Üêneg_neg p, rpow_neg, inv_pos],
    exact rpow_ne_top_of_nonneg (by simp [hp_nonpos]) hx_ne_top, },
end

lemma rpow_lt_one {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : x < 1) (hz : 0 < z) : x^z < 1 :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_lt_of_le hx le_top),
  simp only [coe_lt_one_iff] at hx,
  simp [coe_rpow_of_nonneg _ (le_of_lt hz), nnreal.rpow_lt_one (zero_le x) hx hz],
end

lemma rpow_le_one {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : x ‚â§ 1) (hz : 0 ‚â§ z) : x^z ‚â§ 1 :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_le_of_lt hx coe_lt_top),
  simp only [coe_le_one_iff] at hx,
  simp [coe_rpow_of_nonneg _ hz, nnreal.rpow_le_one hx hz],
end

lemma rpow_lt_one_of_one_lt_of_neg {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : 1 < x) (hz : z < 0) : x^z < 1 :=
begin
  cases x,
  { simp [top_rpow_of_neg hz, ennreal.zero_lt_one] },
  { simp only [some_eq_coe, one_lt_coe_iff] at hx,
    simp [coe_rpow_of_ne_zero (ne_of_gt (lt_trans zero_lt_one hx)),
          nnreal.rpow_lt_one_of_one_lt_of_neg hx hz] },
end

lemma rpow_le_one_of_one_le_of_neg {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : 1 ‚â§ x) (hz : z < 0) : x^z ‚â§ 1 :=
begin
  cases x,
  { simp [top_rpow_of_neg hz, ennreal.zero_lt_one] },
  { simp only [one_le_coe_iff, some_eq_coe] at hx,
    simp [coe_rpow_of_ne_zero (ne_of_gt (lt_of_lt_of_le zero_lt_one hx)),
          nnreal.rpow_le_one_of_one_le_of_nonpos hx (le_of_lt hz)] },
end

lemma one_lt_rpow {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : 1 < x) (hz : 0 < z) : 1 < x^z :=
begin
  cases x,
  { simp [top_rpow_of_pos hz] },
  { simp only [some_eq_coe, one_lt_coe_iff] at hx,
    simp [coe_rpow_of_nonneg _ (le_of_lt hz), nnreal.one_lt_rpow hx hz] }
end

lemma one_le_rpow {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx : 1 ‚â§ x) (hz : 0 < z) : 1 ‚â§ x^z :=
begin
  cases x,
  { simp [top_rpow_of_pos hz] },
  { simp only [one_le_coe_iff, some_eq_coe] at hx,
    simp [coe_rpow_of_nonneg _ (le_of_lt hz), nnreal.one_le_rpow hx (le_of_lt hz)] },
end

lemma one_lt_rpow_of_pos_of_lt_one_of_neg {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx1 : 0 < x) (hx2 : x < 1)
  (hz : z < 0) : 1 < x^z :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_lt_of_le hx2 le_top),
  simp only [coe_lt_one_iff, coe_pos] at ‚ä¢ hx1 hx2,
  simp [coe_rpow_of_ne_zero (ne_of_gt hx1), nnreal.one_lt_rpow_of_pos_of_lt_one_of_neg hx1 hx2 hz],
end

lemma one_le_rpow_of_pos_of_le_one_of_neg {x : ‚Ñù‚â•0‚àû} {z : ‚Ñù} (hx1 : 0 < x) (hx2 : x ‚â§ 1)
  (hz : z < 0) : 1 ‚â§ x^z :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_le_of_lt hx2 coe_lt_top),
  simp only [coe_le_one_iff, coe_pos] at ‚ä¢ hx1 hx2,
  simp [coe_rpow_of_ne_zero (ne_of_gt hx1),
        nnreal.one_le_rpow_of_pos_of_le_one_of_nonpos hx1 hx2 (le_of_lt hz)],
end

lemma to_nnreal_rpow (x : ‚Ñù‚â•0‚àû) (z : ‚Ñù) : (x.to_nnreal) ^ z = (x ^ z).to_nnreal :=
begin
  rcases lt_trichotomy z 0 with H|H|H,
  { cases x, { simp [H, ne_of_lt] },
    by_cases hx : x = 0,
    { simp [hx, H, ne_of_lt] },
    { simp [coe_rpow_of_ne_zero hx] } },
  { simp [H] },
  { cases x, { simp [H, ne_of_gt] },
    simp [coe_rpow_of_nonneg _ (le_of_lt H)] }
end

lemma to_real_rpow (x : ‚Ñù‚â•0‚àû) (z : ‚Ñù) : (x.to_real) ^ z = (x ^ z).to_real :=
by rw [ennreal.to_real, ennreal.to_real, ‚Üênnreal.coe_rpow, ennreal.to_nnreal_rpow]

lemma of_real_rpow_of_pos {x p : ‚Ñù} (hx_pos : 0 < x) :
  ennreal.of_real x ^ p = ennreal.of_real (x ^ p) :=
begin
  simp_rw ennreal.of_real,
  rw [coe_rpow_of_ne_zero, coe_eq_coe, real.to_nnreal_rpow_of_nonneg hx_pos.le],
  simp [hx_pos],
end

lemma of_real_rpow_of_nonneg {x p : ‚Ñù} (hx_nonneg : 0 ‚â§ x) (hp_nonneg : 0 ‚â§ p) :
  ennreal.of_real x ^ p = ennreal.of_real (x ^ p) :=
begin
  by_cases hp0 : p = 0,
  { simp [hp0], },
  by_cases hx0 : x = 0,
  { rw ‚Üê ne.def at hp0,
    have hp_pos : 0 < p := lt_of_le_of_ne hp_nonneg hp0.symm,
    simp [hx0, hp_pos, hp_pos.ne.symm], },
  rw ‚Üê ne.def at hx0,
  exact of_real_rpow_of_pos (hx_nonneg.lt_of_ne hx0.symm),
end

lemma rpow_left_injective {x : ‚Ñù} (hx : x ‚â† 0) :
  function.injective (Œª y : ‚Ñù‚â•0‚àû, y^x) :=
begin
  intros y z hyz,
  dsimp only at hyz,
  rw [‚Üêrpow_one y, ‚Üêrpow_one z, ‚Üê_root_.mul_inv_cancel hx, rpow_mul, rpow_mul, hyz],
end

lemma rpow_left_surjective {x : ‚Ñù} (hx : x ‚â† 0) :
  function.surjective (Œª y : ‚Ñù‚â•0‚àû, y^x) :=
Œª y, ‚ü®y ^ x‚Åª¬π, by simp_rw [‚Üêrpow_mul, _root_.inv_mul_cancel hx, rpow_one]‚ü©

lemma rpow_left_bijective {x : ‚Ñù} (hx : x ‚â† 0) :
  function.bijective (Œª y : ‚Ñù‚â•0‚àû, y^x) :=
‚ü®rpow_left_injective hx, rpow_left_surjective hx‚ü©

theorem tendsto_rpow_at_top {y : ‚Ñù} (hy : 0 < y) :
  tendsto (Œª (x : ‚Ñù‚â•0‚àû), x ^ y) (ùìù ‚ä§) (ùìù ‚ä§) :=
begin
  rw tendsto_nhds_top_iff_nnreal,
  intros x,
  obtain ‚ü®c, _, hc‚ü© :=
    (at_top_basis_Ioi.tendsto_iff at_top_basis_Ioi).mp (nnreal.tendsto_rpow_at_top hy) x trivial,
  have hc' : set.Ioi (‚Üëc) ‚àà ùìù (‚ä§ : ‚Ñù‚â•0‚àû) := Ioi_mem_nhds coe_lt_top,
  refine eventually_of_mem hc' _,
  intros a ha,
  by_cases ha' : a = ‚ä§,
  { simp [ha', hy] },
  lift a to ‚Ñù‚â•0 using ha',
  change ‚Üëc < ‚Üëa at ha,
  rw coe_rpow_of_nonneg _ hy.le,
  exact_mod_cast hc a (by exact_mod_cast ha),
end

private lemma continuous_at_rpow_const_of_pos {x : ‚Ñù‚â•0‚àû} {y : ‚Ñù} (h : 0 < y) :
  continuous_at (Œª a : ‚Ñù‚â•0‚àû, a ^ y) x :=
begin
  by_cases hx : x = ‚ä§,
  { rw [hx, continuous_at],
    convert tendsto_rpow_at_top h,
    simp [h] },
  lift x to ‚Ñù‚â•0 using hx,
  rw continuous_at_coe_iff,
  convert continuous_coe.continuous_at.comp
    (nnreal.continuous_at_rpow_const (or.inr h.le)) using 1,
  ext1 x,
  simp [coe_rpow_of_nonneg _ h.le]
end

@[continuity]
lemma continuous_rpow_const {y : ‚Ñù} : continuous (Œª a : ‚Ñù‚â•0‚àû, a ^ y) :=
begin
  apply continuous_iff_continuous_at.2 (Œª x, _),
  rcases lt_trichotomy 0 y with hy|rfl|hy,
  { exact continuous_at_rpow_const_of_pos hy },
  { simp, exact continuous_at_const },
  { obtain ‚ü®z, hz‚ü© : ‚àÉ z, y = -z := ‚ü®-y, (neg_neg _).symm‚ü©,
    have z_pos : 0 < z, by simpa [hz] using hy,
    simp_rw [hz, rpow_neg],
    exact ennreal.continuous_inv.continuous_at.comp (continuous_at_rpow_const_of_pos z_pos) }
end

lemma tendsto_const_mul_rpow_nhds_zero_of_pos {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† ‚àû) {y : ‚Ñù} (hy : 0 < y) :
  tendsto (Œª x : ‚Ñù‚â•0‚àû, c * x ^ y) (ùìù 0) (ùìù 0) :=
begin
  convert ennreal.tendsto.const_mul (ennreal.continuous_rpow_const.tendsto 0) _,
  { simp [hy] },
  { exact or.inr hc }
end

end ennreal

lemma filter.tendsto.ennrpow_const {Œ± : Type*} {f : filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0‚àû} {a : ‚Ñù‚â•0‚àû} (r : ‚Ñù)
  (hm : tendsto m f (ùìù a)) :
  tendsto (Œª x, (m x) ^ r) f (ùìù (a ^ r)) :=
(ennreal.continuous_rpow_const.tendsto a).comp hm

namespace norm_num
open tactic

theorem rpow_pos (a b : ‚Ñù) (b' : ‚Ñï) (c : ‚Ñù) (hb : b = b') (h : a ^ b' = c) : a ^ b = c :=
by rw [‚Üê h, hb, real.rpow_nat_cast]
theorem rpow_neg (a b : ‚Ñù) (b' : ‚Ñï) (c c' : ‚Ñù)
  (a0 : 0 ‚â§ a) (hb : b = b') (h : a ^ b' = c) (hc : c‚Åª¬π = c') : a ^ -b = c' :=
by rw [‚Üê hc, ‚Üê h, hb, real.rpow_neg a0, real.rpow_nat_cast]

/-- Evaluate `real.rpow a b` where `a` is a rational numeral and `b` is an integer.
(This cannot go via the generalized version `prove_rpow'` because `rpow_pos` has a side condition;
we do not attempt to evaluate `a ^ b` where `a` and `b` are both negative because it comes
out to some garbage.) -/
meta def prove_rpow (a b : expr) : tactic (expr √ó expr) := do
  na ‚Üê a.to_rat,
  ic ‚Üê mk_instance_cache `(‚Ñù),
  match match_sign b with
  | sum.inl b := do
    (ic, a0) ‚Üê guard (na ‚â• 0) >> prove_nonneg ic a,
    nc ‚Üê mk_instance_cache `(‚Ñï),
    (ic, nc, b', hb) ‚Üê prove_nat_uncast ic nc b,
    (ic, c, h) ‚Üê prove_pow a na ic b',
    cr ‚Üê c.to_rat,
    (ic, c', hc) ‚Üê prove_inv ic c cr,
    pure (c', (expr.const ``rpow_neg []).mk_app [a, b, b', c, c', a0, hb, h, hc])
  | sum.inr ff := pure (`(1:‚Ñù), expr.const ``real.rpow_zero [] a)
  | sum.inr tt := do
    nc ‚Üê mk_instance_cache `(‚Ñï),
    (ic, nc, b', hb) ‚Üê prove_nat_uncast ic nc b,
    (ic, c, h) ‚Üê prove_pow a na ic b',
    pure (c, (expr.const ``rpow_pos []).mk_app [a, b, b', c, hb, h])
  end

/-- Generalized version of `prove_cpow`, `prove_nnrpow`, `prove_ennrpow`. -/
meta def prove_rpow' (pos neg zero : name) (Œ± Œ≤ one a b : expr) : tactic (expr √ó expr) := do
  na ‚Üê a.to_rat,
  icŒ± ‚Üê mk_instance_cache Œ±,
  icŒ≤ ‚Üê mk_instance_cache Œ≤,
  match match_sign b with
  | sum.inl b := do
    nc ‚Üê mk_instance_cache `(‚Ñï),
    (icŒ≤, nc, b', hb) ‚Üê prove_nat_uncast icŒ≤ nc b,
    (icŒ±, c, h) ‚Üê prove_pow a na icŒ± b',
    cr ‚Üê c.to_rat,
    (icŒ±, c', hc) ‚Üê prove_inv icŒ± c cr,
    pure (c', (expr.const neg []).mk_app [a, b, b', c, c', hb, h, hc])
  | sum.inr ff := pure (one, expr.const zero [] a)
  | sum.inr tt := do
    nc ‚Üê mk_instance_cache `(‚Ñï),
    (icŒ≤, nc, b', hb) ‚Üê prove_nat_uncast icŒ≤ nc b,
    (icŒ±, c, h) ‚Üê prove_pow a na icŒ± b',
    pure (c, (expr.const pos []).mk_app [a, b, b', c, hb, h])
  end

open_locale nnreal ennreal

theorem cpow_pos (a b : ‚ÑÇ) (b' : ‚Ñï) (c : ‚ÑÇ) (hb : b = b') (h : a ^ b' = c) : a ^ b = c :=
by rw [‚Üê h, hb, complex.cpow_nat_cast]
theorem cpow_neg (a b : ‚ÑÇ) (b' : ‚Ñï) (c c' : ‚ÑÇ)
  (hb : b = b') (h : a ^ b' = c) (hc : c‚Åª¬π = c') : a ^ -b = c' :=
by rw [‚Üê hc, ‚Üê h, hb, complex.cpow_neg, complex.cpow_nat_cast]

theorem nnrpow_pos (a : ‚Ñù‚â•0) (b : ‚Ñù) (b' : ‚Ñï) (c : ‚Ñù‚â•0)
  (hb : b = b') (h : a ^ b' = c) : a ^ b = c :=
by rw [‚Üê h, hb, nnreal.rpow_nat_cast]
theorem nnrpow_neg (a : ‚Ñù‚â•0) (b : ‚Ñù) (b' : ‚Ñï) (c c' : ‚Ñù‚â•0)
  (hb : b = b') (h : a ^ b' = c) (hc : c‚Åª¬π = c') : a ^ -b = c' :=
by rw [‚Üê hc, ‚Üê h, hb, nnreal.rpow_neg, nnreal.rpow_nat_cast]

theorem ennrpow_pos (a : ‚Ñù‚â•0‚àû) (b : ‚Ñù) (b' : ‚Ñï) (c : ‚Ñù‚â•0‚àû)
  (hb : b = b') (h : a ^ b' = c) : a ^ b = c :=
by rw [‚Üê h, hb, ennreal.rpow_nat_cast]
theorem ennrpow_neg (a : ‚Ñù‚â•0‚àû) (b : ‚Ñù) (b' : ‚Ñï) (c c' : ‚Ñù‚â•0‚àû)
  (hb : b = b') (h : a ^ b' = c) (hc : c‚Åª¬π = c') : a ^ -b = c' :=
by rw [‚Üê hc, ‚Üê h, hb, ennreal.rpow_neg, ennreal.rpow_nat_cast]

/-- Evaluate `complex.cpow a b` where `a` is a rational numeral and `b` is an integer. -/
meta def prove_cpow : expr ‚Üí expr ‚Üí tactic (expr √ó expr) :=
prove_rpow' ``cpow_pos ``cpow_neg ``complex.cpow_zero `(‚ÑÇ) `(‚ÑÇ) `(1:‚ÑÇ)

/-- Evaluate `nnreal.rpow a b` where `a` is a rational numeral and `b` is an integer. -/
meta def prove_nnrpow : expr ‚Üí expr ‚Üí tactic (expr √ó expr) :=
prove_rpow' ``nnrpow_pos ``nnrpow_neg ``nnreal.rpow_zero `(‚Ñù‚â•0) `(‚Ñù) `(1:‚Ñù‚â•0)

/-- Evaluate `ennreal.rpow a b` where `a` is a rational numeral and `b` is an integer. -/
meta def prove_ennrpow : expr ‚Üí expr ‚Üí tactic (expr √ó expr) :=
prove_rpow' ``ennrpow_pos ``ennrpow_neg ``ennreal.rpow_zero `(‚Ñù‚â•0‚àû) `(‚Ñù) `(1:‚Ñù‚â•0‚àû)

/-- Evaluates expressions of the form `rpow a b`, `cpow a b` and `a ^ b` in the special case where
`b` is an integer and `a` is a positive rational (so it's really just a rational power). -/
@[norm_num] meta def eval_rpow_cpow : expr ‚Üí tactic (expr √ó expr)
| `(@has_pow.pow _ _ real.has_pow %%a %%b) := b.to_int >> prove_rpow a b
| `(real.rpow %%a %%b) := b.to_int >> prove_rpow a b
| `(@has_pow.pow _ _ complex.has_pow %%a %%b) := b.to_int >> prove_cpow a b
| `(complex.cpow %%a %%b) := b.to_int >> prove_cpow a b
| `(@has_pow.pow _ _ nnreal.real.has_pow %%a %%b) := b.to_int >> prove_nnrpow a b
| `(nnreal.rpow %%a %%b) := b.to_int >> prove_nnrpow a b
| `(@has_pow.pow _ _ ennreal.real.has_pow %%a %%b) := b.to_int >> prove_ennrpow a b
| `(ennreal.rpow %%a %%b) := b.to_int >> prove_ennrpow a b
| _ := tactic.failed

end norm_num
