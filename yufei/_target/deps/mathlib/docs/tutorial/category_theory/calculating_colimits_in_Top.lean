import topology.category.Top.limits
import category_theory.limits.shapes
import topology.instances.real
import topology.tactic

/-! This file contains some demos of using the (co)limits API to do topology. -/

noncomputable theory

open category_theory
open category_theory.limits

def R : Top := Top.of ‚Ñù
def I : Top := Top.of (set.Icc 0 1 : set ‚Ñù)
def pt : Top := Top.of unit

section MappingCylinder
-- Let's construct the mapping cylinder.
def to_pt (X : Top) : X ‚ü∂ pt :=
{ to_fun := Œª _, unit.star, } -- We don't need to prove continuity: this is done automatically.
def I‚ÇÄ : pt ‚ü∂ I :=
{ to_fun := Œª _, ‚ü®(0 : ‚Ñù), by norm_num [set.left_mem_Icc]‚ü©, }
def I‚ÇÅ : pt ‚ü∂ I :=
{ to_fun := Œª _, ‚ü®(1 : ‚Ñù), by norm_num [set.right_mem_Icc]‚ü©, }

def cylinder (X : Top) : Top := prod X I
-- To define a map to the cylinder, we give a map to each factor.
-- `prod.lift` is a helper method, providing a wrapper around `limit.lift` for binary products.
def cylinder‚ÇÄ (X : Top) : X ‚ü∂ cylinder X :=
prod.lift (ùüô X) (to_pt X ‚â´ I‚ÇÄ)
def cylinder‚ÇÅ (X : Top) : X ‚ü∂ cylinder X :=
prod.lift (ùüô X) (to_pt X ‚â´ I‚ÇÅ)

-- The mapping cylinder is the pushout of the diagram
--    X
--   ‚Üô ‚Üò
--  Y   (X x I)
-- (`pushout` is implemented just as a wrapper around `colimit`) is
def mapping_cylinder {X Y : Top} (f : X ‚ü∂ Y) : Top := pushout f (cylinder‚ÇÅ X)

/-- We construct the map from `X` into the "bottom" of the mapping cylinder
for `f : X ‚ü∂ Y`, as the composition of the inclusion of `X` into the bottom of the
cylinder `prod X I`, followed by the map `pushout.inr` of `prod X I` into `mapping_cylinder f`. -/
def mapping_cylinder‚ÇÄ {X Y : Top} (f : X ‚ü∂ Y) : X ‚ü∂ mapping_cylinder f :=
cylinder‚ÇÄ X ‚â´ pushout.inr

/--
The mapping cone is defined as the pushout of
```
         X
        ‚Üô ‚Üò
 (Cyl f)   pt
```
(where the left arrow is `mapping_cylinder‚ÇÄ`).

This makes it an iterated colimit; one could also define it in one step as the colimit of
```
--    X        X
--   ‚Üô ‚Üò      ‚Üô ‚Üò
--  Y   (X x I)  pt
```
-/
def mapping_cone {X Y : Top} (f : X ‚ü∂ Y) : Top := pushout (mapping_cylinder‚ÇÄ f) (to_pt X)

-- TODO Hopefully someone will write a nice tactic for generating diagrams quickly,
-- and we'll be able to verify that this iterated construction is the same as the colimit
-- over a single diagram.
end MappingCylinder

section Gluing

-- Here's two copies of the real line glued together at a point.
def f : pt ‚ü∂ R := { to_fun := Œª _, (0 : ‚Ñù), }

/-- Two copies of the real line glued together at 0. -/
def X : Top := pushout f f

-- To define a map out of it, we define maps out of each copy of the line,
-- and check the maps agree at 0.
def g : X ‚ü∂ R :=
pushout.desc (ùüô _) (ùüô _) rfl

end Gluing

section Products

/-- The countably infinite product of copies of `‚Ñù`. -/
def Y : Top := ‚àè (Œª n : ‚Ñï, R)

/--
We can define a point in this infinite product by specifying its coordinates.
Let's define the point whose `n`-th coordinate is `n + 1` (as a real number).
-/
def q : pt ‚ü∂ Y :=
pi.lift (Œª (n : ‚Ñï), ‚ü®Œª (_ : pt), (n + 1 : ‚Ñù), by continuity‚ü©)

-- Note that writing `Y := ‚àè (Œª n : ‚Ñï, R)` gives us *some* topological space which satisfies the
-- universal property of the product, not some explicit construction of the product, so we cannot
-- rely on any definitional properties of `Y` or `q`.
-- If we really want to talk about a specific construction of the limit, we have to work directly
-- with the corresponding limit cones. In this case, `Top.limit_cone`.

end Products
