/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.natural_transformation

/-!
# The bicategory of oplax functors between two bicategories

Given bicategories `B` and `C`, we give a bicategory structure on `oplax_functor B C` whose
* objects are oplax functors,
* 1-morphisms are oplax natural transformations, and
* 2-morphisms are modifications.
-/

namespace category_theory

open category bicategory
open_locale bicategory

universes w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variables {B : Type u‚ÇÅ} [bicategory.{w‚ÇÅ v‚ÇÅ} B] {C : Type u‚ÇÇ} [bicategory.{w‚ÇÇ v‚ÇÇ} C]
variables {F G H I : oplax_functor B C}

namespace oplax_nat_trans

/-- Left whiskering of an oplax natural transformation and a modification. -/
@[simps]
def whisker_left (Œ∑ : F ‚ü∂ G) {Œ∏ Œπ : G ‚ü∂ H} (Œì : Œ∏ ‚ü∂ Œπ) : Œ∑ ‚â´ Œ∏ ‚ü∂ Œ∑ ‚â´ Œπ :=
{ app := Œª a, Œ∑.app a ‚óÅ Œì.app a,
  naturality' := Œª a b f, by
  { dsimp,
    simp only [assoc],
    rw [associator_inv_naturality_right_assoc, whisker_exchange_assoc,
        associator_naturality_right_assoc, Œì.whisker_left_naturality_assoc,
        associator_inv_naturality_middle] } }

/-- Right whiskering of an oplax natural transformation and a modification. -/
@[simps]
def whisker_right {Œ∑ Œ∏ : F ‚ü∂ G} (Œì : Œ∑ ‚ü∂ Œ∏) (Œπ : G ‚ü∂ H) : Œ∑ ‚â´ Œπ ‚ü∂ Œ∏ ‚â´ Œπ :=
{ app := Œª a, Œì.app a ‚ñ∑ Œπ.app a,
  naturality' := Œª a b f, by
  { dsimp,
    simp only [assoc],
    rw [associator_inv_naturality_middle_assoc, Œì.whisker_right_naturality_assoc,
        associator_naturality_left_assoc, ‚Üêwhisker_exchange_assoc,
        associator_inv_naturality_left] } }

/-- Associator for the vertical composition of oplax natural transformations. -/
@[simps]
def associator (Œ∑ : F ‚ü∂ G) (Œ∏ : G ‚ü∂ H) (Œπ : H ‚ü∂ I) : (Œ∑ ‚â´ Œ∏) ‚â´ Œπ ‚âÖ Œ∑ ‚â´ (Œ∏ ‚â´ Œπ) :=
modification_iso.of_components (Œª a, Œ±_ (Œ∑.app a) (Œ∏.app a) (Œπ.app a))
begin
  intros a b f,
  dsimp,
  simp only [whisker_right_comp, whisker_left_comp, assoc],
  rw [‚Üêpentagon_inv_inv_hom_hom_inv_assoc, ‚Üêassociator_naturality_left_assoc,
      pentagon_hom_hom_inv_hom_hom_assoc, ‚Üêassociator_naturality_middle_assoc,
      ‚Üêpentagon_inv_hom_hom_hom_hom_assoc, ‚Üêassociator_naturality_right_assoc,
      pentagon_hom_inv_inv_inv_hom]
end

/-- Left unitor for the vertical composition of oplax natural transformations. -/
@[simps]
def left_unitor (Œ∑ : F ‚ü∂ G) : ùüô F ‚â´ Œ∑ ‚âÖ Œ∑ :=
modification_iso.of_components (Œª a, Œª_ (Œ∑.app a))
begin
  intros a b f,
  dsimp,
  simp only [triangle_assoc_comp_right_assoc, whisker_right_comp, assoc, whisker_exchange_assoc],
  rw [‚Üêleft_unitor_comp, left_unitor_naturality, left_unitor_comp],
  simp only [iso.hom_inv_id_assoc, inv_hom_whisker_right_assoc, assoc, whisker_exchange_assoc]
end

/-- Right unitor for the vertical composition of oplax natural transformations. -/
@[simps]
def right_unitor (Œ∑ : F ‚ü∂ G) : Œ∑ ‚â´ ùüô G ‚âÖ Œ∑ :=
modification_iso.of_components (Œª a, œÅ_ (Œ∑.app a))
begin
  intros a b f,
  dsimp,
  simp only [triangle_assoc_comp_left_inv, inv_hom_whisker_right_assoc, whisker_exchange,
    assoc, whisker_left_comp],
  rw [‚Üêright_unitor_comp, right_unitor_naturality, right_unitor_comp],
  simp only [iso.inv_hom_id_assoc, assoc]
end

end oplax_nat_trans

variables (B C)

/-- A bicategory structure on the oplax functors between bicategories. -/
@[simps]
instance oplax_functor.bicategory : bicategory (oplax_functor B C) :=
{ whisker_left  := Œª F G H Œ∑ _ _ Œì, oplax_nat_trans.whisker_left Œ∑ Œì,
  whisker_right := Œª F G H _ _ Œì Œ∑, oplax_nat_trans.whisker_right Œì Œ∑,
  associator := Œª F G H I, oplax_nat_trans.associator,
  left_unitor   := Œª F G, oplax_nat_trans.left_unitor,
  right_unitor  := Œª F G, oplax_nat_trans.right_unitor,
  associator_naturality_left'   := by { intros, ext, apply associator_naturality_left },
  associator_naturality_middle' := by { intros, ext, apply associator_naturality_middle },
  associator_naturality_right'  := by { intros, ext, apply associator_naturality_right },
  left_unitor_naturality'   := by { intros, ext, apply left_unitor_naturality },
  right_unitor_naturality'  := by { intros, ext, apply right_unitor_naturality },
  pentagon' := by { intros, ext, apply pentagon },
  triangle' := by { intros, ext, apply triangle } }

end category_theory
