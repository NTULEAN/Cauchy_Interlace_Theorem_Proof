/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.structured_arrow
import category_theory.groupoid
import category_theory.punit

/-!
# The category of elements

This file defines the category of elements, also known as (a special case of) the Grothendieck
construction.

Given a functor `F : C ‚•§ Type`, an object of `F.elements` is a pair `(X : C, x : F.obj X)`.
A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.

## Implementation notes

This construction is equivalent to a special case of a comma construction, so this is mostly just a
more convenient API. We prove the equivalence in
`category_theory.category_of_elements.structured_arrow_equivalence`.

## References
* [Emily Riehl, *Category Theory in Context*, Section 2.4][riehl2017]
* <https://en.wikipedia.org/wiki/Category_of_elements>
* <https://ncatlab.org/nlab/show/category+of+elements>

## Tags
category of elements, Grothendieck construction, comma category
-/

namespace category_theory

universes w v u
variables {C : Type u} [category.{v} C]

/--
The type of objects for the category of elements of a functor `F : C ‚•§ Type`
is a pair `(X : C, x : F.obj X)`.
-/
@[nolint has_inhabited_instance]
def functor.elements (F : C ‚•§ Type w) := (Œ£ c : C, F.obj c)

/-- The category structure on `F.elements`, for `F : C ‚•§ Type`.
    A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.
 -/
instance category_of_elements (F : C ‚•§ Type w) : category.{v} F.elements :=
{ hom := Œª p q, { f : p.1 ‚ü∂ q.1 // (F.map f) p.2 = q.2 },
  id := Œª p, ‚ü®ùüô p.1, by obviously‚ü©,
  comp := Œª p q r f g, ‚ü®f.val ‚â´ g.val, by obviously‚ü© }

namespace category_of_elements

@[ext]
lemma ext (F : C ‚•§ Type w) {x y : F.elements} (f g : x ‚ü∂ y) (w : f.val = g.val) : f = g :=
subtype.ext_val w

@[simp] lemma comp_val {F : C ‚•§ Type w} {p q r : F.elements} {f : p ‚ü∂ q} {g : q ‚ü∂ r} :
  (f ‚â´ g).val = f.val ‚â´ g.val := rfl

@[simp] lemma id_val {F : C ‚•§ Type w} {p : F.elements} : (ùüô p : p ‚ü∂ p).val = ùüô p.1 := rfl

end category_of_elements

noncomputable
instance groupoid_of_elements {G : Type u} [groupoid.{v} G] (F : G ‚•§ Type w) :
  groupoid F.elements :=
{ inv := Œª p q f, ‚ü®inv f.val,
      calc F.map (inv f.val) q.2 = F.map (inv f.val) (F.map f.val p.2) : by rw f.2
                             ... = (F.map f.val ‚â´ F.map (inv f.val)) p.2 : by simp
                             ... = p.2 : by {rw ‚Üêfunctor.map_comp, simp}‚ü©, }

namespace category_of_elements
variable (F : C ‚•§ Type w)

/-- The functor out of the category of elements which forgets the element. -/
@[simps]
def œÄ : F.elements ‚•§ C :=
{ obj := Œª X, X.1,
  map := Œª X Y f, f.val }

/--
A natural transformation between functors induces a functor between the categories of elements.
-/
@[simps]
def map {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : F‚ÇÅ.elements ‚•§ F‚ÇÇ.elements :=
{ obj := Œª t, ‚ü®t.1, Œ±.app t.1 t.2‚ü©,
  map := Œª t‚ÇÅ t‚ÇÇ k, ‚ü®k.1, by simpa [‚Üêk.2] using (functor_to_types.naturality _ _ Œ± k.1 t‚ÇÅ.2).symm‚ü© }

@[simp] lemma map_œÄ {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : map Œ± ‚ãô œÄ F‚ÇÇ = œÄ F‚ÇÅ := rfl

/-- The forward direction of the equivalence `F.elements ‚âÖ (*, F)`. -/
def to_structured_arrow : F.elements ‚•§ structured_arrow punit F :=
{ obj := Œª X, structured_arrow.mk (Œª _, X.2),
  map := Œª X Y f, structured_arrow.hom_mk f.val (by tidy) }

@[simp] lemma to_structured_arrow_obj (X) :
  (to_structured_arrow F).obj X = { left := punit.star, right := X.1, hom := Œª _, X.2 } := rfl
@[simp] lemma to_comma_map_right {X Y} (f : X ‚ü∂ Y) :
  ((to_structured_arrow F).map f).right = f.val := rfl

/-- The reverse direction of the equivalence `F.elements ‚âÖ (*, F)`. -/
def from_structured_arrow : structured_arrow punit F ‚•§ F.elements :=
{ obj := Œª X, ‚ü®X.right, X.hom (punit.star)‚ü©,
  map := Œª X Y f, ‚ü®f.right, congr_fun f.w'.symm punit.star‚ü© }

@[simp] lemma from_structured_arrow_obj (X) :
  (from_structured_arrow F).obj X = ‚ü®X.right, X.hom (punit.star)‚ü© := rfl
@[simp] lemma from_structured_arrow_map {X Y} (f : X ‚ü∂ Y) :
  (from_structured_arrow F).map f = ‚ü®f.right, congr_fun f.w'.symm punit.star‚ü© := rfl

/-- The equivalence between the category of elements `F.elements`
    and the comma category `(*, F)`. -/
@[simps]
def structured_arrow_equivalence : F.elements ‚âå structured_arrow punit F :=
equivalence.mk (to_structured_arrow F) (from_structured_arrow F)
  (nat_iso.of_components (Œª X, eq_to_iso (by tidy)) (by tidy))
  (nat_iso.of_components
    (Œª X, { hom := { right := ùüô _ }, inv := { right := ùüô _ } })
    (by tidy))

open opposite

/--
The forward direction of the equivalence `F.elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `category_theory.yoneda_sections`.
-/
@[simps]
def to_costructured_arrow (F : C·µí·µñ ‚•§ Type v) : (F.elements)·µí·µñ ‚•§ costructured_arrow yoneda F :=
{ obj := Œª X, costructured_arrow.mk
    ((yoneda_sections (unop (unop X).fst) F).inv (ulift.up (unop X).2)),
  map := Œª X Y f,
  begin
    fapply costructured_arrow.hom_mk,
    exact f.unop.val.unop,
    ext y,
    simp only [costructured_arrow.mk_hom_eq_self, yoneda_map_app, functor_to_types.comp, op_comp,
      yoneda_sections_inv_app, functor_to_types.map_comp_apply, quiver.hom.op_unop,
      subtype.val_eq_coe],
    congr,
    exact f.unop.2,
  end }

/--
The reverse direction of the equivalence `F.elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `category_theory.yoneda_equiv`.
-/
@[simps]
def from_costructured_arrow (F : C·µí·µñ ‚•§ Type v) : (costructured_arrow yoneda F)·µí·µñ ‚•§ F.elements :=
{ obj := Œª X, ‚ü®op (unop X).1, yoneda_equiv.1 (unop X).3‚ü©,
  map := Œª X Y f, ‚ü®f.unop.1.op,
  begin
    convert (congr_fun ((unop X).hom.naturality f.unop.left.op) (ùüô _)).symm,
    simp only [equiv.to_fun_as_coe, quiver.hom.unop_op, yoneda_equiv_apply,
      types_comp_apply, category.comp_id, yoneda_obj_map],
    have : yoneda.map f.unop.left ‚â´ (unop X).hom = (unop Y).hom,
    { convert f.unop.3, erw category.comp_id },
    erw ‚Üê this,
    simp only [yoneda_map_app, functor_to_types.comp],
    erw category.id_comp
  end ‚ü©}

@[simp]
lemma from_costructured_arrow_obj_mk (F : C·µí·µñ ‚•§ Type v) {X : C} (f : yoneda.obj X ‚ü∂ F) :
  (from_costructured_arrow F).obj (op (costructured_arrow.mk f)) = ‚ü®op X, yoneda_equiv.1 f‚ü© := rfl

/-- The unit of the equivalence `F.elements·µí·µñ ‚âÖ (yoneda, F)` is indeed iso. -/
lemma from_to_costructured_arrow_eq (F : C·µí·µñ ‚•§ Type v) :
 (to_costructured_arrow F).right_op ‚ãô from_costructured_arrow F = ùü≠ _ :=
begin
  apply functor.ext,
  intros X Y f,
  have : ‚àÄ {a b : F.elements} (H : a = b),
    ‚Üë(eq_to_hom H) = eq_to_hom (show a.fst = b.fst, by { cases H, refl }) :=
    Œª _ _ H, by { cases H, refl },
  ext, simp[this],
  tidy
end

/-- The counit of the equivalence `F.elements·µí·µñ ‚âÖ (yoneda, F)` is indeed iso. -/
lemma to_from_costructured_arrow_eq (F : C·µí·µñ ‚•§ Type v) :
  (from_costructured_arrow F).right_op ‚ãô to_costructured_arrow F = ùü≠ _ :=
begin
  apply functor.hext,
  { intro X, cases X, cases X_right,
    simp only [functor.id_obj, functor.right_op_obj,
      to_costructured_arrow_obj, functor.comp_obj, costructured_arrow.mk],
    congr,
    ext x f,
    convert congr_fun (X_hom.naturality f.op).symm (ùüô X_left),
    simp only [quiver.hom.unop_op, yoneda_obj_map],
    erw category.comp_id },
  intros X Y f,
  cases X, cases Y, cases f, cases X_right, cases Y_right,
  simp[costructured_arrow.hom_mk],
  delta costructured_arrow.mk,
  congr,
  { ext x f,
    convert congr_fun (X_hom.naturality f.op).symm (ùüô X_left),
    simp only [quiver.hom.unop_op, category_theory.yoneda_obj_map],
    erw category.comp_id },
  { ext x f,
    convert congr_fun (Y_hom.naturality f.op).symm (ùüô Y_left),
    simp only [quiver.hom.unop_op, category_theory.yoneda_obj_map],
    erw category.comp_id },
  simp,
  exact proof_irrel_heq _ _,
end


/-- The equivalence `F.elements·µí·µñ ‚âÖ (yoneda, F)` given by yoneda lemma. -/
@[simps] def costructured_arrow_yoneda_equivalence (F : C·µí·µñ ‚•§ Type v) :
  (F.elements)·µí·µñ ‚âå costructured_arrow yoneda F :=
equivalence.mk (to_costructured_arrow F) (from_costructured_arrow F).right_op
  (nat_iso.op (eq_to_iso (from_to_costructured_arrow_eq F)))
  (eq_to_iso $ to_from_costructured_arrow_eq F)

/--
The equivalence `(-.elements)·µí·µñ ‚âÖ (yoneda, -)` of is actually a natural isomorphism of functors.
-/
lemma costructured_arrow_yoneda_equivalence_naturality {F‚ÇÅ F‚ÇÇ : C·µí·µñ ‚•§ Type v}
  (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : (map Œ±).op ‚ãô to_costructured_arrow F‚ÇÇ =
    to_costructured_arrow F‚ÇÅ ‚ãô costructured_arrow.map Œ± :=
begin
  fapply functor.ext,
  { intro X,
    simp only [costructured_arrow.map_mk, to_costructured_arrow_obj,
      functor.op_obj, functor.comp_obj],
    congr,
    ext x f,
    simpa using congr_fun (Œ±.naturality f.op).symm (unop X).snd },
  { intros X Y f, ext,
    have : ‚àÄ {F : C·µí·µñ ‚•§ Type v} {a b : costructured_arrow yoneda F} (H : a = b),
      comma_morphism.left (eq_to_hom H) = eq_to_hom (show a.left = b.left, by { cases H, refl }) :=
      Œª _ _ _ H, by { cases H, refl },
    simp [this] }
end

end category_of_elements
end category_theory
