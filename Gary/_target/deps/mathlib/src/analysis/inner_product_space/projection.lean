/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Fr√©d√©ric Dupuis, Heather Macbeth
-/
import analysis.convex.basic
import analysis.inner_product_space.basic
import analysis.normed_space.is_R_or_C

/-!
# The orthogonal projection

Given a nonempty complete subspace `K` of an inner product space `E`, this file constructs
`orthogonal_projection K : E ‚ÜíL[ùïú] K`, the orthogonal projection of `E` onto `K`.  This map
satisfies: for any point `u` in `E`, the point `v = orthogonal_projection K u` in `K` minimizes the
distance `‚à•u - v‚à•` to `u`.

Also a linear isometry equivalence `reflection K : E ‚âÉ‚Çó·µ¢[ùïú] E` is constructed, by choosing, for
each `u : E`, the point `reflection K u` to satisfy
`u + (reflection K u) = 2 ‚Ä¢ orthogonal_projection K u`.

Basic API for `orthogonal_projection` and `reflection` is developed.

Next, the orthogonal projection is used to prove a series of more subtle lemmas about the
the orthogonal complement of complete subspaces of `E` (the orthogonal complement itself was
defined in `analysis.inner_product_space.basic`); the lemma
`submodule.sup_orthogonal_of_is_complete`, stating that for a complete subspace `K` of `E` we have
`K ‚äî K·óÆ = ‚ä§`, is a typical example.

The last section covers orthonormal bases, etc. The lemma
`maximal_orthonormal_iff_orthogonal_complement_eq_bot` states that an orthonormal set in an inner
product space is maximal, if and only the orthogonal complement of its span is trivial.
Various consequences are stated for finite-dimensional `E`, including that a maximal orthonormal
set is a basis (`maximal_orthonormal_iff_basis_of_finite_dimensional`); these consequences require
the theory on the orthogonal complement developed earlier in this file.  For consequences in
infinite dimension (Hilbert bases, etc.), see the file `analysis.inner_product_space.l2_space`.

## References

The orthogonal projection construction is adapted from
*  [Cl√©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
*  [Cl√©ment & Martin, *A Coq formal proof of the Lax‚ÄìMilgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/

noncomputable theory

open is_R_or_C real filter
open_locale big_operators topological_space

variables {ùïú E F : Type*} [is_R_or_C ùïú]
variables [inner_product_space ùïú E] [inner_product_space ‚Ñù F]
local notation `‚ü™`x`, `y`‚ü´` := @inner ùïú E _ x y
local notation `absR` := has_abs.abs

/-! ### Orthogonal projection in inner product spaces -/

/--
Existence of minimizers
Let `u` be a point in a real inner product space, and let `K` be a nonempty complete convex subset.
Then there exists a (unique) `v` in `K` that minimizes the distance `‚à•u - v‚à•` to `u`.
 -/
-- FIXME this monolithic proof causes a deterministic timeout with `-T50000`
-- It should be broken in a sequence of more manageable pieces,
-- perhaps with individual statements for the three steps below.
theorem exists_norm_eq_infi_of_complete_convex {K : set F} (ne : K.nonempty) (h‚ÇÅ : is_complete K)
  (h‚ÇÇ : convex ‚Ñù K) : ‚àÄ u : F, ‚àÉ v ‚àà K, ‚à•u - v‚à• = ‚®Ö w : K, ‚à•u - w‚à• := assume u,
begin
  let Œ¥ := ‚®Ö w : K, ‚à•u - w‚à•,
  letI : nonempty K := ne.to_subtype,
  have zero_le_Œ¥ : 0 ‚â§ Œ¥ := le_cinfi (Œª _, norm_nonneg _),
  have Œ¥_le : ‚àÄ w : K, Œ¥ ‚â§ ‚à•u - w‚à•,
    from cinfi_le ‚ü®0, set.forall_range_iff.2 $ Œª _, norm_nonneg _‚ü©,
  have Œ¥_le' : ‚àÄ w ‚àà K, Œ¥ ‚â§ ‚à•u - w‚à• := assume w hw, Œ¥_le ‚ü®w, hw‚ü©,
  -- Step 1: since `Œ¥` is the infimum, can find a sequence `w : ‚Ñï ‚Üí K` in `K`
  -- such that `‚à•u - w n‚à• < Œ¥ + 1 / (n + 1)` (which implies `‚à•u - w n‚à• --> Œ¥`);
  -- maybe this should be a separate lemma
  have exists_seq : ‚àÉ w : ‚Ñï ‚Üí K, ‚àÄ n, ‚à•u - w n‚à• < Œ¥ + 1 / (n + 1),
  { have hŒ¥ : ‚àÄn:‚Ñï, Œ¥ < Œ¥ + 1 / (n + 1), from
      Œª n, lt_add_of_le_of_pos le_rfl nat.one_div_pos_of_nat,
    have h := Œª n, exists_lt_of_cinfi_lt (hŒ¥ n),
    let w : ‚Ñï ‚Üí K := Œª n, classical.some (h n),
    exact ‚ü®w, Œª n, classical.some_spec (h n)‚ü© },
  rcases exists_seq with ‚ü®w, hw‚ü©,
  have norm_tendsto : tendsto (Œª n, ‚à•u - w n‚à•) at_top (nhds Œ¥),
  { have h : tendsto (Œª n:‚Ñï, Œ¥) at_top (nhds Œ¥) := tendsto_const_nhds,
    have h' : tendsto (Œª n:‚Ñï, Œ¥ + 1 / (n + 1)) at_top (nhds Œ¥),
    { convert h.add tendsto_one_div_add_at_top_nhds_0_nat, simp only [add_zero] },
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le h h'
      (Œª x, Œ¥_le _) (Œª x, le_of_lt (hw _)) },
  -- Step 2: Prove that the sequence `w : ‚Ñï ‚Üí K` is a Cauchy sequence
  have seq_is_cauchy : cauchy_seq (Œª n, ((w n):F)),
  { rw cauchy_seq_iff_le_tendsto_0, -- splits into three goals
    let b := Œª n:‚Ñï, (8 * Œ¥ * (1/(n+1)) + 4 * (1/(n+1)) * (1/(n+1))),
    use (Œªn, sqrt (b n)),
    split,
    -- first goal :  `‚àÄ (n : ‚Ñï), 0 ‚â§ sqrt (b n)`
    assume n, exact sqrt_nonneg _,
    split,
    -- second goal : `‚àÄ (n m N : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí dist ‚Üë(w n) ‚Üë(w m) ‚â§ sqrt (b N)`
    assume p q N hp hq,
    let wp := ((w p):F), let wq := ((w q):F),
    let a := u - wq, let b := u - wp,
    let half := 1 / (2:‚Ñù), let div := 1 / ((N:‚Ñù) + 1),
    have : 4 * ‚à•u - half ‚Ä¢ (wq + wp)‚à• * ‚à•u - half ‚Ä¢ (wq + wp)‚à• + ‚à•wp - wq‚à• * ‚à•wp - wq‚à• =
      2 * (‚à•a‚à• * ‚à•a‚à• + ‚à•b‚à• * ‚à•b‚à•) :=
    calc
      4 * ‚à•u - half‚Ä¢(wq + wp)‚à• * ‚à•u - half‚Ä¢(wq + wp)‚à• + ‚à•wp - wq‚à• * ‚à•wp - wq‚à•
          = (2*‚à•u - half‚Ä¢(wq + wp)‚à•) * (2 * ‚à•u - half‚Ä¢(wq + wp)‚à•) + ‚à•wp-wq‚à•*‚à•wp-wq‚à• : by ring
      ... = (absR ((2:‚Ñù)) * ‚à•u - half‚Ä¢(wq + wp)‚à•) * (absR ((2:‚Ñù)) * ‚à•u - half‚Ä¢(wq+wp)‚à•) +
            ‚à•wp-wq‚à•*‚à•wp-wq‚à• :
      by { rw _root_.abs_of_nonneg, exact zero_le_two }
      ... = ‚à•(2:‚Ñù) ‚Ä¢ (u - half ‚Ä¢ (wq + wp))‚à• * ‚à•(2:‚Ñù) ‚Ä¢ (u - half ‚Ä¢ (wq + wp))‚à• +
            ‚à•wp-wq‚à• * ‚à•wp-wq‚à• :
      by simp [norm_smul]
      ... = ‚à•a + b‚à• * ‚à•a + b‚à• + ‚à•a - b‚à• * ‚à•a - b‚à• :
      begin
        rw [smul_sub, smul_smul, mul_one_div_cancel (_root_.two_ne_zero : (2 : ‚Ñù) ‚â† 0),
            ‚Üê one_add_one_eq_two, add_smul],
        simp only [one_smul],
        have eq‚ÇÅ : wp - wq = a - b, from (sub_sub_sub_cancel_left _ _ _).symm,
        have eq‚ÇÇ : u + u - (wq + wp) = a + b, show u + u - (wq + wp) = (u - wq) + (u - wp), abel,
        rw [eq‚ÇÅ, eq‚ÇÇ],
      end
      ... = 2 * (‚à•a‚à• * ‚à•a‚à• + ‚à•b‚à• * ‚à•b‚à•) : parallelogram_law_with_norm,
    have eq : Œ¥ ‚â§ ‚à•u - half ‚Ä¢ (wq + wp)‚à•,
    { rw smul_add,
      apply Œ¥_le', apply h‚ÇÇ,
        repeat {exact subtype.mem _},
        repeat {exact le_of_lt one_half_pos},
        exact add_halves 1 },
    have eq‚ÇÅ : 4 * Œ¥ * Œ¥ ‚â§ 4 * ‚à•u - half ‚Ä¢ (wq + wp)‚à• * ‚à•u - half ‚Ä¢ (wq + wp)‚à•,
    { mono, mono, norm_num, apply mul_nonneg, norm_num, exact norm_nonneg _ },
    have eq‚ÇÇ : ‚à•a‚à• * ‚à•a‚à• ‚â§ (Œ¥ + div) * (Œ¥ + div) :=
      mul_self_le_mul_self (norm_nonneg _)
        (le_trans (le_of_lt $ hw q) (add_le_add_left (nat.one_div_le_one_div hq) _)),
    have eq‚ÇÇ' : ‚à•b‚à• * ‚à•b‚à• ‚â§ (Œ¥ + div) * (Œ¥ + div) :=
      mul_self_le_mul_self (norm_nonneg _)
        (le_trans (le_of_lt $ hw p) (add_le_add_left (nat.one_div_le_one_div hp) _)),
    rw dist_eq_norm,
    apply nonneg_le_nonneg_of_sq_le_sq, { exact sqrt_nonneg _ },
    rw mul_self_sqrt,
    calc
      ‚à•wp - wq‚à• * ‚à•wp - wq‚à• = 2 * (‚à•a‚à•*‚à•a‚à• + ‚à•b‚à•*‚à•b‚à•) -
        4 * ‚à•u - half ‚Ä¢ (wq+wp)‚à• * ‚à•u - half ‚Ä¢ (wq+wp)‚à• : by { rw ‚Üê this, simp }
      ... ‚â§ 2 * (‚à•a‚à• * ‚à•a‚à• + ‚à•b‚à• * ‚à•b‚à•) - 4 * Œ¥ * Œ¥ : sub_le_sub_left eq‚ÇÅ _
      ... ‚â§ 2 * ((Œ¥ + div) * (Œ¥ + div) + (Œ¥ + div) * (Œ¥ + div)) - 4 * Œ¥ * Œ¥ :
        sub_le_sub_right (mul_le_mul_of_nonneg_left (add_le_add eq‚ÇÇ eq‚ÇÇ') (by norm_num)) _
      ... = 8 * Œ¥ * div + 4 * div * div : by ring,
    exact add_nonneg
      (mul_nonneg (mul_nonneg (by norm_num) zero_le_Œ¥) (le_of_lt nat.one_div_pos_of_nat))
      (mul_nonneg (mul_nonneg (by norm_num) nat.one_div_pos_of_nat.le) nat.one_div_pos_of_nat.le),
    -- third goal : `tendsto (Œª (n : ‚Ñï), sqrt (b n)) at_top (ùìù 0)`
    apply tendsto.comp,
    { convert continuous_sqrt.continuous_at, exact sqrt_zero.symm },
    have eq‚ÇÅ : tendsto (Œª (n : ‚Ñï), 8 * Œ¥ * (1 / (n + 1))) at_top (nhds (0:‚Ñù)),
    { convert (@tendsto_const_nhds _ _ _ (8 * Œ¥) _).mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    have : tendsto (Œª (n : ‚Ñï), (4:‚Ñù) * (1 / (n + 1))) at_top (nhds (0:‚Ñù)),
    { convert (@tendsto_const_nhds _ _ _ (4:‚Ñù) _).mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    have eq‚ÇÇ : tendsto (Œª (n : ‚Ñï), (4:‚Ñù) * (1 / (n + 1)) * (1 / (n + 1))) at_top (nhds (0:‚Ñù)),
    { convert this.mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    convert eq‚ÇÅ.add eq‚ÇÇ, simp only [add_zero] },
  -- Step 3: By completeness of `K`, let `w : ‚Ñï ‚Üí K` converge to some `v : K`.
  -- Prove that it satisfies all requirements.
  rcases cauchy_seq_tendsto_of_is_complete h‚ÇÅ (Œª n, _) seq_is_cauchy with ‚ü®v, hv, w_tendsto‚ü©,
  use v, use hv,
  have h_cont : continuous (Œª v, ‚à•u - v‚à•) :=
    continuous.comp continuous_norm (continuous.sub continuous_const continuous_id),
  have : tendsto (Œª n, ‚à•u - w n‚à•) at_top (nhds ‚à•u - v‚à•),
    convert (tendsto.comp h_cont.continuous_at w_tendsto),
  exact tendsto_nhds_unique this norm_tendsto,
  exact subtype.mem _
end

/-- Characterization of minimizers for the projection on a convex set in a real inner product
space. -/
theorem norm_eq_infi_iff_real_inner_le_zero {K : set F} (h : convex ‚Ñù K) {u : F} {v : F}
  (hv : v ‚àà K) : ‚à•u - v‚à• = (‚®Ö w : K, ‚à•u - w‚à•) ‚Üî ‚àÄ w ‚àà K, ‚ü™u - v, w - v‚ü´_‚Ñù ‚â§ 0 :=
iff.intro
begin
  assume eq w hw,
  let Œ¥ := ‚®Ö w : K, ‚à•u - w‚à•, let p := ‚ü™u - v, w - v‚ü´_‚Ñù, let q := ‚à•w - v‚à•^2,
  letI : nonempty K := ‚ü®‚ü®v, hv‚ü©‚ü©,
  have zero_le_Œ¥ : 0 ‚â§ Œ¥,
    apply le_cinfi, intro, exact norm_nonneg _,
  have Œ¥_le : ‚àÄ w : K, Œ¥ ‚â§ ‚à•u - w‚à•,
    assume w, apply cinfi_le, use (0:‚Ñù), rintros _ ‚ü®_, rfl‚ü©, exact norm_nonneg _,
  have Œ¥_le' : ‚àÄ w ‚àà K, Œ¥ ‚â§ ‚à•u - w‚à• := assume w hw, Œ¥_le ‚ü®w, hw‚ü©,
  have : ‚àÄŒ∏:‚Ñù, 0 < Œ∏ ‚Üí Œ∏ ‚â§ 1 ‚Üí 2 * p ‚â§ Œ∏ * q,
    assume Œ∏ hŒ∏‚ÇÅ hŒ∏‚ÇÇ,
    have : ‚à•u - v‚à•^2 ‚â§ ‚à•u - v‚à•^2 - 2 * Œ∏ * ‚ü™u - v, w - v‚ü´_‚Ñù + Œ∏*Œ∏*‚à•w - v‚à•^2 :=
    calc
      ‚à•u - v‚à•^2 ‚â§ ‚à•u - (Œ∏‚Ä¢w + (1-Œ∏)‚Ä¢v)‚à•^2 :
      begin
        simp only [sq], apply mul_self_le_mul_self (norm_nonneg _),
        rw [eq], apply Œ¥_le',
        apply h hw hv,
        exacts [le_of_lt hŒ∏‚ÇÅ, sub_nonneg.2 hŒ∏‚ÇÇ, add_sub_cancel'_right _ _],
      end
      ... = ‚à•(u - v) - Œ∏ ‚Ä¢ (w - v)‚à•^2 :
      begin
        have : u - (Œ∏‚Ä¢w + (1-Œ∏)‚Ä¢v) = (u - v) - Œ∏ ‚Ä¢ (w - v),
        { rw [smul_sub, sub_smul, one_smul],
          simp only [sub_eq_add_neg, add_comm, add_left_comm, add_assoc, neg_add_rev] },
        rw this
      end
      ... = ‚à•u - v‚à•^2 - 2 * Œ∏ * inner (u - v) (w - v) + Œ∏*Œ∏*‚à•w - v‚à•^2 :
      begin
        rw [norm_sub_sq, inner_smul_right, norm_smul],
        simp only [sq],
        show ‚à•u-v‚à•*‚à•u-v‚à•-2*(Œ∏*inner(u-v)(w-v))+absR (Œ∏)*‚à•w-v‚à•*(absR (Œ∏)*‚à•w-v‚à•)=
                ‚à•u-v‚à•*‚à•u-v‚à•-2*Œ∏*inner(u-v)(w-v)+Œ∏*Œ∏*(‚à•w-v‚à•*‚à•w-v‚à•),
        rw abs_of_pos hŒ∏‚ÇÅ, ring
      end,
    have eq‚ÇÅ : ‚à•u-v‚à•^2-2*Œ∏*inner(u-v)(w-v)+Œ∏*Œ∏*‚à•w-v‚à•^2=‚à•u-v‚à•^2+(Œ∏*Œ∏*‚à•w-v‚à•^2-2*Œ∏*inner(u-v)(w-v)),
      by abel,
    rw [eq‚ÇÅ, le_add_iff_nonneg_right] at this,
    have eq‚ÇÇ : Œ∏*Œ∏*‚à•w-v‚à•^2-2*Œ∏*inner(u-v)(w-v)=Œ∏*(Œ∏*‚à•w-v‚à•^2-2*inner(u-v)(w-v)), ring,
    rw eq‚ÇÇ at this,
    have := le_of_sub_nonneg (nonneg_of_mul_nonneg_left this hŒ∏‚ÇÅ),
    exact this,
  by_cases hq : q = 0,
  { rw hq at this,
    have : p ‚â§ 0,
      have := this (1:‚Ñù) (by norm_num) (by norm_num),
      linarith,
    exact this },
  { have q_pos : 0 < q,
      apply lt_of_le_of_ne, exact sq_nonneg _, intro h, exact hq h.symm,
    by_contradiction hp, rw not_le at hp,
    let Œ∏ := min (1:‚Ñù) (p / q),
    have eq‚ÇÅ : Œ∏*q ‚â§ p := calc
      Œ∏*q ‚â§ (p/q) * q : mul_le_mul_of_nonneg_right (min_le_right _ _) (sq_nonneg _)
      ... = p : div_mul_cancel _ hq,
    have : 2 * p ‚â§ p := calc
      2 * p ‚â§ Œ∏*q : by { refine this Œ∏ (lt_min (by norm_num) (div_pos hp q_pos)) (by norm_num) }
      ... ‚â§ p : eq‚ÇÅ,
    linarith }
end
begin
  assume h,
  letI : nonempty K := ‚ü®‚ü®v, hv‚ü©‚ü©,
  apply le_antisymm,
  { apply le_cinfi, assume w,
    apply nonneg_le_nonneg_of_sq_le_sq (norm_nonneg _),
    have := h w w.2,
    calc
      ‚à•u - v‚à• * ‚à•u - v‚à• ‚â§ ‚à•u - v‚à• * ‚à•u - v‚à• - 2 * inner (u - v) ((w:F) - v) : by linarith
      ... ‚â§ ‚à•u - v‚à•^2 - 2 * inner (u - v) ((w:F) - v) + ‚à•(w:F) - v‚à•^2 :
        by { rw sq, refine le_add_of_nonneg_right _, exact sq_nonneg _ }
      ... = ‚à•(u - v) - (w - v)‚à•^2 : norm_sub_sq.symm
      ... = ‚à•u - w‚à• * ‚à•u - w‚à• :
        by { have : (u - v) - (w - v) = u - w, abel, rw [this, sq] } },
  { show (‚®Ö (w : K), ‚à•u - w‚à•) ‚â§ (Œªw:K, ‚à•u - w‚à•) ‚ü®v, hv‚ü©,
      apply cinfi_le, use 0, rintros y ‚ü®z, rfl‚ü©, exact norm_nonneg _ }
end

variables (K : submodule ùïú E)

/--
Existence of projections on complete subspaces.
Let `u` be a point in an inner product space, and let `K` be a nonempty complete subspace.
Then there exists a (unique) `v` in `K` that minimizes the distance `‚à•u - v‚à•` to `u`.
This point `v` is usually called the orthogonal projection of `u` onto `K`.
-/
theorem exists_norm_eq_infi_of_complete_subspace
  (h : is_complete (‚ÜëK : set E)) : ‚àÄ u : E, ‚àÉ v ‚àà K, ‚à•u - v‚à• = ‚®Ö w : (K : set E), ‚à•u - w‚à• :=
begin
  letI : inner_product_space ‚Ñù E := inner_product_space.is_R_or_C_to_real ùïú E,
  letI : module ‚Ñù E := restrict_scalars.module ‚Ñù ùïú E,
  let K' : submodule ‚Ñù E := submodule.restrict_scalars ‚Ñù K,
  exact exists_norm_eq_infi_of_complete_convex ‚ü®0, K'.zero_mem‚ü© h K'.convex
end

/--
Characterization of minimizers in the projection on a subspace, in the real case.
Let `u` be a point in a real inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `‚à•u - v‚à•` over points in `K` if and only if
for all `w ‚àà K`, `‚ü™u - v, w‚ü´ = 0` (i.e., `u - v` is orthogonal to the subspace `K`).
This is superceded by `norm_eq_infi_iff_inner_eq_zero` that gives the same conclusion over
any `is_R_or_C` field.
-/
theorem norm_eq_infi_iff_real_inner_eq_zero (K : submodule ‚Ñù F) {u : F} {v : F}
  (hv : v ‚àà K) : ‚à•u - v‚à• = (‚®Ö w : (‚ÜëK : set F), ‚à•u - w‚à•) ‚Üî ‚àÄ w ‚àà K, ‚ü™u - v, w‚ü´_‚Ñù = 0 :=
iff.intro
begin
  assume h,
  have h : ‚àÄ w ‚àà K, ‚ü™u - v, w - v‚ü´_‚Ñù ‚â§ 0,
  { rwa [norm_eq_infi_iff_real_inner_le_zero] at h, exacts [K.convex, hv] },
  assume w hw,
  have le : ‚ü™u - v, w‚ü´_‚Ñù ‚â§ 0,
    let w' := w + v,
    have : w' ‚àà K := submodule.add_mem _ hw hv,
    have h‚ÇÅ := h w' this,
    have h‚ÇÇ : w' - v = w, simp only [add_neg_cancel_right, sub_eq_add_neg],
    rw h‚ÇÇ at h‚ÇÅ, exact h‚ÇÅ,
  have ge : ‚ü™u - v, w‚ü´_‚Ñù ‚â• 0,
    let w'' := -w + v,
    have : w'' ‚àà K := submodule.add_mem _ (submodule.neg_mem _ hw) hv,
    have h‚ÇÅ := h w'' this,
    have h‚ÇÇ : w'' - v = -w, simp only [neg_inj, add_neg_cancel_right, sub_eq_add_neg],
    rw [h‚ÇÇ, inner_neg_right] at h‚ÇÅ,
    linarith,
    exact le_antisymm le ge
end
begin
  assume h,
  have : ‚àÄ w ‚àà K, ‚ü™u - v, w - v‚ü´_‚Ñù ‚â§ 0,
    assume w hw,
    let w' := w - v,
    have : w' ‚àà K := submodule.sub_mem _ hw hv,
    have h‚ÇÅ := h w' this,
    exact le_of_eq h‚ÇÅ,
  rwa norm_eq_infi_iff_real_inner_le_zero,
  exacts [submodule.convex _, hv]
end

/--
Characterization of minimizers in the projection on a subspace.
Let `u` be a point in an inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `‚à•u - v‚à•` over points in `K` if and only if
for all `w ‚àà K`, `‚ü™u - v, w‚ü´ = 0` (i.e., `u - v` is orthogonal to the subspace `K`)
-/
theorem norm_eq_infi_iff_inner_eq_zero {u : E} {v : E}
  (hv : v ‚àà K) : ‚à•u - v‚à• = (‚®Ö w : (‚ÜëK : set E), ‚à•u - w‚à•) ‚Üî ‚àÄ w ‚àà K, ‚ü™u - v, w‚ü´ = 0 :=
begin
  letI : inner_product_space ‚Ñù E := inner_product_space.is_R_or_C_to_real ùïú E,
  letI : module ‚Ñù E := restrict_scalars.module ‚Ñù ùïú E,
  let K' : submodule ‚Ñù E := K.restrict_scalars ‚Ñù,
  split,
  { assume H,
    have A : ‚àÄ w ‚àà K, re ‚ü™u - v, w‚ü´ = 0 := (norm_eq_infi_iff_real_inner_eq_zero K' hv).1 H,
    assume w hw,
    apply ext,
    { simp [A w hw] },
    { symmetry, calc
      im (0 : ùïú) = 0 : im.map_zero
      ... = re ‚ü™u - v, (-I) ‚Ä¢ w‚ü´ : (A _ (K.smul_mem (-I) hw)).symm
      ... = re ((-I) * ‚ü™u - v, w‚ü´) : by rw inner_smul_right
      ... = im ‚ü™u - v, w‚ü´ : by simp } },
  { assume H,
    have : ‚àÄ w ‚àà K', ‚ü™u - v, w‚ü´_‚Ñù = 0,
    { assume w hw,
      rw [real_inner_eq_re_inner, H w hw],
      exact zero_re' },
    exact (norm_eq_infi_iff_real_inner_eq_zero K' hv).2 this }
end

section orthogonal_projection
variables [complete_space K]

/-- The orthogonal projection onto a complete subspace, as an
unbundled function.  This definition is only intended for use in
setting up the bundled version `orthogonal_projection` and should not
be used once that is defined. -/
def orthogonal_projection_fn (v : E) :=
(exists_norm_eq_infi_of_complete_subspace K (complete_space_coe_iff_is_complete.mp ‚Äπ_‚Ä∫) v).some

variables {K}

/-- The unbundled orthogonal projection is in the given subspace.
This lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
lemma orthogonal_projection_fn_mem (v : E) : orthogonal_projection_fn K v ‚àà K :=
(exists_norm_eq_infi_of_complete_subspace K
  (complete_space_coe_iff_is_complete.mp ‚Äπ_‚Ä∫) v).some_spec.some

/-- The characterization of the unbundled orthogonal projection.  This
lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
lemma orthogonal_projection_fn_inner_eq_zero (v : E) :
  ‚àÄ w ‚àà K, ‚ü™v - orthogonal_projection_fn K v, w‚ü´ = 0 :=
begin
  rw ‚Üênorm_eq_infi_iff_inner_eq_zero K (orthogonal_projection_fn_mem v),
  exact (exists_norm_eq_infi_of_complete_subspace K
    (complete_space_coe_iff_is_complete.mp ‚Äπ_‚Ä∫) v).some_spec.some_spec
end

/-- The unbundled orthogonal projection is the unique point in `K`
with the orthogonality property.  This lemma is only intended for use
in setting up the bundled version and should not be used once that is
defined. -/
lemma eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero
  {u v : E} (hvm : v ‚àà K) (hvo : ‚àÄ w ‚àà K, ‚ü™u - v, w‚ü´ = 0) :
  orthogonal_projection_fn K u = v :=
begin
  rw [‚Üêsub_eq_zero, ‚Üêinner_self_eq_zero],
  have hvs : orthogonal_projection_fn K u - v ‚àà K :=
    submodule.sub_mem K (orthogonal_projection_fn_mem u) hvm,
  have huo : ‚ü™u - orthogonal_projection_fn K u, orthogonal_projection_fn K u - v‚ü´ = 0 :=
    orthogonal_projection_fn_inner_eq_zero u _ hvs,
  have huv : ‚ü™u - v, orthogonal_projection_fn K u - v‚ü´ = 0 := hvo _ hvs,
  have houv : ‚ü™(u - v) - (u - orthogonal_projection_fn K u), orthogonal_projection_fn K u - v‚ü´ = 0,
  { rw [inner_sub_left, huo, huv, sub_zero] },
  rwa sub_sub_sub_cancel_left at houv
end

variables (K)

lemma orthogonal_projection_fn_norm_sq (v : E) :
  ‚à•v‚à• * ‚à•v‚à• = ‚à•v - (orthogonal_projection_fn K v)‚à• * ‚à•v - (orthogonal_projection_fn K v)‚à•
            + ‚à•orthogonal_projection_fn K v‚à• * ‚à•orthogonal_projection_fn K v‚à• :=
begin
  set p := orthogonal_projection_fn K v,
  have h' : ‚ü™v - p, p‚ü´ = 0,
  { exact orthogonal_projection_fn_inner_eq_zero _ _ (orthogonal_projection_fn_mem v) },
  convert norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (v - p) p h' using 2;
  simp,
end

/-- The orthogonal projection onto a complete subspace. -/
def orthogonal_projection : E ‚ÜíL[ùïú] K :=
linear_map.mk_continuous
  { to_fun := Œª v, ‚ü®orthogonal_projection_fn K v, orthogonal_projection_fn_mem v‚ü©,
    map_add' := Œª x y, begin
      have hm : orthogonal_projection_fn K x + orthogonal_projection_fn K y ‚àà K :=
        submodule.add_mem K (orthogonal_projection_fn_mem x) (orthogonal_projection_fn_mem y),
      have ho :
        ‚àÄ w ‚àà K, ‚ü™x + y - (orthogonal_projection_fn K x + orthogonal_projection_fn K y), w‚ü´ = 0,
      { intros w hw,
        rw [add_sub_comm, inner_add_left, orthogonal_projection_fn_inner_eq_zero _ w hw,
            orthogonal_projection_fn_inner_eq_zero _ w hw, add_zero] },
      ext,
      simp [eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero hm ho]
    end,
    map_smul' := Œª c x, begin
      have hm : c ‚Ä¢ orthogonal_projection_fn K x ‚àà K :=
        submodule.smul_mem K _ (orthogonal_projection_fn_mem x),
      have ho : ‚àÄ w ‚àà K, ‚ü™c ‚Ä¢ x - c ‚Ä¢ orthogonal_projection_fn K x, w‚ü´ = 0,
      { intros w hw,
        rw [‚Üêsmul_sub, inner_smul_left, orthogonal_projection_fn_inner_eq_zero _ w hw, mul_zero] },
      ext,
      simp [eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero hm ho]
    end }
  1
  (Œª x, begin
    simp only [one_mul, linear_map.coe_mk],
    refine le_of_pow_le_pow 2 (norm_nonneg _) (by norm_num) _,
    change ‚à•orthogonal_projection_fn K x‚à• ^ 2 ‚â§ ‚à•x‚à• ^ 2,
    nlinarith [orthogonal_projection_fn_norm_sq K x]
  end)

variables {K}

@[simp]
lemma orthogonal_projection_fn_eq (v : E) :
  orthogonal_projection_fn K v = (orthogonal_projection K v : E) :=
rfl

/-- The characterization of the orthogonal projection.  -/
@[simp]
lemma orthogonal_projection_inner_eq_zero (v : E) :
  ‚àÄ w ‚àà K, ‚ü™v - orthogonal_projection K v, w‚ü´ = 0 :=
orthogonal_projection_fn_inner_eq_zero v

/-- The difference of `v` from its orthogonal projection onto `K` is in `K·óÆ`.  -/
@[simp] lemma sub_orthogonal_projection_mem_orthogonal (v : E) :
  v - orthogonal_projection K v ‚àà K·óÆ :=
begin
  intros w hw,
  rw inner_eq_zero_sym,
  exact orthogonal_projection_inner_eq_zero _ _ hw
end

/-- The orthogonal projection is the unique point in `K` with the
orthogonality property. -/
lemma eq_orthogonal_projection_of_mem_of_inner_eq_zero
  {u v : E} (hvm : v ‚àà K) (hvo : ‚àÄ w ‚àà K, ‚ü™u - v, w‚ü´ = 0) :
  (orthogonal_projection K u : E) = v :=
eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero hvm hvo

/-- The orthogonal projections onto equal subspaces are coerced back to the same point in `E`. -/
lemma eq_orthogonal_projection_of_eq_submodule
  {K' : submodule ùïú E} [complete_space K'] (h : K = K') (u : E) :
  (orthogonal_projection K u : E) = (orthogonal_projection K' u : E) :=
begin
  change orthogonal_projection_fn K u = orthogonal_projection_fn K' u,
  congr,
  exact h
end

/-- The orthogonal projection sends elements of `K` to themselves. -/
@[simp] lemma orthogonal_projection_mem_subspace_eq_self (v : K) : orthogonal_projection K v = v :=
by { ext, apply eq_orthogonal_projection_of_mem_of_inner_eq_zero; simp }

/-- A point equals its orthogonal projection if and only if it lies in the subspace. -/
lemma orthogonal_projection_eq_self_iff {v : E} :
  (orthogonal_projection K v : E) = v ‚Üî v ‚àà K :=
begin
  refine ‚ü®Œª h, _, Œª h, eq_orthogonal_projection_of_mem_of_inner_eq_zero h _‚ü©,
  { rw ‚Üê h,
    simp },
  { simp }
end

lemma linear_isometry.map_orthogonal_projection {E E' : Type*} [inner_product_space ùïú E]
  [inner_product_space ùïú E'] (f : E ‚Üí‚Çó·µ¢[ùïú] E') (p : submodule ùïú E) [complete_space p]
  (x : E) :
  f (orthogonal_projection p x) = orthogonal_projection (p.map f.to_linear_map) (f x) :=
begin
  refine (eq_orthogonal_projection_of_mem_of_inner_eq_zero (submodule.apply_coe_mem_map _ _) $
    Œª y hy, _).symm,
  rcases hy with ‚ü®x', hx', rfl : f x' = y‚ü©,
  rw [f.coe_to_linear_map, ‚Üê f.map_sub, f.inner_map_map,
    orthogonal_projection_inner_eq_zero x x' hx']
end

/-- Orthogonal projection onto the `submodule.map` of a subspace. -/
lemma orthogonal_projection_map_apply {E E' : Type*} [inner_product_space ùïú E]
  [inner_product_space ùïú E'] (f : E ‚âÉ‚Çó·µ¢[ùïú] E') (p : submodule ùïú E) [complete_space p]
  (x : E') :
  (orthogonal_projection (p.map (f.to_linear_equiv : E ‚Üí‚Çó[ùïú] E')) x : E')
  = f (orthogonal_projection p (f.symm x)) :=
by simpa only [f.coe_to_linear_isometry, f.apply_symm_apply]
  using (f.to_linear_isometry.map_orthogonal_projection p (f.symm x)).symm

/-- The orthogonal projection onto the trivial submodule is the zero map. -/
@[simp] lemma orthogonal_projection_bot : orthogonal_projection (‚ä• : submodule ùïú E) = 0 :=
by ext

variables (K)

/-- The orthogonal projection has norm `‚â§ 1`. -/
lemma orthogonal_projection_norm_le : ‚à•orthogonal_projection K‚à• ‚â§ 1 :=
linear_map.mk_continuous_norm_le _ (by norm_num) _

variables (ùïú)

lemma smul_orthogonal_projection_singleton {v : E} (w : E) :
  (‚à•v‚à• ^ 2 : ùïú) ‚Ä¢ (orthogonal_projection (ùïú ‚àô v) w : E) = ‚ü™v, w‚ü´ ‚Ä¢ v :=
begin
  suffices : ‚Üë(orthogonal_projection (ùïú ‚àô v) ((‚à•v‚à• ^ 2 : ùïú) ‚Ä¢ w)) = ‚ü™v, w‚ü´ ‚Ä¢ v,
  { simpa using this },
  apply eq_orthogonal_projection_of_mem_of_inner_eq_zero,
  { rw submodule.mem_span_singleton,
    use ‚ü™v, w‚ü´ },
  { intros x hx,
    obtain ‚ü®c, rfl‚ü© := submodule.mem_span_singleton.mp hx,
    have hv : ‚Üë‚à•v‚à• ^ 2 = ‚ü™v, v‚ü´ := by { norm_cast, simp [norm_sq_eq_inner] },
    simp [inner_sub_left, inner_smul_left, inner_smul_right, ring_equiv.map_div, mul_comm, hv,
      inner_product_space.conj_sym, hv] }
end

/-- Formula for orthogonal projection onto a single vector. -/
lemma orthogonal_projection_singleton {v : E} (w : E) :
  (orthogonal_projection (ùïú ‚àô v) w : E) = (‚ü™v, w‚ü´ / ‚à•v‚à• ^ 2) ‚Ä¢ v :=
begin
  by_cases hv : v = 0,
  { rw [hv, eq_orthogonal_projection_of_eq_submodule submodule.span_zero_singleton],
    { simp },
    { apply_instance } },
  have hv' : ‚à•v‚à• ‚â† 0 := ne_of_gt (norm_pos_iff.mpr hv),
  have key : ((‚à•v‚à• ^ 2 : ùïú)‚Åª¬π * ‚à•v‚à• ^ 2) ‚Ä¢ ‚Üë(orthogonal_projection (ùïú ‚àô v) w)
              = ((‚à•v‚à• ^ 2 : ùïú)‚Åª¬π * ‚ü™v, w‚ü´) ‚Ä¢ v,
  { simp [mul_smul, smul_orthogonal_projection_singleton ùïú w] },
  convert key;
  field_simp [hv']
end

/-- Formula for orthogonal projection onto a single unit vector. -/
lemma orthogonal_projection_unit_singleton {v : E} (hv : ‚à•v‚à• = 1) (w : E) :
  (orthogonal_projection (ùïú ‚àô v) w : E) = ‚ü™v, w‚ü´ ‚Ä¢ v :=
by { rw ‚Üê smul_orthogonal_projection_singleton ùïú w, simp [hv] }

end orthogonal_projection

section reflection
variables {ùïú} (K) [complete_space K]

/-- Auxiliary definition for `reflection`: the reflection as a linear equivalence. -/
def reflection_linear_equiv : E ‚âÉ‚Çó[ùïú] E :=
linear_equiv.of_involutive
  (bit0 (K.subtype.comp (orthogonal_projection K).to_linear_map) - linear_map.id)
  (Œª x, by simp [bit0])

/-- Reflection in a complete subspace of an inner product space.  The word "reflection" is
sometimes understood to mean specifically reflection in a codimension-one subspace, and sometimes
more generally to cover operations such as reflection in a point.  The definition here, of
reflection in a subspace, is a more general sense of the word that includes both those common
cases. -/
def reflection : E ‚âÉ‚Çó·µ¢[ùïú] E :=
{ norm_map' := begin
    intros x,
    let w : K := orthogonal_projection K x,
    let v := x - w,
    have : ‚ü™v, w‚ü´ = 0 := orthogonal_projection_inner_eq_zero x w w.2,
    convert norm_sub_eq_norm_add this using 2,
    { rw [linear_equiv.coe_mk, reflection_linear_equiv,
        linear_equiv.to_fun_eq_coe, linear_equiv.coe_of_involutive,
        linear_map.sub_apply, linear_map.id_apply, bit0, linear_map.add_apply,
        linear_map.comp_apply, submodule.subtype_apply,
        continuous_linear_map.to_linear_map_eq_coe, continuous_linear_map.coe_coe],
      dsimp [w, v],
      abel, },
    { simp only [add_sub_cancel'_right, eq_self_iff_true], }
  end,
  ..reflection_linear_equiv K }

variables {K}

/-- The result of reflecting. -/
lemma reflection_apply (p : E) : reflection K p = bit0 ‚Üë(orthogonal_projection K p) - p := rfl

/-- Reflection is its own inverse. -/
@[simp] lemma reflection_symm : (reflection K).symm = reflection K := rfl

/-- Reflection is its own inverse. -/
@[simp] lemma reflection_inv : (reflection K)‚Åª¬π = reflection K := rfl

variables (K)

/-- Reflecting twice in the same subspace. -/
@[simp] lemma reflection_reflection (p : E) : reflection K (reflection K p) = p :=
(reflection K).left_inv p

/-- Reflection is involutive. -/
lemma reflection_involutive : function.involutive (reflection K) := reflection_reflection K

/-- Reflection is involutive. -/
@[simp] lemma reflection_trans_reflection :
  (reflection K).trans (reflection K) = linear_isometry_equiv.refl ùïú E :=
linear_isometry_equiv.ext $ reflection_involutive K

/-- Reflection is involutive. -/
@[simp] lemma reflection_mul_reflection : reflection K * reflection K = 1 :=
reflection_trans_reflection _

variables {K}

/-- A point is its own reflection if and only if it is in the subspace. -/
lemma reflection_eq_self_iff (x : E) : reflection K x = x ‚Üî x ‚àà K :=
begin
  rw [‚Üêorthogonal_projection_eq_self_iff, reflection_apply, sub_eq_iff_eq_add', ‚Üê two_smul ùïú,
    ‚Üê two_smul' ùïú],
  refine (smul_right_injective E _).eq_iff,
  exact two_ne_zero
end

lemma reflection_mem_subspace_eq_self {x : E} (hx : x ‚àà K) : reflection K x = x :=
(reflection_eq_self_iff x).mpr hx

/-- Reflection in the `submodule.map` of a subspace. -/
lemma reflection_map_apply {E E' : Type*} [inner_product_space ùïú E] [inner_product_space ùïú E']
  (f : E ‚âÉ‚Çó·µ¢[ùïú] E') (K : submodule ùïú E) [complete_space K] (x : E') :
  reflection (K.map (f.to_linear_equiv : E ‚Üí‚Çó[ùïú] E')) x = f (reflection K (f.symm x)) :=
by simp [bit0, reflection_apply, orthogonal_projection_map_apply f K x]

/-- Reflection in the `submodule.map` of a subspace. -/
lemma reflection_map {E E' : Type*} [inner_product_space ùïú E] [inner_product_space ùïú E']
  (f : E ‚âÉ‚Çó·µ¢[ùïú] E') (K : submodule ùïú E) [complete_space K] :
  reflection (K.map (f.to_linear_equiv : E ‚Üí‚Çó[ùïú] E')) = f.symm.trans ((reflection K).trans f) :=
linear_isometry_equiv.ext $ reflection_map_apply f K

/-- Reflection through the trivial subspace {0} is just negation. -/
@[simp] lemma reflection_bot : reflection (‚ä• : submodule ùïú E) = linear_isometry_equiv.neg ùïú :=
by ext; simp [reflection_apply]

end reflection

section orthogonal

/-- If `K‚ÇÅ` is complete and contained in `K‚ÇÇ`, `K‚ÇÅ` and `K‚ÇÅ·óÆ ‚äì K‚ÇÇ` span `K‚ÇÇ`. -/
lemma submodule.sup_orthogonal_inf_of_complete_space {K‚ÇÅ K‚ÇÇ : submodule ùïú E} (h : K‚ÇÅ ‚â§ K‚ÇÇ)
  [complete_space K‚ÇÅ] : K‚ÇÅ ‚äî (K‚ÇÅ·óÆ ‚äì K‚ÇÇ) = K‚ÇÇ :=
begin
  ext x,
  rw submodule.mem_sup,
  let v : K‚ÇÅ := orthogonal_projection K‚ÇÅ x,
  have hvm : x - v ‚àà K‚ÇÅ·óÆ := sub_orthogonal_projection_mem_orthogonal x,
  split,
  { rintro ‚ü®y, hy, z, hz, rfl‚ü©,
    exact K‚ÇÇ.add_mem (h hy) hz.2 },
  { exact Œª hx, ‚ü®v, v.prop, x - v, ‚ü®hvm, K‚ÇÇ.sub_mem hx (h v.prop)‚ü©, add_sub_cancel'_right _ _‚ü© }
end

variables {K}

/-- If `K` is complete, `K` and `K·óÆ` span the whole space. -/
lemma submodule.sup_orthogonal_of_complete_space [complete_space K] : K ‚äî K·óÆ = ‚ä§ :=
begin
  convert submodule.sup_orthogonal_inf_of_complete_space (le_top : K ‚â§ ‚ä§),
  simp
end

variables (K)

/-- If `K` is complete, any `v` in `E` can be expressed as a sum of elements of `K` and `K·óÆ`. -/
lemma submodule.exists_sum_mem_mem_orthogonal [complete_space K] (v : E) :
  ‚àÉ (y ‚àà K) (z ‚àà K·óÆ), v = y + z :=
begin
  have h_mem : v ‚àà K ‚äî K·óÆ := by simp [submodule.sup_orthogonal_of_complete_space],
  obtain ‚ü®y, hy, z, hz, hyz‚ü© := submodule.mem_sup.mp h_mem,
  exact ‚ü®y, hy, z, hz, hyz.symm‚ü©
end

/-- If `K` is complete, then the orthogonal complement of its orthogonal complement is itself. -/
@[simp] lemma submodule.orthogonal_orthogonal [complete_space K] : K·óÆ·óÆ = K :=
begin
  ext v,
  split,
  { obtain ‚ü®y, hy, z, hz, rfl‚ü© := K.exists_sum_mem_mem_orthogonal v,
    intros hv,
    have hz' : z = 0,
    { have hyz : ‚ü™z, y‚ü´ = 0 := by simp [hz y hy, inner_eq_zero_sym],
      simpa [inner_add_right, hyz] using hv z hz },
    simp [hy, hz'] },
  { intros hv w hw,
    rw inner_eq_zero_sym,
    exact hw v hv }
end

lemma submodule.orthogonal_orthogonal_eq_closure [complete_space E] :
  K·óÆ·óÆ = K.topological_closure :=
begin
  refine le_antisymm _ _,
  { convert submodule.orthogonal_orthogonal_monotone K.submodule_topological_closure,
    haveI : complete_space K.topological_closure :=
      K.is_closed_topological_closure.complete_space_coe,
    rw K.topological_closure.orthogonal_orthogonal },
  { exact K.topological_closure_minimal K.le_orthogonal_orthogonal K·óÆ.is_closed_orthogonal }
end

variables {K}

/-- If `K` is complete, `K` and `K·óÆ` are complements of each other. -/
lemma submodule.is_compl_orthogonal_of_complete_space [complete_space K] : is_compl K K·óÆ :=
‚ü®K.orthogonal_disjoint, le_of_eq submodule.sup_orthogonal_of_complete_space.symm‚ü©

@[simp] lemma submodule.orthogonal_eq_bot_iff [complete_space (K : set E)] :
  K·óÆ = ‚ä• ‚Üî K = ‚ä§ :=
begin
  refine ‚ü®_, Œª h, by rw [h, submodule.top_orthogonal_eq_bot] ‚ü©,
  intro h,
  have : K ‚äî K·óÆ = ‚ä§ := submodule.sup_orthogonal_of_complete_space,
  rwa [h, sup_comm, bot_sup_eq] at this,
end

/-- A point in `K` with the orthogonality property (here characterized in terms of `K·óÆ`) must be the
orthogonal projection. -/
lemma eq_orthogonal_projection_of_mem_orthogonal
  [complete_space K] {u v : E} (hv : v ‚àà K) (hvo : u - v ‚àà K·óÆ) :
  (orthogonal_projection K u : E) = v :=
eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero hv (Œª w, inner_eq_zero_sym.mp ‚àò (hvo w))

/-- A point in `K` with the orthogonality property (here characterized in terms of `K·óÆ`) must be the
orthogonal projection. -/
lemma eq_orthogonal_projection_of_mem_orthogonal'
  [complete_space K] {u v z : E} (hv : v ‚àà K) (hz : z ‚àà K·óÆ) (hu : u = v + z) :
  (orthogonal_projection K u : E) = v :=
eq_orthogonal_projection_of_mem_orthogonal hv (by simpa [hu])

/-- The orthogonal projection onto `K` of an element of `K·óÆ` is zero. -/
lemma orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero
  [complete_space K] {v : E} (hv : v ‚àà K·óÆ) :
  orthogonal_projection K v = 0 :=
by { ext, convert eq_orthogonal_projection_of_mem_orthogonal _ _; simp [hv] }

/-- The reflection in `K` of an element of `K·óÆ` is its negation. -/
lemma reflection_mem_subspace_orthogonal_complement_eq_neg
  [complete_space K] {v : E} (hv : v ‚àà K·óÆ) :
  reflection K v = - v :=
by simp [reflection_apply, orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero hv]

/-- The orthogonal projection onto `K·óÆ` of an element of `K` is zero. -/
lemma orthogonal_projection_mem_subspace_orthogonal_precomplement_eq_zero
  [complete_space E] {v : E} (hv : v ‚àà K) :
  orthogonal_projection K·óÆ v = 0 :=
orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero (K.le_orthogonal_orthogonal hv)

/-- The reflection in `K·óÆ` of an element of `K` is its negation. -/
lemma reflection_mem_subspace_orthogonal_precomplement_eq_neg
  [complete_space E] {v : E} (hv : v ‚àà K) :
  reflection K·óÆ v = -v :=
reflection_mem_subspace_orthogonal_complement_eq_neg (K.le_orthogonal_orthogonal hv)

/-- The orthogonal projection onto `(ùïú ‚àô v)·óÆ` of `v` is zero. -/
lemma orthogonal_projection_orthogonal_complement_singleton_eq_zero [complete_space E] (v : E) :
  orthogonal_projection (ùïú ‚àô v)·óÆ v = 0 :=
orthogonal_projection_mem_subspace_orthogonal_precomplement_eq_zero
  (submodule.mem_span_singleton_self v)

/-- The reflection in `(ùïú ‚àô v)·óÆ` of `v` is `-v`. -/
lemma reflection_orthogonal_complement_singleton_eq_neg [complete_space E] (v : E) :
  reflection (ùïú ‚àô v)·óÆ v = -v :=
reflection_mem_subspace_orthogonal_precomplement_eq_neg (submodule.mem_span_singleton_self v)

lemma reflection_sub [complete_space F] {v w : F} (h : ‚à•v‚à• = ‚à•w‚à•) :
  reflection (‚Ñù ‚àô (v - w))·óÆ v = w :=
begin
  set R : F ‚âÉ‚Çó·µ¢[‚Ñù] F := reflection (‚Ñù ‚àô (v - w))·óÆ,
  suffices : R v + R v = w + w,
  { apply smul_right_injective F (by norm_num : (2:‚Ñù) ‚â† 0),
    simpa [two_smul] using this },
  have h‚ÇÅ : R (v - w) = -(v - w) := reflection_orthogonal_complement_singleton_eq_neg (v - w),
  have h‚ÇÇ : R (v + w) = v + w,
  { apply reflection_mem_subspace_eq_self,
    apply mem_orthogonal_singleton_of_inner_left,
    rw real_inner_add_sub_eq_zero_iff,
    exact h },
  convert congr_arg2 (+) h‚ÇÇ h‚ÇÅ using 1,
  { simp },
  { abel }
end

variables (K)

/-- In a complete space `E`, a vector splits as the sum of its orthogonal projections onto a
complete submodule `K` and onto the orthogonal complement of `K`.-/
lemma eq_sum_orthogonal_projection_self_orthogonal_complement
  [complete_space E] [complete_space K] (w : E) :
  w = (orthogonal_projection K w : E) + (orthogonal_projection K·óÆ w : E) :=
begin
  obtain ‚ü®y, hy, z, hz, hwyz‚ü© := K.exists_sum_mem_mem_orthogonal w,
  convert hwyz,
  { exact eq_orthogonal_projection_of_mem_orthogonal' hy hz hwyz },
  { rw add_comm at hwyz,
    refine eq_orthogonal_projection_of_mem_orthogonal' hz _ hwyz,
    simp [hy] }
end

/-- In a complete space `E`, the projection maps onto a complete subspace `K` and its orthogonal
complement sum to the identity. -/
lemma id_eq_sum_orthogonal_projection_self_orthogonal_complement
  [complete_space E] [complete_space K] :
  continuous_linear_map.id ùïú E
  = K.subtypeL.comp (orthogonal_projection K)
  + K·óÆ.subtypeL.comp (orthogonal_projection K·óÆ) :=
by { ext w, exact eq_sum_orthogonal_projection_self_orthogonal_complement K w }

/-- The orthogonal projection is self-adjoint. -/
lemma inner_orthogonal_projection_left_eq_right [complete_space E]
  [complete_space K] (u v : E) :
  ‚ü™‚Üë(orthogonal_projection K u), v‚ü´ = ‚ü™u, orthogonal_projection K v‚ü´ :=
begin
  nth_rewrite 0 eq_sum_orthogonal_projection_self_orthogonal_complement K v,
  nth_rewrite 1 eq_sum_orthogonal_projection_self_orthogonal_complement K u,
  rw [inner_add_left, inner_add_right,
    submodule.inner_right_of_mem_orthogonal (submodule.coe_mem (orthogonal_projection K u))
      (submodule.coe_mem (orthogonal_projection K·óÆ v)),
    submodule.inner_left_of_mem_orthogonal (submodule.coe_mem (orthogonal_projection K v))
      (submodule.coe_mem (orthogonal_projection K·óÆ u))],
end

open finite_dimensional

/-- Given a finite-dimensional subspace `K‚ÇÇ`, and a subspace `K‚ÇÅ`
containined in it, the dimensions of `K‚ÇÅ` and the intersection of its
orthogonal subspace with `K‚ÇÇ` add to that of `K‚ÇÇ`. -/
lemma submodule.finrank_add_inf_finrank_orthogonal {K‚ÇÅ K‚ÇÇ : submodule ùïú E}
  [finite_dimensional ùïú K‚ÇÇ] (h : K‚ÇÅ ‚â§ K‚ÇÇ) :
  finrank ùïú K‚ÇÅ + finrank ùïú (K‚ÇÅ·óÆ ‚äì K‚ÇÇ : submodule ùïú E) = finrank ùïú K‚ÇÇ :=
begin
  haveI := submodule.finite_dimensional_of_le h,
  haveI := proper_is_R_or_C ùïú K‚ÇÅ,
  have hd := submodule.dim_sup_add_dim_inf_eq K‚ÇÅ (K‚ÇÅ·óÆ ‚äì K‚ÇÇ),
  rw [‚Üêinf_assoc, (submodule.orthogonal_disjoint K‚ÇÅ).eq_bot, bot_inf_eq, finrank_bot,
      submodule.sup_orthogonal_inf_of_complete_space h] at hd,
  rw add_zero at hd,
  exact hd.symm
end

/-- Given a finite-dimensional subspace `K‚ÇÇ`, and a subspace `K‚ÇÅ`
containined in it, the dimensions of `K‚ÇÅ` and the intersection of its
orthogonal subspace with `K‚ÇÇ` add to that of `K‚ÇÇ`. -/
lemma submodule.finrank_add_inf_finrank_orthogonal' {K‚ÇÅ K‚ÇÇ : submodule ùïú E}
  [finite_dimensional ùïú K‚ÇÇ] (h : K‚ÇÅ ‚â§ K‚ÇÇ) {n : ‚Ñï} (h_dim : finrank ùïú K‚ÇÅ + n = finrank ùïú K‚ÇÇ) :
  finrank ùïú (K‚ÇÅ·óÆ ‚äì K‚ÇÇ : submodule ùïú E) = n :=
by { rw ‚Üê add_right_inj (finrank ùïú K‚ÇÅ),
     simp [submodule.finrank_add_inf_finrank_orthogonal h, h_dim] }

/-- Given a finite-dimensional space `E` and subspace `K`, the dimensions of `K` and `K·óÆ` add to
that of `E`. -/
lemma submodule.finrank_add_finrank_orthogonal [finite_dimensional ùïú E] (K : submodule ùïú E) :
  finrank ùïú K + finrank ùïú K·óÆ = finrank ùïú E :=
begin
  convert submodule.finrank_add_inf_finrank_orthogonal (le_top : K ‚â§ ‚ä§) using 1,
  { rw inf_top_eq },
  { simp }
end

/-- Given a finite-dimensional space `E` and subspace `K`, the dimensions of `K` and `K·óÆ` add to
that of `E`. -/
lemma submodule.finrank_add_finrank_orthogonal' [finite_dimensional ùïú E] {K : submodule ùïú E} {n : ‚Ñï}
  (h_dim : finrank ùïú K + n = finrank ùïú E) :
  finrank ùïú K·óÆ = n :=
by { rw ‚Üê add_right_inj (finrank ùïú K), simp [submodule.finrank_add_finrank_orthogonal, h_dim] }

local attribute [instance] fact_finite_dimensional_of_finrank_eq_succ

/-- In a finite-dimensional inner product space, the dimension of the orthogonal complement of the
span of a nonzero vector is one less than the dimension of the space. -/
lemma finrank_orthogonal_span_singleton {n : ‚Ñï} [_i : fact (finrank ùïú E = n + 1)]
  {v : E} (hv : v ‚â† 0) :
  finrank ùïú (ùïú ‚àô v)·óÆ = n :=
submodule.finrank_add_finrank_orthogonal' $ by simp [finrank_span_singleton hv, _i.elim, add_comm]

/-- An element `œÜ` of the orthogonal group of `F` can be factored as a product of reflections, and
specifically at most as many reflections as the dimension of the complement of the fixed subspace
of `œÜ`. -/
lemma linear_isometry_equiv.reflections_generate_dim_aux [finite_dimensional ‚Ñù F] {n : ‚Ñï}
  (œÜ : F ‚âÉ‚Çó·µ¢[‚Ñù] F)
  (hn : finrank ‚Ñù (continuous_linear_map.id ‚Ñù F - œÜ.to_continuous_linear_equiv).ker·óÆ ‚â§ n) :
  ‚àÉ l : list F, l.length ‚â§ n ‚àß œÜ = (l.map (Œª v, reflection (‚Ñù ‚àô v)·óÆ)).prod :=
begin
  -- We prove this by strong induction on `n`, the dimension of the orthogonal complement of the
  -- fixed subspace of the endomorphism `œÜ`
  induction n with n IH generalizing œÜ,
  { -- Base case: `n = 0`, the fixed subspace is the whole space, so `œÜ = id`
    refine ‚ü®[], rfl.le, show œÜ = 1, from _‚ü©,
    have : (continuous_linear_map.id ‚Ñù F - œÜ.to_continuous_linear_equiv).ker = ‚ä§,
    { rwa [nat.le_zero_iff, finrank_eq_zero, submodule.orthogonal_eq_bot_iff] at hn },
    symmetry,
    ext x,
    have := linear_map.congr_fun (linear_map.ker_eq_top.mp this) x,
    rwa [continuous_linear_map.coe_sub, linear_map.zero_apply, linear_map.sub_apply, sub_eq_zero]
      at this },
  { -- Inductive step.  Let `W` be the fixed subspace of `œÜ`.  We suppose its complement to have
    -- dimension at most n + 1.
    let W := (continuous_linear_map.id ‚Ñù F - œÜ.to_continuous_linear_equiv).ker,
    have hW : ‚àÄ w ‚àà W, œÜ w = w := Œª w hw, (sub_eq_zero.mp hw).symm,
    by_cases hn' : finrank ‚Ñù W·óÆ ‚â§ n,
    { obtain ‚ü®V, hV‚ÇÅ, hV‚ÇÇ‚ü© := IH œÜ hn',
      exact ‚ü®V, hV‚ÇÅ.trans n.le_succ, hV‚ÇÇ‚ü© },
    -- Take a nonzero element `v` of the orthogonal complement of `W`.
    haveI : nontrivial W·óÆ := nontrivial_of_finrank_pos (by linarith [zero_le n] : 0 < finrank ‚Ñù W·óÆ),
    obtain ‚ü®v, hv‚ü© := exists_ne (0 : W·óÆ),
    have hœÜv : œÜ v ‚àà W·óÆ,
    { intros w hw,
      rw [‚Üê hW w hw, linear_isometry_equiv.inner_map_map],
      exact v.prop w hw },
    have hv' : (v:F) ‚àâ W,
    { intros h,
      exact hv ((submodule.mem_left_iff_eq_zero_of_disjoint W.orthogonal_disjoint).mp h) },
    -- Let `œÅ` be the reflection in `v - œÜ v`; this is designed to swap `v` and `œÜ v`
    let x : F := v - œÜ v,
    let œÅ := reflection (‚Ñù ‚àô x)·óÆ,
    -- Notation: Let `V` be the fixed subspace of `œÜ.trans œÅ`
    let V := (continuous_linear_map.id ‚Ñù F - (œÜ.trans œÅ).to_continuous_linear_equiv).ker,
    have hV : ‚àÄ w, œÅ (œÜ w) = w ‚Üí w ‚àà V,
    { intros w hw,
      change w - œÅ (œÜ w) = 0,
      rw [sub_eq_zero, hw] },
    -- Everything fixed by `œÜ` is fixed by `œÜ.trans œÅ`
    have H‚ÇÇV : W ‚â§ V,
    { intros w hw,
      apply hV,
      rw hW w hw,
      refine reflection_mem_subspace_eq_self _,
      apply mem_orthogonal_singleton_of_inner_left,
      exact submodule.sub_mem _ v.prop hœÜv _ hw },
    -- `v` is also fixed by `œÜ.trans œÅ`
    have H‚ÇÅV : (v : F) ‚àà V,
    { apply hV,
      have : œÅ v = œÜ v := reflection_sub (œÜ.norm_map v).symm,
      rw ‚Üêthis,
      exact reflection_reflection _ _, },
    -- By dimension-counting, the complement of the fixed subspace of `œÜ.trans œÅ` has dimension at
    -- most `n`
    have : finrank ‚Ñù V·óÆ ‚â§ n,
    { change finrank ‚Ñù W·óÆ ‚â§ n + 1 at hn,
      have : finrank ‚Ñù W + 1 ‚â§ finrank ‚Ñù V :=
        submodule.finrank_lt_finrank_of_lt (set_like.lt_iff_le_and_exists.2 ‚ü®H‚ÇÇV, v, H‚ÇÅV, hv'‚ü©),
      have : finrank ‚Ñù V + finrank ‚Ñù V·óÆ = finrank ‚Ñù F := V.finrank_add_finrank_orthogonal,
      have : finrank ‚Ñù W + finrank ‚Ñù W·óÆ = finrank ‚Ñù F := W.finrank_add_finrank_orthogonal,
      linarith },
    -- So apply the inductive hypothesis to `œÜ.trans œÅ`
    obtain ‚ü®l, hl, hœÜl‚ü© := IH (œÅ * œÜ) this,
    -- Prepend `œÅ` to the factorization into reflections obtained for `œÜ.trans œÅ`; this gives a
    -- factorization into reflections for `œÜ`.
    refine ‚ü®x :: l, nat.succ_le_succ hl, _‚ü©,
    rw [list.map_cons, list.prod_cons],
    have := congr_arg ((*) œÅ) hœÜl,
    rwa [‚Üêmul_assoc, reflection_mul_reflection, one_mul] at this, }
end

/-- The orthogonal group of `F` is generated by reflections; specifically each element `œÜ` of the
orthogonal group is a product of at most as many reflections as the dimension of `F`.

Special case of the **Cartan‚ÄìDieudonn√© theorem**. -/
lemma linear_isometry_equiv.reflections_generate_dim [finite_dimensional ‚Ñù F] (œÜ : F ‚âÉ‚Çó·µ¢[‚Ñù] F) :
  ‚àÉ l : list F, l.length ‚â§ finrank ‚Ñù F ‚àß œÜ = (l.map (Œª v, reflection (‚Ñù ‚àô v)·óÆ)).prod :=
let ‚ü®l, hl‚ÇÅ, hl‚ÇÇ‚ü© := œÜ.reflections_generate_dim_aux le_rfl in
‚ü®l, hl‚ÇÅ.trans (submodule.finrank_le _), hl‚ÇÇ‚ü©

/-- The orthogonal group of `F` is generated by reflections. -/
lemma linear_isometry_equiv.reflections_generate [finite_dimensional ‚Ñù F] :
  subgroup.closure (set.range (Œª v : F, reflection (‚Ñù ‚àô v)·óÆ)) = ‚ä§ :=
begin
  rw subgroup.eq_top_iff',
  intros œÜ,
  rcases œÜ.reflections_generate_dim with ‚ü®l, _, rfl‚ü©,
  apply (subgroup.closure _).list_prod_mem,
  intros x hx,
  rcases list.mem_map.mp hx with ‚ü®a, _, hax‚ü©,
  exact subgroup.subset_closure ‚ü®a, hax‚ü©,
end

end orthogonal

section orthogonal_family
variables {Œπ : Type*}

/-- An orthogonal family of subspaces of `E` satisfies `direct_sum.submodule_is_internal` (that is,
they provide an internal direct sum decomposition of `E`) if and only if their span has trivial
orthogonal complement. -/
lemma orthogonal_family.submodule_is_internal_iff_of_is_complete [decidable_eq Œπ]
  {V : Œπ ‚Üí submodule ùïú E} (hV : @orthogonal_family ùïú _ _ _ _ (Œª i, V i) _ (Œª i, (V i).subtype‚Çó·µ¢))
  (hc : is_complete (‚Üë(supr V) : set E)) :
  direct_sum.submodule_is_internal V ‚Üî (supr V)·óÆ = ‚ä• :=
begin
  haveI : complete_space ‚Ü•(supr V) := hc.complete_space_coe,
  simp only [direct_sum.submodule_is_internal_iff_independent_and_supr_eq_top, hV.independent,
    true_and, submodule.orthogonal_eq_bot_iff]
end

/-- An orthogonal family of subspaces of `E` satisfies `direct_sum.submodule_is_internal` (that is,
they provide an internal direct sum decomposition of `E`) if and only if their span has trivial
orthogonal complement. -/
lemma orthogonal_family.submodule_is_internal_iff [decidable_eq Œπ] [finite_dimensional ùïú E]
  {V : Œπ ‚Üí submodule ùïú E} (hV : @orthogonal_family ùïú _ _ _ _ (Œª i, V i) _ (Œª i, (V i).subtype‚Çó·µ¢)) :
  direct_sum.submodule_is_internal V ‚Üî (supr V)·óÆ = ‚ä• :=
begin
  haveI h := finite_dimensional.proper_is_R_or_C ùïú ‚Ü•(supr V),
  exact hV.submodule_is_internal_iff_of_is_complete
    (complete_space_coe_iff_is_complete.mp infer_instance)
end

end orthogonal_family

section orthonormal_basis

/-! ### Existence of orthonormal basis, etc. -/

variables {ùïú E} {v : set E}

open finite_dimensional submodule set

/-- An orthonormal set in an `inner_product_space` is maximal, if and only if the orthogonal
complement of its span is empty. -/
lemma maximal_orthonormal_iff_orthogonal_complement_eq_bot (hv : orthonormal ùïú (coe : v ‚Üí E)) :
  (‚àÄ u ‚äá v, orthonormal ùïú (coe : u ‚Üí E) ‚Üí u = v) ‚Üî (span ùïú v)·óÆ = ‚ä• :=
begin
  rw submodule.eq_bot_iff,
  split,
  { contrapose!,
    -- ** direction 1: nonempty orthogonal complement implies nonmaximal
    rintros ‚ü®x, hx', hx‚ü©,
    -- take a nonzero vector and normalize it
    let e := (‚à•x‚à•‚Åª¬π : ùïú) ‚Ä¢ x,
    have he : ‚à•e‚à• = 1 := by simp [e, norm_smul_inv_norm hx],
    have he' : e ‚àà (span ùïú v)·óÆ := smul_mem' _ _ hx',
    have he'' : e ‚àâ v,
    { intros hev,
      have : e = 0,
      { have : e ‚àà (span ùïú v) ‚äì (span ùïú v)·óÆ := ‚ü®subset_span hev, he'‚ü©,
        simpa [(span ùïú v).inf_orthogonal_eq_bot] using this },
      have : e ‚â† 0 := hv.ne_zero ‚ü®e, hev‚ü©,
      contradiction },
    -- put this together with `v` to provide a candidate orthonormal basis for the whole space
    refine ‚ü®v.insert e, v.subset_insert e, ‚ü®_, _‚ü©, (v.ne_insert_of_not_mem he'').symm‚ü©,
    { -- show that the elements of `v.insert e` have unit length
      rintros ‚ü®a, ha'‚ü©,
      cases eq_or_mem_of_mem_insert ha' with ha ha,
      { simp [ha, he] },
      { exact hv.1 ‚ü®a, ha‚ü© } },
    { -- show that the elements of `v.insert e` are orthogonal
      have h_end : ‚àÄ a ‚àà v, ‚ü™a, e‚ü´ = 0,
      { intros a ha,
        exact he' a (submodule.subset_span ha) },
      rintros ‚ü®a, ha'‚ü©,
      cases eq_or_mem_of_mem_insert ha' with ha ha,
      { rintros ‚ü®b, hb'‚ü© hab',
        have hb : b ‚àà v,
        { refine mem_of_mem_insert_of_ne hb' _,
          intros hbe',
          apply hab',
          simp [ha, hbe'] },
        rw inner_eq_zero_sym,
        simpa [ha] using h_end b hb },
      rintros ‚ü®b, hb'‚ü© hab',
      cases eq_or_mem_of_mem_insert hb' with hb hb,
      { simpa [hb] using h_end a ha },
      have : (‚ü®a, ha‚ü© : v) ‚â† ‚ü®b, hb‚ü©,
      { intros hab'',
        apply hab',
        simpa using hab'' },
      exact hv.2 this } },
    { -- ** direction 2: empty orthogonal complement implies maximal
      simp only [subset.antisymm_iff],
      rintros h u (huv : v ‚äÜ u) hu,
      refine ‚ü®_, huv‚ü©,
      intros x hxu,
      refine ((mt (h x)) (hu.ne_zero ‚ü®x, hxu‚ü©)).imp_symm _,
      intros hxv y hy,
      have hxv' : (‚ü®x, hxu‚ü© : u) ‚àâ (coe ‚Åª¬π' v : set u) := by simp [huv, hxv],
      obtain ‚ü®l, hl, rfl‚ü© :
        ‚àÉ l ‚àà finsupp.supported ùïú ùïú (coe ‚Åª¬π' v : set u), (finsupp.total ‚Ü•u E ùïú coe) l = y,
      { rw ‚Üê finsupp.mem_span_image_iff_total,
        simp [huv, inter_eq_self_of_subset_left, hy] },
      exact hu.inner_finsupp_eq_zero hxv' hl }
end

section finite_dimensional

variables [finite_dimensional ùïú E]

/-- An orthonormal set in a finite-dimensional `inner_product_space` is maximal, if and only if it
is a basis. -/
lemma maximal_orthonormal_iff_basis_of_finite_dimensional
  (hv : orthonormal ùïú (coe : v ‚Üí E)) :
  (‚àÄ u ‚äá v, orthonormal ùïú (coe : u ‚Üí E) ‚Üí u = v) ‚Üî ‚àÉ b : basis v ùïú E, ‚áëb = coe :=
begin
  haveI := proper_is_R_or_C ùïú (span ùïú v),
  rw maximal_orthonormal_iff_orthogonal_complement_eq_bot hv,
  have hv_compl : is_complete (span ùïú v : set E) := (span ùïú v).complete_of_finite_dimensional,
  rw submodule.orthogonal_eq_bot_iff,
  have hv_coe : range (coe : v ‚Üí E) = v := by simp,
  split,
  { refine Œª h, ‚ü®basis.mk hv.linear_independent _, basis.coe_mk _ _‚ü©,
    convert h },
  { rintros ‚ü®h, coe_h‚ü©,
    rw [‚Üê h.span_eq, coe_h, hv_coe] }
end

/-- In a finite-dimensional `inner_product_space`, any orthonormal subset can be extended to an
orthonormal basis. -/
lemma exists_subset_is_orthonormal_basis
  (hv : orthonormal ùïú (coe : v ‚Üí E)) :
  ‚àÉ (u ‚äá v) (b : basis u ùïú E), orthonormal ùïú b ‚àß ‚áëb = coe :=
begin
  obtain ‚ü®u, hus, hu, hu_max‚ü© := exists_maximal_orthonormal hv,
  obtain ‚ü®b, hb‚ü© := (maximal_orthonormal_iff_basis_of_finite_dimensional hu).mp hu_max,
  exact ‚ü®u, hus, b, by rwa hb, hb‚ü©
end

variables (ùïú E)

/-- Index for an arbitrary orthonormal basis on a finite-dimensional `inner_product_space`. -/
def orthonormal_basis_index : set E :=
classical.some (exists_subset_is_orthonormal_basis (orthonormal_empty ùïú E))

/-- A finite-dimensional `inner_product_space` has an orthonormal basis. -/
def orthonormal_basis :
  basis (orthonormal_basis_index ùïú E) ùïú E :=
(exists_subset_is_orthonormal_basis (orthonormal_empty ùïú E)).some_spec.some_spec.some

lemma orthonormal_basis_orthonormal :
  orthonormal ùïú (orthonormal_basis ùïú E) :=
(exists_subset_is_orthonormal_basis (orthonormal_empty ùïú E)).some_spec.some_spec.some_spec.1

@[simp] lemma coe_orthonormal_basis :
  ‚áë(orthonormal_basis ùïú E) = coe :=
(exists_subset_is_orthonormal_basis (orthonormal_empty ùïú E)).some_spec.some_spec.some_spec.2

instance : fintype (orthonormal_basis_index ùïú E) :=
@is_noetherian.fintype_basis_index _ _ _ _ _ _ _
  (is_noetherian.iff_fg.2 infer_instance) (orthonormal_basis ùïú E)

variables {ùïú E}

/-- An `n`-dimensional `inner_product_space` has an orthonormal basis indexed by `fin n`. -/
def fin_orthonormal_basis {n : ‚Ñï} (hn : finrank ùïú E = n) :
  basis (fin n) ùïú E :=
have h : fintype.card (orthonormal_basis_index ùïú E) = n,
by rw [‚Üê finrank_eq_card_basis (orthonormal_basis ùïú E), hn],
(orthonormal_basis ùïú E).reindex (fintype.equiv_fin_of_card_eq h)

lemma fin_orthonormal_basis_orthonormal {n : ‚Ñï} (hn : finrank ùïú E = n) :
  orthonormal ùïú (fin_orthonormal_basis hn) :=
suffices orthonormal ùïú (orthonormal_basis _ _ ‚àò equiv.symm _),
by { simp only [fin_orthonormal_basis, basis.coe_reindex], assumption }, -- why doesn't simpa work?
(orthonormal_basis_orthonormal ùïú E).comp _ (equiv.injective _)

section subordinate_orthonormal_basis
open direct_sum
variables {n : ‚Ñï} (hn : finrank ùïú E = n) {Œπ : Type*} [fintype Œπ] [decidable_eq Œπ]
  {V : Œπ ‚Üí submodule ùïú E} (hV : submodule_is_internal V)

/-- Exhibit a bijection between `fin n` and the index set of a certain basis of an `n`-dimensional
inner product space `E`.  This should not be accessed directly, but only via the subsequent API. -/
@[irreducible] def direct_sum.submodule_is_internal.sigma_orthonormal_basis_index_equiv :
  (Œ£ i, orthonormal_basis_index ùïú (V i)) ‚âÉ fin n :=
let b := hV.collected_basis (Œª i, orthonormal_basis ùïú (V i)) in
fintype.equiv_fin_of_card_eq $ (finite_dimensional.finrank_eq_card_basis b).symm.trans hn

/-- An `n`-dimensional `inner_product_space` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `fin n` and subordinate to that direct sum. -/
@[irreducible] def direct_sum.submodule_is_internal.subordinate_orthonormal_basis :
  basis (fin n) ùïú E :=
(hV.collected_basis (Œª i, orthonormal_basis ùïú (V i))).reindex
  (hV.sigma_orthonormal_basis_index_equiv hn)

/-- An `n`-dimensional `inner_product_space` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `fin n` and subordinate to that direct sum. This function
provides the mapping by which it is subordinate. -/
def direct_sum.submodule_is_internal.subordinate_orthonormal_basis_index (a : fin n) : Œπ :=
((hV.sigma_orthonormal_basis_index_equiv hn).symm a).1

/-- The basis constructed in `orthogonal_family.subordinate_orthonormal_basis` is orthonormal. -/
lemma direct_sum.submodule_is_internal.subordinate_orthonormal_basis_orthonormal
  (hV' : @orthogonal_family ùïú _ _ _ _ (Œª i, V i) _ (Œª i, (V i).subtype‚Çó·µ¢)) :
  orthonormal ùïú (hV.subordinate_orthonormal_basis hn) :=
begin
  simp only [direct_sum.submodule_is_internal.subordinate_orthonormal_basis, basis.coe_reindex],
  have : orthonormal ùïú (hV.collected_basis (Œª i, orthonormal_basis ùïú (V i))) :=
    hV.collected_basis_orthonormal hV' (Œª i, orthonormal_basis_orthonormal ùïú (V i)),
  exact this.comp _ (equiv.injective _),
end

/-- The basis constructed in `orthogonal_family.subordinate_orthonormal_basis` is subordinate to
the `orthogonal_family` in question. -/
lemma direct_sum.submodule_is_internal.subordinate_orthonormal_basis_subordinate (a : fin n) :
  hV.subordinate_orthonormal_basis hn a ‚àà V (hV.subordinate_orthonormal_basis_index hn a) :=
by simpa only [direct_sum.submodule_is_internal.subordinate_orthonormal_basis, basis.coe_reindex]
  using hV.collected_basis_mem (Œª i, orthonormal_basis ùïú (V i))
    ((hV.sigma_orthonormal_basis_index_equiv hn).symm a)

attribute [irreducible] direct_sum.submodule_is_internal.subordinate_orthonormal_basis_index

end subordinate_orthonormal_basis

end finite_dimensional

end orthonormal_basis
